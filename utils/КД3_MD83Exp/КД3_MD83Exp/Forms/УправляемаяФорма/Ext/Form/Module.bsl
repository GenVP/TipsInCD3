
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтаФорма.Заголовок = "Выгрузка описания структуры метаданных ("+ВерсияОбъектаНаСервере()+")";
	
	Объект.НеВыгружатьРегистрыСведений    = Истина;
	Объект.НеВыгружатьРегистрыНакопления  = Истина;
	Объект.НеВыгружатьРегистрыБухгалтерии = Истина;
	Объект.НеВыгружатьРегистрыРасчета     = Истина;
	Объект.НеВыгружатьДвиженияДокументов  = Истина;
	//+КД3
	Объект.НеВыгружатьОбработки           = Истина;
	Объект.НеВыгружатьОтчеты              = Истина;
	Объект.НеВыгружатьОбщиеМодули         = Истина;
	//-КД3
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИнициализироватьЗначения();
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Не ЭтоКлиент Тогда
		
		ПроверяемыеРеквизиты.Добавить("ИмяФайлаВыгрузки");
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ИмяФайлаВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДиалогВыбораФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Сохранение);
	
	ДиалогВыбораФайла.Фильтр = "Файл данных (*.xml)|*.xml";
	ДиалогВыбораФайла.Заголовок = "Выберите файл";
	ДиалогВыбораФайла.ПредварительныйПросмотр = Ложь;
	ДиалогВыбораФайла.Расширение = "xml";
	ДиалогВыбораФайла.ИндексФильтра = 0;
	ДиалогВыбораФайла.ПолноеИмяФайла = Элемент.ТекстРедактирования;
	ДиалогВыбораФайла.ПроверятьСуществованиеФайла = Ложь;
	
	Если ДиалогВыбораФайла.Выбрать() Тогда
		
		ИмяФайлаВыгрузки = ДиалогВыбораФайла.ПолноеИмяФайла;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НеВыгружатьРегистрыСведенийПриИзменении(Элемент)
	
	ИнвертироватьФлажок(ВыгружатьРегистрыСведений, "НеВыгружатьРегистрыСведений");
	
КонецПроцедуры

&НаКлиенте
Процедура НеВыгружатьРегистрыНакопленияПриИзменении(Элемент)
	
	ИнвертироватьФлажок(ВыгружатьРегистрыНакопления, "НеВыгружатьРегистрыНакопления");
	
КонецПроцедуры

 &НаКлиенте
Процедура НеВыгружатьРегистрыБухгалтерииПриИзменении(Элемент)
	
	ИнвертироватьФлажок(ВыгружатьРегистрыБухгалтерии, "НеВыгружатьРегистрыБухгалтерии");
	
КонецПроцедуры

&НаКлиенте
Процедура НеВыгружатьРегистрыРасчетаПриИзменении(Элемент)
	
	ИнвертироватьФлажок(ВыгружатьРегистрыРасчета, "НеВыгружатьРегистрыРасчета");
	
КонецПроцедуры

&НаКлиенте
Процедура ЧастичнаяВыгрузкаДокументовПриИзменении(Элемент)
	
	ИзменитьВыгрузкуДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгружатьДвиженияДокументовПриИзменении(Элемент)
	
	ИзменитьВыгрузкуДокументов();
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ВыгружатьОтчетыПриИзменении(Элемент)
	
	ИнвертироватьФлажок(ВыгружатьОтчеты, "НеВыгружатьОтчеты");
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ВыгружатьОбработкиПриИзменении(Элемент)
	
	ИнвертироватьФлажок(ВыгружатьОбработки, "НеВыгружатьОбработки");
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ВыгружатьОбщиеМодулиПриИзменении(Элемент)
	
	ИнвертироватьФлажок(ВыгружатьОбщиеМодули, "НеВыгружатьОбщиеМодули");
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура НаКлиенте(Команда)
	
	Если Не ЭтоКлиент Тогда
		
		ЭтоКлиент = Истина;
		
		ИзменитьРежимОбработки();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаСервере(Команда)
	
	Если ЭтоКлиент Тогда
		
		ЭтоКлиент = Ложь;
		
		ИзменитьРежимОбработки();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	Перем АдресФайла;
	
	ОчиститьСообщения();
	
	Состояние(Нстр("ru='Выполняется выгрузка описания структуры метаданных конфигурации. Пожалуйста, подождите...'"));
	Если ВыполнитьВыгрузкуНаСервере(АдресФайла) Тогда
		
		Сообщить("Выгрузка описания структуры конфигурации успешно завершена.");
		
		Если ЭтоКлиент Тогда
			
			ПолучитьФайл(АдресФайла, "Описание структуры метаданных.xml");
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ИнициализироватьЗначения()
	
	ИзменитьРежимОбработки();
	УстановитьЗначенияФлажков();
	ИзменитьВыгрузкуДокументов();
	
КонецПроцедуры

&НаСервере
Функция ВерсияОбъектаНаСервере()
	
	Возврат РеквизитФормыВЗначение("Объект").ВерсияОбъекта();
	
КонецФункции

&НаСервере
Функция ВыполнитьВыгрузкуНаСервере(АдресФайла)
	
	Если Не ПроверитьЗаполнение() ТОгда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЭтоКлиент Тогда
	
		ВременныйФайл = ПолучитьИмяВременногоФайла(".xml");
		Файл = Новый Файл(ВременныйФайл);
		Объект.ИмяФайлаВыгрузки = ВременныйФайл;
		
	Иначе
		
		Объект.ИмяФайлаВыгрузки = ИмяФайлаВыгрузки;
		
	КонецЕсли;
	
	ОбъектДляСервера = РеквизитФормыВЗначение("Объект");
	ОбъектДляСервера.СостояниеИндикатораПрогресса = Новый Структура("Заголовок");
	ОбъектДляСервера.Индикатор = Новый Структура("Значение, МаксимальноеЗначение");
	ВыгрузкаЗавершена = ОбъектДляСервера.ВыполнитьВыгрузку();
	
	Если ЭтоКлиент Тогда
		
		АдресФайла = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(Объект.ИмяФайлаВыгрузки), ЭтаФорма.УникальныйИдентификатор);
		УдалитьФайлы(Объект.ИмяФайлаВыгрузки);
		
	КонецЕсли;
	
	Если Не ВыгрузкаЗавершена Тогда
		
		Сообщить("Ошибка создания файла.");
		
	КонецЕсли;
	
	Возврат ВыгрузкаЗавершена;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьРежимОбработки()
	
	Элементы.ИмяФайлаВыгрузки.Доступность = Не ЭтоКлиент;
	Элементы.Настройки.Видимость = Истина;
	
	КоманднаяПанель.ПодчиненныеЭлементы.РежимРаботы.ПодчиненныеЭлементы.ФормаНаКлиенте.Пометка = ЭтоКлиент;
	КоманднаяПанель.ПодчиненныеЭлементы.РежимРаботы.ПодчиненныеЭлементы.ФормаНаСервере.Пометка = Не ЭтоКлиент;
	
	КоманднаяПанель.ПодчиненныеЭлементы.РежимРаботы.Заголовок = 
	?(ЭтоКлиент, НСтр("ru = 'Режим работы (на клиенте)'"), НСтр("ru = 'Режим работы (на сервере)'"));
	
КонецПроцедуры

&НаСервере
Процедура ИнвертироватьФлажок(Реквизит, ИмяРеквизита)
	
	Объект[ИмяРеквизита] = Не Реквизит;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначенияФлажков()
	
	ВыгружатьРегистрыСведений = Не Объект.НеВыгружатьРегистрыСведений;
	ВыгружатьРегистрыНакопления = Не Объект.НеВыгружатьРегистрыНакопления;
	ВыгружатьРегистрыБухгалтерии = Не Объект.НеВыгружатьРегистрыБухгалтерии;
	ВыгружатьРегистрыРасчета = Не Объект.НеВыгружатьРегистрыРасчета;
	ВыгружатьДвиженияДокументов = Объект.ВыгружатьДвиженияТолькоУДокументовСЗапретомПроведения;
	//+КД3
	ВыгружатьОбработки = НЕ Объект.НеВыгружатьОбработки;
	ВыгружатьОтчеты = НЕ Объект.НеВыгружатьОтчеты;
	ВыгружатьОбщиеМодули = НЕ Объект.НеВыгружатьОбщиеМодули;
	//-КД3
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВыгрузкуДокументов()
	
	Если ВыгружатьДвиженияДокументов Тогда
		
		Объект.ВыгружатьДвиженияТолькоУДокументовСЗапретомПроведения = Истина;
		Объект.НеВыгружатьДвиженияДокументов = ЧастичнаяВыгрузкаДокументов = 1;
		
	Иначе
		
		Объект.НеВыгружатьДвиженияДокументов = Истина;
		Объект.ВыгружатьДвиженияТолькоУДокументовСЗапретомПроведения = Ложь;
		
	КонецЕсли;
	
	Элементы.ЧастичнаяВыгрузкаДокументов.Доступность = ВыгружатьДвиженияДокументов;
	
КонецПроцедуры

