
&НаСервере
Процедура КД3_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Обработчики = Новый СписокЗначений;
	Если КонвертацияXDTO Тогда
		Обработчики.Добавить("АлгоритмПриОбработке", "OnProcessing");
		Обработчики.Добавить("АлгоритмВыборкаДанных", "DataFetch");
		СтраницыОбработчиков = Элементы.СтраницыОбработчикиXDTO;
	Иначе
		Обработчики.Добавить("АлгоритмПередОбработкойПравила", "BeforeRuleProcessing");
		Обработчики.Добавить("АлгоритмПередВыгрузкойОбъекта", "BeforeUnloadingObject");
		Обработчики.Добавить("АлгоритмПослеВыгрузкиОбъекта", "AfterUnloadingObject");
		Обработчики.Добавить("АлгоритмПослеОбработкиПравила", "AfterRuleProcessing");
		СтраницыОбработчиков = Элементы.СтраницыОбработчикиXML;
	КонецЕсли;
	КД3_Метаданные.ПриСозданииНаСервере(ЭтотОбъект, Отказ, КонвертацияXDTO, Обработчики, СтраницыОбработчиков);
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПриОткрытииПосле(Отказ)
	КД3_МетаданныеКлиент.ПриОткрытии(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
	КД3_МетаданныеКлиент.ПередЗаписью(ЭтотОбъект, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПередЗакрытиемПосле(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	КД3_МетаданныеКлиент.ПередЗаписью(ЭтотОбъект, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура КД3_СтраницыПриСменеСтраницыПосле(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.ОбработчикиСобытийXDTO Тогда
		КД3_СтраницыОбработчикиXDTOПриСменеСтраницыПосле(Элементы.СтраницыОбработчикиXDTO, Элементы.СтраницыОбработчикиXDTO.ТекущаяСтраница);
	ИначеЕсли ТекущаяСтраница = Элементы.ОбработчикиСобытийXML Тогда
		КД3_СтраницыОбработчикиXMLПриСменеСтраницыПосле(Элементы.СтраницыОбработчикиXML, Элементы.СтраницыОбработчикиXML.ТекущаяСтраница);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_СтраницыОбработчикиXDTOПриСменеСтраницыПосле(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.Страница_ПриОбработке Тогда
		ИмяОбработчика = "АлгоритмПриОбработке";
	ИначеЕсли ТекущаяСтраница = Элементы.Страница_ВыборкаДанных Тогда
		ИмяОбработчика = "АлгоритмВыборкаДанных";
	Иначе
		Возврат;
	КонецЕсли;
	КД3_МетаданныеКлиент.ПриСменеСтраницы(ЭтотОбъект, ИмяОбработчика);
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_СтраницыОбработчикиXMLПриСменеСтраницыПосле(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.ПередОбработкой Тогда
		ИмяОбработчика = "АлгоритмПередОбработкойПравила";
	ИначеЕсли ТекущаяСтраница = Элементы.ПередВыгрузкой Тогда
		ИмяОбработчика = "АлгоритмПередВыгрузкойОбъекта";
	ИначеЕсли ТекущаяСтраница = Элементы.ПослеВыгрузки Тогда
		ИмяОбработчика = "АлгоритмПослеВыгрузкиОбъекта";
	ИначеЕсли ТекущаяСтраница = Элементы.ПослеОбработки Тогда
		ИмяОбработчика = "АлгоритмПослеОбработкиПравила";
	Иначе
		Возврат;
	КонецЕсли;
	КД3_МетаданныеКлиент.ПриСменеСтраницы(ЭтотОбъект, ИмяОбработчика);
	
КонецПроцедуры

&НаСервере
&После("ОбработкаВыбораНаСервере")
Процедура КД3_ОбработкаВыбораНаСервере()
	КД3_Метаданные.ПриИзмененииКонвертаций(ЭтотОбъект, КонвертацияXDTO);
КонецПроцедуры

&НаСервере
&После("ДополнитьСоставКонвертации")
Процедура КД3_ДополнитьСоставКонвертации(Конвертация, ЭлементКонвертации, ОтказДобавления)
	Если НЕ ОтказДобавления Тогда
		КД3_Метаданные.ПриИзмененииКонвертаций(ЭтотОбъект, КонвертацияXDTO);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ПриИзмененииКонфигурации(Элемент)
	КД3_МетаданныеКлиент.ОбновитьМетаданныеКонфигурации(ЭтотОбъект, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ДокументСформирован(Элемент)
	ИмяОбработчика = Сред(Элемент.Имя, 5);
	КД3_МетаданныеКлиент.ИнициализацияРедактора(ЭтотОбъект, Элемент.Имя);
	КД3_МетаданныеКлиент.УстановитьТекст(ЭтотОбъект, Элемент.Имя, Объект[ИмяОбработчика]);
	КД3_МетаданныеКлиент.ОбновитьМетаданные(ЭтотОбъект, ИмяОбработчика);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_HTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	КД3_МетаданныеКлиент.ОбработатьСобытиеHTML(ЭтотОбъект, Элемент, ДанныеСобытия);
КонецПроцедуры
