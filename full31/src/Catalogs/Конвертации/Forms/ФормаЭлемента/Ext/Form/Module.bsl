&НаСервере
Процедура КД3_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Обработчики = Новый СписокЗначений;
	
	Если КонвертацияXDTO Тогда
		Обработчики.Добавить("АлгоритмПередКонвертацией", "BeforeConversion");
		Обработчики.Добавить("АлгоритмПередОтложеннымЗаполнением", "BeforeDelayedFilling");
		Обработчики.Добавить("АлгоритмПослеКонвертации", "AfterConversion");
		СтраницыОбработчиков = Элементы.СтраницыОбработчики;
	Иначе
		Обработчики.Добавить("АлгоритмПослеЗагрузкиПравилОбмена", "AfterDownloadRulesExchange");
		Обработчики.Добавить("АлгоритмПередВыгрузкойДанных", "BeforeUploadingData");
		Обработчики.Добавить("АлгоритмПередПолучениемИзмененныхОбъектов", "BeforeGettingChangedObjects");
		Обработчики.Добавить("АлгоритмПередВыгрузкойОбъекта", "BeforeUnloadingObject");
		Обработчики.Добавить("АлгоритмПередОтправкойИнформацииОбУдалении", "BeforeSendingRemovalInformation");
		Обработчики.Добавить("АлгоритмПередКонвертациейОбъекта", "BeforeConvertingObject");
		Обработчики.Добавить("АлгоритмПослеВыгрузкиДанных", "AfterUploadingData");
		Обработчики.Добавить("АлгоритмПередЗагрузкойДанных", "BeforeLoadingData", Истина);
		Обработчики.Добавить("АлгоритмПослеЗагрузкиПараметров", "AfterDownloadParameters", Истина);
		Обработчики.Добавить("АлгоритмПослеПолученияИнформацииОбУзлахОбмена", "AfterReceivingInformationAboutExchangeNodes", Истина);
		Обработчики.Добавить("АлгоритмПередЗагрузкойОбъекта", "BeforeLoadingObject", Истина);
		Обработчики.Добавить("АлгоритмПриПолученииИнформацииОбУдалении", "OnReceivingDeletionInformation", Истина);
		Обработчики.Добавить("АлгоритмПослеЗагрузкиДанных", "AfterDataLoad", Истина);
		СтраницыОбработчиков = Элементы.ОбработчикиXML;
	КонецЕсли;
	КД3_Метаданные.ПриСозданииНаСервере(ЭтотОбъект, Отказ, КонвертацияXDTO, Обработчики, СтраницыОбработчиков);
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПриОткрытииПосле(Отказ)
	КД3_МетаданныеКлиент.ПриОткрытии(ЭтотОбъект);
	Если НЕ КонвертацияXDTO Тогда
		КД3_СтраницыПриСменеСтраницыПосле(Элементы.Страницы, Элементы.СтраницаКонвертацияXML);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
	КД3_МетаданныеКлиент.ПередЗаписью(ЭтотОбъект, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПередЗакрытиемПосле(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	КД3_МетаданныеКлиент.ПередЗаписью(ЭтотОбъект, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура КД3_КонфигурацияПриИзмененииПосле(Элемент)
	ЭтотОбъект.КД3_Конфигурация = Объект.Конфигурация;
	КД3_МетаданныеКлиент.ОбновитьМетаданныеКонфигурации(ЭтотОбъект, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура КД3_КонфигурацияИсточникПриИзмененииПосле(Элемент)
	ЭтотОбъект.КД3_Конфигурация = Объект.Конфигурация;
	КД3_МетаданныеКлиент.ОбновитьМетаданныеКонфигурации(ЭтотОбъект, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура КД3_КонфигурацияКорреспондентПриИзмененииПосле(Элемент)
	ЭтотОбъект.КД3_КонфигурацияКорреспондент = Объект.КонфигурацияКорреспондент;
	КД3_МетаданныеКлиент.ОбновитьМетаданныеКонфигурации(ЭтотОбъект, Истина);
КонецПроцедуры

&НаКлиенте
Процедура КД3_СтраницыПриСменеСтраницыПосле(Элемент, ТекущаяСтраница)
	Если ТекущаяСтраница = Элементы.СтраницаКонвертацияXDTO Тогда
		КД3_ГруппаДанныеКонвертацииXDTOПриСменеСтраницыПосле(Элемент, Элементы.ГруппаДанныеКонвертацииXDTO.ТекущаяСтраница)
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаКонвертацияXML Тогда
		КД3_ОбработчикиXMLПриСменеСтраницыПосле(Элемент, Элементы.ОбработчикиXML.ТекущаяСтраница)
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КД3_ГруппаДанныеКонвертацииXDTOПриСменеСтраницыПосле(Элемент, ТекущаяСтраница)
	Если ТекущаяСтраница = Элементы.СтраницаОбработчики Тогда
		КД3_СтраницыОбработчикиПриСменеСтраницыПосле(Элемент, Элементы.СтраницыОбработчики.ТекущаяСтраница);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КД3_СтраницыОбработчикиПриСменеСтраницыПосле(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.ПередКонвертацией Тогда
		ИмяОбработчика = "АлгоритмПередКонвертацией";
	ИначеЕсли ТекущаяСтраница = Элементы.ПередОтложеннымЗаполнением Тогда
		ИмяОбработчика = "АлгоритмПередОтложеннымЗаполнением";
	ИначеЕсли ТекущаяСтраница = Элементы.ПослеКонвертации Тогда
		ИмяОбработчика = "АлгоритмПослеКонвертации";
	Иначе
		Возврат;
	КонецЕсли;
	КД3_МетаданныеКлиент.ПриСменеСтраницы(ЭтотОбъект, ИмяОбработчика);
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ОбработчикиXMLПриСменеСтраницыПосле(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.ПослеЗагрузкиПравилОбмена Тогда
		ИмяОбработчика = "АлгоритмПослеЗагрузкиПравилОбмена";
	ИначеЕсли ТекущаяСтраница = Элементы.ПередВыгрузкойДанных Тогда
		ИмяОбработчика = "АлгоритмПередВыгрузкойДанных";
	ИначеЕсли ТекущаяСтраница = Элементы.ПередПолучениемИзмененных Тогда
		ИмяОбработчика = "АлгоритмПередПолучениемИзмененныхОбъектов";
	ИначеЕсли ТекущаяСтраница = Элементы.ПередВыгрузкойОбъекта Тогда
		ИмяОбработчика = "АлгоритмПередВыгрузкойОбъекта";
	ИначеЕсли ТекущаяСтраница = Элементы.ПередОтправкойУдаления Тогда
		ИмяОбработчика = "АлгоритмПередОтправкойИнформацииОбУдалении";
	ИначеЕсли ТекущаяСтраница = Элементы.ПередКонвертациейОбъекта Тогда
		ИмяОбработчика = "АлгоритмПередКонвертациейОбъекта";
	ИначеЕсли ТекущаяСтраница = Элементы.ПослеВыгрузкиОбъекта Тогда
		ИмяОбработчика = "АлгоритмПослеВыгрузкиОбъекта";
	ИначеЕсли ТекущаяСтраница = Элементы.ПослеВыгрузкиДанных Тогда
		ИмяОбработчика = "АлгоритмПослеВыгрузкиДанных";
	ИначеЕсли ТекущаяСтраница = Элементы.ПередЗагрузкойДанных Тогда
		ИмяОбработчика = "АлгоритмПередЗагрузкойДанных";
	ИначеЕсли ТекущаяСтраница = Элементы.ПослеЗагрузкиПараметров Тогда
		ИмяОбработчика = "АлгоритмПослеЗагрузкиПараметров";
	ИначеЕсли ТекущаяСтраница = Элементы.ПослеПолученияУзловОбмена Тогда
		ИмяОбработчика = "АлгоритмПослеПолученияИнформацииОбУзлахОбмена";
	ИначеЕсли ТекущаяСтраница = Элементы.ПередЗагрузкойОбъекта Тогда
		ИмяОбработчика = "АлгоритмПередЗагрузкойОбъекта";
	ИначеЕсли ТекущаяСтраница = Элементы.ПриПолученииУдаления Тогда
		ИмяОбработчика = "АлгоритмПриПолученииИнформацииОбУдалении";
	ИначеЕсли ТекущаяСтраница = Элементы.ПослеЗагрузкиОбъекта Тогда
		ИмяОбработчика = "АлгоритмПослеЗагрузкиОбъекта";
	ИначеЕсли ТекущаяСтраница = Элементы.ПослеЗагрузкиДанных Тогда
		ИмяОбработчика = "АлгоритмПослеЗагрузкиДанных";
	Иначе
		Возврат;
	КонецЕсли;
	КД3_МетаданныеКлиент.ПриСменеСтраницы(ЭтотОбъект, ИмяОбработчика);
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ДокументСформирован(Элемент)
	ИмяОбработчика = Сред(Элемент.Имя, 5);
	КД3_МетаданныеКлиент.ИнициализацияРедактора(ЭтотОбъект, Элемент.Имя);
	КД3_МетаданныеКлиент.УстановитьТекст(ЭтотОбъект, Элемент.Имя, Объект[ИмяОбработчика]);
	КД3_МетаданныеКлиент.ОбновитьМетаданные(ЭтотОбъект, ИмяОбработчика);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_HTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	КД3_МетаданныеКлиент.ОбработатьСобытиеHTML(ЭтотОбъект, Элемент, ДанныеСобытия);
КонецПроцедуры
