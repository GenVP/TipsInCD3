
#Область ПодключаемыеОбработчики

&НаКлиенте
Процедура КД3_Подключаемый_ДокументСформирован(Элемент);
	КД3_УправлениеФормойКлиент.ИнициализацияРедактора(ЭтотОбъект, Элемент.Имя, Объект);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ИнициализацияМетаданных() Экспорт
	ИзмененныйКонтекст = КД3_ЗаполнитьИзмененныйКонтекст_Параметры(ИзмененныйКонтекст);
	КД3_УправлениеФормойКлиент.ИнициализацияМетаданных(ЭтотОбъект, КонвертацияXDTO, ИзмененныйКонтекст);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_HTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	КД3_УправлениеФормойКлиент.ОбработатьСобытиеHTML(ЭтотОбъект, Элемент, ДанныеСобытия);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_КомандаРедактора(Команда)
	КД3_УправлениеФормойКлиент.КомандаРедактора(ЭтотОбъект, Команда);
КонецПроцедуры

#КонецОбласти

#Область ЗаменяемыеОбработчики

&НаСервере
Процедура КД3_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Если НЕ КД3_Настройки.ИспользоватьРедакторКода() Тогда
		Возврат;
	КонецЕсли;
	
	Обработчики = Новый СписокЗначений;
	
	Если КонвертацияXDTO Тогда
		Обработчики.Добавить("АлгоритмПередКонвертацией");
		Обработчики.Добавить("АлгоритмПередОтложеннымЗаполнением");
		Обработчики.Добавить("АлгоритмПослеКонвертации");
		СтраницыОбработчиков = Элементы.СтраницыОбработчики;
	Иначе
		Обработчики.Добавить("АлгоритмПослеЗагрузкиПравилОбмена");
		Обработчики.Добавить("АлгоритмПередВыгрузкойДанных");
		Обработчики.Добавить("АлгоритмПередПолучениемИзмененныхОбъектов");
		Обработчики.Добавить("АлгоритмПередВыгрузкойОбъекта");
		Обработчики.Добавить("АлгоритмПередОтправкойИнформацииОбУдалении");
		Обработчики.Добавить("АлгоритмПередКонвертациейОбъекта");
		Обработчики.Добавить("АлгоритмПослеВыгрузкиОбъекта");
		Обработчики.Добавить("АлгоритмПослеВыгрузкиДанных");
		Обработчики.Добавить("АлгоритмПередЗагрузкойДанных", , Истина);
		Обработчики.Добавить("АлгоритмПослеЗагрузкиПараметров", , Истина);
		Обработчики.Добавить("АлгоритмПослеПолученияИнформацииОбУзлахОбмена", , Истина);
		Обработчики.Добавить("АлгоритмПередЗагрузкойОбъекта", , Истина);
		Обработчики.Добавить("АлгоритмПриПолученииИнформацииОбУдалении", , Истина);
		Обработчики.Добавить("АлгоритмПослеЗагрузкиОбъекта", , Истина);
		Обработчики.Добавить("АлгоритмПослеЗагрузкиДанных", , Истина);
		СтраницыОбработчиков = Элементы.ОбработчикиXML;
	КонецЕсли;
	КД3_УправлениеФормой.ПриСозданииНаСервере(ЭтотОбъект, Отказ, КонвертацияXDTO, "Конвертация", Обработчики, СтраницыОбработчиков);
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПриОткрытииПосле(Отказ)
	Если НЕ КонвертацияXDTO Тогда
		КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, Элементы.ОбработчикиXML.ТекущаяСтраница);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
	КД3_УправлениеФормойКлиент.ПередЗаписью(ЭтотОбъект, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПриЗакрытииПосле(ЗавершениеРаботы) Экспорт
	КД3_УправлениеФормойКлиент.ПриЗакрытии(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КД3_КонфигурацияПриИзмененииПосле(Элемент)
	
	Если КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() Тогда
		ЭтотОбъект.КД3_Конфигурация = Объект.Конфигурация;
		КД3_УправлениеФормойКлиент.ОчиститьМетаданныеКонфигурации(ЭтотОбъект, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_КонфигурацияИсточникПриИзмененииПосле(Элемент)
	
	Если КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() Тогда
		ЭтотОбъект.КД3_Конфигурация = Объект.Конфигурация;
		КД3_УправлениеФормойКлиент.ОчиститьМетаданныеКонфигурации(ЭтотОбъект, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_КонфигурацияКорреспондентПриИзмененииПосле(Элемент)
	
	Если КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() Тогда
		ЭтотОбъект.КД3_КонфигурацияКорреспондент = Объект.КонфигурацияКорреспондент;
		КД3_УправлениеФормойКлиент.ОчиститьМетаданныеКонфигурации(ЭтотОбъект, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПараметрыПриИзмененииПосле(Элемент)
	
	Если КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() Тогда
		ИзмененныйКонтекст = КД3_ЗаполнитьИзмененныйКонтекст_Параметры(ИзмененныйКонтекст);
		КД3_УправлениеФормойКлиент.ОбновитьЛокальныйКонтекст(ЭтотОбъект, КонвертацияXDTO, ИзмененныйКонтекст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_СтраницыПриСменеСтраницыПосле(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.СтраницаКонвертацияXDTO Тогда
		КД3_ГруппаДанныеКонвертацииXDTOПриСменеСтраницыПосле(, Элементы.ГруппаДанныеКонвертацииXDTO.ТекущаяСтраница)
	ИначеЕсли ТекущаяСтраница = Элементы.СтраницаКонвертацияXML Тогда
		КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, Элементы.ОбработчикиXML.ТекущаяСтраница);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ГруппаДанныеКонвертацииXDTOПриСменеСтраницыПосле(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.СтраницаОбработчики Тогда
		КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, Элементы.СтраницыОбработчики.ТекущаяСтраница);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_СтраницыОбработчикиПриСменеСтраницыПосле(Элемент, ТекущаяСтраница)
	КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, ТекущаяСтраница);
КонецПроцедуры

&НаКлиенте
Процедура КД3_ОбработчикиXMLПриСменеСтраницыПосле(Элемент, ТекущаяСтраница)
	КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, ТекущаяСтраница);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаКлиенте
Функция КД3_ЗаполнитьИзмененныйКонтекст_Параметры(ИзмененныйКонтекст)
	ИзмененныйКонтекст = Новый Структура;
	ИзмененныйКонтекст.Вставить("ПараметрыКонвертации", Новый СписокЗначений);
	Для Каждого СтрокаПараметра Из Объект.Параметры Цикл
		ИзмененныйКонтекст.ПараметрыКонвертации.Добавить(СтрокаПараметра.Параметр,, СтрокаПараметра.ИспользуетсяПриЗагрузке И НЕ СтрокаПараметра.ПередаватьПараметрПриВыгрузке);
	КонецЦикла;
	Возврат ИзмененныйКонтекст;
КонецФункции

#КонецОбласти