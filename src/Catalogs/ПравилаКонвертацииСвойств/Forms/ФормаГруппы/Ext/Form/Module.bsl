
#Область ПодключаемыеОбработчики

&НаКлиенте
Процедура КД3_Подключаемый_ГруппаXMLПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если ТекущаяСтраница = Элементы.Обработчики Тогда
		КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, Элементы.СтраницыОбработчики.ТекущаяСтраница);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ОбработчикиПриСменеСтраницы(Элемент, ТекущаяСтраница)
	КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, ТекущаяСтраница);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ПриИзмененииКонфигурации(Элемент)
	КД3_УправлениеФормойКлиент.ОчиститьМетаданныеКонфигурации(ЭтотОбъект, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ДокументСформирован(Элемент)
	КД3_УправлениеФормойКлиент.ИнициализацияРедактора(ЭтотОбъект, Элемент.Имя, Объект);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_HTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	КД3_УправлениеФормойКлиент.ОбработатьСобытиеHTML(ЭтотОбъект, Элемент, ДанныеСобытия);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_КомандаРедактора(Команда)
	КД3_УправлениеФормойКлиент.КомандаРедактора(ЭтотОбъект, Команда);
КонецПроцедуры

#КонецОбласти

#Область ЗаменяемыеОбработчики

&НаСервере
Процедура КД3_Подключаемый_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	ПриСозданииНаСервере(Отказ, СтандартнаяОбработка); // Типовой
	
	Если НЕ ЭтоКонвертацияXDTO Тогда
		Обработчики = Новый СписокЗначений;
		
		Обработчики.Добавить("АлгоритмПередОбработкойСвойства");
		Обработчики.Добавить("АлгоритмПослеОбработкиСвойства");
		
		Обработчики.Добавить("АлгоритмПередВыгрузкойСвойства");
		Обработчики.Добавить("АлгоритмПриВыгрузкеСвойства");
		Обработчики.Добавить("АлгоритмПослеВыгрузкиСвойства");
		СтраницыОбработчиков = Элементы.СтраницыОбработчики;
		КД3_УправлениеФормой.ПриСозданииНаСервере(ЭтотОбъект, Отказ, ЭтоКонвертацияXDTO, Обработчики, СтраницыОбработчиков);
		
		УстановитьДействие("ПриОткрытии", "КД3_Подключаемый_ПриОткрытииПосле");
		УстановитьДействие("ПередЗаписью", "КД3_Подключаемый_ПередЗаписьюПосле");
		УстановитьДействие("ОбработкаОповещения", "КД3_Подключаемый_ОбработкаОповещенияПосле");
		Элементы.ГруппаXML.УстановитьДействие("ПриСменеСтраницы", "КД3_Подключаемый_ГруппаXMLПриСменеСтраницы");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ПриОткрытииПосле(Отказ)
	ПриОткрытии(Отказ); // Типовой
	КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, Элементы.СтраницыОбработчики.ТекущаяСтраница);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
	
	КД3_УправлениеФормойКлиент.ПередЗаписью(ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ОбработкаОповещенияПосле(ИмяСобытия, Параметр, Источник)
	КД3_УправлениеФормойКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр);
КонецПроцедуры

#Если Сервер Тогда
	Если КД3_Настройки.ИспользоватьРедакторКода() Тогда
		УстановитьДействие("ПриСозданииНаСервере", "КД3_Подключаемый_ПриСозданииНаСервереПосле");
	КонецЕсли;
#КонецЕсли

#КонецОбласти
