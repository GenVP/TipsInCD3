
&НаКлиенте
&После("ВыбранаКонвертация")
Процедура КД3_ВыбранаКонвертация(РезультатЗакрытия, ДополнительныеПараметры)
	// Обработка включения в состав конвертаций
	КД3_УправлениеФормойКлиент.ПриИзмененииКонвертаций(ЭтотОбъект, Истина);
КонецПроцедуры

&НаКлиенте
&После("ВыбранОбъектВыборки")
Процедура КД3_ВыбранОбъектВыборки(РезультатЗакрытия, ДополнительныеПараметры)
	КД3_УправлениеФормойКлиент.ПриИзмененииОбъектаКонфигурации(ЭтотОбъект, Объект.ОбъектВыборки, Истина, Ложь);
КонецПроцедуры

#Область ПодключаемыеОбработчики
&НаКлиенте
Процедура КД3_Подключаемый_СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если ТекущаяСтраница = Элементы.ОбработчикиСобытийXDTO Тогда
		КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, Элементы.СтраницыОбработчикиXDTO.ТекущаяСтраница);
	ИначеЕсли ТекущаяСтраница = Элементы.ОбработчикиСобытийXML Тогда
		КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, Элементы.СтраницыОбработчикиXML.ТекущаяСтраница);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ОбработчикиПриСменеСтраницы(Элемент, ТекущаяСтраница)
	КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, ТекущаяСтраница);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ПриИзмененииКонфигурации(Элемент)
	КД3_УправлениеФормойКлиент.ОчиститьМетаданныеКонфигурации(ЭтотОбъект, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ДокументСформирован(Элемент)
	КД3_УправлениеФормойКлиент.ИнициализацияРедактора(ЭтотОбъект, Элемент.Имя, Объект);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ИнициализацияМетаданных() Экспорт
	КД3_УправлениеФормойКлиент.ИнициализацияМетаданных(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_HTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	КД3_УправлениеФормойКлиент.ОбработатьСобытиеHTML(ЭтотОбъект, Элемент, ДанныеСобытия);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_КомандаРедактора(Команда)
	КД3_УправлениеФормойКлиент.КомандаРедактора(ЭтотОбъект, Команда);
КонецПроцедуры

#КонецОбласти

#Область ЗаменяемыеОбработчики

&НаСервере
Процедура КД3_Подключаемый_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	ПриСозданииНаСервере(Отказ, СтандартнаяОбработка); // Типовой
	
	Обработчики = Новый СписокЗначений;
	Если КонвертацияXDTO Тогда
		Обработчики.Добавить("АлгоритмПриОбработке");
		Обработчики.Добавить("АлгоритмВыборкаДанных");
		СтраницыОбработчиков = Элементы.СтраницыОбработчикиXDTO;
	Иначе
		Обработчики.Добавить("АлгоритмПередОбработкойПравила");
		Обработчики.Добавить("АлгоритмПередВыгрузкойОбъекта");
		Обработчики.Добавить("АлгоритмПослеВыгрузкиОбъекта");
		Обработчики.Добавить("АлгоритмПослеОбработкиПравила");
		СтраницыОбработчиков = Элементы.СтраницыОбработчикиXML;
	КонецЕсли;
	КД3_УправлениеФормой.ПриСозданииНаСервере(ЭтотОбъект, Отказ, Истина, Обработчики, СтраницыОбработчиков);
	
	УстановитьДействие("ПередЗаписью", "КД3_Подключаемый_ПередЗаписьюПосле");
	УстановитьДействие("ОбработкаВыбора", "КД3_Подключаемый_ОбработкаВыбора");
	Элементы.Страницы.УстановитьДействие("ПриСменеСтраницы", "КД3_Подключаемый_СтраницыПриСменеСтраницы");
	Элементы.ОбъектВыборки.УстановитьДействие("ПриИзменении", "КД3_Подключаемый_ОбъектВыборкиПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
	КД3_УправлениеФормойКлиент.ПередЗаписью(ЭтотОбъект, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора); // Типовой
	// Обработка исключения из состава конвертаций
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СписокЗначений") Тогда
		КД3_УправлениеФормойКлиент.ПриИзмененииКонвертаций(ЭтотОбъект, Истина);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ОбъектВыборкиПриИзменении(Элемент)
	КД3_УправлениеФормойКлиент.ПриИзмененииОбъектаКонфигурации(ЭтотОбъект, Объект.ОбъектВыборки, Истина, Ложь);
КонецПроцедуры

#Если Сервер Тогда
КД3_УправлениеФормой.ПриПервомОткрытииФормы(ЭтотОбъект);
#КонецЕсли

#КонецОбласти
