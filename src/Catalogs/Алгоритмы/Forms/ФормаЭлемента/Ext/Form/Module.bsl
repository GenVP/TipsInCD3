&НаСервере
&После("ОбработкаВыбораНаСервере")
Процедура КД3_ОбработкаВыбораНаСервере()
	КД3_УправлениеФормой.ПриИзмененииКонвертаций(ЭтотОбъект, ЭтоКонвертацияXDTO);
КонецПроцедуры

&НаСервере
&После("ДополнитьСоставКонвертации")
Процедура КД3_ДополнитьСоставКонвертации(Конвертация, ЭлементКонвертации, ОтказДобавления)
	Если НЕ ОтказДобавления Тогда
		КД3_УправлениеФормой.ПриИзмененииКонвертаций(ЭтотОбъект, ЭтоКонвертацияXDTO);
	КонецЕсли;
КонецПроцедуры

#Область ПодключаемыеОбработчики

&НаКлиенте
Процедура КД3_Подключаемый_ПриИзмененииКонфигурации(Элемент)
	КД3_УправлениеФормойКлиент.ОчиститьМетаданныеКонфигурации(ЭтотОбъект, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ПриИзмененииКонфигурацииКорреспондента(Элемент)
	КД3_УправлениеФормойКлиент.ОчиститьМетаданныеКонфигурации(ЭтотОбъект, Истина);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ДокументСформирован(Элемент)
	КД3_УправлениеФормойКлиент.ИнициализацияРедактора(ЭтотОбъект, Элемент.Имя, Объект);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_HTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	КД3_УправлениеФормойКлиент.ОбработатьСобытиеHTML(ЭтотОбъект, Элемент, ДанныеСобытия);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_КомандаРедактора(Команда)
	КД3_УправлениеФормойКлиент.КомандаРедактора(ЭтотОбъект, Команда);
КонецПроцедуры

#КонецОбласти

#Область ЗаменяемыеОбработчики

&НаСервере
Процедура КД3_Подключаемый_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	ПриСозданииНаСервере(Отказ, СтандартнаяОбработка); // Типовой
	
	Обработчики = Новый СписокЗначений;
	Обработчики.Добавить("Алгоритм", , Объект.ИспользуетсяПриЗагрузке);
	
	КД3_УправлениеФормой.ПриСозданииНаСервере(ЭтотОбъект, Отказ, ЭтоКонвертацияXDTO, Обработчики, Элементы.Алгоритм);
	
	УстановитьДействие("ПриОткрытии", "КД3_Подключаемый_ПриОткрытииПосле");
	УстановитьДействие("ПередЗаписью", "КД3_Подключаемый_ПередЗаписьюПосле");
	УстановитьДействие("ОбработкаОповещения", "КД3_Подключаемый_ОбработкаОповещенияПосле");
	Элементы.ИспользуетсяПриЗагрузке.УстановитьДействие("ПриИзменении", "КД3_Подключаемый_ИспользуетсяПриЗагрузкеПриИзмененииПосле");
	
	Если НЕ ЭтоКонвертацияXDTO Тогда
		Элементы.КД3_Конфигурация.Доступность = НЕ Объект.ИспользуетсяПриЗагрузке;
		Элементы.КД3_КонфигурацияКорреспондент.Доступность = Объект.ИспользуетсяПриЗагрузке;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ПриОткрытииПосле(Отказ)
	ПриОткрытии(Отказ); // Типовой
	КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, "Алгоритм");
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
	КД3_УправлениеФормойКлиент.ПередЗаписью(ЭтотОбъект, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ОбработкаОповещенияПосле(ИмяСобытия, Параметр, Источник)
	ОбработкаОповещения(ИмяСобытия, Параметр, Источник); // Типовой
	КД3_УправлениеФормойКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ИспользуетсяПриЗагрузкеПриИзмененииПосле(Элемент)
	
	ЭтотОбъект.КД3_Обработчики[0].Пометка = Объект.ИспользуетсяПриЗагрузке;
	
	Элементы.КД3_Конфигурация.Доступность = НЕ Объект.ИспользуетсяПриЗагрузке;
	Если НЕ ЭтоКонвертацияXDTO Тогда
		Элементы.КД3_КонфигурацияКорреспондент.Доступность = Объект.ИспользуетсяПриЗагрузке;
	КонецЕсли;
	
	КД3_УправлениеФормойКлиент.ОчиститьМетаданныеКонфигурации(ЭтотОбъект, Объект.ИспользуетсяПриЗагрузке);
	
КонецПроцедуры

#Если Сервер Тогда
	Если КД3_Настройки.ИспользоватьРедакторКода() Тогда
		УстановитьДействие("ПриСозданииНаСервере", "КД3_Подключаемый_ПриСозданииНаСервереПосле");
	КонецЕсли;
#КонецЕсли

#КонецОбласти
