
#Область ПодключаемыеОбработчики

&НаКлиенте
Процедура КД3_Подключаемый_ПриИзмененииКонфигурации(Элемент)
	КД3_УправлениеФормойКлиент.ОчиститьМетаданныеКонфигурации(ЭтотОбъект, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ПриИзмененииКонфигурацииКорреспондента(Элемент)
	КД3_УправлениеФормойКлиент.ОчиститьМетаданныеКонфигурации(ЭтотОбъект, Истина);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ДокументСформирован(Элемент)
	КД3_УправлениеФормойКлиент.ИнициализацияРедактора(ЭтотОбъект, Элемент.Имя, Объект);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ИнициализацияМетаданных() Экспорт
	ИзмененныйКонтекст = Новый Структура("Параметры", Объект.Параметры);
	КД3_УправлениеФормойКлиент.ИнициализацияМетаданных(ЭтотОбъект, ЭтоКонвертацияXDTO, ИзмененныйКонтекст);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_HTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	КД3_УправлениеФормойКлиент.ОбработатьСобытиеHTML(ЭтотОбъект, Элемент, ДанныеСобытия);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_КомандаРедактора(Команда)
	КД3_УправлениеФормойКлиент.КомандаРедактора(ЭтотОбъект, Команда);
КонецПроцедуры

#КонецОбласти

#Область ЗаменяемыеОбработчики

&НаСервере
Процедура КД3_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Если НЕ КД3_Настройки.ИспользоватьРедакторКода() Тогда
		Возврат;
	КонецЕсли;
	
	Обработчики = Новый СписокЗначений;
	Обработчики.Добавить("Алгоритм", , Объект.ИспользуетсяПриЗагрузке);
	
	КД3_УправлениеФормой.ПриСозданииНаСервере(ЭтотОбъект, Отказ, ЭтоКонвертацияXDTO, "Алгоритм", Обработчики, Элементы.Алгоритм);
	
	Если КД3_Настройки.ИспользоватьКонтекстнуюПодсказку() Тогда
		Если НЕ ЭтоКонвертацияXDTO Тогда
			Элементы.КД3_Конфигурация.Доступность = НЕ Объект.ИспользуетсяПриЗагрузке;
			Элементы.КД3_КонфигурацияКорреспондент.Доступность = Объект.ИспользуетсяПриЗагрузке;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПриОткрытииПосле(Отказ)
	КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, "Алгоритм");
КонецПроцедуры

&НаКлиенте
Процедура КД3_ОбработкаОповещенияПосле(ИмяСобытия, Параметр, Источник)
	КД3_УправлениеФормойКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
	КД3_УправлениеФормойКлиент.ПередЗаписью(ЭтотОбъект, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПриЗакрытииПосле(ЗавершениеРаботы) Экспорт
	КД3_УправлениеФормойКлиент.ПриЗакрытии(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КД3_ОбработкаВыбораПосле(ВыбранноеЗначение, ИсточникВыбора)
	
	Если КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() Тогда
		// Обработка исключения из состава конвертаций
		Если ТипЗнч(ВыбранноеЗначение) = Тип("СписокЗначений") Тогда
			КД3_УправлениеФормойКлиент.ПриИзмененииКонвертаций(ЭтотОбъект, ЭтоКонвертацияXDTO);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ИспользуетсяПриЗагрузкеПриИзмененииПосле(Элемент)
	
	Если КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() Тогда
		ЭтотОбъект.КД3_Обработчики[0].Пометка = Объект.ИспользуетсяПриЗагрузке;
		
		Элементы.КД3_Конфигурация.Доступность = НЕ Объект.ИспользуетсяПриЗагрузке;
		Если НЕ ЭтоКонвертацияXDTO Тогда
			Элементы.КД3_КонфигурацияКорреспондент.Доступность = Объект.ИспользуетсяПриЗагрузке;
		КонецЕсли;
		
		КД3_УправлениеФормойКлиент.ОчиститьМетаданныеКонфигурации(ЭтотОбъект, Объект.ИспользуетсяПриЗагрузке);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПараметрыПриИзмененииПосле(Элемент)
	
	Если КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() Тогда
		ИзмененныйКонтекст = Новый Структура("Параметры", Объект.Параметры);
		КД3_УправлениеФормойКлиент.ОбновитьЛокальныйКонтекст(ЭтотОбъект, Ложь, ИзмененныйКонтекст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
&После("ВыбранаКонвертация")
Процедура КД3_ВыбранаКонвертация(РезультатЗакрытия, ДополнительныеПараметры)
	
	Если КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() Тогда
		// Обработка включения в состав конвертаций
		КД3_УправлениеФормойКлиент.ПриИзмененииКонвертаций(ЭтотОбъект, ЭтоКонвертацияXDTO);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

