
#Область ПодключаемыеОбработчики

&НаКлиенте
Процедура КД3_Подключаемый_ПриИзмененииКонфигурации(Элемент)
	КД3_УправлениеФормойКлиент.ОчиститьМетаданныеКонфигурации(ЭтотОбъект, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ПриИзмененииКонфигурацииКорреспондента(Элемент)
	КД3_УправлениеФормойКлиент.ОчиститьМетаданныеКонфигурации(ЭтотОбъект, Истина);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ДокументСформирован(Элемент)
	КД3_УправлениеФормойКлиент.ИнициализацияРедактора(ЭтотОбъект, Элемент.Имя, Объект, Элемент.ТолькоПросмотр);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ИнициализацияМетаданных() Экспорт
	ИзмененныйКонтекст = Новый Структура("ОбъектКонфигурации", Объект.ОбъектКонфигурации);
	КД3_УправлениеФормойКлиент.ИнициализацияМетаданных(ЭтотОбъект, ЭтоКонвертацияXDTO, ИзмененныйКонтекст);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_HTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	КД3_УправлениеФормойКлиент.ОбработатьСобытиеHTML(ЭтотОбъект, Элемент, ДанныеСобытия);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_КомандаРедактора(Команда)
	КД3_УправлениеФормойКлиент.КомандаРедактора(ЭтотОбъект, Команда);
КонецПроцедуры

#КонецОбласти

#Область ЗаменяемыеОбработчики

&НаСервере
Процедура КД3_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	Если НЕ КД3_Настройки.ИспользоватьРедакторКода() Тогда
		Возврат;
	КонецЕсли;
	
	Обработчики = Новый СписокЗначений;
	
	Если ЭтоКонвертацияXDTO Тогда
		Обработчики.Добавить("АлгоритмПриОтправкеДанных");
		Обработчики.Добавить("АлгоритмПередПолучениемДанных");
		Обработчики.Добавить("АлгоритмПриПолученииДанных");
		Обработчики.Добавить("АлгоритмПослеЗагрузкиВсехДанныхТекст");
		СтраницыОбработчиков = Элементы.ОбработчикиXDTO;
	Иначе
		Обработчики.Добавить("АлгоритмПередВыгрузкойОбъекта");
		Обработчики.Добавить("АлгоритмПриВыгрузкеОбъекта");
		Обработчики.Добавить("АлгоритмПослеВыгрузкиОбъекта");
		Обработчики.Добавить("АлгоритмПослеВыгрузкиОбъектаВФайлОбмена");
		Обработчики.Добавить("АлгоритмПоследовательностьПолейПоиска", , Истина);
		Обработчики.Добавить("АлгоритмПередЗагрузкойОбъекта", , Истина);
		Обработчики.Добавить("АлгоритмПриЗагрузкеОбъекта", , Истина);
		Обработчики.Добавить("АлгоритмПослеЗагрузкиОбъекта", , Истина);
		СтраницыОбработчиков = Элементы.ОбработчикиXML;
	КонецЕсли;
	КД3_УправлениеФормой.ПриСозданииНаСервере(ЭтотОбъект, Отказ, ЭтоКонвертацияXDTO, "ПКО", Обработчики, СтраницыОбработчиков);
	
	Если ЭтоКонвертацияXDTO И НЕ Объект.ИспользоватьДляОтправки Тогда
		// Исправление текущей страницы на видимую при этих условиях
		Элементы.ОбработчикиXDTO.ТекущаяСтраница = Элементы.Страница_ПриКонвертацииДанныхXDTO;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
	КД3_УправлениеФормойКлиент.ПередЗаписью(ЭтотОбъект, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПриЗакрытииПосле(ЗавершениеРаботы) Экспорт
	КД3_УправлениеФормойКлиент.ПриЗакрытии(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура КД3_ОбработкаОповещенияПосле(ИмяСобытия, Параметр, Источник)
	КД3_УправлениеФормойКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
КонецПроцедуры

&НаКлиенте
Процедура КД3_ОбработкаВыбораПосле(ВыбранноеЗначение, ИсточникВыбора)
	
	Если КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() Тогда
		// Обработка исключения из состава конвертаций
		Если ТипЗнч(ВыбранноеЗначение) = Тип("СписокЗначений") Тогда
			КД3_УправлениеФормойКлиент.ПриИзмененииКонвертаций(ЭтотОбъект, ЭтоКонвертацияXDTO);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_АлгоритмПослеЗагрузкиВсехДанныхПриИзмененииВместо(Элемент)
	
	ВидимостьДоИзменения = Элементы.КД3_АлгоритмПослеЗагрузкиВсехДанныхТекст.Видимость;
	
	ПродолжитьВызов(Элемент); //Типовой
	
	Если КД3_НастройкиКлиент.ИспользоватьРедакторКода() Тогда
		Если ВидимостьДоИзменения Тогда
			КД3_УправлениеФормойКлиент.ПриИзмененииТолькоПросмотрФормы(ЭтотОбъект, "АлгоритмПослеЗагрузкиВсехДанныхТекст");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_СтраницыПриСменеСтраницыПосле(Элемент, ТекущаяСтраница)
	
	Если ТекущаяСтраница = Элементы.Страница_ОбработчикиXDTO Тогда
		КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, Элементы.ОбработчикиXDTO.ТекущаяСтраница);
	ИначеЕсли ТекущаяСтраница = Элементы.Страница_ОбработчикиXML Тогда
		КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, Элементы.ОбработчикиXML.ТекущаяСтраница);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ОбработчикиXMLПриСменеСтраницыПосле(Элемент, ТекущаяСтраница)
	КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, ТекущаяСтраница);
КонецПроцедуры

&НаКлиенте
Процедура КД3_ОбработчикиXDTOПриСменеСтраницыПосле(Элемент, ТекущаяСтраница)
	КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, ТекущаяСтраница);
КонецПроцедуры

&НаКлиенте
Процедура КД3_ОбъектКонфигурацииПриИзмененииПосле(Элемент)
	
	Если КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() Тогда
		КД3_УправлениеФормойКлиент.ПриИзмененииОбъектаКонфигурации(ЭтотОбъект, Объект.ОбъектКонфигурации, ЭтоКонвертацияXDTO, Ложь);
		ИзмененныйКонтекст = Новый Структура("ОбъектКонфигурации", Объект.ОбъектКонфигурации);
		КД3_УправлениеФормойКлиент.ОбновитьЛокальныйКонтекст(ЭтотОбъект, ЭтоКонвертацияXDTO, ИзмененныйКонтекст);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ОбъектКонфигурацииКорреспондентПриИзмененииПосле(Элемент)
	
	Если КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() Тогда
		КД3_УправлениеФормойКлиент.ПриИзмененииОбъектаКонфигурации(ЭтотОбъект, Объект.ОбъектКонфигурацииКорреспондент, ЭтоКонвертацияXDTO, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
&После("ВыбранаКонвертация")
Процедура КД3_ВыбранаКонвертация(РезультатЗакрытия, ДополнительныеПараметры)
	
	Если КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() Тогда
		// Обработка включения в состав конвертаций
		КД3_УправлениеФормойКлиент.ПриИзмененииКонвертаций(ЭтотОбъект, ЭтоКонвертацияXDTO);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
&После("УстановитьВидимость")
Процедура КД3_УстановитьВидимость()
	
	Если КД3_НастройкиКлиент.ИспользоватьРедакторКода() Тогда
		Если ЭтоКонвертацияXDTO Тогда
			Элементы.АлгоритмПослеЗагрузкиВсехДанныхТекст.Видимость = Ложь;
			Элементы.КД3_АлгоритмПослеЗагрузкиВсехДанныхТекст.Видимость = ЗначениеЗаполнено(АлгоритмПослеЗагрузкиВсехДанныхСсылка)
				И ПоказатьТекстАлгоритмаПослеЗагрузкиВсехДанных;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
&После("ВыбранОбъектКонфигурации")
Процедура КД3_ВыбранОбъектКонфигурации(РезультатЗакрытия, ДополнительныеПараметры)
	
	Если КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() Тогда
		Если ДополнительныеПараметры = "ОбъектКонфигурацииКорреспондент" Тогда
			КД3_УправлениеФормойКлиент.ПриИзмененииОбъектаКонфигурации(ЭтотОбъект, Объект.ОбъектКонфигурацииКорреспондент, ЭтоКонвертацияXDTO, Истина);
		Иначе
			КД3_УправлениеФормойКлиент.ПриИзмененииОбъектаКонфигурации(ЭтотОбъект, Объект.ОбъектКонфигурации, ЭтоКонвертацияXDTO, Ложь);
			ИзмененныйКонтекст = Новый Структура("ОбъектКонфигурации", Объект.ОбъектКонфигурации);
			КД3_УправлениеФормойКлиент.ОбновитьЛокальныйКонтекст(ЭтотОбъект, ЭтоКонвертацияXDTO, ИзмененныйКонтекст);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
