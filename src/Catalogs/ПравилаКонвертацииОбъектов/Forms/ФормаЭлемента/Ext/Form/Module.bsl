
&НаКлиенте
&После("ВыбранаКонвертация")
Процедура КД3_ВыбранаКонвертация(РезультатЗакрытия, ДополнительныеПараметры)
	// Обработка включения в состав конвертаций
	КД3_УправлениеФормойКлиент.ПриИзмененииКонвертаций(ЭтотОбъект, ЭтоКонвертацияXDTO);
КонецПроцедуры

&НаКлиенте
&После("УстановитьВидимость")
Процедура КД3_УстановитьВидимость()
	Если НЕ КД3_НастройкиКлиент.ИспользоватьРедакторКода() Тогда
		Возврат;
	КонецЕсли;
	Если ЭтоКонвертацияXDTO Тогда
		Элементы.АлгоритмПослеЗагрузкиВсехДанныхТекст.Видимость = Ложь;
		Элементы.КД3_АлгоритмПослеЗагрузкиВсехДанныхТекст.Видимость = ЗначениеЗаполнено(АлгоритмПослеЗагрузкиВсехДанныхСсылка)
			И ПоказатьТекстАлгоритмаПослеЗагрузкиВсехДанных;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
&После("ВыбранОбъектКонфигурации")
Процедура КД3_ВыбранОбъектКонфигурации(РезультатЗакрытия, ДополнительныеПараметры)
	Если ДополнительныеПараметры = "ОбъектКонфигурацииКорреспондент" Тогда
		КД3_УправлениеФормойКлиент.ПриИзмененииОбъектаКонфигурации(ЭтотОбъект, Объект.ОбъектКонфигурацииКорреспондент, ЭтоКонвертацияXDTO, Истина);
	Иначе
		КД3_УправлениеФормойКлиент.ПриИзмененииОбъектаКонфигурации(ЭтотОбъект, Объект.ОбъектКонфигурации, ЭтоКонвертацияXDTO, Ложь);
	КонецЕсли;
КонецПроцедуры

#Область ПодключаемыеОбработчики

&НаКлиенте
Процедура КД3_Подключаемый_СтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если ТекущаяСтраница = Элементы.Страница_ОбработчикиXDTO Тогда
		КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, Элементы.ОбработчикиXDTO.ТекущаяСтраница);
	ИначеЕсли ТекущаяСтраница = Элементы.Страница_ОбработчикиXML Тогда
		КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, Элементы.ОбработчикиXML.ТекущаяСтраница);
	КонецЕсли;
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура КД3_Подключаемый_ОбработчикиПриСменеСтраницы(Элемент, ТекущаяСтраница)
	КД3_УправлениеФормойКлиент.ПриСменеСтраницы(ЭтотОбъект, ТекущаяСтраница);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура КД3_Подключаемый_ПриИзмененииКонфигурации(Элемент)
	КД3_УправлениеФормойКлиент.ОчиститьМетаданныеКонфигурации(ЭтотОбъект, Ложь);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура КД3_Подключаемый_ПриИзмененииКонфигурацииКорреспондента(Элемент)
	КД3_УправлениеФормойКлиент.ОчиститьМетаданныеКонфигурации(ЭтотОбъект, Истина);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура КД3_Подключаемый_ДокументСформирован(Элемент)
	КД3_УправлениеФормойКлиент.ИнициализацияРедактора(ЭтотОбъект, Элемент.Имя, Объект, Элемент.ТолькоПросмотр);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ИнициализацияМетаданных() Экспорт
	КД3_УправлениеФормойКлиент.ИнициализацияМетаданных(ЭтотОбъект);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура КД3_Подключаемый_HTMLПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	КД3_УправлениеФормойКлиент.ОбработатьСобытиеHTML(ЭтотОбъект, Элемент, ДанныеСобытия);
КонецПроцедуры

//@skip-warning
&НаКлиенте
Процедура КД3_Подключаемый_КомандаРедактора(Команда)
	КД3_УправлениеФормойКлиент.КомандаРедактора(ЭтотОбъект, Команда);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ОбъектКонфигурацииПриИзменении(Элемент)
	КД3_УправлениеФормойКлиент.ПриИзмененииОбъектаКонфигурации(ЭтотОбъект, Объект.ОбъектКонфигурации, ЭтоКонвертацияXDTO, Ложь);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ОбъектКонфигурацииКорреспондентПриИзменении(Элемент)
	КД3_УправлениеФормойКлиент.ПриИзмененииОбъектаКонфигурации(ЭтотОбъект, Объект.ОбъектКонфигурацииКорреспондент, ЭтоКонвертацияXDTO, Истина);
КонецПроцедуры

#КонецОбласти

#Область ЗаменяемыеОбработчики

//@skip-warning
&НаСервере
Процедура КД3_Подключаемый_ПриСозданииНаСервереПосле(Отказ, СтандартнаяОбработка)
	
	ПриСозданииНаСервере(Отказ, СтандартнаяОбработка); //Типовой
	
	Обработчики = Новый СписокЗначений;
	
	Если ЭтоКонвертацияXDTO Тогда
		Обработчики.Добавить("АлгоритмПриОтправкеДанных");
		Обработчики.Добавить("АлгоритмПередПолучениемДанных");
		Обработчики.Добавить("АлгоритмПриПолученииДанных");
		Обработчики.Добавить("АлгоритмПослеЗагрузкиВсехДанныхТекст");
		СтраницыОбработчиков = Элементы.ОбработчикиXDTO;
	Иначе
		Обработчики.Добавить("АлгоритмПередВыгрузкойОбъекта");
		Обработчики.Добавить("АлгоритмПриВыгрузкеОбъекта");
		Обработчики.Добавить("АлгоритмПослеВыгрузкиОбъекта");
		Обработчики.Добавить("АлгоритмПослеВыгрузкиОбъектаВФайлОбмена");
		Обработчики.Добавить("АлгоритмПоследовательностьПолейПоиска", , Истина);
		Обработчики.Добавить("АлгоритмПередЗагрузкойОбъекта", , Истина);
		Обработчики.Добавить("АлгоритмПриЗагрузкеОбъекта", , Истина);
		Обработчики.Добавить("АлгоритмПослеЗагрузкиОбъекта", , Истина);
		СтраницыОбработчиков = Элементы.ОбработчикиXML;
	КонецЕсли;
	КД3_УправлениеФормой.ПриСозданииНаСервере(ЭтотОбъект, Отказ, ЭтоКонвертацияXDTO, Обработчики, СтраницыОбработчиков);
	
	УстановитьДействие("ПередЗаписью", "КД3_Подключаемый_ПередЗаписьюПосле");
	УстановитьДействие("ОбработкаВыбора", "КД3_Подключаемый_ОбработкаВыбора");
	Элементы.Страницы.УстановитьДействие("ПриСменеСтраницы", "КД3_Подключаемый_СтраницыПриСменеСтраницы");
	Элементы.АлгоритмПослеЗагрузкиВсехДанных.УстановитьДействие("ПриИзменении", "КД3_Подключаемый_АлгоритмПослеЗагрузкиВсехДанныхПриИзмененииПосле");
	Элементы.ОбъектКонфигурации.УстановитьДействие("ПриИзменении", "КД3_Подключаемый_ОбъектКонфигурацииПриИзменении");
	Элементы.ОбъектКонфигурацииКорреспондент.УстановитьДействие("ПриИзменении", "КД3_Подключаемый_ОбъектКонфигурацииКорреспондентПриИзменении");
	
	Если ЭтоКонвертацияXDTO И НЕ Объект.ИспользоватьДляОтправки Тогда
		// Исправление текущей страницы на видимую при этих условиях
		Элементы.ОбработчикиXDTO.ТекущаяСтраница = Элементы.Страница_ПриКонвертацииДанныхXDTO;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ПередЗаписьюПосле(Отказ, ПараметрыЗаписи)
	ПередЗаписью(Отказ, ПараметрыЗаписи); // Типовой
	КД3_УправлениеФормойКлиент.ПередЗаписью(ЭтотОбъект, Отказ);
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора); // Типовой
	// Обработка исключения из состава конвертаций
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СписокЗначений") Тогда
		КД3_УправлениеФормойКлиент.ПриИзмененииКонвертаций(ЭтотОбъект, ЭтоКонвертацияXDTO);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура КД3_Подключаемый_АлгоритмПослеЗагрузкиВсехДанныхПриИзмененииПосле(Элемент)
	АлгоритмПослеЗагрузкиВсехДанныхПриИзменении(Элемент); //Типовой
	КД3_УправлениеФормойКлиент.УстановитьТекстПриТолькоПросмотр(ЭтотОбъект, "АлгоритмПослеЗагрузкиВсехДанныхТекст");
КонецПроцедуры

#Если Сервер Тогда
КД3_УправлениеФормой.ПриПервомОткрытииФормы(ЭтотОбъект);
#КонецЕсли

#КонецОбласти
