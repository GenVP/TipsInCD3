#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Объект.ВариантЗагрузкиДвижений = 1;
	Если Параметры.Свойство("парКонфигурация") И ЗначениеЗаполнено(Параметры.парКонфигурация) Тогда
		Объект.Конфигурация = Параметры.ПарКонфигурация;
		СпособЗагрузки = 1;
		Если Параметры.Свойство("ИсточникДанных") Тогда
			Объект.ИсточникДанных = Параметры.ИсточникДанных;
		КонецЕсли;
		Если Параметры.Свойство("КаталогЗагрузки") Тогда
			Объект.ИмяКаталогаЗагрузки = Параметры.КаталогЗагрузки;
		КонецЕсли;
		Если Параметры.Свойство("ВариантЗагрузкиДвижений") Тогда
			Объект.ВариантЗагрузкиДвижений = Параметры.ВариантЗагрузкиДвижений;
		КонецЕсли;

		Если Параметры.Свойство("ЕстьРасширения") Тогда
			Объект.ЕстьРасширения = Параметры.ЕстьРасширения;
			Если Параметры.ЕстьРасширения И Параметры.Свойство("Расширения") Тогда
				Для Каждого ИмяКаталога Из Параметры.Расширения Цикл
					НовСтрока = Объект.Расширения.Добавить();
					НовСтрока.ИмяКаталогаЗагрузки = ИмяКаталога;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	//+КД3
	КД3_ИспользоватьКонтекстнуюПодсказку = КД3_Настройки.ИспользоватьКонтекстнуюПодсказку();
	
	ЭтоФаловаяБаза = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	Если ЭтоФаловаяБаза ИЛИ ОбщегоНазначения.РежимОтладки() Тогда
		КД3_КоличествоПотоков = 1;
		Элементы.КД3_КоличествоПотоков.Доступность = Ложь;
	КонецЕсли;
	
	Если ЭтоФаловаяБаза Тогда
		Элементы.КД3_РежимРаботы.Видимость = Ложь;
	КонецЕсли;
	
	Если КД3_ИспользоватьКонтекстнуюПодсказку Тогда
		НастройкиКонфигурации = КД3_Метаданные.НастройкиКонфигурации(
			?(СпособЗагрузки = 0, Неопределено, Объект.Конфигурация));
		КД3_МестоХраненияИндексов = НастройкиКонфигурации.МестоХраненияИндексов;
		КД3_КаталогИндексов = НастройкиКонфигурации.КаталогИндексов;
		КД3_ЗагружатьМетодыМодулей = НастройкиКонфигурации.ЗагружатьМетодыМодулей;
		
		КД3_ЗаполнитьМестоХраненияИндексов(ЭтотОбъект, НастройкиКонфигурации.ЗагружатьИзФайлов);
		КД3_ОбновитьНадписьЗагружаемыхМодулей(ЭтотОбъект, НастройкиКонфигурации.ЗагружатьИзФайлов);
	КонецЕсли;
	
	Элементы.КД3_ГруппаКонтекстнаяПодсказка.Видимость = КД3_ИспользоватьКонтекстнуюПодсказку;
	Элементы.КД3_ФормаОтменитьЗагрузку.Видимость = Ложь;
	//-КД3
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗначениеЗаполнено(Объект.Конфигурация) Тогда
		СпособЗагрузки = 1;
	КонецЕсли;
	
	УстановитьВидимость();
	//+КД3
	КД3_ИзменитьРежимОбработки();
	КД3_УстановитьВидимостьПоМестуХраненияИндексов(ЭтотОбъект);
	//-КД3
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	//+КД3
	// Если ЗавершениеРаботы = Истина и загрузка выполняется, то временные файлы удалены не будут
	КД3_ЗагрузкаПрервана = Истина;
	Если НЕ ЗавершениеРаботы И ЗначениеЗаполнено(КД3_ИдентификаторЗадания) Тогда
		КД3_ОтменитьЗагрузкуКонфигурацииНаСервере(КД3_ИдентификаторЗадания);
	КонецЕсли;
	//-КД3
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Асинх Процедура ИмяКаталогаЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяКаталогаЗагрузки = Ждать КД3_ВыбратьИсточникАсинх(Объект.ИмяКаталогаЗагрузки);
	Если ИмяКаталогаЗагрузки <> Неопределено Тогда
		Объект.ИмяКаталогаЗагрузки = ИмяКаталогаЗагрузки;
		ОбновитьОтображениеДанных();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура РасширенияИмяКаталогаЗагрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Расширения.ТекущиеДанные;
	ИмяКаталогаЗагрузки = Ждать КД3_ВыбратьИсточникАсинх(ТекущиеДанные.ИмяКаталогаЗагрузки);
	Если ИмяКаталогаЗагрузки <> Неопределено Тогда
		ТекущиеДанные.ИмяКаталогаЗагрузки = ИмяКаталогаЗагрузки;
		ОбновитьОтображениеДанных();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СпособЗагрузкиПриИзменении(Элемент)
	Если СпособЗагрузки = 0 Тогда
		Объект.ТолькоДобавлятьНовые = Ложь;
		Объект.ТолькоОбновитьПланыОбмена = Ложь;
	КонецЕсли;
	УстановитьВидимость();
	//+КД3
	КД3_ПриИзмененииКонфигурации();
	//-КД3
КонецПроцедуры

&НаКлиенте
Процедура ЕстьРасширенияПриИзменении(Элемент)
	УстановитьВидимость();
КонецПроцедуры

&НаКлиенте
Асинх Процедура КД3_КаталогИндексовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбора.Каталог = Объект.КД3_КаталогИндексов;
	Результат = Ждать ДиалогВыбора.ВыбратьАсинх();
	Если Результат <> Неопределено Тогда
		Объект.КД3_КаталогИндексов = Результат[0];
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ЗагружатьМетодыМодулейПриИзменении(Элемент)
	КД3_ОбновитьНадписьЗагружаемыхМодулей(ЭтотОбъект, КД3_СохранитьКаталогДляКонфигурации);
КонецПроцедуры

&НаКлиенте
Процедура КД3_КонфигурацияПриИзменении(Элемент)
	КД3_ПриИзмененииКонфигурации();
КонецПроцедуры

&НаКлиенте
Процедура КД3_МестоХранениеИндексовПриИзменении(Элемент)
	
	КД3_УстановитьВидимостьПоМестуХраненияИндексов(ЭтотОбъект);
	
	КД3_ОбновитьДоступностьТиповойЗагрузки();
	
	Если КД3_МестоХраненияИндексов = 0 Тогда
		КД3_ЗагружатьМетодыМодулей = Ложь;
		КД3_СохранитьКаталогДляКонфигурации = Ложь;
		НастройкиКонфигурации = КД3_МетаданныеКлиент.НастройкиКонфигурации(
			?(СпособЗагрузки = 0, Неопределено, Объект.Конфигурация));
		КД3_ЗаполнитьМестоХраненияИндексов(ЭтотОбъект, НастройкиКонфигурации.ЗагружатьИзФайлов);
		КД3_ОбновитьНадписьЗагружаемыхМодулей(ЭтотОбъект, НастройкиКонфигурации.ЗагружатьИзФайлов ИЛИ КД3_СохранитьКаталогДляКонфигурации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_КаталогИндексовОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ФайловаяСистемаКлиент.ОткрытьПроводник(КД3_КаталогИндексов);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьЗагрузку(Команда)
	Если НЕ ЗначениеЗаполнено(Объект.ИмяКаталогаЗагрузки) Тогда
		Если Объект.ИсточникДанных = 0 Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Не выбран каталог со структурой конфигурации'"));
		Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Не выбран файл проекта EDT project'"));
		КонецЕсли;
		Возврат;
	КонецЕсли;
	Если СпособЗагрузки = 1 И НЕ ЗначениеЗаполнено(Объект.Конфигурация) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Не выбрана конфигурация для загрузки'"));
		Возврат;
	КонецЕсли;
	ИзменитьДоступностьЭлементовФормы(Ложь);
	//+КД3
	ПодготовитьФайлыДляСервераНачало();
	//-КД3
КонецПроцедуры

&НаКлиенте
Процедура КД3_ОтменитьЗагрузку(Команда)
	
	Элементы.КД3_ФормаОтменитьЗагрузку.Видимость = Ложь;
	
	КД3_ЗагрузкаПрервана = Истина;
	Если ЗначениеЗаполнено(КД3_ИдентификаторЗадания) Тогда
		КД3_ОтменитьЗагрузкуКонфигурацииНаСервере(КД3_ИдентификаторЗадания);
		КД3_ИдентификаторЗадания = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_НаКлиенте(Команда)
	
	Если КД3_ЭтоСервер Тогда
		КД3_ЭтоСервер = Ложь;
		КД3_ИзменитьРежимОбработки();
	КонецЕсли;
	
	КД3_ОбновитьДоступностьТиповойЗагрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_НаСервере(Команда)
	
	Если НЕ КД3_ЭтоСервер Тогда
		КД3_ЭтоСервер = Истина;
		КД3_ИзменитьРежимОбработки();
	КонецЕсли;
	
	КД3_ОбновитьДоступностьТиповойЗагрузки();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Асинх Функция КД3_ВыбратьИсточникАсинх(ТекущийИсточник)
	
	Если Объект.ИсточникДанных = 0 Тогда
		ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
		ДиалогВыбора.Каталог = Объект.ИмяКаталогаЗагрузки;
	Иначе
		ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
		ДиалогВыбора.Заголовок = НСтр("ru='Выберите файл проекта project'");
		ДиалогВыбора.Фильтр = НСтр("ru='Файл проекта (project)|*project*|Все файлы (*.*)|*.*'");
		ДиалогВыбора.ПроверятьСуществованиеФайла = Истина;
		ДиалогВыбора.Каталог = Объект.ИмяКаталогаЗагрузки;
		ДиалогВыбора.ПолноеИмяФайла = ".project";
	КонецЕсли;
	
	Результат = Ждать ДиалогВыбора.ВыбратьАсинх();
	
	Если Результат <> Неопределено Тогда
		Результат = Результат[0];
		Если Объект.ИсточникДанных = 1 Тогда
			ФайлПроекта = Новый Файл(Результат);
			Результат = Лев(ФайлПроекта.Путь, СтрДлина(ФайлПроекта.Путь) - 1);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьДоступностьЭлементовФормы(ФлагДоступность)
	Элементы.ГруппаСтраницы.ТекущаяСтраница = ?(ФлагДоступность, Элементы.Основная, Элементы.Ожидание);
	Элементы.ФормаВыполнитьЗагрузку.Доступность = ФлагДоступность;
	Элементы.КД3_РежимРаботы.Доступность = ФлагДоступность;
	Элементы.КД3_ФормаОтменитьЗагрузку.Видимость = НЕ ФлагДоступность;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимость()
	ЗагружатьВКонфигурацию = (СпособЗагрузки = 1);
	Элементы.Конфигурация.Видимость = ЗагружатьВКонфигурацию;
	Элементы.ТолькоДобавлятьНовые.Видимость = ЗагружатьВКонфигурацию;
	Элементы.Расширения.Видимость = Объект.ЕстьРасширения;
	Элементы.ТолькоОбновитьПланыОбмена.Видимость = ЗагружатьВКонфигурацию;
КонецПроцедуры

&НаКлиенте
Процедура КД3_ИзменитьРежимОбработки()
	
	КоманднаяПанель.ПодчиненныеЭлементы.КД3_РежимРаботы.ПодчиненныеЭлементы.ФормаНаКлиенте.Пометка = НЕ КД3_ЭтоСервер;
	КоманднаяПанель.ПодчиненныеЭлементы.КД3_РежимРаботы.ПодчиненныеЭлементы.ФормаНаСервере.Пометка = КД3_ЭтоСервер;
	
	КоманднаяПанель.ПодчиненныеЭлементы.КД3_РежимРаботы.Заголовок = 
	?(КД3_ЭтоСервер, НСтр("ru = 'Режим работы (на сервере)'"), НСтр("ru = 'Режим работы (на клиенте)'"));
	
	Если Элементы.КД3_КоличествоПотоков.Доступность Тогда
		КД3_КоличествоПотоков = ?(КД3_ЭтоСервер, КД3_НастройкиКлиент.ЗначениеИзКэша("КоличествоПотоков"), 1);
	КонецЕсли;
	Элементы.КД3_КоличествоПотоков.Видимость = Истина;
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ПодготовитьФайлыДляСервераНачало()
	
	ПараметрыЗагрузки = Новый Структура;
	ПараметрыЗагрузки.Вставить("ИсточникДанных", Объект.ИсточникДанных);
	ПараметрыЗагрузки.Вставить("КаталогЗагрузки", Объект.ИмяКаталогаЗагрузки);
	ПараметрыЗагрузки.Вставить("ТолькоОбновитьПланыОбмена", Объект.ТолькоОбновитьПланыОбмена);
	ПараметрыЗагрузки.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	
	ПараметрыЗагрузки.Вставить("КаталогиРасширений", Новый Массив);
	Если Объект.ЕстьРасширения Тогда
		Для Каждого СтрокаРасширения Из Объект.Расширения Цикл
			Если НЕ ПустаяСтрока(СтрокаРасширения.ИмяКаталогаЗагрузки) Тогда
				ПараметрыЗагрузки.КаталогиРасширений.Добавить(СтрокаРасширения.ИмяКаталогаЗагрузки);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	ПараметрыЗагрузки.Вставить("ЕстьРасширения", ПараметрыЗагрузки.КаталогиРасширений.Количество() > 0);
	
	УстановитьВидимость();
	
	//+КД3
	КД3_ЗагрузкаПрервана = Ложь;
	КД3_КаталогВременныхФайлов = КД3_НовыйКаталогВременныхФайловНаСервере();
	
	КД3_ПрогрессПроцент = 0;
	КД3_ПрогрессТекст = "Получение файлов конфигурации";
	
	ПараметрыЗагрузки.Вставить("КД3_КаталогВременныхФайлов", КД3_КаталогВременныхФайлов);
	ПараметрыЗагрузки.Вставить("КД3_ЗагружатьМетодыМодулей", КД3_ЗагружатьМетодыМодулей);
	ПараметрыЗагрузки.Вставить("КД3_ИспользоватьКонтекстнуюПодсказку", КД3_ИспользоватьКонтекстнуюПодсказку);
	ПараметрыЗагрузки.Вставить("КД3_ЭтоСервер", КД3_ЭтоСервер);
	
	Если НЕ КД3_ЭтоСервер Тогда
		Если НЕ Ждать КД3_ПодготовитьФайлыДляСервераАсинх(ПараметрыЗагрузки) Тогда
			КД3_ЗавершениеЗагрузкиКонфигурации();
			Возврат;
		КонецЕсли;
	КонецЕсли;
	//-КД3
	
	НачалоЗагрузкиКонфигурации(ПараметрыЗагрузки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция КД3_НовыйКаталогВременныхФайловНаСервере()
	
	ВременныйКаталогКонфигурации = "_conf_" + (Новый УникальныйИдентификатор);
	Возврат ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ФайловаяСистема.ОбщийКаталогВременныхФайлов(ВременныйКаталогКонфигурации));
	
КонецФункции

&НаКлиенте
Процедура НачалоЗагрузкиКонфигурации(ПараметрыЗагрузки)
	//+КД3
	Элементы.КД3_ПрогрессПроцент.МаксимальноеЗначение = 100;
	КД3_ПрогрессПроцент = 0;
	КД3_ПрогрессТекст = НСтр("ru='Чтение данных конфигурации'");
	
	Результат = НачатьЗагрузкуСервер(ПараметрыЗагрузки);
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ОкончаниеЗагрузкиКонфигурации", ЭтотОбъект);

	Если Результат.Статус <> "Выполняется" Тогда
		ВыполнитьОбработкуОповещения(ОповещениеОЗавершении, Результат);
		Возврат;
	КонецЕсли;
	
	КД3_ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ПараметрыОжидания.ВыводитьОкноОжидания = Ложь;
	ПараметрыОжидания.Интервал = 5; // Фиксированный, а не прогрессирующий интервал ожидания
	ПараметрыОжидания.ОповещениеОПрогрессеВыполнения = Новый ОписаниеОповещения("ОбработчикОжиданияДлительнойОперации", ЭтотОбъект);
	
	ДлительныеОперацииКлиент.ОжидатьЗавершение(Результат, ОповещениеОЗавершении, ПараметрыОжидания);
	//+КД3
КонецПроцедуры

&НаСервере
Функция НачатьЗагрузкуСервер(СтруктураПараметров)
	
	ПараметрыЗагрузки = Новый Структура;
	
	Для Каждого КлючИЗначение Из СтруктураПараметров Цикл
		Если КД3_ТиповаяЗагрузка И СтрНачинаетсяС(КлючИЗначение.Ключ, "КД3_") Тогда
			Продолжить; // Параметры КД3 не переносятся
		КонецЕсли;
		ПараметрыЗагрузки.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
	КонецЦикла;
	
	ПараметрыЗагрузки.Вставить("СпособЗагрузки", СпособЗагрузки);
	ПараметрыЗагрузки.Вставить("Конфигурация", Объект.Конфигурация);
	ПараметрыЗагрузки.Вставить("ВариантЗагрузкиДвижений", Объект.ВариантЗагрузкиДвижений);
	ПараметрыЗагрузки.Вставить("ТолькоДобавлятьНовые", Объект.ТолькоДобавлятьНовые);
	
	ПараметрыЗагрузки.Вставить("КД3_ЭтоСервер", КД3_ЭтоСервер);
	ПараметрыЗагрузки.Вставить("КД3_ТиповаяЗагрузка", КД3_ТиповаяЗагрузка);
	ПараметрыЗагрузки.Вставить("КД3_КаталогВременныхФайлов", КД3_КаталогВременныхФайлов);
	
	//+КД3
	Если НЕ КД3_ТиповаяЗагрузка Тогда
		ПараметрыЗагрузки.Вставить("КД3_УникальныйИдентификаторФормы", УникальныйИдентификатор);
		ПараметрыЗагрузки.Вставить("КД3_МестоХраненияИндексов", КД3_МестоХраненияИндексов);
		ПараметрыЗагрузки.Вставить("КД3_КаталогИндексов", КД3_КаталогИндексов);
		ПараметрыЗагрузки.Вставить("КД3_СохранитьКаталогДляКонфигурации", КД3_СохранитьКаталогДляКонфигурации);
		ПараметрыЗагрузки.Вставить("КД3_КоличествоПотоков", КД3_КоличествоПотоков);
		
		КД3_ЗагрузкаМетаданных.НужноСообщатьПрогресс(ПараметрыЗагрузки, НЕ ОбщегоНазначения.РежимОтладки());
	КонецЕсли;
	
	ПараметрыФоновогоЗадания = ДлительныеОперации.ПараметрыВыполненияПроцедуры();
	ПараметрыФоновогоЗадания.НаименованиеФоновогоЗадания = НСтр("ru = 'Загрузка структуры метаданных из каталога (КД3)'");
	ПараметрыФоновогоЗадания.ЗапуститьВФоне = Истина;
	ПараметрыФоновогоЗадания.ПрерватьВыполнениеЕслиОшибка = Истина;
	ПараметрыФоновогоЗадания.Вставить("АдресРезультата", ПоместитьВоВременноеХранилище(Ложь, УникальныйИдентификатор));
	
	Если КД3_ТиповаяЗагрузка Тогда
		ИмяМетода = "Обработки.КД3_ЗагрузкаСтруктурыКонфигурацииИзФайловXMLМногопоточно.ВыполнитьЗагрузкуМетаданныхТиповая";
	Иначе
		Имяметода = "Обработки.КД3_ЗагрузкаСтруктурыКонфигурацииИзФайловXMLМногопоточно.ВыполнитьЗагрузкуМетаданных";
	КонецЕсли;
	Возврат ДлительныеОперации.ВыполнитьПроцедуру(ПараметрыФоновогоЗадания, ИмяМетода, ПараметрыЗагрузки, ПараметрыФоновогоЗадания.АдресРезультата);
	//-КД3
КонецФункции

&НаКлиенте
Процедура ОбработчикОжиданияДлительнойОперации(ДлительнаяОперация, ДополнительныеПараметры) Экспорт
	//+КД3
	Если ДлительнаяОперация = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ДлительнаяОперация.Сообщения <> Неопределено Тогда
		Для Каждого Сообщение Из ДлительнаяОперация.Сообщения Цикл
			Сообщение.Сообщить();
		КонецЦикла;
	КонецЕсли;
	ДанныеПрогресса = ДлительнаяОперация.Прогресс;
	Если ДанныеПрогресса <> Неопределено Тогда
		КД3_ПрогрессПроцент = ДанныеПрогресса.Процент;
		КД3_ПрогрессТекст = ДанныеПрогресса.Текст;
	КонецЕсли;
	//-КД3
КонецПроцедуры

&НаКлиенте
Процедура ОкончаниеЗагрузкиКонфигурации(Результат, ДополнительныеПараметры) Экспорт
	//+КД3
	КД3_ИдентификаторЗадания = Неопределено;
	
	Если Результат = Неопределено ИЛИ Результат.Статус = "Отменено" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Загрузка структуры конфигурации отменена'"));
			
			КД3_ЗагрузкаПрервана = Истина;
			
	ИначеЕсли Результат.Статус = "Ошибка" Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'При загрузке структуры конфигурации произошла ошибка'")
				+ Символы.ПС + Результат.КраткоеПредставлениеОшибки);
			
			КД3_ЗагрузкаПрервана = Истина;
			
	ИначеЕсли Результат.Статус = "Выполнено" Тогда
		РезультатОбработки = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если РезультатОбработки.Успешно Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Загрузка структуры конфигурации выполнена успешно'"));
		Иначе
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru='Загрузка структуры конфигурации не выполнена'"));
		КонецЕсли;
		Если РезультатОбработки.Успешно Тогда
			Если СпособЗагрузки = 1 И ЗначениеЗаполнено(Объект.Конфигурация) Тогда
				// При успешной загрузке в существующую конфигурацию необходимо очистить ее локальный кэш
				КД3_МетаданныеКлиент.СброситьОписаниеМетаданныхВКэше(Объект.Конфигурация);
				КД3_МетаданныеКлиент.СброситьНастройкиКонфигурацииВКэше(Объект.Конфигурация);
			КонецЕсли;
			Если СпособЗагрузки = 0 Тогда
				Объект.Конфигурация = РезультатОбработки.Релиз;
				СпособЗагрузки = 1;
				УстановитьВидимость();
				КД3_ПриИзмененииКонфигурации();
			КонецЕсли;
			//// Изменения настроек хранения индексов для конфигурации на выбранные в форме обработки
			//Если НастройкиКонфигурации.МестоХраненияИндексов <> ОбщиеПеременные.КД3_МестоХраненияИндексов
			// ИЛИ НастройкиКонфигурации.КаталогИндексов <> ОбщиеПеременные.КД3_КаталогИндексов
			// ИЛИ НастройкиКонфигурации.ЗагружатьМетодыМодулей <> ОбщиеПеременные.КД3_ЗагружатьМетодыМодулей
			// ИЛИ (НЕ НастройкиКонфигурации.ЗагружатьИзФайлов И ОбщиеПеременные.КД3_СохранитьКаталогДляКонфигурации) Тогда
			//	ИзменитьНастройкиКонфигурации = Истина;
			//КонецЕсли;
			//Если ИзменитьНастройкиКонфигурации Тогда
			//	Если ОбщиеПеременные.КД3_СохранитьКаталогДляКонфигурации  Тогда
			//		КД3_СохранитьКаталогКонфигурации(ОбщиеПеременные, НастройкиКонфигурации);
			//		НастройкиКонфигурации.ЗагружатьИзФайлов = Истина;
			//	КонецЕсли;
			//	НастройкиКонфигурации.МестоХраненияИндексов = ОбщиеПеременные.КД3_МестоХраненияИндексов;
			//	НастройкиКонфигурации.КаталогИндексов = ОбщиеПеременные.КД3_КаталогИндексов;
			//	НастройкиКонфигурации.ЗагружатьМетодыМодулей = ОбщиеПеременные.КД3_ЗагружатьМетодыМодулей;
			//	КД3_Метаданные.ИзменитьНастройкиКонфигурации(ОбщиеПеременные.Конфигурация, НастройкиКонфигурации);
			//КонецЕсли;
		КонецЕсли;
		
		КД3_КаталогВременныхФайлов = ""; // Каталог удален в фоновом задании
	КонецЕсли;
	
	КД3_ЗавершениеЗагрузкиКонфигурации();
	//-КД3
КонецПроцедуры

&НаСервереБезКонтекста
Процедура КД3_ОтменитьЗагрузкуКонфигурацииНаСервере(ИдентификаторЗадания)
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ЗавершениеЗагрузкиКонфигурации()
	
	Если КД3_ЗагрузкаПрервана Тогда
		КД3_ЗавершениеЗагрузкиКонфигурацииНаСервере();
		КД3_ЗагрузкаПрервана = Ложь;
	КонецЕсли;
	
	КД3_КаталогВременныхФайлов = "";
	ИзменитьДоступностьЭлементовФормы(Истина);
	
КонецПроцедуры

&НаСервере
Процедура КД3_ЗавершениеЗагрузкиКонфигурацииНаСервере()
	
	// Отмена служебных фоновых заданий зарузки данных обектов
	ОтборЗаданий = Обработки.КД3_ЗагрузкаСтруктурыКонфигурацииИзФайловXMLМногопоточно.КД_ПараметрыСлужебныхФоновыхЗаданий(УникальныйИдентификатор);
	ОтборЗаданий.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	Задания = ФоновыеЗадания.ПолучитьФоновыеЗадания(ОтборЗаданий);
	Для Каждого Задание Из Задания Цикл
		Задание.Отменить();
	КонецЦикла;
	
	Если НЕ ПустаяСтрока(КД3_КаталогВременныхФайлов) Тогда
		Попытка
			УдалитьФайлы(КД3_КаталогВременныхФайлов);
		Исключение
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Не удалось удалить временные файлы конфигурации!'") + Символы.ПС + КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура КД3_ОбновитьНадписьЗагружаемыхМодулей(Форма, ЗагружатьИзФайлов)
	ЭлементНадпись = Форма.Элементы.КД3_ИнфоЗагружаемыеМодули;
	ЭлементНадпись.Видимость = Форма.КД3_ЗагружатьМетодыМодулей;
	Если ЗагружатьИзФайлов Тогда
		ЭлементНадпись.Заголовок =
			"Для конфигурации указан каталог исходных кодов.
			|Будут загружены только процедуры/функции глобального контекста.";
	Иначе
		ЭлементНадпись.Заголовок =
			"Для конфигурации не указан каталог исходных кодов.
			|Будут загружены процедуры/функции всех модулей.";
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Асинх Функция КД3_ПодготовитьФайлыДляСервераАсинх(ПараметрыЗагрузки)
	
	ВсеНайденныеФайлы = Новый Соответствие;
	ОбщаяРасшифровка = Новый Соответствие;
	
	ПараметрыЗагрузки.Вставить("КД3_ФайлыКОбработке", Новый Соответствие);
	ПараметрыЗагрузки.Вставить("КД3_МинимальныйРазмерПорции", 10);
	
	// Поиск всех файлов к обработке
	
	ОбщееКоличествоФайлов = 0;
	
	Обещания = Новый Массив;
	
	Обещания.Добавить(КД3_НайтиПомещаемыеФайлыАсинх(ПараметрыЗагрузки, ПараметрыЗагрузки.КаталогЗагрузки, Ложь));
	Если ПараметрыЗагрузки.ЕстьРасширения Тогда
		Для Каждого ИмяКаталогаРасширения Из ПараметрыЗагрузки.КаталогиРасширений Цикл
			Обещания.Добавить(КД3_НайтиПомещаемыеФайлыАсинх(ПараметрыЗагрузки, ИмяКаталогаРасширения, Истина));
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого Обещание Из Обещания Цикл
		ОбщееКоличествоФайлов = ОбщееКоличествоФайлов + Ждать Обещание;
	КонецЦикла;
	Если КД3_ЗагрузкаПрервана Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Элементы.КД3_ПрогрессПроцент.МаксимальноеЗначение = ОбщееКоличествоФайлов;
	
	// Передача файлов на сервер и создание временных файлов
	
	ПараметрыЗагрузки.Вставить("ПомещенныеФайлы", Новый Массив);
	
	Обещания = Новый Массив;
	
	Обещания.Добавить(КД3_ИтерацияПодготовкиФайловАсинх(ПараметрыЗагрузки, ПараметрыЗагрузки.КаталогЗагрузки, Ложь, ОбщаяРасшифровка));
	Если ПараметрыЗагрузки.ЕстьРасширения Тогда
		Для Каждого ИмяКаталогаРасширения Из ПараметрыЗагрузки.КаталогиРасширений Цикл
			Обещания.Добавить(КД3_ИтерацияПодготовкиФайловАсинх(ПараметрыЗагрузки, ИмяКаталогаРасширения, Истина, ОбщаяРасшифровка));
		КонецЦикла;
	КонецЕсли;
	
	Для Каждого Обещание Из Обещания Цикл
		Ждать Обещание;
	КонецЦикла;
	Если КД3_ЗагрузкаПрервана Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Асинх Функция КД3_НайтиПомещаемыеФайлыАсинх(ПараметрыЗагрузки, ИмяКаталогаЗагрузки, ЭтоРасширение)
	
	КоличествоКаталога = 0;
	
	ДанныеОбщейГруппы = Новый Структура;
	ДанныеОбщейГруппы.Вставить("Количество", 0);
	ДанныеОбщейГруппы.Вставить("НайденныеФайлы", Новый Массив);
	
	РазделительПути = ПолучитьРазделительПути();
	
	Попытка
		Если НЕ ЭтоРасширение Тогда
			Если ПараметрыЗагрузки.ИсточникДанных = 0 Тогда
				КаталогКонфигурации = ИмяКаталогаЗагрузки;
				ИмяФайлаКонфигурации = "Configuration.xml";
			Иначе
				КаталогКонфигурации = ИмяКаталогаЗагрузки + РазделительПути + "src" + РазделительПути + "Configuration";
				ИмяФайлаКонфигурации = "Configuration.mdo";
			КонецЕсли;
			НайденныеФайлы = Ждать НайтиФайлыАсинх(КаталогКонфигурации, ИмяФайлаКонфигурации, Ложь);
			Если НайденныеФайлы.Количество() = 0 Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон(НСтр("ru='Не найден файл %1 в каталоге: %2'"), ИмяФайлаКонфигурации, КаталогКонфигурации));
				КД3_ЗагрузкаПрервана = Истина;
				Возврат 0;
			КонецЕсли;
			КД3_ДобавитьФайлыКОбработке(ДанныеОбщейГруппы, НайденныеФайлы);
		КонецЕсли;
	
		КаталогиДляОбработки = Новый Соответствие;
		Если ПараметрыЗагрузки.ТолькоОбновитьПланыОбмена Тогда
			КаталогиДляОбработки.Вставить("ExchangePlans", Ложь);
		Иначе
			КаталогиДляОбработки.Вставить("Catalogs", Истина);
			КаталогиДляОбработки.Вставить("Documents", Ложь);
			КаталогиДляОбработки.Вставить("Enums", Ложь);
			КаталогиДляОбработки.Вставить("Tasks", Ложь);
			КаталогиДляОбработки.Вставить("BusinessProcesses", Ложь);
			КаталогиДляОбработки.Вставить("ChartsOfAccounts", Истина);
			КаталогиДляОбработки.Вставить("ChartsOfCalculationTypes", Истина);
			КаталогиДляОбработки.Вставить("ChartsOfCharacteristicTypes", Истина);
			КаталогиДляОбработки.Вставить("AccountingRegisters", Ложь);
			КаталогиДляОбработки.Вставить("AccumulationRegisters", Ложь);
			КаталогиДляОбработки.Вставить("CalculationRegisters", Ложь);
			КаталогиДляОбработки.Вставить("InformationRegisters", Ложь);
			КаталогиДляОбработки.Вставить("ExchangePlans", Ложь);
			КаталогиДляОбработки.Вставить("EventSubscriptions", Ложь);
			КаталогиДляОбработки.Вставить("Constants", Ложь);
			КаталогиДляОбработки.Вставить("DefinedTypes", Ложь);
			КаталогиДляОбработки.Вставить("CommonAttributes", Ложь);
		КонецЕсли;
		//+КД3
		Если ПараметрыЗагрузки.КД3_ИспользоватьКонтекстнуюПодсказку И КД3_ЗагружатьМетодыМодулей Тогда
			КаталогиДляОбработки.Вставить("CommonModules", Ложь);
		КонецЕсли;
		//-КД3
		
		ВсеНайденныеФайлы = Новый Соответствие;
		
		Для Каждого КлючИЗначение Из КаталогиДляОбработки Цикл
			
			ИмяГруппы = КлючИЗначение.Ключ;
			ЕстьПредопределенные = КлючИЗначение.Значение;
			
			ДанныеГруппы = Новый Структура;
			ДанныеГруппы.Вставить("Количество", 0);
			ДанныеГруппы.Вставить("НайденныеФайлы", Новый Массив);
			
			// Поиск файлов для переноса на сервер
			Если ПараметрыЗагрузки.ИсточникДанных = 0 Тогда
				КаталогГруппы = ИмяКаталогаЗагрузки + РазделительПути + ИмяГруппы;
				
				
				КД3_ДобавитьФайлыКОбработке(ДанныеГруппы, Ждать НайтиФайлыАсинх(КаталогГруппы, "*.xml", Ложь));
				Если КД3_ЗагрузкаПрервана Тогда
					Возврат 0;
				КонецЕсли;
				
				Если ЕстьПредопределенные Тогда
					КД3_ДобавитьФайлыКОбработке(ДанныеГруппы, Ждать НайтиФайлыАсинх(КаталогГруппы, "Predefined.xml", Истина));
					Если КД3_ЗагрузкаПрервана Тогда
						Возврат Ложь;
					КонецЕсли;
				КонецЕсли;
				
				Если ИмяГруппы = "ExchangePlans" Тогда
					КД3_ДобавитьФайлыКОбработке(ДанныеГруппы, Ждать НайтиФайлыАсинх(КаталогГруппы, "Content.xml", Истина));
					Если КД3_ЗагрузкаПрервана Тогда
						Возврат 0;
					КонецЕсли;
				КонецЕсли;
			Иначе
				КаталогГруппы = ИмяКаталогаЗагрузки + РазделительПути + "src" + РазделительПути  + ИмяГруппы;
				
				КД3_ДобавитьФайлыКОбработке(ДанныеГруппы, Ждать НайтиФайлыАсинх(КаталогГруппы, "*.mdo", Истина));
				Если КД3_ЗагрузкаПрервана Тогда
					Возврат 0;
				КонецЕсли;
			КонецЕсли;
			
			Если ДанныеГруппы.Количество > 0 Тогда
				ВсеНайденныеФайлы[ИмяГруппы] = ДанныеГруппы;
			КонецЕсли;
			
			КоличествоКаталога = КоличествоКаталога + ДанныеГруппы.Количество;
		КонецЦикла;
		
		Если ДанныеОбщейГруппы.Количество > 0 Тогда
			ВсеНайденныеФайлы[""] = ДанныеОбщейГруппы;
			КоличествоКаталога = КоличествоКаталога + ДанныеОбщейГруппы.Количество;
		КонецЕсли;
		
		Если ВсеНайденныеФайлы.Количество() > 0 Тогда
			ПараметрыЗагрузки.КД3_ФайлыКОбработке[ИмяКаталогаЗагрузки] = ВсеНайденныеФайлы;
		КонецЕсли;
		
	Исключение
		ОбщегоНазначенияКлиент.СообщитьПользователю(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КД3_ЗагрузкаПрервана = Истина; // Отмена выполнения при ошибке должна устанавливаться в попытке внутри "асинх" функции, иначе будет лишние потери на ожидание в цикле с "Ждать"
		КоличествоКаталога = 0;
	КонецПопытки;
	
	Возврат КоличествоКаталога;
	
КонецФункции

&НаКлиенте
Процедура КД3_ДобавитьФайлыКОбработке(ДанныеГруппы, НайденныеФайлы)
	
	Если НайденныеФайлы.Количество() > 0 Тогда
		Для Каждого Файл Из НайденныеФайлы Цикл
			ДанныеГруппы.НайденныеФайлы.Добавить(Файл.ПолноеИмя);
		КонецЦикла;
		ДанныеГруппы.Количество = ДанныеГруппы.Количество + НайденныеФайлы.Количество();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Асинх Функция КД3_ИтерацияПодготовкиФайловАсинх(ПараметрыЗагрузки, ИмяКаталогаЗагрузки, ЭтоРасширение, ОбщаяРасшифровка)
	
	Попытка
		ДанныеПрогресса = Новый Структура;
		ДанныеПрогресса.Вставить("ТекстГруппы", "");
		ДанныеПрогресса.Вставить("ПроцентГруппы", 0);
		ДанныеПрогресса.Вставить("ПроцентОбщий", 0);
		ОбщаяРасшифровка[ИмяКаталогаЗагрузки] = ДанныеПрогресса;
		
		ПомещенныеФайлы = Новый Массив;
		
		Для Каждого КлючИЗначение Из ПараметрыЗагрузки.КД3_ФайлыКОбработке[ИмяКаталогаЗагрузки] Цикл
			
			ИмяГруппы = КлючИЗначение.Ключ;
			ДанныеГруппы = КлючИЗначение.Значение;
			
			Если КД3_ЗагрузкаПрервана Тогда
				Возврат Ложь;
			КонецЕсли;
			
			ДанныеПрогресса.ТекстГруппы = ИмяГруппы;
			ДанныеПрогресса.ПроцентГруппы = 0;
			
			РазмерПорции = Макс(Окр(ДанныеГруппы.Количество / 100), ПараметрыЗагрузки.КД3_МинимальныйРазмерПорции);
			ПрогрессГруппыШаг = (ДанныеГруппы.Количество + 1) / 100;
			ПрогрессГруппыОбработано = 0;
			
			// Передача найденных файлов порциями на сервер и создание временных файлов
			ФайлыКОбработке = Новый Массив;
			
			Для Каждого ПолноеИмяФайла Из ДанныеГруппы.НайденныеФайлы Цикл
				
				Если КД3_ЗагрузкаПрервана Тогда
					Возврат Ложь;
				КонецЕсли;
				
				ФайлыКОбработке.Добавить(Новый ОписаниеПередаваемогоФайла(ПолноеИмяФайла));
				
				КоличествоОбработано = ФайлыКОбработке.Количество();
				Если КоличествоОбработано < РазмерПорции Тогда
					Продолжить;
				КонецЕсли;
				
				Если НЕ Ждать КД3_ИтерацияПодготовкиПорцииФайловАсинх(ПараметрыЗагрузки, ФайлыКОбработке, ЭтоРасширение) Тогда
					Возврат Ложь;
				КонецЕсли;
				
				КД3_ПрогрессПроцент = КД3_ПрогрессПроцент + КоличествоОбработано;
				ПрогрессГруппыОбработано = ПрогрессГруппыОбработано + КоличествоОбработано;
				
				ФайлыКОбработке.Очистить();
				
				Если ПрогрессГруппыОбработано > 0 Тогда
					НовыйПроцент = Окр(ПрогрессГруппыОбработано / ПрогрессГруппыШаг);
					Если ДанныеПрогресса.ПроцентГруппы <> НовыйПроцент Тогда
						ДанныеПрогресса.ПроцентГруппы = НовыйПроцент;
						КД3_ВывестиПрогрессНаФорму(ОбщаяРасшифровка);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			КоличествоОбработано = ФайлыКОбработке.Количество();
			
			Если КоличествоОбработано > 0 Тогда
				Если НЕ Ждать КД3_ИтерацияПодготовкиПорцииФайловАсинх(ПараметрыЗагрузки, ФайлыКОбработке, ЭтоРасширение) Тогда
					Возврат Ложь;
				КонецЕсли;
				КД3_ПрогрессПроцент = КД3_ПрогрессПроцент + КоличествоОбработано;
				ПрогрессГруппыОбработано = ПрогрессГруппыОбработано + КоличествоОбработано;
			КонецЕсли;
			
		КонецЦикла;
		
		ОбщаяРасшифровка.Удалить(ИмяКаталогаЗагрузки);
		КД3_ВывестиПрогрессНаФорму(ОбщаяРасшифровка);
		
		Возврат Истина;
	Исключение
		ОбщегоНазначенияКлиент.СообщитьПользователю(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КД3_ЗагрузкаПрервана = Истина; // Отмена выполнения при ошибке должна устанавливаться в попытке внутри "асинх" функции, иначе будет лишние потери на ожидание в цикле с "Ждать"
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Асинх Функция КД3_ИтерацияПодготовкиПорцииФайловАсинх(ПараметрыЗагрузки, ФайлыКОбработке, ЭтоРасширение)
	
	ОбработанныеФайлы = Ждать ПоместитьФайлыНаСерверАсинх(,, ФайлыКОбработке, УникальныйИдентификатор);
	
	Если КД3_ЗагрузкаПрервана Тогда
		Возврат Ложь;
	КонецЕсли;
	
	НовыйФайлы = Новый Массив;
	Для Каждого ОбработанныйФайл Из ОбработанныеФайлы Цикл
		ДанныеФайла = Новый Структура;
		ДанныеФайла.Вставить("ЭтоРасширение", ЭтоРасширение);
		ДанныеФайла.Вставить("Хранение", ОбработанныйФайл.Адрес);
		ДанныеФайла.Вставить("Расширение", ОбработанныйФайл.СсылкаНаФайл.Файл.Расширение);
		ДанныеФайла.Вставить("ПолноеИмя", ОбработанныйФайл.СсылкаНаФайл.Файл.ПолноеИмя);
		НовыйФайлы.Добавить(ДанныеФайла);
	КонецЦикла;
	
	КД3_СохранитьВременныеФайлыНаСервере(НовыйФайлы, ПараметрыЗагрузки.КД3_КаталогВременныхФайлов);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ПараметрыЗагрузки.ПомещенныеФайлы, НовыйФайлы);
	
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Функция КД3_СохранитьВременныеФайлыНаСервере(НовыеФайлы, КаталогВременныхФайлов)
	
	Для Каждого НовыйФайл Из НовыеФайлы Цикл
		
		ИмяФайлаНаСервере = КаталогВременныхФайлов + Строка(Новый УникальныйИдентификатор) + НовыйФайл.Расширение;
		ДвоичныеДанные = ПолучитьИзВременногоХранилища(НовыйФайл.Хранение);
		ДвоичныеДанные.Записать(ИмяФайлаНаСервере);
		УдалитьИзВременногоХранилища(НовыйФайл.Хранение);
		
		НовыйФайл.Удалить("Расширение");
		НовыйФайл.Хранение = ИмяФайлаНаСервере;
	КонецЦикла;
	
КонецФункции

&НаКлиенте
Процедура КД3_ВывестиПрогрессНаФорму(ОбщаяРасшифровка)
	
	ПрогрессТекст = Новый Массив;
	ПрогрессТекст.Добавить(НСтр("ru='Получение файлов конфигурации'"));
	ПрогрессТекст.Добавить("");
	Для Каждого КлючИЗначение Из ОбщаяРасшифровка Цикл
		ИмяКаталогаЗагрузки = КлючИЗначение.Ключ;
		ДанныеПрогресса = КлючИЗначение.Значение;
		ПрогрессТекст.Добавить(СтрШаблон("%1%% %2: %3", ДанныеПрогресса.ПроцентГруппы, ДанныеПрогресса.ТекстГруппы, ИмяКаталогаЗагрузки));
	КонецЦикла;
	
	КД3_ПрогрессТекст = СтрСоединить(ПрогрессТекст, Символы.ПС);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура КД3_ЗаполнитьМестоХраненияИндексов(Форма, ЗагружатьИзФайлов)
	
	СписокВыбора = Форма.Элементы.КД3_МестоХраненияИндексов.СписокВыбора;
	
	СписокВыбора.Очистить();
	СписокВыбора.Добавить(0, НСтр("ru='Не использовать'"));
	СписокВыбора.Добавить(1, НСтр("ru='В каталоге'"));
	СписокВыбора.Добавить(2, НСтр("ru='В информационной базе'"));
	Если ЗагружатьИзФайлов Тогда
		СписокВыбора.Добавить(3, НСтр("ru='Не хранить'"));
	КонецЕсли;
	
	ТекстПодсказки =
	"Место хранения дополнительных метаданных (методов модулей):
	|В каталоге - сохраняются в выбранном каталоге
	|В информационной базе - сохраняются в РС ""Хранилище безопасных данных""";
	Если ЗагружатьИзФайлов Тогда
		ТекстПодсказки = ТекстПодсказки + "
		|Не хранить - метаданные подсказки получаются в момент вызова";
	КонецЕсли;
	
	Форма.Элементы.КД3_МестоХраненияИндексов.Подсказка = ТекстПодсказки;
	Форма.Элементы.КД3_МестоХраненияИндексов.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	
	Если СписокВыбора.НайтиПоЗначению(Форма.КД3_МестоХраненияИндексов) = Неопределено Тогда
		Форма.КД3_МестоХраненияИндексов = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_УказатьКаталогФайловВКонфигурацииПриИзменении(Элемент)
	НастройкиКонфигурации = КД3_МетаданныеКлиент.НастройкиКонфигурации(
		?(СпособЗагрузки = 0, Неопределено, Объект.Конфигурация));
	КД3_ОбновитьНадписьЗагружаемыхМодулей(ЭтотОбъект, НастройкиКонфигурации.ЗагружатьИзФайлов ИЛИ КД3_СохранитьКаталогДляКонфигурации);
	КД3_ЗаполнитьМестоХраненияИндексов(ЭтотОбъект, НастройкиКонфигурации.ЗагружатьИзФайлов ИЛИ КД3_СохранитьКаталогДляКонфигурации);
КонецПроцедуры

&НаКлиенте
Процедура КД3_УстановитьВидимостьПоМестуХраненияИндексов(Форма)
	Форма.Элементы.КД3_КаталогИндексов.Видимость = Форма.КД3_МестоХраненияИндексов = 1;
	Форма.Элементы.КД3_ЗагружатьМетодыМодулей.Доступность = Форма.КД3_МестоХраненияИндексов > 0;
	Форма.Элементы.КД3_СохранитьКаталогДляКонфигурации.Доступность = Форма.КД3_МестоХраненияИндексов > 0;
КонецПроцедуры

&НаКлиенте
Процедура КД3_ПриИзмененииКонфигурации()
	
	НастройкиКонфигурации = КД3_МетаданныеКлиент.НастройкиКонфигурации(
		?(СпособЗагрузки = 0, Неопределено, Объект.Конфигурация));
		
	КД3_МестоХраненияИндексов = НастройкиКонфигурации.МестоХраненияИндексов;
	КД3_КаталогИндексов = НастройкиКонфигурации.КаталогИндексов;
	КД3_ЗагружатьМетодыМодулей = НастройкиКонфигурации.ЗагружатьМетодыМодулей;
	Если КД3_МестоХраненияИндексов = 0 Тогда
		КД3_ЗагружатьМетодыМодулей = Ложь;
		КД3_СохранитьКаталогДляКонфигурации = Ложь;
	КонецЕсли;
	
	КД3_ЗаполнитьМестоХраненияИндексов(ЭтотОбъект, НастройкиКонфигурации.ЗагружатьИзФайлов);
	КД3_ОбновитьНадписьЗагружаемыхМодулей(ЭтотОбъект, НастройкиКонфигурации.ЗагружатьИзФайлов ИЛИ КД3_СохранитьКаталогДляКонфигурации);
	КД3_УстановитьВидимостьПоМестуХраненияИндексов(ЭтотОбъект);
	
	Если ЗначениеЗаполнено(Объект.Конфигурация) Тогда
		ПриИзмененииКонфигурацииНаСервере();
		КД3_ИзменитьРежимОбработки();
	КонецЕсли;
	
	КД3_ОбновитьДоступностьТиповойЗагрузки();
	
	УстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииКонфигурацииНаСервере()
	
	ДанныеКонфигурации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Конфигурация,
		"ОбновлятьМетаданныеПоРасписанию,ИсточникДанных,КаталогЗагрузки,Расширения");
	
	Если ДанныеКонфигурации.ОбновлятьМетаданныеПоРасписанию Тогда
		Если ДанныеКонфигурации.ИсточникДанных = 1 Тогда
			Возврат; // Загрузка из git сервера EDT
		Конецесли;
		
		КД3_ЭтоСервер = Истина;
		
		// Заполнение настроек загрузки по данным настроек конфигурации
		Объект.ИсточникДанных = ДанныеКонфигурации.ИсточникДанных;
		Объект.ИмяКаталогаЗагрузки = ДанныеКонфигурации.КаталогЗагрузки;
		
		Объект.Расширения.Очистить();
		ВыборкаРасширений = ДанныеКонфигурации.Расширения.Выбрать();
		Пока ВыборкаРасширений.Следующий() Цикл
			СтрокаРасширения = Объект.Расширения.Добавить();
			СтрокаРасширения.ИмяКаталогаЗагрузки = ВыборкаРасширений.ИмяКаталогаЗагрузки;
		КонецЦикла;
	КонецЕсли;
	
	Объект.ЕстьРасширения = Объект.Расширения.Количество() > 0;
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_КоличествоПотоковПриИзменении(Элемент)
	
	КД3_ОбновитьДоступностьТиповойЗагрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура КД3_ОбновитьДоступностьТиповойЗагрузки()
	
	ДоступностьТиповойЗагрузки = КД3_КоличествоПотоков = 1 И КД3_МестоХраненияИндексов = 0;
	
	Элементы.КД3_ТиповаяЗагрузка.Доступность = ДоступностьТиповойЗагрузки;
	Если НЕ ДоступностьТиповойЗагрузки И КД3_ТиповаяЗагрузка Тогда
		КД3_ТиповаяЗагрузка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
