#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Область ПрограммныйИнтерфейс

Функция КД_ПараметрыСлужебныхФоновыхЗаданий(УникальныйИдентификаторФормы) Экспорт
	
	Отбор = Новый Структура;
	Отбор.Вставить("ИмяМетода", "КД3_ЗагрузкаМетаданных.ОбработатьГруппуОбъектовМногопоточно");
	Отбор.Вставить("Наименование", "КД3_ЗагрузкаМетаданныхМногопоточно_" + Строка(УникальныйИдентификаторФормы));
	
	Возврат Отбор;
	
КонецФункции

Процедура ВыполнитьЗагрузкуМетаданныхТиповая(ОбщиеПеременные, АдресВременногоХранилища) Экспорт
	
	Если ОбщиеПеременные.КД3_ЭтоСервер Тогда
		КонвертацияДанныхXDTOСервер.КД3_ПодготовитьФайлыДляЗагрузкиНаСервере(ОбщиеПеременные);
	КонецЕсли;
	
	ОбщиеПеременные.Удалить("КД3_ТиповаяЗагрузка");
	ОбщиеПеременные.Удалить("КД3_ЭтоСервер");
	ОбщиеПеременные.Удалить("КД3_КаталогВременныхФайлов");
	
	Обработки.ЗагрузкаСтруктурыКонфигурацииИзФайловXML.ВыполнитьЗагрузкуМетаданных(ОбщиеПеременные, АдресВременногоХранилища);
	
КонецПроцедуры

// Выполняет загузку метаданных из файлов.
// Параметры:
//   ОбщиеПеременные - Структура - Переменные, которые используются при работе.
//   АдресВременногоХранилища - Произвольный - Адрес, в который будет помещен результат выполнения процедуры.
Процедура ВыполнитьЗагрузкуМетаданных(ОбщиеПеременные, АдресВременногоХранилища) Экспорт
	
	ОбщиеПеременные.Вставить("ЗагрузитьНовую", (ОбщиеПеременные.СпособЗагрузки = 0));
	ОбщиеПеременные.Вставить("Отказ", Ложь);
	ОбщиеПеременные.Вставить("АдресВременногоХранилища", АдресВременногоХранилища);
	ОбщиеПеременные.Вставить("ЭтоРасширение", Ложь);
	ОбщиеПеременные.Вставить("ПутьКФайламРасширения", "");
	ОбщиеПеременные.Вставить("Ошибки", "");
	ОбщиеПеременные.Вставить("СинхронизироватьПоУИД", Ложь);
	ОбщиеПеременные.Вставить("СоздаватьПредопределенныеСвойства", Истина);
	
	//+КД3 Отображение прогресса
	Если КД3_ЗагрузкаМетаданных.НужноСообщатьПрогресс(ОбщиеПеременные) Тогда
		КоличествоОбщихЭтапов = 9 + ?(ОбщиеПеременные.КД3_ЭтоСервер, 1, 0); // Количество УвеличитьСообщитьПрогрессОбщий() в ВыполнитьЗагрузкуМетаданных
		КоличествоЭтаповГрупп = ?(ОбщиеПеременные.ТолькоОбновитьПланыОбмена, 1, 17); // Количество групп в КД3_ОбработатьКаталогиСДанными()
		Если ОбщиеПеременные.ЕстьРасширения Тогда
			КоличествоЭтаповГрупп = КоличествоЭтаповГрупп + КоличествоЭтаповГрупп * ОбщиеПеременные.КаталогиРасширений.Количество();
		КонецЕсли;
		КД3_ЗагрузкаМетаданных.НачатьПрогрессОбщий(ОбщиеПеременные, КоличествоОбщихЭтапов + КоличествоЭтаповГрупп + 1);
	КонецЕсли;
	ОбщиеПеременные.Вставить("КД3_ОсновнойПоток", Истина);
	//-КД3 Отображение прогресса
	
	//+КД3
	Если ОбщиеПеременные.КД3_ЭтоСервер Тогда
		КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессОбщий(ОбщиеПеременные, НСтр("ru='Получение файлов конфигурации'"));
		КонвертацияДанныхXDTOСервер.КД3_ПодготовитьФайлыДляЗагрузкиНаСервере(ОбщиеПеременные);
	КонецЕсли;
	//-КД3
	
	ПолучитьРазделительПутиФайлов(ОбщиеПеременные);
	ПрочитатьДанныеОКонфигурации(ОбщиеПеременные);
	Если ОбщиеПеременные.Отказ Тогда
		Возврат;
	КонецЕсли;
	ОбщиеПеременные.Вставить("ПредыдущийСпособЗагрузки", РегистрыСведений.СостоянияЗагрузокМетаданных.ПрочитатьПредыдущийСпособЗагрузкиМетаданных(ОбщиеПеременные.Конфигурация));
	Если ЗначениеЗаполнено(ОбщиеПеременные.ПредыдущийСпособЗагрузки) И ОбщиеПеременные.ПредыдущийСпособЗагрузки <> Перечисления.СпособыЗагрузкиМетаданных.ИзФайлаXMLПрошлаяВерсия Тогда
		ОбщиеПеременные.Вставить("СинхронизироватьПоУИД", Истина);
	КонецЕсли;
	
	//+КД3
	Если ОбщиеПеременные.КД3_ИспользоватьКонтекстнуюПодсказку Тогда
		КД3_НастройкиКонфигурации = КД3_Метаданные.НастройкиКонфигурации(ОбщиеПеременные.Конфигурация);
		Если НЕ ОбщиеПеременные.ЗагрузитьНовую Тогда
			// Удаление старых индексов метаданных для конфигурации
			КД3_Метаданные.УдалитьОписаниеМетаданных(ОбщиеПеременные.Конфигурация, КД3_НастройкиКонфигурации);
		КонецЕсли;
		ОбщиеПеременные.Вставить("КД3_НастройкиКонфигурации", КД3_НастройкиКонфигурации);
		ОбщиеПеременные.Вставить("КД3_ПарсерМодулей", КД3_Настройки.ПарсерМодулей());
	КонецЕсли;
	//-КД3
	
	// Начало загрузки.
	ПараметрыРезультатаЗагрузки = Новый Структура("Релиз, НачалоЗагрузки", ОбщиеПеременные.Конфигурация, Истина);
	РегистрыСведений.СостоянияЗагрузокМетаданных.СформироватьЗаписьРезультатЗагрузкиМетаданных(ПараметрыРезультатаЗагрузки);
	
	//+КД3
	КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессОбщий(ОбщиеПеременные, НСтр("ru='Получение существующих объектов'"));
	Если ОбщиеПеременные.КД3_КоличествоПотоков = 1 Тогда
		Если ОбщиеПеременные.ТолькоОбновитьПланыОбмена Тогда
			КД3_ПолучитьСуществующиеСсылки(ОбщиеПеременные, Перечисления.ТипыОбъектов.ПланОбмена);
		Иначе
			КД3_ПолучитьСуществующиеСсылки(ОбщиеПеременные, Неопределено);
		КонецЕсли;
	Иначе
		// Существующие ссылки будут получены многопоточно (для каждой группы объектов отдельно)
		// В ОбщиеПеременные изначально будут только простые типы, которые всегда существуют
		ОбщиеПеременные.Вставить("СуществующиеОбъекты", Новый Массив);
		ОбщиеПеременные.Вставить("СуществующиеСвойства", Новый Массив);
		ОбщиеПеременные.Вставить("СуществующиеЗначения", Новый Массив);
	КонецЕсли;
	//-КД3
	
	//+КД3 FIX
	// Исправление спроса определяемых типов, если есть расширения
	ОбщиеПеременные.Вставить("ОпределяемыеТипы", Новый Структура);
	//-КД3 FIX
	
	// Обработка объектов для конфигурации и всех расширений
	
	СоздатьОбъектыПростыеТипы(ОбщиеПеременные);
	
	КД3_ОбработатьГруппыОбъектовМногопоточно(ОбщиеПеременные);
	
	// Обработка общих для конфигурации и расширений данных объектов
	
	КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессОбщий(ОбщиеПеременные, НСтр("ru='Заполнение типов планов видов расчета'"));
	КД3_ЗаполнениеТиповПлановВидовРасчета(ОбщиеПеременные);
	
	СвойстваТипаХарактеристика = Новый Соответствие;
	
	КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессОбщий(ОбщиеПеременные, НСтр("ru='Заполнение типов загруженных свойств'"));
	КД3_ЗаполнениеТиповЗагруженныхСвойств(ОбщиеПеременные, СвойстваТипаХарактеристика);
	
	КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессОбщий(ОбщиеПеременные, НСтр("ru='Заполнение типов свойств Характеристика'"));
	КД3_ЗаполнениеТипаСвойствХарактеристика(ОбщиеПеременные, СвойстваТипаХарактеристика);
	
	КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессОбщий(ОбщиеПеременные, НСтр("ru='Привязка задач к бизнес-процессам'"));
	КД3_ПривязкаЗадачКБизнесПроцессам(ОбщиеПеременные);
	
	КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессОбщий(ОбщиеПеременные, НСтр("ru='Заполнение движений по регистрам у документов и типов регистраторов у регистров'"));
	КД3_ОбработатьРегистраторы(ОбщиеПеременные);
	
	// Пометка на удаление лишних объектов, свойств, значений.
	КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессОбщий(ОбщиеПеременные, НСтр("ru='Пометка на удаление неактуальных объектов'"));
	Если НЕ ОбщиеПеременные.ЗагрузитьНовую Тогда
		КД3_УдалитьОтсутствующиеСсылки(ОбщиеПеременные, ОбщиеПеременные.СуществующиеОбъекты);
		КД3_УдалитьОтсутствующиеСсылки(ОбщиеПеременные, ОбщиеПеременные.СуществующиеСвойства);
		КД3_УдалитьОтсутствующиеСсылки(ОбщиеПеременные, ОбщиеПеременные.СуществующиеЗначения);
	КонецЕсли;
	
	КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессОбщий(ОбщиеПеременные, НСтр("ru='Удаление временных файлов'"));
	Попытка
		УдалитьФайлы(ОбщиеПеременные.КД3_КаталогВременныхФайлов);
	Исключение
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru='Не удалось удалить временные файлы конфигурации!'") + Символы.ПС + КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	// Результат загрузки.
	ПараметрыРезультатаЗагрузки = Новый Структура("Релиз, Отказ", ОбщиеПеременные.Конфигурация, ОбщиеПеременные.Отказ);
	Если ОбщиеПеременные.ТолькоОбновитьПланыОбмена Тогда
		ПараметрыРезультатаЗагрузки.Вставить("СпособЗагрузки", ОбщиеПеременные.ПредыдущийСпособЗагрузки);
	Иначе
		ПараметрыРезультатаЗагрузки.Вставить("СпособЗагрузки", ?(ОбщиеПеременные.ИсточникДанных = 0, Перечисления.СпособыЗагрузкиМетаданных.ИзФайловXML, Перечисления.СпособыЗагрузкиМетаданных.ИзФайловEDT));
	КонецЕсли;
	РегистрыСведений.СостоянияЗагрузокМетаданных.СформироватьЗаписьРезультатЗагрузкиМетаданных(ПараметрыРезультатаЗагрузки);
	
	Если АдресВременногоХранилища <> Неопределено Тогда
		ВозвращаемоеЗначение = Новый Структура("Релиз, Успешно, Ошибки", ОбщиеПеременные.Конфигурация, НЕ ОбщиеПеременные.Отказ, "");
		ПоместитьВоВременноеХранилище(ВозвращаемоеЗначение, АдресВременногоХранилища);
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьЗагрузкуМетаданных()

#КонецОбласти
#Область СлужебныеПроцедурыИФункции

Процедура ПрочитатьДанныеОКонфигурации(ОбщиеПеременные)
	Если ОбщиеПеременные.ИсточникДанных = 0 Тогда
		ИмяФайлаДляПоиска = "Configuration.xml";
		ТекстСообщенияОбОшибке = НСтр("ru='В каталоге не найден файл Configuration.xml'");
	Иначе
		ИмяФайлаДляПоиска = "Configuration" + ОбщиеПеременные.РазделительПути + "Configuration.mdo";
		ТекстСообщенияОбОшибке = НСтр("ru='В каталоге не найден файл Configuration.mdo'");
	КонецЕсли;
	ИмяФайлаСведений = НайтиПомещенныеФайлыПоИмени(ОбщиеПеременные, ИмяФайлаДляПоиска);
	Если ИмяФайлаСведений = "" Тогда
		ЗаписатьСообщениеОбОшибке(ОбщиеПеременные, ТекстСообщенияОбОшибке); 
		Возврат;
	КонецЕсли;

	ИмяКонфигурации = "";
	СинонимКонфигурации = "";
	ВерсияКонфигурации = "";
	КомментарийКонфигурации = "";
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ИмяФайлаСведений);
	Если ОбщиеПеременные.ИсточникДанных = 0 Тогда
		ЧтениеXML.Прочитать(); // MetaDataObject
		Если НЕ (ЧтениеXML.Имя = "MetaDataObject" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента) Тогда
			ЗаписатьСообщениеОбОшибке(ОбщиеПеременные, НСтр("ru='Файл не содержит описания конфигурации'"));
			Возврат;
		КонецЕсли;
		ЧтениеXML.Прочитать(); // Configuration
		Пока ЧтениеXML.Прочитать() Цикл
			Если ЧтениеXML.Имя = "Properties" Тогда
				Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
					
					Пока ЧтениеXML.Прочитать() Цикл
						Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
							Если ЧтениеXML.Имя = "Name" Тогда
								ИмяКонфигурации = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
							ИначеЕсли ЧтениеXML.Имя = "Synonym" Или ЧтениеXML.Имя = "v8:item"  Тогда
								СинонимКонфигурации = ПрочитатьСиноним(ЧтениеXML);
							ИначеЕсли ЧтениеXML.Имя = "Version" Тогда
								ВерсияКонфигурации = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
							Иначе
								ЧтениеXML.Пропустить();
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
				ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
					Прервать;
				КонецЕсли;
			Иначе
				ЧтениеXML.Пропустить();
			КонецЕсли;
		КонецЦикла;
	Иначе
		ЧтениеXML.Прочитать(); // projectDescription
		Если НЕ (ЧтениеXML.Имя = "mdclass:Configuration" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента) Тогда
			ЗаписатьСообщениеОбОшибке(ОбщиеПеременные, НСтр("ru='Файл не содержит описания конфигурации'"));
			Возврат;
		КонецЕсли;
		ЧтениеXML.Прочитать();
		Пока НЕ (ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента И ЧтениеXML.Имя = "mdclass:Configuration") Цикл
			Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				Если НРег(ЧтениеXML.Имя) = "name" Тогда
					ИмяКонфигурации = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				ИначеЕсли НРег(ЧтениеXML.Имя) = "synonym" Тогда
					СинонимКонфигурации = ПрочитатьСиноним(ЧтениеXML);
				ИначеЕсли НРег(ЧтениеXML.Имя) = "version" Тогда
					ВерсияКонфигурации = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
					Прервать;
				Иначе
					ЧтениеXML.Пропустить();
				КонецЕсли;
			ИначеЕсли НЕ ЗначениеЗаполнено(ЧтениеXML.Имя) Тогда
				Прервать;
			Иначе
				ЧтениеXML.Прочитать();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(ИмяКонфигурации) Тогда
		ЗаписатьСообщениеОбОшибке(ОбщиеПеременные, НСтр("ru='Не удалось идентифицировать конфигурацию'"));
		Возврат;
	КонецЕсли;
	Если ОбщиеПеременные.ЗагрузитьНовую Тогда
		КонфигурацияОбъект = Справочники.Релизы.СоздатьЭлемент();
		КонфигурацияОбъект.Синоним = СинонимКонфигурации;
		КонфигурацияОбъект.Комментарий = КомментарийКонфигурации;
		КонфигурацияОбъект.КонфигурацияВладелец = Справочники.Конфигурации.НайтиПоНаименованию(ИмяКонфигурации);
		Если НЕ ЗначениеЗаполнено(КонфигурацияОбъект.КонфигурацияВладелец) Тогда
			КонфигурацияОбъект.КонфигурацияВладелец = Справочники.Конфигурации.НеизвестнаяКонфигурация;
		КонецЕсли;
	Иначе
		КонфигурацияОбъект = ОбщиеПеременные.Конфигурация.ПолучитьОбъект();
	КонецЕсли;
	КонфигурацияОбъект.Имя = ИмяКонфигурации;

	Если ОбщиеПеременные.Свойство("ИмяНовойКонфигурации")
		И ЗначениеЗаполнено(ОбщиеПеременные.ИмяНовойКонфигурации) Тогда
		КонфигурацияОбъект.Наименование = ОбщиеПеременные.ИмяНовойКонфигурации;
	ИначеЕсли НЕ ЗначениеЗаполнено(КонфигурацияОбъект.Наименование) Тогда
		КонфигурацияОбъект.Наименование = ИмяКонфигурации + " " + ВерсияКонфигурации;
	КонецЕсли;
	КонфигурацияОбъект.Версия = ВерсияКонфигурации;
	КонфигурацияОбъект.ДатаОбновления = ТекущаяДатаСеанса();
	КонфигурацияОбъект.Записать();
	Если ОбщиеПеременные.ЗагрузитьНовую Тогда
		ОбщиеПеременные.Вставить("Конфигурация", КонфигурацияОбъект.Ссылка);
	КонецЕсли;
КонецПроцедуры

Процедура ОбработатьГруппуОбъектов(ОбщиеПеременные, ИмяГруппы, ИмяКаталога, ПрефиксИменЭлементов, ТипОбъекта)
	
	Если ОбщиеПеременные.ИсточникДанных = 0 Тогда
		ВложенныеФайлы = НайтиПомещенныеФайлыПоМаске(ОбщиеПеременные.ПомещенныеФайлы, ОбщиеПеременные.РазделительПути + ИмяКаталога + ОбщиеПеременные.РазделительПути, ".xml", ОбщиеПеременные);
	Иначе
		ВложенныеФайлы = НайтиПомещенныеФайлыПоМаске(ОбщиеПеременные.ПомещенныеФайлы, "src" + ОбщиеПеременные.РазделительПути + ИмяКаталога + ОбщиеПеременные.РазделительПути, ".mdo", ОбщиеПеременные);
	КонецЕсли;
	Если ВложенныеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	//+КД3 Отображение прогресса
	КД3_ЗагрузкаМетаданных.НачатьПрогрессРасшифровки(ОбщиеПеременные, ВложенныеФайлы.Количество(), ИмяГруппы, ИмяГруппы + ?(ОбщиеПеременные.ЭтоРасширение, " " + ОбщиеПеременные.ПутьКФайламРасширения, ""));
	//-КД3 Отображение прогресса
	Если ИмяГруппы = "ОбщиеРеквизиты" Тогда
		Для Каждого ВложенныйФайл Из ВложенныеФайлы Цикл
			//+КД3 Отображение прогресса
			КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессРасшифровки(ОбщиеПеременные);
			//-КД3 Отображение прогресса
			ЗагрузитьОбщийРеквизит(ОбщиеПеременные, ВложенныйФайл);
		КонецЦикла;
		Возврат;
	КонецЕсли;
	// Поиск / создание группы.
	ТекущийРодитель = Справочники.Объекты.НайтиПоНаименованию(ИмяГруппы, Истина,,ОбщиеПеременные.Конфигурация);
	Если НЕ ЗначениеЗаполнено(ТекущийРодитель) Тогда
		ОбъектПапка = Справочники.Объекты.СоздатьГруппу();
		ОбъектПапка.Наименование = ИмяГруппы;
		ОбъектПапка.Имя = ИмяГруппы;
		ОбъектПапка.Синоним = ИмяГруппы;
		ОбъектПапка.Владелец = ОбщиеПеременные.Конфигурация;
		ОбъектПапка.Записать();
		ТекущийРодитель = ОбъектПапка.Ссылка;
	КонецЕсли;
	Для Каждого ВложенныйФайл Из ВложенныеФайлы Цикл
		//+КД3 Отображение прогресса
		КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессРасшифровки(ОбщиеПеременные);
		//-КД3 Отображение прогресса
		Если ТипОбъекта = Перечисления.ТипыОбъектов.ПодпискаНаСобытие Тогда
			ЗагрузитьПодпискуНаСобытие(ОбщиеПеременные, ТекущийРодитель, ВложенныйФайл, ПрефиксИменЭлементов, ТипОбъекта);
		Иначе
			ЗагрузитьОбъектМетаданных(ОбщиеПеременные, ТекущийРодитель, ВложенныйФайл, ПрефиксИменЭлементов, ТипОбъекта);
			//+КД3
			// Загрузка описание модулей, если выбран режим загрузки индекса
			Если ОбщиеПеременные.КД3_ЗагружатьМетодыМодулей И НЕ ОбщиеПеременные.КД3_НастройкиКонфигурации.ЗагружатьИзФайлов Тогда
				// При выборе места хранения индексов в каталоге или информационной базе и не заполненном расписании
				// загрузки из исходных кодов загружаются все модули, иначе только глобальных контекст
				ВидыМодулей = Новый Массив;
				ВидыМодулей.Добавить("Manager");
				ВидыМодулей.Добавить("Object");
				Для Каждого ВидМодуля Из ВидыМодулей Цикл
					ФайлОбъекта = Новый Файл(ВложенныйФайл.ИсходноеИмяФайла);
					КлючКоллекции = Неопределено;
					КД3_ЗагрузкаМетаданных.ЗагрузитьМодульОбъекта(ОбщиеПеременные, КлючКоллекции, ОбщиеПеременные.Конфигурация, ВидМодуля, НРЕг(ИмяГруппы), ФайлОбъекта.ИмяБезРасширения);
				КонецЦикла;
			КонецЕсли;
			//-КД3
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура РассчитатьКоличествоОбъектов(ОбщиеПеременные, ИмяКаталога)
	Если ОбщиеПеременные.ИсточникДанных = 0 Тогда
		ВложенныеФайлы = НайтиПомещенныеФайлыПоМаске(ОбщиеПеременные.ПомещенныеФайлы, ОбщиеПеременные.РазделительПути + ИмяКаталога + ОбщиеПеременные.РазделительПути, ".xml", ОбщиеПеременные);
	Иначе
		ВложенныеФайлы = НайтиПомещенныеФайлыПоМаске(ОбщиеПеременные.ПомещенныеФайлы, "src" + ОбщиеПеременные.РазделительПути + ИмяКаталога + ОбщиеПеременные.РазделительПути, ".mdo", ОбщиеПеременные);
	КонецЕсли;
	Если ВложенныеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	ЧислоОбъектов = ОбщиеПеременные.ЧислоОбъектовВсего + ВложенныеФайлы.Количество();
	ОбщиеПеременные.Вставить("ЧислоОбъектовВсего", ЧислоОбъектов);
КонецПроцедуры

Процедура ЗагрузитьОбъектМетаданных(ОбщиеПеременные, ТекущийРодитель, ВложенныйФайл, ПрефиксИменЭлементов, ТипОбъекта)
	ДанныеОбъекта = Новый Структура();
	ДанныеОбъекта.Вставить("ИмяЗадачиБизнесПроцесса", "");
	ДанныеОбъекта.Вставить("ПланСчетов", "");
	ДанныеОбъекта.Вставить("ПериодичностьРегистраСведений", "");
	ДанныеОбъекта.Вставить("РежимЗаписиРегистраСведений", "");
	ДанныеОбъекта.Вставить("ПланВидовРасчета", "");
	ДанныеОбъекта.Вставить("Корреспонденция", Ложь);
	ДанныеОбъекта.Вставить("ЕстьБазовыйПериод", Ложь);
	ДанныеОбъекта.Вставить("ЕстьПериодДействия", Ложь);
	ДанныеОбъекта.Вставить("ТипыВладельца", Новый Массив);
	ДанныеОбъекта.Вставить("МассивРегистров", Новый Массив);
	ДанныеОбъекта.Вставить("ТипыЗначенийХарактеристик", Новый Массив);
	ДанныеОбъекта.Вставить("ПредопределенныеСвойства", Новый Структура); 
	ДанныеОбъекта.Вставить("ТекущийРодитель", ТекущийРодитель);
	ДанныеОбъекта.Вставить("ПрефиксИменЭлементов", ПрефиксИменЭлементов);
	ДанныеОбъекта.Вставить("ТипОбъекта", ТипОбъекта);
	ДанныеОбъекта.Вставить("ЗапрещеноПроведениеДокумента", Ложь);
	ДанныеОбъекта.Вставить("ДобавленНовый", Ложь);
	ДанныеОбъекта.Вставить("ТипРегистраНакопления", Неопределено);
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ВложенныйФайл.ТекущееИмяФайла);

	Если ОбщиеПеременные.ИсточникДанных = 0 Тогда
		ТекОбъект = ПрочитатьСведенияОбОбъектеИзXML(ЧтениеXML, ДанныеОбъекта, ОбщиеПеременные);
	Иначе
		ТекОбъект = ПрочитатьСведенияОбОбъектеИзEDT(ЧтениеXML, ДанныеОбъекта, ОбщиеПеременные);
	КонецЕсли;
	Если ТекОбъект = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ДанныеОбъекта.ДобавленНовый Или НЕ ОбщиеПеременные.ТолькоДобавлятьНовые
		Или НЕ ОбщиеПеременные.ТолькоОбновитьПланыОбмена Тогда
		Если ЗначениеЗаполнено(ДанныеОбъекта.ИмяЗадачиБизнесПроцесса) Тогда
			// Бизнес - процесс, привязка к задаче. Надо заполнить тип свойства БизнесПроцесс у соотв задачи.
			//+КД3
			// Там как при многопоточной обработке в текущем потоке может не быть всех типовов свойств, то они будут обработаны в основном потоке после всех объектов
			ОбщиеПеременные.КД3_ОтложеннаяПривязкаБизнесПроцессаКЗадаче.Вставить(ДанныеОбъекта.ИмяЗадачиБизнесПроцесса, ТекОбъект.Ссылка);
			//-КД3
		КонецЕсли;
	КонецЕсли;
	Если ДанныеОбъекта.МассивРегистров.Количество() > 0 Тогда
		Для Каждого Регистр Из ДанныеОбъекта.МассивРегистров Цикл
			СтрокаТЗ = ОбщиеПеременные.РегистраторыИРегистры.Добавить();
			СтрокаТЗ.Регистратор = ТекОбъект.Ссылка;
			СтрокаТЗ.Регистр = Регистр;
			СтрокаТЗ.ЗапрещеноПроведениеДокумента = ДанныеОбъекта.ЗапрещеноПроведениеДокумента;
		КонецЦикла;
	КонецЕсли;
	// Дочерние объекты (свойства).
	// Предопределенные свойства для разных типов объектов.
	Если НЕ ДанныеОбъекта.ДобавленНовый И ОбщиеПеременные.ТолькоДобавлятьНовые Тогда
		ОбщиеПеременные.Вставить("СоздаватьПредопределенныеСвойства", Ложь);
	Иначе
		ОбщиеПеременные.Вставить("СоздаватьПредопределенныеСвойства", Истина);
	КонецЕсли;
	Если ДанныеОбъекта.ТипыВладельца.Количество() > 0 Тогда
		ДобавитьПредопределенноеСвойство("Владелец", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, ДанныеОбъекта.ТипыВладельца);
	КонецЕсли;
	Если ТекОбъект.Иерархический Тогда
		Если ТекОбъект.ВидИерархии <> "ИерархияЭлементов" Тогда
			ДобавитьПредопределенноеСвойство("ЭтоГруппа", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		КонецЕсли;
		ДобавитьПредопределенноеСвойство("Родитель", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, ТекОбъект.Ссылка);
	КонецЕсли;
	Если ТипОбъекта = Перечисления.ТипыОбъектов.Документ Тогда
		ДобавитьПредопределенноеСвойство("Проведен", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		ДобавитьПредопределенноеСвойство("ПометкаУдаления", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		ДанныеОбъекта.ПредопределенныеСвойства.Вставить("Дата", Новый Структура("КвалификаторыДаты_Состав", "Дата и время"));
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.Справочник
		Или ТипОбъекта = Перечисления.ТипыОбъектов.ПланВидовХарактеристик Тогда
		ДобавитьПредопределенноеСвойство("ПометкаУдаления", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		ДобавитьПредопределенноеСвойство("Предопределенный", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Реквизит, ОбщиеПеременные, "Булево");
		Если ТипОбъекта = Перечисления.ТипыОбъектов.ПланВидовХарактеристик И ДанныеОбъекта.ТипыЗначенийХарактеристик.Количество() > 0 Тогда
			ДобавитьПредопределенноеСвойство("ТипЗначения", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Реквизит, ОбщиеПеременные, ДанныеОбъекта.ТипыЗначенийХарактеристик);
		КонецЕсли;
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.БизнесПроцесс Тогда
		ДобавитьПредопределенноеСвойство("Стартован", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		ДобавитьПредопределенноеСвойство("Завершен", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		ДобавитьПредопределенноеСвойство("ВедущаяЗадача", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные);
		ДанныеОбъекта.ПредопределенныеСвойства.Вставить("Дата", Новый Структура("КвалификаторыДаты_Состав", "Дата и время"));
		ДобавитьПредопределенноеСвойство("ПометкаУдаления", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.Задача Тогда
		ДобавитьПредопределенноеСвойство("Выполнена", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		ДанныеОбъекта.ПредопределенныеСвойства.Вставить("Дата", Новый Структура("КвалификаторыДаты_Состав", "Дата и время"));
		ДобавитьПредопределенноеСвойство("ПометкаУдаления", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.РегистрБухгалтерии Тогда
		ДобавитьПредопределенноеСвойство("Период", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Дата");
		ДобавитьПредопределенноеСвойство("Активность", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		ДобавитьПредопределенноеСвойство("Регистратор", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные);
		// Счета
		Если ДанныеОбъекта.Корреспонденция Тогда
			ДобавитьПредопределенноеСвойство("СчетДт", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, ДанныеОбъекта.ПланСчетов);
			ДобавитьПредопределенноеСвойство("СчетКт", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, ДанныеОбъекта.ПланСчетов);
			ДобавитьПредопределенноеСвойство("СубконтоДт", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.ВидыСубконтоСчета, ОбщиеПеременные);
			ДобавитьПредопределенноеСвойство("СубконтоКт", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.ВидыСубконтоСчета, ОбщиеПеременные);
		Иначе
			ДобавитьПредопределенноеСвойство("Счет", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, ДанныеОбъекта.ПланСчетов);
			ДобавитьПредопределенноеСвойство("Субконто", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.ВидыСубконтоСчета, ОбщиеПеременные);
		КонецЕсли;
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.РегистрНакопления Тогда
		ДобавитьПредопределенноеСвойство("Период", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Дата");
		ДобавитьПредопределенноеСвойство("Активность", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		ДобавитьПредопределенноеСвойство("Регистратор", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные);
		Если ДанныеОбъекта.ТипРегистраНакопления = "Остатки" Тогда
			ДобавитьПредопределенноеСвойство("ВидДвижения", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные);
		КонецЕсли;
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.РегистрСведений Тогда
		ДобавитьПредопределенноеСвойство("Активность", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		Если ДанныеОбъекта.ПериодичностьРегистраСведений <> "Nonperiodical" Тогда
			ДобавитьПредопределенноеСвойство("Период", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Дата");
		КонецЕсли;
		Если ДанныеОбъекта.РежимЗаписиРегистраСведений = "RecorderSubordinate" Тогда
			ДобавитьПредопределенноеСвойство("Регистратор", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные);
		КонецЕсли;
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.РегистрРасчета Тогда
		ДобавитьПредопределенноеСвойство("ПериодРегистрации", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Дата");
		ДобавитьПредопределенноеСвойство("Активность", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		ДобавитьПредопределенноеСвойство("Сторно", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		ДобавитьПредопределенноеСвойство("Регистратор", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные);
		ДобавитьПредопределенноеСвойство("ВидРасчета", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, ДанныеОбъекта.ПланВидовРасчета);
		Если ДанныеОбъекта.ЕстьПериодДействия Тогда
			ДобавитьПредопределенноеСвойство("ПериодДействия", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Дата");
			ДобавитьПредопределенноеСвойство("ПериодДействияНачало", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Дата");
			ДобавитьПредопределенноеСвойство("ПериодДействияКонец", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Дата");
		КонецЕсли;
		Если ДанныеОбъекта.ЕстьБазовыйПериод Тогда
			ДобавитьПредопределенноеСвойство("БазовыйПериодНачало", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Дата");
			ДобавитьПредопределенноеСвойство("БазовыйПериодКонец", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Дата");
		КонецЕсли;
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.ПланВидовРасчета Тогда
		Если ДанныеОбъекта.ЕстьПериодДействия Тогда
			ДобавитьПредопределенноеСвойство("ПериодДействияБазовый", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		КонецЕсли;
		ДобавитьПредопределенноеСвойство("ПометкаУдаления", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		//+КД3
		// Добавление предопределенных свойств без типов для котроля удаления ссылок свойств.
		// Типы будут добавлены в основном потоке после обработки всех объекетов.
		ДобавитьТабличныеЧастиВПланВидовРасчета(ТекОбъект.Ссылка, ОбщиеПеременные, Неопределено, ДанныеОбъекта.ЕстьПериодДействия);
		//-КД3
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.ПланОбмена Тогда
		ДобавитьПредопределенноеСвойство("ПометкаУдаления", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		ДобавитьПредопределенноеСвойство("ЭтотУзел", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		КвалификаторыНомера = Новый Структура("КвалификаторыЧисла_Неотрицательное", Истина);
		ДанныеОбъекта.ПредопределенныеСвойства.Вставить("НомерОтправленного", КвалификаторыНомера);
		ДанныеОбъекта.ПредопределенныеСвойства.Вставить("НомерПринятого", КвалификаторыНомера);
	ИначеЕсли ТипОбъекта = Перечисления.ТипыОбъектов.ПланСчетов Тогда
		ДобавитьПредопределенноеСвойство("Забалансовый", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		ДобавитьПредопределенноеСвойство("ПометкаУдаления", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
		ДобавитьПредопределенноеСвойство("Предопределенный", ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, "Булево");
	КонецЕсли;
	Для Каждого ПС Из ДанныеОбъекта.ПредопределенныеСвойства Цикл
		ДобавитьПредопределенноеСвойство(ПС.Ключ, ТекОбъект.Ссылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные,, ПС.Значение);
	КонецЦикла;
	ДанныеОбъекта.Вставить("ТекОбъект", ТекОбъект);
	// Продолжение чтения данных из файла.
	Если ОбщиеПеременные.ИсточникДанных = 0 Тогда
		ПозицияТочки = СтрНайти(ВложенныйФайл.ИсходноеИмяФайла, ".", НаправлениеПоиска.СКонца);
		Если ПозицияТочки > 0 Тогда
			ДанныеОбъекта.Вставить("ПутьКПодчиненным", Лев(ВложенныйФайл.ИсходноеИмяФайла, ПозицияТочки - 1));
		КонецЕсли;
		ПрочитатьСведенияОСвойствахИзXML(ЧтениеXML, ДанныеОбъекта, ОбщиеПеременные);
	Иначе
		ПрочитатьСведенияОСвойствахИзEDT(ЧтениеXML, ДанныеОбъекта, ОбщиеПеременные);
	КонецЕсли;
КонецПроцедуры

Функция ПрочитатьСведенияОбОбъектеИзXML(ЧтениеXML, ДанныеОбъекта, ОбщиеПеременные)
	ЧтениеXML.Прочитать(); // MetaDataObject
	ЧтениеXML.Прочитать(); // Объект - документ, справочник и т.п.
	УникальныйИдентификатор = одАтрибут(ЧтениеXML, "uuid");
	ЧтениеXML.Прочитать(); // InternalInfo
	ЧтениеXML.Пропустить();
	ЧтениеXML.Прочитать(); // Properties
	ТекОбъект = Неопределено;
	Имя = "";
	Если ЗначениеЗаполнено(УникальныйИдентификатор) И ОбщиеПеременные.СинхронизироватьПоУИД Тогда
		// Попытка найти объект по УИДу
		ТекСсылка = Справочники.Объекты.НайтиПоРеквизиту("Идентификатор", УникальныйИдентификатор, ДанныеОбъекта.ТекущийРодитель, ОбщиеПеременные.Конфигурация);
		Если ЗначениеЗаполнено(ТекСсылка) Тогда
			ТекОбъект = ТекСсылка.ПолучитьОбъект();
			Если ТекОбъект.ПометкаУдаления Тогда
				ТекОбъект.ПометкаУдаления = Ложь;
				Если ОбщиеПеременные.ТолькоДобавлятьНовые Тогда
					ТекОбъект.Записать();
				КонецЕсли;
			Иначе
				УдалитьОбъектИзСписка(ТекСсылка, ОбщиеПеременные.СуществующиеОбъекты);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	// Чтение блока Properties - заполнение реквизитов Объекта.
	// Также здесь читаются некоторые параметры свойств.
	Пока НЕ (ЧтениеXML.Имя = "Properties" И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента)  Цикл
		Если НЕ ЗначениеЗаполнено(ЧтениеXML.Имя) Тогда
			Прервать;
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			Если ЧтениеXML.Имя = "Name" Тогда
				Имя = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				// Не нашли объект по УИДу либо не задан УИД.
				Если ТекОбъект = Неопределено Тогда
					ТекСсылка = Справочники.Объекты.НайтиПоНаименованию(ДанныеОбъекта.ПрефиксИменЭлементов + "." + Имя, Истина, ДанныеОбъекта.ТекущийРодитель, ОбщиеПеременные.Конфигурация);
					Если НЕ ЗначениеЗаполнено(ТекСсылка)
						ИЛИ (ЗначениеЗаполнено(УникальныйИдентификатор)
						И ЗначениеЗаполнено(ТекСсылка.Идентификатор)
						И ТекСсылка.Идентификатор <> УникальныйИдентификатор
						И ОбщиеПеременные.СинхронизироватьПоУИД)
						И НЕ ОбщиеПеременные.ЭтоРасширение Тогда
						ТекОбъект = Справочники.Объекты.СоздатьЭлемент();
						ТекОбъект.Наименование = ДанныеОбъекта.ПрефиксИменЭлементов + "." + Имя;
						ТекОбъект.Идентификатор = УникальныйИдентификатор;
						ТекОбъект.Имя = Имя;
						ТекОбъект.Родитель = ДанныеОбъекта.ТекущийРодитель;
						ТекОбъект.Владелец = ОбщиеПеременные.Конфигурация;
						ТекОбъект.Тип = ДанныеОбъекта.ТипОбъекта;
						ДанныеОбъекта.Вставить("ДобавленНовый", Истина);
					Иначе
						ТекОбъект = ТекСсылка.ПолучитьОбъект();
						Если НЕ ЗначениеЗаполнено(ТекОбъект.Идентификатор) Или НЕ ОбщиеПеременные.ЭтоРасширение Тогда
							ТекОбъект.Идентификатор = УникальныйИдентификатор;
						КонецЕсли;
						Если ТекОбъект.ПометкаУдаления Тогда
							ТекОбъект.ПометкаУдаления = Ложь;
							Если ОбщиеПеременные.ТолькоДобавлятьНовые Тогда
								ТекОбъект.Записать();
							КонецЕсли;
						Иначе
							УдалитьОбъектИзСписка(ТекСсылка, ОбщиеПеременные.СуществующиеОбъекты);
						КонецЕсли;
					КонецЕсли;
				Иначе
					// Обновление наименования.
					ТекОбъект.Наименование = ДанныеОбъекта.ПрефиксИменЭлементов + "." + Имя;
					ТекОбъект.Имя = Имя;
				КонецЕсли;
				ДанныеОбъекта.ТипыВладельца = Новый Массив;
			ИначеЕсли ЧтениеXML.Имя = "Synonym" Тогда
				ТекОбъект.Синоним = ПрочитатьСиноним(ЧтениеXML);
			ИначеЕсли ЧтениеXML.Имя = "Comment" Тогда
				ТекОбъект.Комментарий = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
			ИначеЕсли ЧтениеXML.Имя = "Hierarchical" Тогда
				ТекОбъект.Иерархический = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
			ИначеЕсли ЧтениеXML.Имя = "Correspondence" Тогда
				ДанныеОбъекта.Корреспонденция = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
			ИначеЕсли  ЧтениеXML.Имя = "ChartOfAccounts" Тогда
				ДанныеОбъекта.ПланСчетов = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
			ИначеЕсли ЧтениеXML.Имя = "InformationRegisterPeriodicity" Тогда
				ДанныеОбъекта.ПериодичностьРегистраСведений = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
			ИначеЕсли ЧтениеXML.Имя = "WriteMode" Тогда
				ДанныеОбъекта.РежимЗаписиРегистраСведений = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
			ИначеЕсли ЧтениеXML.Имя = "HierarchyType" Тогда
				ТипИерархииСтрокой = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				Если ТипИерархииСтрокой = "HierarchyFoldersAndItems" Тогда
					ТекОбъект.ВидИерархии = "ИерархияГруппИЭлементов";
				ИначеЕсли ТипИерархииСтрокой = "HierarchyOfItems" Тогда
					ТекОбъект.ВидИерархии = "ИерархияЭлементов";
				Иначе
					ТекОбъект.ВидИерархии = "ИерархияГрупп";
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "LimitLevelCount" Тогда
				ТекОбъект.ОграничиватьКоличествоУровней = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
			ИначеЕсли ЧтениеXML.Имя = "LevelCount" Тогда
				ТекОбъект.КоличествоУровней = ПрочитатьXML(ЧтениеXML, Тип("Число"));
			ИначеЕсли ЧтениеXML.Имя = "CodeSeries" Тогда
				СерииКодовСтрокой = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				Если СерииКодовСтрокой = "WithinOwnerSubordination" Тогда
					ТекОбъект.СерииКодов = "ВПределахПодчиненияВладельцу";
				ИначеЕсли СерииКодовСтрокой = "WholeCatalog" Тогда
					ТекОбъект.СерииКодов = "ВоВсемСправочнике";
				Иначе
					ТекОбъект.СерииКодов = "ВПределахПодчинения";
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "CheckUnique" Тогда
				ТекОбъект.КонтрольУникальности = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
			ИначеЕсли ЧтениеXML.Имя = "Autonumbering" Тогда
				ТекОбъект.Автонумерация = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
			ИначеЕсли ЧтениеXML.Имя = "NumberPeriodicity" Тогда
				ПериодичностьСтрокой = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				Если ПериодичностьСтрокой = "Nonperiodical" Тогда
					ТекОбъект.Периодичность = "Непериодический";
				ИначеЕсли ПериодичностьСтрокой = "Year" Тогда
					ТекОбъект.Периодичность = "Год";
				ИначеЕсли ПериодичностьСтрокой = "Month" Тогда
					ТекОбъект.Периодичность = "Месяц";
				ИначеЕсли ПериодичностьСтрокой = "Quarter" Тогда
					ТекОбъект.Периодичность = "Квартал";
				ИначеЕсли ПериодичностьСтрокой = "Day" Тогда
					ТекОбъект.Периодичность = "День"; 
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "NumberLength" Тогда
				ДлинаНомера =  ПрочитатьXML(ЧтениеXML, Тип("Число"));
				Если ДлинаНомера > 0 Тогда
					ДанныеОбъекта.ПредопределенныеСвойства.Вставить("Номер", Новый Структура("КвалификаторыСтроки_Длина", ДлинаНомера));
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "NumberAllowedLength" Тогда
				ДопустимаяДлинаНомера = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				//+КД3 FIX Поддержка переменной длины номера
				Если ДанныеОбъекта.ПредопределенныеСвойства.Свойство("Номер") Тогда
					КвалификаторНомера = ДанныеОбъекта.ПредопределенныеСвойства.Номер;
					Если ДопустимаяДлинаНомера = "Fixed" Тогда
						КвалификаторНомера.Вставить("КвалификаторыСтроки_Фиксированная", Истина);
					ИначеЕсли ДопустимаяДлинаНомера = "Variable" Тогда
						КвалификаторНомера.Вставить("КвалификаторыСтроки_Фиксированная", Ложь);
					КонецЕсли;
				КонецЕсли;
				//-КД3 FIX 
			ИначеЕсли ЧтениеXML.Имя = "CodeLength" Тогда
				ДлинаКода = ПрочитатьXML(ЧтениеXML, Тип("Число"));
				Если ДлинаКода > 0 Тогда
					ДанныеОбъекта.ПредопределенныеСвойства.Вставить("Код", Новый Структура("КвалификаторыСтроки_Длина", ДлинаКода));
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "DescriptionLength" Тогда
				ДлинаНаименования = ПрочитатьXML(ЧтениеXML, Тип("Число"));
				Если ДлинаНаименования > 0 Тогда
					ДанныеОбъекта.ПредопределенныеСвойства.Вставить("Наименование", Новый Структура("КвалификаторыСтроки_Длина", ДлинаНаименования));
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "Task" Тогда
				ДанныеОбъекта.ИмяЗадачиБизнесПроцесса = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
			ИначеЕсли ЧтениеXML.Имя = "ChartOfCalculationTypes" Тогда
				ДанныеОбъекта.ПланВидовРасчета = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
			ИначеЕсли ЧтениеXML.Имя = "BasePeriod" Тогда
				ДанныеОбъекта.ЕстьБазовыйПериод = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
			ИначеЕсли ЧтениеXML.Имя = "ActionPeriod" Или ЧтениеXML.Имя = "ActionPeriodUse" Тогда
				ДанныеОбъекта.ЕстьПериодДействия = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
			ИначеЕсли ЧтениеXML.Имя = "Owners" Тогда
				Пока ЧтениеXML.Прочитать() Цикл
					Если ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента
						И ЧтениеXML.Имя = "Owners" Тогда
						Прервать;
					ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента
						И ЧтениеXML.Имя = "xr:Item" Тогда
						// Есть владельцы.
						ТекОбъект.Подчиненный = Истина;
					ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.Текст И ТекОбъект.Подчиненный Тогда
						ИмяТипаВладельца = ЧтениеXML.Значение;
						ДанныеОбъекта.ТипыВладельца.Добавить(ИмяТипаВладельца);
					КонецЕсли;
				КонецЦикла;
			ИначеЕсли ЧтениеXML.Имя = "RegisterRecords" Тогда
				Пока ЧтениеXML.Прочитать() Цикл
					Если ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента
						И ЧтениеXML.Имя = "RegisterRecords" Тогда
						Прервать;
					ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
						ИмяТипаРегистра = ЧтениеXML.Значение;
						ДанныеОбъекта.МассивРегистров.Добавить(ИмяТипаРегистра);
					КонецЕсли;
				КонецЦикла;
			ИначеЕсли ЧтениеXML.Имя = "RegisterType" Тогда
				// Тип регистра накопления
				ТипРегистра = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				Если ТипРегистра = "Balance" Тогда
					ДанныеОбъекта.ТипРегистраНакопления = "Остатки";
				ИначеЕсли ТипРегистра = "Turnovers" Тогда
					ДанныеОбъекта.ТипРегистраНакопления = "Обороты";
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "Type" Тогда
				// Для плана видов характеристик.
				Квалификаторы = Новый Структура;
				ПростойТип = "";
				ПрочитатьТипы(ЧтениеXML, ДанныеОбъекта.ТипыЗначенийХарактеристик, ПростойТип, Квалификаторы);
			ИначеЕсли ЧтениеXML.Имя = "Posting" Тогда
				ЕстьПроведение = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				Если ЕстьПроведение = "Deny" Тогда
					ДанныеОбъекта.ЗапрещеноПроведениеДокумента = Истина;
				КонецЕсли;
			Иначе
				ЧтениеXML.Прочитать();
			КонецЕсли;
		Иначе
			ЧтениеXML.Прочитать();
		КонецЕсли;
	КонецЦикла;
	Если ТекОбъект <> Неопределено
		И (НЕ ОбщиеПеременные.ТолькоДобавлятьНовые Или ДанныеОбъекта.ДобавленНовый) Тогда
		ТекОбъект.Записать();
	КонецЕсли;
	Возврат ТекОбъект;
КонецФункции

Функция ПрочитатьСведенияОбОбъектеИзEDT(ЧтениеXML, ДанныеОбъекта, ОбщиеПеременные)
	УникальныйИдентификатор = "";
	ЧтениеXML.Прочитать(); // mdclass
	УникальныйИдентификатор = одАтрибут(ЧтениеXML, "uuid");
	ЧтениеXML.Прочитать(); // producedTypes
	ЧтениеXML.Пропустить();
	Имя = "";
	Если ЗначениеЗаполнено(УникальныйИдентификатор) И ОбщиеПеременные.СинхронизироватьПоУИД Тогда
		// Попытка найти объект по УИДу
		ТекСсылка = Справочники.Объекты.НайтиПоРеквизиту("Идентификатор", УникальныйИдентификатор, ДанныеОбъекта.ТекущийРодитель, ОбщиеПеременные.Конфигурация);
		Если ЗначениеЗаполнено(ТекСсылка) Тогда
			ТекОбъект = ТекСсылка.ПолучитьОбъект();
			Если ТекОбъект.ПометкаУдаления Тогда
				ТекОбъект.ПометкаУдаления = Ложь;
				Если ОбщиеПеременные.ТолькоДобавлятьНовые Тогда
					ТекОбъект.Записать();
				КонецЕсли;
			Иначе
				УдалитьОбъектИзСписка(ТекСсылка, ОбщиеПеременные.СуществующиеОбъекты);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	//+КД3 FIX Значения по умолчанию, которые не добавляются в файл, а добавляются только их изменения
	Если ДанныеОбъекта.ТипОбъекта = Перечисления.ТипыОбъектов.РегистрНакопления Тогда
		ДанныеОбъекта.ТипРегистраНакопления = "Остатки";
	ИначеЕсли ДанныеОбъекта.ТипОбъекта = Перечисления.ТипыОбъектов.РегистрСведений Тогда
		ДанныеОбъекта.ПериодичностьРегистраСведений = "Nonperiodical";
	КонецЕсли;
	//+КД3 FIX

	Пока ЗначениеЗаполнено(ЧтениеXML.Имя) И НЕ (ЧтениеXML.Имя = "attributes" Или ЧтениеXML.Имя = "predefined"
		Или ЧтениеXML.Имя = "dimensions" Или ЧтениеXML.Имя = "resources"
		Или ЧтениеXML.Имя = "enumValues" Или ЧтениеXML.Имя = "tabularSections"
		Или ЧтениеXML.Имя = "content") Цикл
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			Если ЧтениеXML.Имя = "name" Тогда
				Имя = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				// Не нашли объект по УИДу либо не задан УИД.
				Если ТекОбъект = Неопределено Тогда 
					ТекСсылка = Справочники.Объекты.НайтиПоНаименованию(ДанныеОбъекта.ПрефиксИменЭлементов + "." + Имя, Истина, ДанныеОбъекта.ТекущийРодитель, ОбщиеПеременные.Конфигурация);
					Если НЕ ЗначениеЗаполнено(ТекСсылка) ИЛИ (ЗначениеЗаполнено(УникальныйИдентификатор)
						И ОбщиеПеременные.СинхронизироватьПоУИД
						И ЗначениеЗаполнено(ТекСсылка.Идентификатор)
						И ТекСсылка.Идентификатор <> УникальныйИдентификатор)
						И НЕ ОбщиеПеременные.ЭтоРасширение Тогда

						ТекОбъект = Справочники.Объекты.СоздатьЭлемент();
						ТекОбъект.Наименование = ДанныеОбъекта.ПрефиксИменЭлементов + "." + Имя;
						ТекОбъект.Идентификатор = УникальныйИдентификатор;
						ТекОбъект.Имя = Имя;
						ТекОбъект.Родитель = ДанныеОбъекта.ТекущийРодитель;
						ТекОбъект.Владелец = ОбщиеПеременные.Конфигурация;
						ТекОбъект.Тип = ДанныеОбъекта.ТипОбъекта;
						ДанныеОбъекта.ДобавленНовый = Истина;
					Иначе
						ТекОбъект = ТекСсылка.ПолучитьОбъект();
						Если НЕ ЗначениеЗаполнено(ТекОбъект.Идентификатор) Или НЕ ОбщиеПеременные.ЭтоРасширение Тогда
							ТекОбъект.Идентификатор = УникальныйИдентификатор;
						КонецЕсли;
						Если ТекОбъект.ПометкаУдаления Тогда
							ТекОбъект.ПометкаУдаления = Ложь;
							Если ОбщиеПеременные.ТолькоДобавлятьНовые Тогда
								ТекОбъект.Записать();
							КонецЕсли;
						Иначе
							УдалитьОбъектИзСписка(ТекСсылка, ОбщиеПеременные.СуществующиеОбъекты);
						КонецЕсли;
					КонецЕсли;
				Иначе
					// Обновление наименования.
					ТекОбъект.Наименование = ДанныеОбъекта.ПрефиксИменЭлементов + "." + Имя;
					ТекОбъект.Имя = Имя;
				КонецЕсли;
				ДанныеОбъекта.ТипыВладельца = Новый Массив;
			ИначеЕсли ЧтениеXML.Имя = "synonym" Тогда
				//+КД3 FIX Чтение синонима на первом языке как при загрузке из XML
				ТекСиноним = ПрочитатьСиноним(ЧтениеXML);
				Если ПустаяСтрока(ТекОбъект.Синоним) Тогда
					ТекОбъект.Синоним = ТекСиноним;
				Конецесли;
				//-КД3 FIX
			//+КД3 FIX
			ИначеЕсли ЧтениеXML.Имя = "comment" Тогда
				// В "Комментарий" сохраняется "Комментарий", а не "Описание" как при загрузке из XML
				ТекОбъект.Комментарий = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
			//ИначеЕсли ЧтениеXML.Имя = "explanation" Тогда
			//	ТекОбъект.Комментарий = ПрочитатьСиноним(ЧтениеXML);
			//+КД3 FIX
			ИначеЕсли ЧтениеXML.Имя = "standardAttributes"
				Или ЧтениеXML.Имя = "standardTabularSections" Тогда
				ЧтениеXML.Пропустить();
			ИначеЕсли ЧтениеXML.Имя = "owners" Тогда
				ИмяТипаВладельца = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				Если ЗначениеЗаполнено(ИмяТипаВладельца) Тогда
					ТекОбъект.Подчиненный = Истина;
					ДанныеОбъекта.ТипыВладельца.Добавить(ИмяТипаВладельца);
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "codeLength" Тогда
				ДлинаКода = ПрочитатьXML(ЧтениеXML, Тип("Число"));
				Если ДлинаКода > 0 Тогда
					ДанныеОбъекта.ПредопределенныеСвойства.Вставить("Код", Новый Структура("КвалификаторыСтроки_Длина", ДлинаКода));
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "numberLength" Тогда
				ДлинаНомера = ПрочитатьXML(ЧтениеXML, Тип("Число"));
				Если ДлинаНомера > 0 Тогда
					//+КД3 FIX По умолчанию длина номера - фиксированная
					КвалификаторНомера = Новый Структура("КвалификаторыСтроки_Длина", ДлинаНомера);
					КвалификаторНомера.Вставить("КвалификаторыСтроки_Фиксированная", Истина);
					ДанныеОбъекта.ПредопределенныеСвойства.Вставить("Номер", КвалификаторНомера);
					//-КД3 FIX 
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "levelCount" Тогда
				ТекОбъект.КоличествоУровней = ПрочитатьXML(ЧтениеXML, Тип("Число"));
			ИначеЕсли ЧтениеXML.Имя = "descriptionLength" Тогда
				ДлинаНаименования = ПрочитатьXML(ЧтениеXML, Тип("Число"));
				Если ДлинаНаименования > 0 Тогда
					ДанныеОбъекта.ПредопределенныеСвойства.Вставить("Наименование", Новый Структура("КвалификаторыСтроки_Длина", ДлинаНаименования));
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "hierarchical" Тогда
				ТекОбъект.Иерархический = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
			//+КД3 FIX Обработка типа иерархии для загрузки из EDT
			ИначеЕсли ЧтениеXML.Имя = "hierarchyType" Тогда
				ТипИерархииСтрокой = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				Если ТипИерархииСтрокой = "HierarchyOfItems" Тогда
					ТекОбъект.ВидИерархии = "ИерархияЭлементов";
				Иначе
					ТекОбъект.ВидИерархии = "ИерархияГрупп";
				КонецЕсли;
			//-КД3 FIX
			ИначеЕсли ЧтениеXML.Имя = "checkUnique" Тогда
				ТекОбъект.КонтрольУникальности = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
			ИначеЕсли ЧтениеXML.Имя = "autonumbering" Тогда
				ТекОбъект.Автонумерация = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
			ИначеЕсли ЧтениеXML.Имя = "numberAllowedLength" Тогда
				ДопустимаяДлинаНомера = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				//+КД3 FIX В EDT "numberAllowedLength" добавляется для переменной дины номера
				Если ДопустимаяДлинаНомера = "Variable" И ДанныеОбъекта.ПредопределенныеСвойства.Свойство("Номер") Тогда
					КвалификаторНомера = ДанныеОбъекта.ПредопределенныеСвойства.Номер;
					КвалификаторНомера.Вставить("КвалификаторыСтроки_Фиксированная", Ложь);
				КонецЕсли;
				//-КД3 FIX

			ИначеЕсли ЧтениеXML.Имя = "codeSeries" Тогда
				СерииКодовСтрокой = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				Если СерииКодовСтрокой = "WithinOwnerSubordination" Тогда
					ТекОбъект.СерииКодов = "ВПределахПодчиненияВладельцу";
				ИначеЕсли СерииКодовСтрокой = "WholeCatalog" Тогда
					ТекОбъект.СерииКодов = "ВоВсемСправочнике";
				Иначе
					ТекОбъект.СерииКодов = "ВПределахПодчинения";
				КонецЕсли; 
			ИначеЕсли ЧтениеXML.Имя = "correspondence" Тогда
				ДанныеОбъекта.Корреспонденция = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
			ИначеЕсли  ЧтениеXML.Имя = "chartOfAccounts" Тогда
				ДанныеОбъекта.ПланСчетов = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
			ИначеЕсли ЧтениеXML.Имя = "task" Тогда
				ДанныеОбъекта.ИмяЗадачиБизнесПроцесса = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
			ИначеЕсли ЧтениеXML.Имя = "basePeriod" Тогда
				ДанныеОбъекта.ЕстьБазовыйПериод = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
			//+КД3 FIX Корректное имя "actionPeriodUse"
			ИначеЕсли ЧтениеXML.Имя = "actionPeriod" Или ЧтениеXML.Имя = "actionPeriodUse" Тогда
			//-КД3 FIX
				ДанныеОбъекта.ЕстьПериодДействия = ПрочитатьXML(ЧтениеXML, Тип("Булево"));
			ИначеЕсли ЧтениеXML.Имя = "chartOfCalculationTypes" Тогда
				ДанныеОбъекта.ПланВидовРасчета = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
			ИначеЕсли ЧтениеXML.Имя = "informationRegisterPeriodicity" Тогда
				ДанныеОбъекта.ПериодичностьРегистраСведений = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
			ИначеЕсли ЧтениеXML.Имя = "writeMode" Тогда
				ДанныеОбъекта.РежимЗаписиРегистраСведений = ПрочитатьXML(ЧтениеXML, Тип("Строка")); 
			ИначеЕсли НРег(ЧтениеXML.Имя) = "type" Тогда
				// Для плана видов характеристик.
				Квалификаторы = Новый Структура;
				ПростойТип = "";
				ПрочитатьТипы(ЧтениеXML, ДанныеОбъекта.ТипыЗначенийХарактеристик, ПростойТип, Квалификаторы);
			ИначеЕсли ЧтениеXML.Имя = "registerRecords" Тогда
				// Движения по регистрам
				ИмяТипаРегистра = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				ДанныеОбъекта.МассивРегистров.Добавить(ИмяТипаРегистра);
			ИначеЕсли НРег(ЧтениеXML.Имя) = "registertype" Тогда
				// Тип регистра накопления
				ТипРегистра = НРег(ПрочитатьXML(ЧтениеXML, Тип("Строка")));
				Если ТипРегистра = "balance" Тогда
					ДанныеОбъекта.ТипРегистраНакопления = "Остатки";
				ИначеЕсли ТипРегистра = "turnovers" Тогда
					ДанныеОбъекта.ТипРегистраНакопления = "Обороты";
				КонецЕсли;
				
			ИначеЕсли ЧтениеXML.Имя = "posting" Тогда
				ЕстьПроведение = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				Если ЕстьПроведение = "Deny" Тогда
					ДанныеОбъекта.ЗапрещеноПроведениеДокумента = Истина;
				КонецЕсли; 
			Иначе
				ЧтениеXML.Пропустить(); 
			КонецЕсли;
		Иначе
			ЧтениеXML.Прочитать();
		КонецЕсли;
	КонецЦикла;
	Если НЕ ОбщиеПеременные.ТолькоДобавлятьНовые Или ДанныеОбъекта.ДобавленНовый Тогда
		ТекОбъект.Записать();
	КонецЕсли;
	Возврат ТекОбъект;
КонецФункции

Процедура ПрочитатьСведенияОСвойствахИзXML(ЧтениеXML, ДанныеОбъекта, ОбщиеПеременные) 
	ТекОбъект = ДанныеОбъекта.ТекОбъект;
	ЧтениеXML.Прочитать();

	ТекСвойство = Неопределено;
	ТекЗначение = Неопределено;
	ТекГруппа = Неопределено;
	ТекТипыСвойства = Новый Массив;
	ТекущийРодительСвойства = Неопределено;
	ЭтоТЧ = Ложь;
	ЭтоЗначение = Ложь;
	УИДСвойства = "";
	Если ЧтениеXML.Имя = "ChildObjects" И ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
		Пока ЧтениеXML.Прочитать() Цикл
			Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				Если ЧтениеXML.Имя = "TabularSection" Тогда
					ЭтоТЧ = Истина;
					ЧтениеXML.Прочитать(); // InternalInfo
					ЧтениеXML.Пропустить();
					ЧтениеXML.Прочитать(); // Properties
					ТекГруппа = Справочники.Свойства.СоздатьГруппу();
					ТекГруппа.Владелец = ТекОбъект.Ссылка;
					ТекГруппа.Вид = Перечисления.ВидыСвойств.ТабличнаяЧасть;
					УИДСвойства = одАтрибут(ЧтениеXML, "uuid");
					Продолжить;
				ИначеЕсли ЧтениеXML.Имя = "Command" Тогда
					ЧтениеXML.Пропустить();
					Продолжить;
				ИначеЕсли ЧтениеXML.Имя = "Attribute"
					Или ЧтениеXML.Имя = "Dimension"
					Или ЧтениеXML.Имя = "Resource"
					Или (ЧтениеXML.Имя = "AccountingFlag" И ДанныеОбъекта.ТипОбъекта <> Перечисления.ТипыОбъектов.РегистрБухгалтерии) Тогда
					ТекСвойство = Справочники.Свойства.СоздатьЭлемент();
					ТекСвойство.Владелец = ТекОбъект.Ссылка;
					Если ЧтениеXML.Имя = "Attribute" Или ЧтениеXML.Имя = "AccountingFlag" Тогда
						ТекСвойство.Вид = Перечисления.ВидыСвойств.Реквизит;
						Если ТекущийРодительСвойства <> Неопределено Тогда
							ТекСвойство.Родитель = ТекущийРодительСвойства;
						КонецЕсли;
					ИначеЕсли ЧтениеXML.Имя = "Dimension" Тогда
						ТекСвойство.Вид = Перечисления.ВидыСвойств.Измерение;
						Баланс = Ложь;
					ИначеЕсли ЧтениеXML.Имя = "Resource" Тогда
						ТекСвойство.Вид = Перечисления.ВидыСвойств.Ресурс;
						Баланс = Ложь; 
					КонецЕсли;
					ТекТипыСвойства = Новый Массив;
					УИДСвойства = одАтрибут(ЧтениеXML, "uuid");
				ИначеЕсли ЧтениеXML.Имя = "EnumValue" Тогда
					ЭтоЗначение = Истина;
					ЭтоТЧ = Ложь;
					ТекСвойство = Справочники.Значения.СоздатьЭлемент();
					ТекСвойство.Владелец = ТекОбъект.Ссылка;
					ТекСвойство.Предопределенное = Истина;
					УИДСвойства = одАтрибут(ЧтениеXML, "uuid");
				ИначеЕсли ЧтениеXML.Имя = "Name" Тогда
					Если ТекГруппа = Неопределено И ТекСвойство = Неопределено Тогда
						ЧтениеXML.Пропустить();
						Продолжить;
					КонецЕсли;
					Наименование = одЗначениеЭлемента(ЧтениеXML);
					Если ЭтоТЧ Тогда
						ТекГруппа.Наименование = Наименование;
						ТекГруппа.Идентификатор = УИДСвойства;
					Иначе
						ТекСвойство.Наименование = Наименование;
						ТекСвойство.Идентификатор = УИДСвойства;
					КонецЕсли;
				ИначеЕсли ЧтениеXML.Имя = "Synonym" Тогда
					Если ТекГруппа = Неопределено И ТекСвойство = Неопределено Тогда
						ЧтениеXML.Пропустить();
						Продолжить;
					КонецЕсли;

					ТекСиноним = ПрочитатьСиноним(ЧтениеXML);
					Если ЭтоТЧ Тогда
						ТекГруппа.Синоним = ТекСиноним;
					Иначе
						ТекСвойство.Синоним = ТекСиноним;
					КонецЕсли;
				ИначеЕсли ЧтениеXML.Имя = "Comment" Тогда
					Если ТекГруппа = Неопределено И ТекСвойство = Неопределено Тогда
						ЧтениеXML.Пропустить();
						Продолжить;
					КонецЕсли;
					Если ЭтоТЧ Тогда
						ТекГруппа.Комментарий = одЗначениеЭлемента(ЧтениеXML);
					Иначе
						ТекСвойство.Комментарий = одЗначениеЭлемента(ЧтениеXML);
					КонецЕсли;
				ИначеЕсли ЧтениеXML.Имя = "Type" Тогда
					Если ТекГруппа = Неопределено И ТекСвойство = Неопределено Тогда
						ЧтениеXML.Пропустить();
						Продолжить;
					КонецЕсли; 
					Квалификаторы = Новый Структура;
					ПростойТип = "";
					ПрочитатьТипы(ЧтениеXML, ТекТипыСвойства, ПростойТип, Квалификаторы);
					Для Каждого Квалификатор Из Квалификаторы Цикл
						ТекСвойство[Квалификатор.Ключ] = Квалификатор.Значение;
					КонецЦикла;
					Если ПростойТип <> "" Тогда
						ЗаполнитьПростойТипСвойства(ОбщиеПеременные, ТекСвойство, ПростойТип, ТекТипыСвойства);
					КонецЕсли;
				ИначеЕсли ЧтениеXML.Имя = "Balance" Тогда
					// Регистр бухгалтерии, Балансовое или не балансовое измерение/ресурс.
					Баланс = одЗначениеЭлемента(ЧтениеXML, Тип("Булево"));
				КонецЕсли;
			ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
				Если ЧтениеXML.Имя = "Attribute" Или ЧтениеXML.Имя = "Dimension"
					Или ЧтениеXML.Имя = "Resource" 
					Или (ЧтениеXML.Имя = "AccountingFlag" И ДанныеОбъекта.ТипОбъекта <> Перечисления.ТипыОбъектов.РегистрБухгалтерии) Тогда
					Если (ЧтениеXML.Имя <> "Attribute" И ЧтениеXML.Имя <> "AccountingFlag") 
						И ДанныеОбъекта.ТипОбъекта = Перечисления.ТипыОбъектов.РегистрБухгалтерии
						И ДанныеОбъекта.Корреспонденция
						И НЕ Баланс Тогда
						// Измерения/ресурса два - Дт и Кт
						// УИД очищаем т.к. становится не информативно.
						ИсходноеИмя = ТекСвойство.Наименование;
						ТекСвойство.Идентификатор = "";
						ТекСвойствоСсылка = ПереопределитьСвойствоИлиЗначение(ТекСвойство, ОбщиеПеременные, Ложь, ИсходноеИмя + "Дт");
						ДобавитьТипВОтложенноеЗаполнение(ОбщиеПеременные, ТекСвойствоСсылка, ТекТипыСвойства);
						
						// Создаем второе свойство
						ТекСвойствоНов = ТекСвойство.Скопировать();
						ТекСвойствоНов.Наименование = ИсходноеИмя + "Кт";
						ТекСвойствоСсылка = ПереопределитьСвойствоИлиЗначение(ТекСвойствоНов, ОбщиеПеременные, Ложь, ИсходноеИмя + "Кт");
						ДобавитьТипВОтложенноеЗаполнение(ОбщиеПеременные, ТекСвойствоСсылка, ТекТипыСвойства);
					Иначе
						ТекСвойствоСсылка = ПереопределитьСвойствоИлиЗначение(ТекСвойство, ОбщиеПеременные, Ложь);
						ДобавитьТипВОтложенноеЗаполнение(ОбщиеПеременные, ТекСвойствоСсылка, ТекТипыСвойства);
					КонецЕсли;
				ИначеЕсли ЧтениеXML.Имя = "Properties" И ЭтоТЧ Тогда
					ТекГруппаСсылка = ПереопределитьСвойствоИлиЗначение(ТекГруппа, ОбщиеПеременные, Истина);
					ЭтоТЧ = Ложь;
					ТекущийРодительСвойства = ТекГруппаСсылка;
				ИначеЕсли ЧтениеXML.Имя = "TabularSection" Тогда
					ТекущийРодитель = Неопределено;
					ЭтоТЧ = Ложь;
					ТекущийРодительСвойства = Неопределено;
					ТекГруппа = Ложь;
				ИначеЕсли ЧтениеXML.Имя = "EnumValue" Тогда
					ПереопределитьСвойствоИлиЗначение(ТекСвойство, ОбщиеПеременные);
					ЭтоЗначение = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	ЧтениеXML.Закрыть();
	//+КД3 FIX Сброс эталона свойства, которое ниже используется как эталон значения
	ТекСвойство = Неопределено;
	//-КД3 FIX
	Если ДанныеОбъекта.ТипОбъекта = Перечисления.ТипыОбъектов.Справочник
		Или ДанныеОбъекта.ТипОбъекта = Перечисления.ТипыОбъектов.ПланВидовРасчета
		Или ДанныеОбъекта.ТипОбъекта = Перечисления.ТипыОбъектов.ПланВидовХарактеристик
		Или ДанныеОбъекта.ТипОбъекта = Перечисления.ТипыОбъектов.ПланСчетов Тогда
		// Предопределенные значения в отдельном файле.
		Если НЕ ДанныеОбъекта.Свойство("ПутьКПодчиненным") Тогда
			Возврат;
		КонецЕсли;
		ПутьКПредопределенным = ДанныеОбъекта.ПутьКПодчиненным + ОбщиеПеременные.РазделительПути + "Ext" + ОбщиеПеременные.РазделительПути + "Predefined.xml"; 
		ФайлСПредопределенными = НайтиПомещенныеФайлыПоИмени(ОбщиеПеременные, ПутьКПредопределенным);
		Если ФайлСПредопределенными = "" Тогда
			Возврат;
		КонецЕсли;

		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.ОткрытьФайл(ФайлСПредопределенными);
		ИерархияЗначений = Новый Массив;

		Пока ЧтениеXML.Прочитать() Цикл
			Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				Если ЧтениеXML.Имя = "Item" Тогда
					Если ТекСвойство <> Неопределено Тогда
						Если НЕ ЗначениеЗаполнено(ТекСвойство.Ссылка) Тогда
							ТекСвойство.Записать();
						КонецЕсли;
						ИерархияЗначений.Добавить(ТекСвойство.Ссылка);
					КонецЕсли;

					ТекСвойство = Справочники.Значения.СоздатьЭлемент();
					ТекСвойство.Владелец = ТекОбъект.Ссылка;
					ТекСвойство.Предопределенное = Истина;
					УИДСвойства = одАтрибут(ЧтениеXML, "uuid");
					Если ИерархияЗначений.Количество() > 0 Тогда
						ТекСвойство.Родитель = ИерархияЗначений[ИерархияЗначений.Количество() - 1];
					КонецЕсли;
				ИначеЕсли ЧтениеXML.Имя = "Name" Тогда
					Наименование = одЗначениеЭлемента(ЧтениеXML);
					// Пробуем найти существующее значение.
					СвойствоСсылка = Неопределено;
					НайденоПоУИД = Истина;
					Если ЗначениеЗаполнено(УИДСвойства) И ОбщиеПеременные.СинхронизироватьПоУИД Тогда
						СвойствоСсылка = Справочники.Значения.НайтиПоРеквизиту("Идентификатор", УИДСвойства,, ТекОбъект.Ссылка);
					КонецЕсли;
					Если НЕ ЗначениеЗаполнено(СвойствоСсылка) Тогда
						НайденоПоУИД = Ложь;
						СвойствоСсылка = Справочники.Значения.НайтиПоНаименованию(Наименование, Истина,, ТекОбъект.Ссылка);
					КонецЕсли;
					Если ЗначениеЗаполнено(СвойствоСсылка) 
						И (НайденоПоУИД Или НЕ ЗначениеЗаполнено(СвойствоСсылка.Идентификатор) Или ОбщиеПеременные.ЭтоРасширение) Тогда
						// Переопределяем переменную содержащую текущее значение.
						ТекущийРодитель = ТекСвойство.Родитель;
						ТекСвойство = СвойствоСсылка.ПолучитьОбъект();
						Если ТекСвойство.ПометкаУдаления Тогда
							ТекСвойство.ПометкаУдаления = Ложь;
						КонецЕсли;
						Если НЕ НайденоПоУИД Тогда
							ТекСвойство.Идентификатор = УИДСвойства;
						КонецЕсли;
						ТекСвойство.Наименование = Наименование; 
						ТекСвойство.Родитель = ТекущийРодитель;
						УдалитьОбъектИзСписка(СвойствоСсылка, ОбщиеПеременные.СуществующиеЗначения);
					Иначе	
						ТекСвойство.Наименование = Наименование;
						ТекСвойство.Идентификатор = УИДСвойства;
					КонецЕсли;
				ИначеЕсли ЧтениеXML.Имя = "Description" Тогда
					ТекСвойство.Синоним = одЗначениеЭлемента(ЧтениеXML);
				ИначеЕсли ЧтениеXML.Имя = "ChildItems" Тогда
					Продолжить;
				КонецЕсли;
			ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента
				И ЧтениеXML.Имя = "Item" Тогда
				Если ТекСвойство = Неопределено Тогда
					Если  ИерархияЗначений.Количество() > 0 Тогда
						ИерархияЗначений.Удалить(ИерархияЗначений.Количество()-1);
					КонецЕсли;
				Иначе
					ТекСвойство.Записать();
					ТекСвойство = Неопределено;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	ИначеЕсли ДанныеОбъекта.ТипОбъекта = Перечисления.ТипыОбъектов.ПланОбмена Тогда
		// Состав в отдельном файле.
		Если НЕ ДанныеОбъекта.Свойство("ПутьКПодчиненным") Тогда
			Возврат;
		КонецЕсли;

		ПутьКСоставу = ДанныеОбъекта.ПутьКПодчиненным + ОбщиеПеременные.РазделительПути + "Ext" + ОбщиеПеременные.РазделительПути + "Content.xml"; 
		ФайлССоставом = НайтиПомещенныеФайлыПоИмени(ОбщиеПеременные, ПутьКСоставу);
		Если ФайлССоставом = "" Тогда
			Возврат;
		КонецЕсли;
		ТЧСоставСсылка = Справочники.Свойства.НайтиПоНаименованию("{Состав}", Истина,, ТекОбъект.Ссылка);
		Если ЗначениеЗаполнено(ТЧСоставСсылка) Тогда
			Если ТЧСоставСсылка.ПометкаУдаления Тогда
				ТЧСоставОбъект = ТЧСоставСсылка.ПолучитьОбъект();
				ТЧСоставОбъект.ПометкаУдаления = Ложь;
				ТЧСоставОбъект.Записать();
			КонецЕсли;
			УдалитьОбъектИзСписка(ТЧСоставСсылка, ОбщиеПеременные.СуществующиеСвойства);
		Иначе
			ТЧСоставОбъект = Справочники.Свойства.СоздатьГруппу();
			ТЧСоставОбъект.Владелец = ТекОбъект.Ссылка;
			ТЧСоставОбъект.Наименование = "{Состав}";
			ТЧСоставОбъект.Синоним = "{Состав}";
			ТЧСоставОбъект.Вид = Перечисления.ВидыСвойств.СоставПланаОбмена;
			ТЧСоставОбъект.Записать();
			ТЧСоставСсылка = ТЧСоставОбъект.Ссылка;
		КонецЕсли;
		
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.ОткрытьФайл(ФайлССоставом);
		ЧтениеXML.Прочитать(); // ExchangePlanContent
		Пока НЕ (ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента
				И ЧтениеXML.Имя = "ExchangePlanContent") Цикл
			Если  ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента
				И ЧтениеXML.Имя = "Metadata" Тогда
				ИмяОбъекта = одЗначениеЭлемента(ЧтениеXML); 
				РодительОбъекта = Неопределено;
				СвойствоОбъект = Неопределено;
				ПолноеИмяОбъекта = ПреобразоватьИмяТипаВИмяОбъекта(ИмяОбъекта, РодительОбъекта, ОбщиеПеременные);
				Если ЗначениеЗаполнено(ПолноеИмяОбъекта) Тогда
					ОбъектСсылка = Справочники.Объекты.НайтиПоНаименованию(ПолноеИмяОбъекта, Истина, РодительОбъекта,ОбщиеПеременные.Конфигурация);
					Если ЗначениеЗаполнено(ОбъектСсылка) Тогда
						НайтиСоздатьЭлементСоставаПланаОбмена(ОбъектСсылка, ОбщиеПеременные, ТекОбъект.Ссылка, ТЧСоставСсылка);
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента
				И ЧтениеXML.Имя = "AutoRecord" И СвойствоОбъект <> Неопределено Тогда
				АвторегистрацияСтрокой = одЗначениеЭлемента(ЧтениеXML);
				СвойствоОбъект.Авторегистрация = (АвторегистрацияСтрокой <> "Deny");
			ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента
				И ЧтениеXML.Имя = "Item" И СвойствоОбъект <> Неопределено Тогда
				СвойствоОбъект.Записать();
				ЧтениеXML.Прочитать();
			Иначе
				ЧтениеXML.Прочитать();
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	ЧтениеXML.Закрыть();

КонецПроцедуры

Процедура ПрочитатьСведенияОСвойствахИзEDT(ЧтениеXML, ДанныеОбъекта, ОбщиеПеременные)
	ТекОбъект = ДанныеОбъекта.ТекОбъект;

	ТекСвойство = Неопределено;
	СтруктураХраненияТипов = Новый Структура;
	ТекЗначение = Неопределено;
	ТекГруппа = Неопределено;
	ТекТипыСвойства = Новый Массив;
	ТекущийРодительСвойства = Неопределено;
	ИерархияЗначений = Новый Массив;
	ЭтоТЧ = Ложь;
	ЭтоЗначение = Ложь;
	УИДСвойства = "";
	ТЧСоставСсылка = Неопределено;
	Если НЕ ЗначениеЗаполнено(ЧтениеXML.Имя) Тогда
		ЧтениеXML.Пропустить();
	КонецЕсли;
	Пока ЧтениеXML.Имя <> "mdclass" И ЗначениеЗаполнено(ЧтениеXML.Имя) Цикл
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			Если ЧтениеXML.Имя = "tabularSections" Тогда
				ЭтоТЧ = Истина;
				УИДСвойства = одАтрибут(ЧтениеXML, "uuid");
				ЧтениеXML.Прочитать(); // producedTypes
				ЧтениеXML.Пропустить();
				ЧтениеXML.Прочитать(); // name
				ТекГруппа = Справочники.Свойства.СоздатьГруппу();
				ТекГруппа.Владелец = ТекОбъект.Ссылка;
				ТекГруппа.Наименование = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				ТекГруппа.Вид = Перечисления.ВидыСвойств.ТабличнаяЧасть;
				ТекГруппа.Идентификатор = УИДСвойства;
				ТекСвойство = Неопределено;
				ТекущийРодительСвойства = Неопределено;
				Продолжить;
			ИначеЕсли ЧтениеXML.Имя = "attributes"
				Или ЧтениеXML.Имя = "dimensions"
				Или ЧтениеXML.Имя = "resources"
				//+КД3 FIX Обработка параметров учета плана счетов
				Или (ЧтениеXML.Имя = "accountingFlags" И НЕ ЭтоЗначение И ДанныеОбъекта.ТипОбъекта <> Перечисления.ТипыОбъектов.РегистрБухгалтерии) Тогда
				//-КД3 FIX 
				ТекСвойство = Справочники.Свойства.СоздатьЭлемент();
				ТекСвойство.Владелец = ТекОбъект.Ссылка;
				//+КД3 FIX Обработка параметров учета плана счетов
				Если ЧтениеXML.Имя = "attributes" Или ЧтениеXML.Имя = "accountingFlags" Тогда
				//-КД3 FIX 
					ТекСвойство.Вид = Перечисления.ВидыСвойств.Реквизит;
				ИначеЕсли ЧтениеXML.Имя = "dimensions" Тогда
					ТекСвойство.Вид = Перечисления.ВидыСвойств.Измерение;
					Баланс = Ложь;
				ИначеЕсли ЧтениеXML.Имя = "resources" Тогда
					ТекСвойство.Вид = Перечисления.ВидыСвойств.Ресурс;
					Баланс = Ложь; 
				КонецЕсли;
				ТекТипыСвойства = Новый Массив;
				УИДСвойства = одАтрибут(ЧтениеXML, "uuid");
				ТекСвойство.Идентификатор = УИДСвойства;

				Если ЭтоТЧ Тогда
					Если ТекущийРодительСвойства = Неопределено Тогда
						ТекущийРодительСвойства = ПереопределитьСвойствоИлиЗначение(ТекГруппа, ОбщиеПеременные, Истина);
					КонецЕсли;
					ТекСвойство.Родитель = ТекущийРодительСвойства;
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "predefined" Тогда
				// Начало узла с предопределенными.
				// Читаем дальше.
			ИначеЕсли ЧтениеXML.Имя = "items"
				Или ЧтениеXML.Имя = "enumValues"
				Или (ЧтениеXML.Имя = "content" И ЭтоЗначение = Истина)
				Или (ЧтениеXML.Имя = "childItems" И ЭтоЗначение = Истина) Тогда
				ЭтоЗначение = Истина;
				ЭтоТЧ = Ложь;
				УИДСвойства = одАтрибут(ЧтениеXML, "uuid");
				ТекРодитель = Неопределено;
				Если ТекСвойство <> Неопределено Тогда
					ТекРодитель = ПереопределитьСвойствоИлиЗначение(ТекСвойство, ОбщиеПеременные);
					ИерархияЗначений.Добавить(ТекРодитель);
				КонецЕсли;
				ТекСвойство = Справочники.Значения.СоздатьЭлемент();
				ТекСвойство.Владелец = ТекОбъект.Ссылка;
				ТекСвойство.Предопределенное = Истина;
				ТекСвойство.Идентификатор = УИДСвойства;
				Если ИерархияЗначений.Количество() > 0 Тогда
					ТекСвойство.Родитель = ИерархияЗначений[ИерархияЗначений.Количество() - 1];
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "name" Тогда
				Если ТекГруппа = Неопределено И ТекСвойство = Неопределено Тогда
					ЧтениеXML.Пропустить();
					Продолжить;
				КонецЕсли;
				Наименование = одЗначениеЭлемента(ЧтениеXML);
				Если ЭтоТЧ И ТекСвойство = Неопределено Тогда
					ТекГруппа.Наименование = Наименование;
				Иначе
					ТекСвойство.Наименование = Наименование;
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "synonym" Тогда
				Если ТекГруппа = Неопределено И ТекСвойство = Неопределено Тогда
					ЧтениеXML.Пропустить();
					Продолжить;
				КонецЕсли;
				ТекСиноним = ПрочитатьСиноним(ЧтениеXML);
				//+КД3 FIX Чтение синонима на первом языке как при загрузке из XML
				Если ЭтоТЧ И ТекСвойство = Неопределено Тогда
					Если ПустаяСтрока(ТекГруппа.Синоним) Тогда
						ТекГруппа.Синоним = ТекСиноним;
					КонецЕсли;
				Иначе
					Если ПустаяСтрока(ТекСвойство.Синоним) Тогда
						ТекСвойство.Синоним = ТекСиноним;	
					КонецЕсли;
				КонецЕсли;
				//-КД3 FIX
			ИначеЕсли ЧтениеXML.Имя = "toolTip" Тогда
				Если ТекГруппа = Неопределено И ТекСвойство = Неопределено Тогда
					ЧтениеXML.Пропустить();
					Продолжить;
				КонецЕсли;
				ТекКомментарий = ПрочитатьСиноним(ЧтениеXML);
				Если ЭтоТЧ И ТекСвойство = Неопределено Тогда
					ТекГруппа.Комментарий = ТекКомментарий;
				Иначе
					ТекСвойство.Комментарий = ТекКомментарий;
				КонецЕсли;
			//+КД3 FIX Поддержка типов расширения
			ИначеЕсли НРег(ЧтениеXML.Имя) = "type" ИЛИ (НРег(ЧтениеXML.Имя) = "typeextension" И ОбщиеПеременные.ЭтоРасширение) Тогда
			//-КД3 FIX
				Если ТекГруппа = Неопределено И ТекСвойство = Неопределено Тогда
					ЧтениеXML.Пропустить();
					Продолжить;
				КонецЕсли;
				Квалификаторы = Новый Структура;
				ПростойТип = "";
				ПрочитатьТипы(ЧтениеXML, ТекТипыСвойства, ПростойТип, Квалификаторы);
				Если ТипЗнч(ТекСвойство) = Тип("СправочникОбъект.Значения") Тогда
					Продолжить;
				КонецЕсли;
				Для Каждого Квалификатор Из Квалификаторы Цикл
					ТекСвойство[Квалификатор.Ключ] = Квалификатор.Значение;
				КонецЦикла;
				Если ПростойТип <> "" Тогда
					ЗаполнитьПростойТипСвойства(ОбщиеПеременные, ТекСвойство, ПростойТип, ТекТипыСвойства);
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "balance" Тогда
				// Регистр бухгалтерии, Балансовое или не балансовое измерение/ресурс.
				Баланс = одЗначениеЭлемента(ЧтениеXML, Тип("Булево"));
			ИначеЕсли ДанныеОбъекта.ТипОбъекта = Перечисления.ТипыОбъектов.ПланОбмена И ЧтениеXML.Имя = "content" Тогда
				// Состав плана обмена.
				Если ТЧСоставСсылка = Неопределено Тогда
					ТЧСоставСсылка = Справочники.Свойства.НайтиПоНаименованию("{Состав}", Истина,, ТекОбъект.Ссылка);
					Если ЗначениеЗаполнено(ТЧСоставСсылка) Тогда
						Если ТЧСоставСсылка.ПометкаУдаления Тогда
							ТЧСоставОбъект = ТЧСоставСсылка.ПолучитьОбъект();
							ТЧСоставОбъект.ПометкаУдаления = Ложь;
							ТЧСоставОбъект.Записать();
						КонецЕсли;
						УдалитьОбъектИзСписка(ТЧСоставСсылка, ОбщиеПеременные.СуществующиеСвойства);
					Иначе
						ТЧСоставОбъект = Справочники.Свойства.СоздатьГруппу();
						ТЧСоставОбъект.Владелец = ТекОбъект.Ссылка;
						ТЧСоставОбъект.Наименование = "{Состав}";
						ТЧСоставОбъект.Синоним = "{Состав}";
						ТЧСоставОбъект.Вид = Перечисления.ВидыСвойств.СоставПланаОбмена;
						ТЧСоставОбъект.Записать();
						ТЧСоставСсылка = ТЧСоставОбъект.Ссылка;
					КонецЕсли; 
				КонецЕсли;
				ЧтениеXML.Прочитать(); // mdObject
				ИмяОбъектаМД = одЗначениеЭлемента(ЧтениеXML);
				РодительПО = Неопределено;
				СвойствоПО = Неопределено;
				ПолноеИмяОбъекта = ПреобразоватьИмяТипаВИмяОбъекта(ИмяОбъектаМД, РодительПО, ОбщиеПеременные);
				Если ЗначениеЗаполнено(ПолноеИмяОбъекта) Тогда
					ОбъектПОСсылка = Справочники.Объекты.НайтиПоНаименованию(ПолноеИмяОбъекта, Истина, РодительПО,ОбщиеПеременные.Конфигурация);
					Если ЗначениеЗаполнено(ОбъектПОСсылка) Тогда
						ДанныеТекОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОбъектПОСсылка, "Имя, Синоним, Наименование");
					Иначе
						ДанныеТекОбъекта = Новый Структура("Имя, Синоним, Наименование");
						ДанныеТекОбъекта.Наименование = ПолноеИмяОбъекта;
						ПозТочки = СтрНайти(ПолноеИмяОбъекта, ".");
						Если ПозТочки > 0 Тогда
							ДанныеТекОбъекта.Синоним = Сред(ПолноеИмяОбъекта, ПозТочки + 1);
						Иначе
							ДанныеТекОбъекта.Синоним = ПолноеИмяОбъекта;
						КонецЕсли;
						ДанныеТекОбъекта.Имя = ДанныеТекОбъекта.Синоним;
					КонецЕсли; 
					НайтиСоздатьЭлементСоставаПланаОбмена(ОбъектПОСсылка, ОбщиеПеременные, ТекОбъект.Ссылка, ТЧСоставСсылка);
				КонецЕсли;
			Иначе
				ЧтениеXML.Пропустить(); 
				Продолжить;
			КонецЕсли;
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			Если ЧтениеXML.Имя = "attributes" Или ЧтениеXML.Имя = "dimensions"
				Или ЧтениеXML.Имя = "resources"
				//+КД3 FIX Обработка параметров учета плана счетов
				Или (ЧтениеXML.Имя = "accountingFlags" И НЕ ЭтоЗначение И ДанныеОбъекта.ТипОбъекта <> Перечисления.ТипыОбъектов.РегистрБухгалтерии) Тогда
				//-КД3 FIX
				Если ЧтениеXML.Имя <> "attributes" И ЧтениеXML.Имя <> "accountingFlags"  
					И ДанныеОбъекта.ТипОбъекта = Перечисления.ТипыОбъектов.РегистрБухгалтерии
					И ДанныеОбъекта.Корреспонденция
					И НЕ Баланс Тогда
					// Измерения/ресурса два - Дт и Кт
					// УИД очищаем т.к. становится не информативно.
					ИсходноеИмя = ТекСвойство.Наименование;
					ТекСвойство.Идентификатор = "";
					ТекСвойствоСсылка = ПереопределитьСвойствоИлиЗначение(ТекСвойство, ОбщиеПеременные, Ложь, ИсходноеИмя + "Дт");
					ДобавитьТипВОтложенноеЗаполнение(ОбщиеПеременные, ТекСвойствоСсылка, ТекТипыСвойства);
					
					// Создаем второе свойство
					ТекСвойствоНов = ТекСвойство.Скопировать();
					ТекСвойствоНов.Наименование = ИсходноеИмя + "Кт";
					ТекСвойствоСсылка = ПереопределитьСвойствоИлиЗначение(ТекСвойство, ОбщиеПеременные, Ложь, ИсходноеИмя + "Кт");
					ДобавитьТипВОтложенноеЗаполнение(ОбщиеПеременные, ТекСвойствоСсылка, ТекТипыСвойства);
				Иначе
					ТекСвойствоСсылка = ПереопределитьСвойствоИлиЗначение(ТекСвойство, ОбщиеПеременные, Ложь);
					ДобавитьТипВОтложенноеЗаполнение(ОбщиеПеременные, ТекСвойствоСсылка, ТекТипыСвойства);
					ТекСвойство = Неопределено;
				КонецЕсли;
			ИначеЕсли ЧтениеXML.Имя = "tabularSections" Тогда
				ЭтоТЧ = Ложь;
				ТекущийРодительСвойства = Неопределено;
			
			ИначеЕсли ЧтениеXML.Имя = "items" Или ЧтениеXML.Имя = "enumValues"
				Или ((ЧтениеXML.Имя = "content" Или ЧтениеXML.Имя = "childItems") И ЭтоЗначение) Тогда
				Если ТекСвойство = Неопределено Тогда
					Если ИерархияЗначений.Количество() > 0 Тогда
						ИерархияЗначений.Удалить(ИерархияЗначений.Количество() - 1);
					КонецЕсли;
				Иначе
					ПереопределитьСвойствоИлиЗначение(ТекСвойство, ОбщиеПеременные);
					ТекСвойство = Неопределено;
				КонецЕсли;
				Если (ЧтениеXML.Имя = "items" Или ЧтениеXML.Имя = "enumValues") Тогда
					ЭтоЗначение = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		ЧтениеXML.Прочитать();
	КонецЦикла;
	ЧтениеXML.Закрыть();
КонецПроцедуры

Функция ПереопределитьСвойствоИлиЗначение(ЭталонныйОбъект, ОбщиеПеременные, ЭтоТЧ = Ложь, Знач Наименование = "")
	// Пробуем найти существующую группу или свойство.
	СвойствоСсылка = Неопределено;
	НайденоПоУИД = Истина;
	ЭтоЗначение = Ложь;
	Если ТипЗнч(ЭталонныйОбъект) = Тип("СправочникОбъект.Значения") Тогда
		МенеджерОбъекта = Справочники.Значения;
		ЭтоЗначение = Истина;
		ТекущийРодитель = Неопределено;
	Иначе
		МенеджерОбъекта = Справочники.Свойства;
		ТекущийРодитель = ЭталонныйОбъект.Родитель;
	КонецЕсли;
	УИД = ЭталонныйОбъект.Идентификатор;
	Если ОбщиеПеременные.СинхронизироватьПоУИД И ЗначениеЗаполнено(УИД) Тогда
		Если ТекущийРодитель = Неопределено Тогда
			СвойствоСсылка = МенеджерОбъекта.НайтиПоРеквизиту("Идентификатор", УИД,, ЭталонныйОбъект.Владелец);
		Иначе
			СвойствоСсылка = МенеджерОбъекта.НайтиПоРеквизиту("Идентификатор", УИД,ТекущийРодитель, ЭталонныйОбъект.Владелец);
		КонецЕсли;
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(СвойствоСсылка) Тогда
		НайденоПоУИД = Ложь;
		НаименованиеДляПоиска = Наименование;
		Если НаименованиеДляПоиска = "" Тогда
			НаименованиеДляПоиска = ЭталонныйОбъект.Наименование;
		КонецЕсли;
		Если ТекущийРодитель = Неопределено Тогда
			СвойствоСсылка = МенеджерОбъекта.НайтиПоНаименованию(НаименованиеДляПоиска, Истина,, ЭталонныйОбъект.Владелец);
		Иначе
			СвойствоСсылка = МенеджерОбъекта.НайтиПоНаименованию(НаименованиеДляПоиска, Истина,ТекущийРодитель, ЭталонныйОбъект.Владелец);
		КонецЕсли;
	КонецЕсли;
	Если ЗначениеЗаполнено(СвойствоСсылка) 
		И (ЭтоЗначение Или СвойствоСсылка.ЭтоГруппа = ЭтоТЧ)
		И (НайденоПоУИД Или НЕ ЗначениеЗаполнено(СвойствоСсылка.Идентификатор) Или НЕ ОбщиеПеременные.СинхронизироватьПоУИД Или ОбщиеПеременные.ЭтоРасширение) Тогда
		Записывать = Ложь;
		// Переопределяем переменную содержащую текущую группу или свойство.
		Если ЭтоТЧ Тогда
			ТекГруппа = СвойствоСсылка.ПолучитьОбъект();
			Если ТекГруппа.ПометкаУдаления Тогда
				ТекГруппа.ПометкаУдаления = Ложь;
				Записывать = Истина;
			КонецЕсли;
			Если НЕ НайденоПоУИД Тогда
				ТекГруппа.Идентификатор = УИД;
				Записывать = Истина;
			КонецЕсли;
			Если НЕ ОбщиеПеременные.ТолькоДобавлятьНовые  Тогда
				Если ЗначениеЗаполнено(Наименование) Тогда
					ТекГруппа.Наименование = Наименование;
				Иначе
					ТекГруппа.Наименование = ЭталонныйОбъект.Наименование;
				КонецЕсли;
				ЗаполнитьЗначенияСвойств(ТекГруппа, ЭталонныйОбъект, "Синоним, Комментарий, Вид");
				Записывать = Истина;
			КонецЕсли;
			Если Записывать Тогда
				ТекГруппа.Записать();
			КонецЕсли;
		Иначе
			ТекСвойство = СвойствоСсылка.ПолучитьОбъект();
			Если ТекСвойство.ПометкаУдаления Тогда
				ТекСвойство.ПометкаУдаления = Ложь;
				Записывать = Истина;
			КонецЕсли;
			Если НЕ НайденоПоУИД И ЗначениеЗаполнено(УИД) Тогда
				ТекСвойство.Идентификатор = УИД;
				Записывать = Истина;
			КонецЕсли;
			Если НЕ ОбщиеПеременные.ТолькоДобавлятьНовые  Тогда
				Записывать = Истина;
				Если ЗначениеЗаполнено(Наименование) Тогда   					
					ТекСвойство.Наименование = Наименование;
				ИначеЕсли НайденоПоУИД Тогда
					ТекСвойство.Наименование = ЭталонныйОбъект.Наименование;
				КонецЕсли;
				ЗаполнитьЗначенияСвойств(ТекСвойство, ЭталонныйОбъект,,"Наименование, Идентификатор, Код, Владелец");
				ТекСвойство.Типы.Загрузить(ЭталонныйОбъект.Типы.Выгрузить());
			КонецЕсли;
			Если Записывать Тогда
				ТекСвойство.Записать();
			КонецЕсли;
		КонецЕсли;
		Если ЭтоЗначение Тогда
			УдалитьОбъектИзСписка(СвойствоСсылка, ОбщиеПеременные.СуществующиеЗначения);
		Иначе
			УдалитьОбъектИзСписка(СвойствоСсылка, ОбщиеПеременные.СуществующиеСвойства);
		КонецЕсли;
		Возврат СвойствоСсылка;
	Иначе
		Если ЗначениеЗаполнено(Наименование) Тогда
			ЭталонныйОбъект.Наименование = Наименование;
		КонецЕсли;
		ЭталонныйОбъект.Записать();
		Возврат ЭталонныйОбъект.Ссылка;
	КонецЕсли;

КонецФункции

Процедура ЗагрузитьПодпискуНаСобытие(ОбщиеПеременные, ТекущийРодитель, ВложенныйФайл, ПрефиксИменЭлементов, ТипОбъекта)
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ВложенныйФайл.ТекущееИмяФайла);
	УникальныйИдентификатор = "";
	Имя = "";
	ТекОбъект = Неопределено;
	Если ОбщиеПеременные.ИсточникДанных = 0 Тогда
		ЧтениеXML.Прочитать(); // MetaDataObject
		ЧтениеXML.Прочитать(); // Объект - подписка на событие
		УникальныйИдентификатор = одАтрибут(ЧтениеXML, "uuid");
		ЧтениеXML.Прочитать(); // Properties
	Иначе
		ЧтениеXML.Прочитать(); // mdclass
		УникальныйИдентификатор = одАтрибут(ЧтениеXML, "uuid"); 
	КонецЕсли;
	Если ЗначениеЗаполнено(УникальныйИдентификатор) И ОбщиеПеременные.СинхронизироватьПоУИД Тогда
		// Попытка найти объект по УИДу
		ТекСсылка = Справочники.Объекты.НайтиПоРеквизиту("Идентификатор", УникальныйИдентификатор, ТекущийРодитель, ОбщиеПеременные.Конфигурация);
		Если ЗначениеЗаполнено(ТекСсылка) Тогда
			ТекОбъект = ТекСсылка.ПолучитьОбъект();
			Если ТекОбъект.ПометкаУдаления Тогда
				ТекОбъект.ПометкаУдаления = Ложь;
				Если ОбщиеПеременные.ТолькоДобавлятьНовые Тогда
					ТекОбъект.Записать();
				КонецЕсли;
			Иначе
				УдалитьОбъектИзСписка(ТекСсылка, ОбщиеПеременные.СуществующиеОбъекты);
			КонецЕсли;
			Если ОбщиеПеременные.ТолькоДобавлятьНовые Тогда
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	//+КД3 FIX Пропуск начала узла Properties / mdclass:EventSubscription
	ЧтениеXML.Прочитать(); 
	
	Пока НЕ ((ЧтениеXML.Имя = "Properties" ИЛИ СтрНайти(ЧтениеXML.Имя, "mdclass") > 0) И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента)
	//-КД3 FIX
		И ЗначениеЗаполнено(ЧтениеXML.Имя) Цикл
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ТекИмяУзла = НРег(ЧтениеXML.Имя);
			Если ТекИмяУзла = "name" Тогда
				Имя = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				// Не нашли объект по УИДу либо не задан УИД.
				Если ТекОбъект = Неопределено Тогда
					ТекСсылка = Справочники.Объекты.НайтиПоНаименованию(ПрефиксИменЭлементов + "." + Имя, Истина, ТекущийРодитель, ОбщиеПеременные.Конфигурация);
					Если НЕ ЗначениеЗаполнено(ТекСсылка)
						ИЛИ (ЗначениеЗаполнено(УникальныйИдентификатор)
						И ОбщиеПеременные.СинхронизироватьПоУИД
						И ЗначениеЗаполнено(ТекСсылка.Идентификатор)
						И ТекСсылка.Идентификатор <> УникальныйИдентификатор
						И НЕ ОбщиеПеременные.ЭтоРасширение) Тогда
						ТекОбъект = Справочники.Объекты.СоздатьЭлемент();
						ТекОбъект.Наименование = ПрефиксИменЭлементов + "." + Имя;
						ТекОбъект.Идентификатор = УникальныйИдентификатор;
						ТекОбъект.Имя = Имя;
						ТекОбъект.Родитель = ТекущийРодитель;
						ТекОбъект.Владелец = ОбщиеПеременные.Конфигурация;
						ТекОбъект.Тип = ТипОбъекта;
					Иначе
						ТекОбъект = ТекСсылка.ПолучитьОбъект();
						ТекОбъект.Идентификатор = УникальныйИдентификатор;
						Если ТекОбъект.ПометкаУдаления Тогда
							ТекОбъект.ПометкаУдаления = Ложь;
							Если ОбщиеПеременные.ТолькоДобавлятьНовые Тогда
								ТекОбъект.Записать();
							КонецЕсли;
						Иначе
							УдалитьОбъектИзСписка(ТекСсылка, ОбщиеПеременные.СуществующиеОбъекты);
						КонецЕсли;
						Если ОбщиеПеременные.ТолькоДобавлятьНовые Тогда
							ЧтениеXML.Закрыть();
							Возврат;
						КонецЕсли;
						//+КД3 FIX Чтение синонима на первом языке как при загрузке из XML
						ТекОбъект.Синоним = "";
						//-КД3 FIX
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли ТекИмяУзла = "synonym" Тогда
				ТекСиноним = ПрочитатьСиноним(ЧтениеXML);
				//+КД3 FIX Чтение синонима на первом языке как при загрузке из XML
				Если ПустаяСтрока(ТекОбъект.Синоним) Тогда
					ТекОбъект.Синоним = ТекСиноним;
				КонецЕсли;
				//-КД3 FIX
			ИначеЕсли ТекИмяУзла = "comment" Тогда
				ТекОбъект.Комментарий = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
			ИначеЕсли ТекИмяУзла = "source" Тогда
				ТекОбъект.Записать();
				// Источник подписки на события будет загружаться в свойства - объект должен быть записан.
				Квалификаторы = Новый Структура;
				ПростойТип = "";
				ИсточникиПодписки = Новый Массив;
				ПрочитатьТипы(ЧтениеXML, ИсточникиПодписки, ПростойТип, Квалификаторы);
				//+КД3 FIX Загрузка определяемых типов в источниках подписки
				Если ИсточникиПодписки.Количество() = 1 Тогда
					Источник = ИсточникиПодписки[0];
					Если СтрНайти(НРег(Источник), "definedtype.") > 0 И ОбщиеПеременные.Свойство("ОпределяемыеТипы") Тогда
						ПозицияТочки = СтрНайти(Источник, ".");
						ИмяТипа = Сред(Источник, ПозицияТочки + 1);
						Если НЕ ОбщиеПеременные.ОпределяемыеТипы.Свойство(ИмяТипа, ИсточникиПодписки) Тогда
							ИсточникиПодписки = Новый Массив;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				Для Каждого Источник Из ИсточникиПодписки Цикл
					Если ТипЗнч(Источник) = Тип("СправочникСсылка.Объекты") Тогда
						ОбъектСсылка = Источник;
					Иначе
						РодительОбъекта = Неопределено;
						ПолноеИмяОбъекта = ПреобразоватьИмяТипаВИмяОбъекта(Источник, РодительОбъекта, ОбщиеПеременные);
						Если НЕ ЗначениеЗаполнено(ПолноеИмяОбъекта) Тогда
							// Возможно это константа. Добавим набор констант (один) вместо отдельных значений констант.
							Если СтрНайти(Источник, "ConstantValueManager") > 0 Тогда
								СвойствоОбъект = НайтиСоздатьСвойствоПоНаименованию("КонстантыНабор", ТекОбъект.Ссылка, ОбщиеПеременные);
								ОбъектСсылка = Справочники.Объекты.НайтиПоНаименованию("КонстантыНабор", Истина, ,ОбщиеПеременные.Конфигурация);
								СвойствоОбъект.Вид = Перечисления.ВидыСвойств.ИсточникПодпискиНаСобытие;
								СвойствоОбъект.Наименование = "КонстантыНабор";
								СвойствоОбъект.Синоним = "Набор констант";
								СвойствоОбъект.ТипыСтрокой = "КонстантыНабор";
								СтрокаТип = СвойствоОбъект.Типы.Добавить();
								СтрокаТип.Тип = ОбъектСсылка;
								СвойствоОбъект.Записать();						
								Прервать;
							КонецЕсли;
							Продолжить;
						КонецЕсли;
						ОбъектСсылка = Справочники.Объекты.НайтиПоНаименованию(ПолноеИмяОбъекта, Истина, РодительОбъекта,ОбщиеПеременные.Конфигурация);
						Если НЕ ЗначениеЗаполнено(ОбъектСсылка) Тогда
							Продолжить;
						КонецЕсли;
					КонецЕсли;
					ДанныеТекОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОбъектСсылка, "Наименование, Имя, Синоним");
					СвойствоОбъект = НайтиСоздатьСвойствоПоНаименованию(ДанныеТекОбъекта.Имя, ТекОбъект.Ссылка, ОбщиеПеременные);
					СвойствоОбъект.Вид = Перечисления.ВидыСвойств.ИсточникПодпискиНаСобытие;
					СвойствоОбъект.Наименование = ДанныеТекОбъекта.Имя;
					СвойствоОбъект.Синоним = ДанныеТекОбъекта.Синоним;
					СвойствоОбъект.ТипыСтрокой = ДанныеТекОбъекта.Наименование;
					//+КД3 FIX Повторное добавление типа при загрузки в существующую конфигурацию
					СвойствоОбъект.Типы.Очистить();
					//-КД3 FIX 
					СтрокаТип = СвойствоОбъект.Типы.Добавить();
					СтрокаТип.Тип = ОбъектСсылка;
					СвойствоОбъект.Записать();
				КонецЦикла;
				//-КД3 FIX
		//+КД3 FIX Загрузка в формате XML
			Иначе
				ЧтениеXML.Пропустить();
			КонецЕсли;
		Иначе
			ЧтениеXML.Прочитать();
		КонецЕсли;
		//-КД3 FIX
	КонецЦикла;
	ЧтениеXML.Закрыть();
КонецПроцедуры

Процедура ЗагрузитьОбщийРеквизит(ОбщиеПеременные, ВложенныйФайл)
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.ОткрытьФайл(ВложенныйФайл.ТекущееИмяФайла);
	УникальныйИдентификатор = "";
	Имя = "";
	ТекОбъект = Неопределено;
	Если ОбщиеПеременные.ИсточникДанных = 0 Тогда
		ЧтениеXML.Прочитать(); // MetaDataObject
		ЧтениеXML.Прочитать(); // Объект - подписка на событие
		УникальныйИдентификатор = одАтрибут(ЧтениеXML, "uuid");
		ЧтениеXML.Прочитать(); // Properties
	Иначе
		ЧтениеXML.Прочитать(); // mdclass
		УникальныйИдентификатор = одАтрибут(ЧтениеXML, "uuid"); 
	КонецЕсли;
	
	ЧтениеXML.Прочитать(); 
	ТекСвойствоОбразец = Справочники.Свойства.СоздатьЭлемент();
	ТекСвойствоОбразец.Вид = Перечисления.ВидыСвойств.ОбщийРеквизит;
	ТекСвойствоОбразец.Идентификатор = УникальныйИдентификатор;
	ТекТипыСвойства = Новый Массив;
	Пока НЕ ((ЧтениеXML.Имя = "Properties" ИЛИ ЧтениеXML.Имя = "mdclass") И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента)
		И ЗначениеЗаполнено(ЧтениеXML.Имя) Цикл
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			ТекИмяУзла = НРег(ЧтениеXML.Имя);
			Если ТекИмяУзла = "name" Тогда
				Имя = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				ТекСвойствоОбразец.Наименование = Имя;
				Продолжить;
			ИначеЕсли ТекИмяУзла = "synonym" Тогда
				ТекСвойствоОбразец.Синоним = ПрочитатьСиноним(ЧтениеXML);
				Продолжить;
			ИначеЕсли ТекИмяУзла = "comment" Тогда
				ТекСвойствоОбразец.Комментарий = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				Продолжить;
			ИначеЕсли ТекИмяУзла = "type" Тогда
				Квалификаторы = Новый Структура;
				ПростойТип = "";
				ПрочитатьТипы(ЧтениеXML, ТекТипыСвойства, ПростойТип, Квалификаторы);
				Для Каждого Квалификатор Из Квалификаторы Цикл
					ТекСвойствоОбразец[Квалификатор.Ключ] = Квалификатор.Значение;
				КонецЦикла;
				Если ПростойТип <> "" Тогда
					ТекТипыСвойстваКопия = Новый Массив;
					Для Каждого ТекТип Из ТекТипыСвойства Цикл
						ТекТипыСвойстваКопия.Добавить(ТекТип);
					КонецЦикла;
					ЗаполнитьПростойТипСвойства(ОбщиеПеременные, ТекСвойствоОбразец, ПростойТип, ТекТипыСвойстваКопия);
				КонецЕсли;
			ИначеЕсли ТекИмяУзла = "content" Тогда
				ПрочитатьПринадлежностьОбщегоРеквизита(ЧтениеXML, ТекСвойствоОбразец, ТекТипыСвойства, ОбщиеПеременные);
			Иначе
				ЧтениеXML.Пропустить();
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ЧтениеXML.Прочитать();
	КонецЦикла;
	ЧтениеXML.Закрыть();

КонецПроцедуры

Процедура ПрочитатьПринадлежностьОбщегоРеквизита(ЧтениеXML, ТекСвойствоОбразец, ТекТипыСвойства, ОбщиеПеременные) 
	Пока ЧтениеXML.Прочитать() Цикл
		ТекИмяУзла = НРег(ЧтениеXML.Имя);
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			Если ТекИмяУзла = НРег("xr:Item") Тогда
				Продолжить;
			ИначеЕсли (ОбщиеПеременные.ИсточникДанных = 1 И ТекИмяУзла = "content") Тогда
				Продолжить;
			ИначеЕсли ТекИмяУзла = НРег("metadata") Или ТекИмяУзла = НРег("xr:Metadata") Тогда
				ТипОбъектаСтрокой = одЗначениеЭлемента(ЧтениеXML, Тип("Строка")); 
				РодительОбъекта = Неопределено;
				ПолноеИмяОбъекта = ПреобразоватьИмяТипаВИмяОбъекта(ТипОбъектаСтрокой, РодительОбъекта, ОбщиеПеременные);
				Если ЗначениеЗаполнено(ПолноеИмяОбъекта) Тогда
					ОбъектСсылка = Справочники.Объекты.НайтиПоНаименованию(ПолноеИмяОбъекта, Истина, РодительОбъекта,ОбщиеПеременные.Конфигурация);
					Если НЕ ЗначениеЗаполнено(ОбъектСсылка) Тогда
						Продолжить;
					КонецЕсли;
					// Попытка найти свойство.
					СвойствоСсылка = Неопределено;
					НайденоПоУИД = Истина;
					Если ОбщиеПеременные.СинхронизироватьПоУИД И ЗначениеЗаполнено(ТекСвойствоОбразец.Идентификатор) Тогда
						СвойствоСсылка = Справочники.Свойства.НайтиПоРеквизиту("Идентификатор", ТекСвойствоОбразец.Идентификатор,, ОбъектСсылка);
					КонецЕсли;
					Если НЕ ЗначениеЗаполнено(СвойствоСсылка) Тогда
						НайденоПоУИД = Ложь;
						СвойствоСсылка = Справочники.Свойства.НайтиПоНаименованию(ТекСвойствоОбразец.Наименование, Истина,, ОбъектСсылка);
					КонецЕсли;
					Записывать = Ложь;
					Если ЗначениеЗаполнено(СвойствоСсылка) Тогда
						СвойствоОбъект = СвойствоСсылка.ПолучитьОбъект();
						Если СвойствоСсылка.ПометкаУдаления = Истина Тогда
							Записывать = Истина;
						КонецЕсли;
						Если НЕ НайденоПоУИД И ЗначениеЗаполнено(ТекСвойствоОбразец.Идентификатор) Тогда
							СвойствоОбъект.Идентификатор = ТекСвойствоОбразец.Идентификатор;
							Записывать = Истина;
						КонецЕсли;
						Если НЕ ОбщиеПеременные.ТолькоДобавлятьНовые Тогда
							Записывать = Истина;
						КонецЕсли;
						Если ТекСвойствоОбразец.Вид <> СвойствоОбъект.Вид Тогда
							Записывать = Истина;
						КонецЕсли;
						Если НЕ Записывать Тогда
							Продолжить;
						КонецЕсли;
					Иначе
						Записывать = Истина;
						СвойствоОбъект = Справочники.Свойства.СоздатьЭлемент();
						СвойствоОбъект.Владелец = ОбъектСсылка;
						СвойствоОбъект.Идентификатор = ТекСвойствоОбразец.Идентификатор;
						СвойствоОбъект.УстановитьНовыйКод();
					КонецЕсли;
					ЗаполнитьЗначенияСвойств(СвойствоОбъект, ТекСвойствоОбразец,,"Владелец, Код, Идентификатор");
					СвойствоОбъект.ПометкаУдаления = Ложь;
					СвойствоОбъект.Записать();
					ДобавитьТипВОтложенноеЗаполнение(ОбщиеПеременные, СвойствоОбъект.Ссылка, ТекТипыСвойства);
					УдалитьОбъектИзСписка(СвойствоОбъект.Ссылка, ОбщиеПеременные.СуществующиеСвойства);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

Процедура ПрочитатьТипы(ЧтениеXML, ТекТипыСвойства, ПростойТипСтрокой, Квалификаторы)
	//+КД3 FIX Имена "type" / "types" для EDT в зависимости от ЭтоРасширение
	КорневойУзел = НРег(ЧтениеXML.Имя);
	ДетальныйУзелEDT = ?(КорневойУзел = "typeextension", "type", "types");
	//-КД3 FIX
	ЕстьТипыПростые = Ложь;
	ЕстьТипыСсылочные = Ложь;
	Пока ЧтениеXML.Прочитать() Цикл
		ТекИмяУзла = НРег(ЧтениеXML.Имя);

		//+КД3 FIX Имена "type" / "types" для EDT в зависимости от ЭтоРасширение
		Если ТекИмяУзла = КорневойУзел Тогда
			Прервать;
		//-КД3 FIX
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			//+КД3 FIX Имена "type" / "types" для EDT в зависимости от ЭтоРасширение
			Если ТекИмяУзла = "v8:type" Или ТекИмяУзла = ДетальныйУзелEDT Или ТекИмяУзла = "v8:typeset" Тогда
			//-КД3 FIX
				ЗначениеТипа = одЗначениеЭлемента(ЧтениеXML);
				ЗначениеТипаПриведенное = НРег(ЗначениеТипа);
				Если СтрНайти(ЗначениеТипаПриведенное, "string") > 0 Тогда
					ТекТипыСвойства.Добавить("Строка");
					ЕстьТипыПростые = Истина;
				ИначеЕсли СтрНайти(ЗначениеТипаПриведенное, "boolean") > 0 Тогда
					ТекТипыСвойства.Добавить("Булево");
					ЕстьТипыПростые = Истина;
				ИначеЕсли СтрНайти(ЗначениеТипаПриведенное, "datetime") > 0
					Или СтрНайти(ЗначениеТипаПриведенное, "date") > 0 Тогда
					ТекТипыСвойства.Добавить("Дата");
					ЕстьТипыПростые = Истина;
				ИначеЕсли СтрНайти(ЗначениеТипаПриведенное, "decimal") > 0
					Или СтрНайти(ЗначениеТипаПриведенное, "number") > 0
					Или СтрНайти(ЗначениеТипаПриведенное, "integer") > 0
					Или СтрНайти(ЗначениеТипаПриведенное, "double") > 0 Тогда
					ТекТипыСвойства.Добавить("Число");
					ЕстьТипыПростые = Истина;
				ИначеЕсли СтрНайти(ЗначениеТипаПриведенное, "valuestorage") > 0 Тогда
					ТекТипыСвойства.Добавить("ХранилищеЗначения");
					ЕстьТипыПростые = Истина;
				ИначеЕсли СтрНайти(ЗначениеТипаПриведенное, "uuid") > 0 Тогда
					ТекТипыСвойства.Добавить("УникальныйИдентификатор");
					ЕстьТипыПростые = Истина; 
				ИначеЕсли СтрНайти(ЗначениеТипаПриведенное, "valuelisttype") > 0 Тогда
					ТекТипыСвойства.Добавить("СписокЗначений");
					ЕстьТипыПростые = Истина; 
				ИначеЕсли СтрНайти(ЗначениеТипаПриведенное, "valuetable") > 0 Тогда
					ТекТипыСвойства.Добавить("ТаблицаЗначений");
					ЕстьТипыПростые = Истина; 
				ИначеЕсли СтрНайти(ЗначениеТипаПриведенное, "valuetree") > 0 Тогда
					ТекТипыСвойства.Добавить("ДеревоЗначений");
					ЕстьТипыПростые = Истина; 
				Иначе
					ЕстьТипыСсылочные = Истина;
					ТекТипыСвойства.Добавить(ЗначениеТипа);
				КонецЕсли;
			ИначеЕсли СтрНайти(НРег(ТекИмяУзла), "stringqualifiers")>0 Тогда
				 ЧтениеXML.Прочитать();
				 Если СтрНайти(НРег(ЧтениеXML.Имя), "length")>0 Тогда
				 	Квалификаторы.Вставить("КвалификаторыСтроки_Длина", одЗначениеЭлемента(ЧтениеXML, Тип("Число")));
				//+КД3 FIX По умолчанию длина строки 0
				Иначе
					Квалификаторы.Вставить("КвалификаторыСтроки_Длина", 0);
				//-КД3 FIX
				КонецЕсли;
			ИначеЕсли СтрНайти(ТекИмяУзла, "numberqualifiers")>0 Тогда
				Если ТекТипыСвойства.Найти("Число") = Неопределено Тогда
					ТекТипыСвойства.Добавить("Число");
					ЕстьТипыПростые = Истина;
				КонецЕсли;
				Пока ЧтениеXML.Прочитать() Цикл
					ТекИмяПодчиненногоУзла = НРег(ЧтениеXML.Имя);
					Если СтрНайти(ТекИмяПодчиненногоУзла, "numberqualifiers") > 0 Тогда
						Прервать;
					ИначеЕсли СтрНайти(ТекИмяПодчиненногоУзла, "fractiondigits")>0 Тогда
						Квалификаторы.Вставить("КвалификаторыЧисла_Точность", одЗначениеЭлемента(ЧтениеXML, Тип("Число")));
					ИначеЕсли СтрНайти(ТекИмяПодчиненногоУзла, "digits")>0 Тогда
						Квалификаторы.Вставить("КвалификаторыЧисла_Длина", одЗначениеЭлемента(ЧтениеXML, Тип("Число")));
					ИначеЕсли СтрНайти(ТекИмяПодчиненногоУзла, "allowedsign")>0 Тогда
						СтроковоеЗначение = НРег(одЗначениеЭлемента(ЧтениеXML));
						Квалификаторы.Вставить("КвалификаторыЧисла_Неотрицательное", СтроковоеЗначение = "nonnegative");
					//+КД3 FIX Чтение признака числа "Неотрицательное" в EDT
					ИначеЕсли СтрНайти(ТекИмяПодчиненногоУзла, "nonNegative")>0 Тогда
						СтроковоеЗначение = НРег(одЗначениеЭлемента(ЧтениеXML));
						Квалификаторы.Вставить("КвалификаторыЧисла_Неотрицательное", ?(СтроковоеЗначение = "true", Истина, Ложь));
					//-КД3 FIX
					ИначеЕсли СтрНайти(ТекИмяПодчиненногоУзла, "precision")>0 Тогда
						Квалификаторы.Вставить("КвалификаторыЧисла_Длина", одЗначениеЭлемента(ЧтениеXML, Тип("Число")));
					ИначеЕсли СтрНайти(ТекИмяПодчиненногоУзла, "scale")>0 Тогда
						Квалификаторы.Вставить("КвалификаторыЧисла_Точность", одЗначениеЭлемента(ЧтениеXML, Тип("Число")));
					КонецЕсли;
					//+КД3 FIX По умолчанию Точность = 0
					Если НЕ Квалификаторы.Свойство("КвалификаторыЧисла_Точность") Тогда
						Квалификаторы.Вставить("КвалификаторыЧисла_Точность", 0);
					КонецЕсли;
					//-КД3 FIX
				КонецЦикла;
			ИначеЕсли СтрНайти(ТекИмяУзла, "datequalifiers")>0 Тогда
				ЧтениеXML.Прочитать();
				Если СтрНайти(НРег(ЧтениеXML.Имя), "datequalifiers") > 0 Тогда
					Прервать;
				ИначеЕсли СтрНайти(НРег(ЧтениеXML.Имя), "datefractions")>0 Тогда
					СтроковоеЗначение = НРег(одЗначениеЭлемента(ЧтениеXML));
					Если СтроковоеЗначение = "date" Тогда
						Квалификатор = "Дата";
					ИначеЕсли СтроковоеЗначение = "time" Тогда
						Квалификатор = "Время";
					Иначе
						Квалификатор = "Дата и время";
					КонецЕсли;
					Квалификаторы.Вставить("КвалификаторыДаты_Состав", Квалификатор);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Если ЕстьТипыСсылочные = Ложь И ЕстьТипыПростые Тогда
		Если Квалификаторы.Количество() > 0 Тогда
			ПростойТипСтрокой = ТипСтрокойПоКвалификаторам(Квалификаторы);
		ИначеЕсли ТекТипыСвойства.Количество() > 0 Тогда
			ПростойТипСтрокой = ТекТипыСвойства[0];
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура СоздатьОбъектыПростыеТипы(ОбщиеПеременные)
	ПростыеТипы = Новый Структура;
	ПростыеТипы.Вставить("Булево", "Булево");
	ПростыеТипы.Вставить("Число", "Число");
	ПростыеТипы.Вставить("Строка", "Строка");
	ПростыеТипы.Вставить("Дата", "Дата");
	ПростыеТипы.Вставить("ХранилищеЗначения", "ХранилищеЗначения");
	ПростыеТипы.Вставить("УникальныйИдентификатор", "УникальныйИдентификатор");  
	ПростыеТипы.Вставить("СписокЗначений"); 
    ПростыеТипы.Вставить("ДеревоЗначений");
	ПростыеТипы.Вставить("ТаблицаЗначений");


	Для Каждого ТекТип Из ПростыеТипы Цикл
		СсылкаТип = Справочники.Объекты.НайтиПоНаименованию(ТекТип.Ключ, Истина,,ОбщиеПеременные.Конфигурация);
		Если ЗначениеЗаполнено(СсылкаТип) Тогда
			ПростыеТипы.Вставить(ТекТип.Ключ, СсылкаТип);
			УдалитьОбъектИзСписка(СсылкаТип, ОбщиеПеременные.СуществующиеОбъекты);
			Продолжить;
		КонецЕсли;
		ОбъектТип = Справочники.Объекты.СоздатьЭлемент();
		ОбъектТип.Владелец = ОбщиеПеременные.Конфигурация;
		ОбъектТип.Наименование = ТекТип.Ключ;
		ОбъектТип.Имя = ТекТип.Ключ;
		ОбъектТип.Синоним = ТекТип.Ключ;
		Если ЗначениеЗаполнено(ТекТип.Значение) Тогда
			ОбъектТип.Тип = Перечисления.ТипыОбъектов[ТекТип.Значение]; 
		КонецЕсли;
		ОбъектТип.Записать();
		ПростыеТипы.Вставить(ТекТип.Ключ, ОбъектТип.Ссылка);
	КонецЦикла;
	ОбщиеПеременные.Вставить("ПростыеТипы", ПростыеТипы);
КонецПроцедуры

Процедура ДобавитьПредопределенноеСвойство(ИмяСвойства, ОбъектСсылка, ВидСвойства, ОбщиеПеременные, ТипСвойства = Неопределено, Квалификаторы = Неопределено, РодительСвойства = Неопределено)
	СвойствоОбъект = НайтиСоздатьСвойствоПоНаименованию(ИмяСвойства, ОбъектСсылка, ОбщиеПеременные, РодительСвойства);
	Если НЕ ОбщиеПеременные.СоздаватьПредопределенныеСвойства Тогда
		Возврат;
	КонецЕсли;
	СвойствоОбъект.Вид = ВидСвойства; 
	СвойствоЗаписано = Ложь;
	Если Квалификаторы <> Неопределено Тогда
		Для Каждого Квалификатор Из Квалификаторы Цикл
			СвойствоОбъект[Квалификатор.Ключ] = Квалификатор.Значение;
		КонецЦикла;
		СвойствоОбъект.ТипыСтрокой = ТипСтрокойПоКвалификаторам(Квалификаторы, СвойствоОбъект.Типы, ОбщиеПеременные);
	ИначеЕсли ТипСвойства <> Неопределено Тогда
		Если ТипЗнч(ТипСвойства) = Тип("Массив") Тогда
			// Если в массиве ссылки на объекты можно сразу заполнить.
			Если ТипЗнч(ТипСвойства[0]) = Тип("СправочникСсылка.Объекты") Тогда
				ТипыСтрокой = "";
				Для СчТипов = 0 По ТипСвойства.Количество() - 1 Цикл
					Если ОбщиеПеременные.ЭтоРасширение Тогда
						ЕстьСтрока = СвойствоОбъект.Типы.Найти(ТипСвойства[СчТипов], "Тип");
						Если ЕстьСтрока <> Неопределено Тогда
							Продолжить;
						КонецЕсли;
					КонецЕсли;
					ТекТип = СвойствоОбъект.Типы.Добавить();
					ТекТип.Тип = ТипСвойства[СчТипов];
					ТипыСтрокой = ТипыСтрокой + СокрЛП(ТипСвойства[СчТипов]) + ","
				КонецЦикла;
				СвойствоОбъект.ТипыСтрокой = Лев(ТипыСтрокой, СтрДлина(ТипыСтрокой) - 1);
			Иначе
				СвойствоОбъект.Записать();
				ДобавитьТипВОтложенноеЗаполнение(ОбщиеПеременные, СвойствоОбъект.Ссылка, ТипСвойства);
				СвойствоЗаписано = Истина;
			КонецЕсли;
		ИначеЕсли ТипЗнч(ТипСвойства) = Тип("СправочникСсылка.Объекты") Тогда
			ДобавлятьТип = Истина;
			Если ОбщиеПеременные.ЭтоРасширение Тогда
				ЕстьСтрока = СвойствоОбъект.Типы.Найти(ТипСвойства, "Тип");
				Если ЕстьСтрока <> Неопределено Тогда
					ДобавлятьТип = Ложь; 
				КонецЕсли;
			КонецЕсли;
			Если ДобавлятьТип Тогда
				ТекТип = СвойствоОбъект.Типы.Добавить();
				ТекТип.Тип = ТипСвойства;
				СвойствоОбъект.ТипыСтрокой = ТипСвойства.Наименование;
			КонецЕсли;
		ИначеЕсли ТипЗнч(ТипСвойства) = Тип("Строка") Тогда
			ПозТочки = СтрНайти(ТипСвойства,".");
			ПростойТип = (ПозТочки = 0);
			Если ПростойТип И ОбщиеПеременные.ПростыеТипы.Свойство(ТипСвойства) Тогда
				ДобавлятьТип = Истина;
				Если ОбщиеПеременные.ЭтоРасширение Тогда
					ЕстьСтрока = СвойствоОбъект.Типы.Найти(ТипСвойства, "Тип");
					Если ЕстьСтрока <> Неопределено Тогда
						ДобавлятьТип = Ложь; 
					КонецЕсли;
				КонецЕсли;
				Если ДобавлятьТип Тогда
					ТекТип = СвойствоОбъект.Типы.Добавить();
					ТекТип.Тип = ОбщиеПеременные.ПростыеТипы[ТипСвойства];
					СвойствоОбъект.ТипыСтрокой = ТипСвойства;
				КонецЕсли;
			Иначе
				СвойствоЗаписано = Истина;
				СвойствоОбъект.Записать();
				ТекТипыСвойства = Новый Массив;
				ТекТипыСвойства.Добавить(ТипСвойства);
				ДобавитьТипВОтложенноеЗаполнение(ОбщиеПеременные, СвойствоОбъект.Ссылка, ТекТипыСвойства);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Если НЕ СвойствоЗаписано Тогда
		СвойствоОбъект.Записать();
	КонецЕсли;
КонецПроцедуры

Процедура ДобавитьТабличныеЧастиВПланВидовРасчета(ОбъектСсылка, ОбщиеПеременные, СоставТипа, ЕстьПериодДействияБазовый)
	Если ЕстьПериодДействияБазовый Тогда
		РодительСсылка = НайтиСоздатьГруппуСвойств("ВытесняющиеВидыРасчета", ОбъектСсылка, ОбщиеПеременные);
		ДобавитьПредопределенноеСвойство("ВидРасчета", ОбъектСсылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, СоставТипа, , РодительСсылка);
	КонецЕсли;
	РодительСсылка = НайтиСоздатьГруппуСвойств("ВедущиеВидыРасчета", ОбъектСсылка, ОбщиеПеременные);
	ДобавитьПредопределенноеСвойство("ВидРасчета", ОбъектСсылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, СоставТипа, , РодительСсылка);

	РодительСсылка = НайтиСоздатьГруппуСвойств("БазовыеВидыРасчета", ОбъектСсылка, ОбщиеПеременные);
	ДобавитьПредопределенноеСвойство("ВидРасчета", ОбъектСсылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные, СоставТипа, , РодительСсылка);
КонецПроцедуры

Функция НайтиСоздатьГруппуСвойств(ИмяГруппы, ОбъектСсылка, ОбщиеПеременные)
	РодительСсылка = Справочники.Свойства.НайтиПоНаименованию(ИмяГруппы, Истина, , ОбъектСсылка);
	Если ЗначениеЗаполнено(РодительСсылка) И РодительСсылка.ЭтоГруппа Тогда
		СвойствоОбъект = РодительСсылка.ПолучитьОбъект();
		Если РодительСсылка.ПометкаУдаления Тогда
			СвойствоОбъект.ПометкаУдаления = Ложь;
		КонецЕсли;
	Иначе
		СвойствоОбъект = Справочники.Свойства.СоздатьГруппу();
		СвойствоОбъект.Наименование = ИмяГруппы;
		СвойствоОбъект.Владелец = ОбъектСсылка;
		СвойствоОбъект.Синоним = ИмяГруппы;
		СвойствоОбъект.Вид = Перечисления.ВидыСвойств.ТабличнаяЧасть;
	КонецЕсли;
	СвойствоОбъект.Записать();
	УдалитьОбъектИзСписка(СвойствоОбъект.Ссылка, ОбщиеПеременные.СуществующиеСвойства);
	Возврат СвойствоОбъект.Ссылка;
КонецФункции

Функция НайтиСоздатьСвойствоПоНаименованию(ИмяСвойства, ОбъектСсылка, ОбщиеПеременные, РодительСвойства = Неопределено)
	СвойствоСсылка = Справочники.Свойства.НайтиПоНаименованию(ИмяСвойства, Истина, РодительСвойства, ОбъектСсылка);
	Если ЗначениеЗаполнено(СвойствоСсылка) Тогда
		СвойствоОбъект = СвойствоСсылка.ПолучитьОбъект();
		Если СвойствоСсылка.ПометкаУдаления Тогда
			СвойствоОбъект.ПометкаУдаления = Ложь;
		КонецЕсли;
		Если НЕ ОбщиеПеременные.ЭтоРасширение Тогда
			СвойствоОбъект.Типы.Очистить();
		КонецЕсли;
		УдалитьОбъектИзСписка(СвойствоСсылка, ОбщиеПеременные.СуществующиеСвойства);
	Иначе
		СвойствоОбъект = Справочники.Свойства.СоздатьЭлемент();
		СвойствоОбъект.Наименование = ИмяСвойства;
		СвойствоОбъект.Владелец = ОбъектСсылка;
		СвойствоОбъект.Синоним = СвойствоОбъект.Наименование;
		Если РодительСвойства <> Неопределено Тогда
			СвойствоОбъект.Родитель = РодительСвойства;
		КонецЕсли;
	КонецЕсли;
	Возврат СвойствоОбъект;
КонецФункции   

Процедура НайтиСоздатьЭлементСоставаПланаОбмена(СсылочныйТипСвойства, ОбщиеПеременные, ВладелецСвойства, РодительСвойства) 
	ДанныеОбъекта = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СсылочныйТипСвойства, "Имя, Синоним, Наименование");
	
	ЗапросСостав = Новый Запрос;
	ЗапросСостав.Текст = "ВЫБРАТЬ
	|	Шапка.Ссылка,
	|	Шапка.ПометкаУдаления
	|ИЗ Справочник.Свойства.Типы КАК ТипСостав
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Свойства КАК Шапка
	|ПО ТипСостав.Ссылка = Шапка.Ссылка
	|ГДЕ ТипСостав.Тип = &Тип И Шапка.Родитель = &Родитель И Шапка.Владелец = &Владелец
	|	И Шапка.Наименование = &Наименование";
	ЗапросСостав.УстановитьПараметр("Владелец", ВладелецСвойства);
	ЗапросСостав.УстановитьПараметр("Родитель", РодительСвойства);
	ЗапросСостав.УстановитьПараметр("Тип", СсылочныйТипСвойства);
	ЗапросСостав.УстановитьПараметр("Наименование", ДанныеОбъекта.Имя);
	Выборка = ЗапросСостав.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если Выборка.ПометкаУдаления Тогда
			СвойствоОбъект = Выборка.Ссылка.ПолучитьОбъект();
			СвойствоОбъект.ПометкаУдаления = Ложь;
			СвойствоОбъект.Записать();
		КонецЕсли;
		УдалитьОбъектИзСписка(Выборка.Ссылка, ОбщиеПеременные.СуществующиеСвойства);
        Возврат;
	КонецЕсли;
	СвойствоОбъект = Справочники.Свойства.СоздатьЭлемент();
	СвойствоОбъект.Наименование = ДанныеОбъекта.Имя;
	СвойствоОбъект.Владелец = ВладелецСвойства;
	СвойствоОбъект.Синоним = ДанныеОбъекта.Наименование;
	СвойствоОбъект.Родитель = РодительСвойства;
	
	СвойствоОбъект.Вид = Перечисления.ВидыСвойств.ЭлементСоставаПланаОбмена;
	СвойствоОбъект.Наименование = ДанныеОбъекта.Имя;
	СвойствоОбъект.Синоним = ДанныеОбъекта.Синоним;
	СвойствоОбъект.ТипыСтрокой = ДанныеОбъекта.Наименование;
	СтрокаТип = СвойствоОбъект.Типы.Добавить();
	СтрокаТип.Тип = СсылочныйТипСвойства;
	СвойствоОбъект.Записать();

КонецПроцедуры

Функция ТипСтрокойПоКвалификаторам(Квалификаторы, ТЧТипы = Неопределено, ОбщиеПеременные = Неопределено)
	ПростойТипСтрокой = "";
	ИмяТипа = "";
	Если Квалификаторы.Свойство("КвалификаторыДаты_Состав") Тогда
		ИмяТипа = "Дата";
		ПростойТипСтрокой = "Дата";
	ИначеЕсли Квалификаторы.Свойство("КвалификаторыСтроки_Длина") Тогда
		ИмяТипа = "Строка";
		ПростойТипСтрокой = "Строка ";
		Если Квалификаторы.Свойство("КвалификаторыСтроки_Фиксированная")
			И Квалификаторы.КвалификаторыСтроки_Фиксированная Тогда
			ПростойТипСтрокой = ПростойТипСтрокой + "Ф";
		Иначе
			ПростойТипСтрокой = ПростойТипСтрокой + "П";
		КонецЕсли;
		ПростойТипСтрокой = ПростойТипСтрокой + "("  + Квалификаторы.КвалификаторыСтроки_Длина + ")";
	ИначеЕсли Квалификаторы.Свойство("КвалификаторыЧисла_Длина") Тогда
		ИмяТипа = "Число";
		ПростойТипСтрокой = "Число (" + Квалификаторы.КвалификаторыЧисла_Длина;
		Если Квалификаторы.Свойство("КвалификаторыЧисла_Точность") Тогда
			ПростойТипСтрокой = ПростойТипСтрокой + "," + Квалификаторы.КвалификаторыЧисла_Точность + ")";
		Иначе
			ПростойТипСтрокой = ПростойТипСтрокой + ")";
		КонецЕсли;
	ИначеЕсли Квалификаторы.Свойство("КвалификаторыЧисла_Неотрицательное") Тогда
		ИмяТипа = "Число";
		ПростойТипСтрокой = "Число";
	КонецЕсли;
	Если ИмяТипа = "" Тогда
		Возврат "";
	КонецЕсли;
	Если ТЧТипы <> Неопределено Тогда
		ТипСсылка = Неопределено;
		ОбщиеПеременные.ПростыеТипы.Свойство(ИмяТипа, ТипСсылка);
		Если ТипСсылка <> Неопределено Тогда
			СтрокаТип = ТЧТипы.Добавить();
			СтрокаТип.Тип = ТипСсылка;
		КонецЕсли;
	КонецЕсли;
	Возврат ПростойТипСтрокой;
КонецФункции

Процедура ПривязатьБизнесПроцессКЗадаче(ПолноеИмяЗадачи, БизнесПроцессСсылка, ОбщиеПеременные)
	ПозТочки = СтрНайти(ПолноеИмяЗадачи, ".");
	Если ПозТочки = 0 Тогда
		Возврат;
	КонецЕсли;
	ИмяЗадачи = "ЗадачаСсылка." + Сред(ПолноеИмяЗадачи, ПозТочки + 1);
	ЗадачаСсылка = Справочники.Объекты.НайтиПоНаименованию(ИмяЗадачи, Истина,,ОбщиеПеременные.Конфигурация);
	СвойствоБП = Справочники.Свойства.НайтиПоНаименованию("БизнесПроцесс", Истина,,ЗадачаСсылка);
	Если НЕ ЗначениеЗаполнено(СвойствоБП) Тогда
		ДобавитьПредопределенноеСвойство("БизнесПроцесс", ЗадачаСсылка, Перечисления.ВидыСвойств.Свойство, ОбщиеПеременные);
		СвойствоБП = Справочники.Свойства.НайтиПоНаименованию("БизнесПроцесс", Истина,,ЗадачаСсылка);
	КонецЕсли;
	ТекСвойствоБП = СвойствоБП.ПолучитьОбъект();
		
	ТекСвойствоБП.ТипыСтрокой = СокрЛП(БизнесПроцессСсылка);
	ТекТип = ТекСвойствоБП.Типы.Добавить();
	ТекТип.Тип = БизнесПроцессСсылка;
	ТекСвойствоБП.Записать();
КонецПроцедуры

Процедура ОбработатьКонстанты(ОбщиеПеременные)
	Если ОбщиеПеременные.ИсточникДанных = 0 Тогда
		ВложенныеФайлы = НайтиПомещенныеФайлыПоМаске(ОбщиеПеременные.ПомещенныеФайлы, ОбщиеПеременные.РазделительПути + "Constants" + ОбщиеПеременные.РазделительПути, ".xml", ОбщиеПеременные);
	Иначе
		ВложенныеФайлы = НайтиПомещенныеФайлыПоМаске(ОбщиеПеременные.ПомещенныеФайлы, "src" + ОбщиеПеременные.РазделительПути + "Constants" + ОбщиеПеременные.РазделительПути, ".mdo", ОбщиеПеременные);
	КонецЕсли;
	Если ВложенныеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	//+КД3 Отображение прогресса
	КД3_ЗагрузкаМетаданных.НачатьПрогрессРасшифровки(ОбщиеПеременные, ВложенныеФайлы.Количество(), "Константы");
	//-КД3 Отображение прогресса
	// Поиск / создание объекта.
	ТекущийОбъект = Справочники.Объекты.НайтиПоНаименованию("КонстантыНабор", Истина,,ОбщиеПеременные.Конфигурация);
	Если НЕ ЗначениеЗаполнено(ТекущийОбъект) Тогда
		ОбъектКонстанты = Справочники.Объекты.СоздатьЭлемент();
		ОбъектКонстанты.Наименование = "КонстантыНабор";
		ОбъектКонстанты.Имя = "КонстантыНабор";
		ОбъектКонстанты.Синоним = "Набор констант";
		ОбъектКонстанты.Владелец = ОбщиеПеременные.Конфигурация;
		ОбъектКонстанты.Тип = Перечисления.ТипыОбъектов.НаборКонстант;
		ОбъектКонстанты.Записать();
		ТекущийОбъект = ОбъектКонстанты.Ссылка;
	КонецЕсли;
	Для Каждого ВложенныйФайл Из ВложенныеФайлы Цикл
		//+КД3 Отображение прогресса
		КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессРасшифровки(ОбщиеПеременные);
		//-КД3 Отображение прогресса
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.ОткрытьФайл(ВложенныйФайл.ТекущееИмяФайла);
		Если ОбщиеПеременные.ИсточникДанных = 0 Тогда 
			ЧтениеXML.Прочитать(); // MetaDataObject
			ЧтениеXML.Прочитать(); // Constant
			УникальныйИдентификатор = одАтрибут(ЧтениеXML, "uuid");
			ЧтениеXML.Прочитать(); // InternalInfo
			ЧтениеXML.Пропустить();
			ЧтениеXML.Прочитать(); // Properties
		Иначе
			ЧтениеXML.Прочитать(); // mdclass
			УникальныйИдентификатор = одАтрибут(ЧтениеXML, "uuid");
		КонецЕсли;
		ТекОбъект = Неопределено;
		ТекТипыСвойства = Новый Массив;
		Если ЗначениеЗаполнено(УникальныйИдентификатор) И ОбщиеПеременные.СинхронизироватьПоУИД Тогда
			// Попытка найти свойство по УИДу
			ТекСсылка = Справочники.Свойства.НайтиПоРеквизиту("Идентификатор", УникальныйИдентификатор, , ТекущийОбъект);
			Если ЗначениеЗаполнено(ТекСсылка) Тогда
				ТекОбъект = ТекСсылка.ПолучитьОбъект();
				Если ТекОбъект.ПометкаУдаления Тогда
					ТекОбъект.ПометкаУдаления = Ложь;
					Если ОбщиеПеременные.ТолькоДобавлятьНовые Тогда
						ТекОбъект.Записать();
					КонецЕсли;
				Иначе
					УдалитьОбъектИзСписка(ТекСсылка, ОбщиеПеременные.СуществующиеСвойства);
				КонецЕсли;
				Если ОбщиеПеременные.ТолькоДобавлятьНовые Тогда
					ЧтениеXML.Закрыть();
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		// Чтение блока Properties - заполнение реквизитов Объекта.
		// Также здесь читаются некоторые параметры свойств.
		Пока НЕ ((ЧтениеXML.Имя = "Properties" ИЛИ СтрНайти(ЧтениеXML.Имя, "mdclass") > 0) И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента)  Цикл
			Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				ТекИмяУзла = НРег(ЧтениеXML.Имя);
				Если ТекИмяУзла = "name" Тогда
					Имя = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
					// Не нашли объект по УИДу либо не задан УИД.
					Если ТекОбъект = Неопределено Тогда
						ТекСсылка = Справочники.Свойства.НайтиПоНаименованию(Имя, Истина, , ТекущийОбъект);
						Если НЕ ЗначениеЗаполнено(ТекСсылка)
							ИЛИ (ЗначениеЗаполнено(УникальныйИдентификатор)
							И ОбщиеПеременные.СинхронизироватьПоУИД
							И ЗначениеЗаполнено(ТекСсылка.Идентификатор)
							И ТекСсылка.Идентификатор <> УникальныйИдентификатор
							И НЕ ОбщиеПеременные.ЭтоРасширение) Тогда
							ТекОбъект = Справочники.Свойства.СоздатьЭлемент();
							ТекОбъект.Наименование = Имя;
							ТекОбъект.Идентификатор = УникальныйИдентификатор;
							ТекОбъект.Владелец = ТекущийОбъект;
							ТекОбъект.Вид = Перечисления.ВидыСвойств.Реквизит;
						Иначе
							ТекОбъект = ТекСсылка.ПолучитьОбъект();
							ТекОбъект.Идентификатор = УникальныйИдентификатор;
							Если ТекОбъект.ПометкаУдаления Тогда
								ТекОбъект.ПометкаУдаления = Ложь;
								Если ОбщиеПеременные.ТолькоДобавлятьНовые Тогда
									ТекОбъект.Записать();
								КонецЕсли;
							Иначе
								УдалитьОбъектИзСписка(ТекСсылка, ОбщиеПеременные.СуществующиеСвойства);
							КонецЕсли;
							Если ОбщиеПеременные.ТолькоДобавлятьНовые Тогда
								ЧтениеXML.Закрыть();
								Возврат;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				ИначеЕсли ЧтениеXML.Имя = "synonym" Тогда
					ТекОбъект.Синоним = ПрочитатьСиноним(ЧтениеXML);
				ИначеЕсли ЧтениеXML.Имя = "comment" Тогда
					ТекОбъект.Комментарий = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				ИначеЕсли НРег(ЧтениеXML.Имя) = "type" Тогда
					Квалификаторы = Новый Структура;
					ПростойТип = "";
					ПрочитатьТипы(ЧтениеXML, ТекТипыСвойства, ПростойТип, Квалификаторы);
					Для Каждого Квалификатор Из Квалификаторы Цикл
						ТекОбъект[Квалификатор.Ключ] = Квалификатор.Значение;
					КонецЦикла;
					Если ПростойТип <> "" Тогда
						ЗаполнитьПростойТипСвойства(ОбщиеПеременные, ТекОбъект, ПростойТип, ТекТипыСвойства);
					КонецЕсли;
				Иначе
					ЧтениеXML.Прочитать();
				КонецЕсли;
			Иначе
				ЧтениеXML.Прочитать();
			КонецЕсли;
		КонецЦикла;
		ТекОбъект.Записать();
		ДобавитьТипВОтложенноеЗаполнение(ОбщиеПеременные, ТекОбъект.Ссылка, ТекТипыСвойства);
		ЧтениеXML.Закрыть();
	КонецЦикла;  // Для Каждого ВложенныйФайл Из ВложенныеФайлы Цикл
КонецПроцедуры

Процедура ОбработатьОпределяемыеТипы(ОбщиеПеременные)
	//+КД3 FIX
	// Исправление спроса определяемых типов, если есть расширения
	//ОбщиеПеременные.Вставить("ОпределяемыеТипы", Новый Структура);
	//-КД3 FIX
	Если ОбщиеПеременные.ИсточникДанных = 0 Тогда
		ВложенныеФайлы = НайтиПомещенныеФайлыПоМаске(ОбщиеПеременные.ПомещенныеФайлы, ОбщиеПеременные.РазделительПути + "DefinedTypes" + ОбщиеПеременные.РазделительПути, ".xml", ОбщиеПеременные);
	Иначе
		ВложенныеФайлы = НайтиПомещенныеФайлыПоМаске(ОбщиеПеременные.ПомещенныеФайлы, "src" + ОбщиеПеременные.РазделительПути + "DefinedTypes" + ОбщиеПеременные.РазделительПути, ".mdo", ОбщиеПеременные);
	КонецЕсли;
	Если ВложенныеФайлы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	//+КД3 Отображение прогресса
	КД3_ЗагрузкаМетаданных.НачатьПрогрессРасшифровки(ОбщиеПеременные, ВложенныеФайлы.Количество(), "ОпределяемыеТипы" + ?(ОбщиеПеременные.ЭтоРасширение, " " + ОбщиеПеременные.ПутьКФайламРасширения, ""));
	//-КД3 Отображение прогресса

	//+КД3 FIX
	// Исправление спроса определяемых типов, если есть расширения
	ОпределяемыеТипы = ОбщиеПеременные.ОпределяемыеТипы;
	//+КД3 FIX
	Для Каждого ВложенныйФайл Из ВложенныеФайлы Цикл
		//+КД3 Отображение прогресса
		КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессРасшифровки(ОбщиеПеременные);
		//-КД3 Отображение прогресса
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.ОткрытьФайл(ВложенныйФайл.ТекущееИмяФайла);
		Если ОбщиеПеременные.ИсточникДанных = 0 Тогда 
			ЧтениеXML.Прочитать(); // MetaDataObject
			ЧтениеXML.Прочитать(); // DefinedType
			ЧтениеXML.Прочитать(); // InternalInfo
			ЧтениеXML.Пропустить();
			ЧтениеXML.Прочитать(); // Properties
		Иначе
			ЧтениеXML.Прочитать(); // mdclass
			ЧтениеXML.Прочитать(); // producedTypes
			ЧтениеXML.Пропустить();
			ЧтениеXML.Прочитать(); // Properties
		КонецЕсли;
		ИмяТипа = "";
		ВложенныеТипы = Новый Массив;
		Пока НЕ ((ЧтениеXML.Имя = "Properties" ИЛИ СтрНайти(ЧтениеXML.Имя, "mdclass") > 0) И ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента)  Цикл
			Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				ТекИмяУзла = НРег(ЧтениеXML.Имя);
				Если ТекИмяУзла = "name" Тогда
					ИмяТипа = ПрочитатьXML(ЧтениеXML, Тип("Строка"));
				//+КД3 FIX Имена "type" / "types" для EDT в зависимости от ЭтоРасширение
				ИначеЕсли ТекИмяУзла = "type" ИЛИ (ТекИмяУзла = "typeextension" И ОбщиеПеременные.ИсточникДанных = 1 И ОбщиеПеременные.ЭтоРасширение) Тогда
				//-КД3 FIX
					Квалификаторы = Новый Структура;
					ПростойТип = "";
					ПрочитатьТипы(ЧтениеXML, ВложенныеТипы, ПростойТип, Квалификаторы);
					Если ВложенныеТипы.Количество() > 0 Тогда
						//+КД3 FIX При использовании расширений определяемый тип содержит только последнее дополнение
						Если ОбщиеПеременные.ЭтоРасширение Тогда
							ВложенныеТипыПреобразованные = Неопределено;
							Если НЕ ОпределяемыеТипы.Свойство(ИмяТипа, ВложенныеТипыПреобразованные) Тогда
								ВложенныеТипыПреобразованные = Новый Массив;
							КонецЕсли;
						Иначе
							ВложенныеТипыПреобразованные = Новый Массив;
						КонецЕсли;
						//-КД3 FIX
						Для Каждого ТекТип Из ВложенныеТипы Цикл
							Если СтрНайти(ТекТип, ".") = 0 Тогда
								// Поиск простого типа.
								ТипСсылка = Неопределено;
								ОбщиеПеременные.ПростыеТипы.Свойство(ТекТип, ТипСсылка);
								Если ТипСсылка <> Неопределено И ВложенныеТипыПреобразованные.Найти(ТипСсылка) = Неопределено Тогда
									Если Квалификаторы.Количество() > 0 Тогда
										ВложенныеТипыПреобразованные.Добавить(Новый Структура("Тип, Квалификаторы",ТипСсылка, Квалификаторы));
									Иначе
										ВложенныеТипыПреобразованные.Добавить(ТипСсылка);
									КонецЕсли;
								КонецЕсли;
							Иначе
								// Ссылочный тип.
								РодительОбъекта = "";
								ПолноеИмяОбъекта = ПреобразоватьИмяТипаВИмяОбъекта(ТекТип, РодительОбъекта, ОбщиеПеременные);
								Если ЗначениеЗаполнено(ПолноеИмяОбъекта) Тогда
									ОбъектСсылка = Справочники.Объекты.НайтиПоНаименованию(ПолноеИмяОбъекта, Истина, РодительОбъекта,ОбщиеПеременные.Конфигурация);
									Если ЗначениеЗаполнено(ОбъектСсылка) Тогда
										//+КД3 FIX Исправление дублирование ссылочного типа при использовании расширений  
										Если ВложенныеТипыПреобразованные.Найти(ОбъектСсылка) = Неопределено Тогда
											ВложенныеТипыПреобразованные.Добавить(ОбъектСсылка);
										КонецЕсли;
										//-КД3 FIX 
									КонецЕсли;
								КонецЕсли;
							КонецЕсли;
						КонецЦикла;
						Если ВложенныеТипыПреобразованные.Количество() > 0 Тогда
							ОпределяемыеТипы.Вставить(ИмяТипа, ВложенныеТипыПреобразованные);
						КонецЕсли;
					КонецЕсли;
				Иначе
					ЧтениеXML.Прочитать();
				КонецЕсли;
			Иначе
				ЧтениеXML.Прочитать();
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	//+КД3 FIX
	// Исправление спроса определяемых типов, если есть расширения
	//ОбщиеПеременные.Вставить("ОпределяемыеТипы", ОпределяемыеТипы);
	//-КД3 FIX
КонецПроцедуры

Функция ПреобразоватьИмяТипаВИмяОбъекта(ТипСвойства, РодительОбъекта, ОбщиеПеременные)
	ПозТочки = СтрНайти(ТипСвойства, ".");
	Если ПозТочки = 0 Тогда
		Возврат "";
	КонецЕсли;
	ИмяРодителя = "";
	ПрефиксИмени = "";
	Часть1 = Лев(ТипСвойства, ПозТочки - 1);
	Часть2 = Сред(ТипСвойства, ПозТочки + 1);
	Если СтрНайти(Часть1, "Catalog") > 0 Тогда
		ИмяРодителя = "Справочники";
		ПрефиксИмени = "СправочникСсылка";
	ИначеЕсли СтрНайти(Часть1, "Document") > 0 Тогда
		ИмяРодителя = "Документы";
		ПрефиксИмени = "ДокументСсылка";
	ИначеЕсли СтрНайти(Часть1, "Task") > 0 Тогда
		ИмяРодителя = "Задачи";
		ПрефиксИмени = "ЗадачаСсылка";
	ИначеЕсли СтрНайти(Часть1, "Enum") > 0 Тогда
		ИмяРодителя = "Перечисления";
		ПрефиксИмени = "ПеречислениеСсылка";
	ИначеЕсли СтрНайти(Часть1, "BusinessProcess") > 0 Тогда
		ИмяРодителя = "БизнесПроцессы";
		ПрефиксИмени = "БизнесПроцессСсылка";
	ИначеЕсли СтрНайти(Часть1, "ChartOfAccounts") > 0 Тогда
		ИмяРодителя = "ПланыСчетов";
		ПрефиксИмени = "ПланСчетовСсылка";
	ИначеЕсли СтрНайти(Часть1, "ChartsOfCalculation") > 0
		Или СтрНайти(Часть1, "ChartOfCalculation") > 0 Тогда
		ИмяРодителя = "ПланыВидовРасчета";
		ПрефиксИмени = "ПланВидовРасчетаСсылка";
	ИначеЕсли СтрНайти(Часть1, "ChartsOfCharacteristic") > 0
		Или СтрНайти(Часть1, "ChartOfCharacteristicTypes") > 0 Тогда
		ИмяРодителя = "ПланыВидовХарактеристик";
		ПрефиксИмени = "ПланВидовХарактеристикСсылка";
	ИначеЕсли СтрНайти(Часть1, "AccountingRegister") > 0 Тогда
		ИмяРодителя = "РегистрыБухгалтерии";
		ПрефиксИмени = "РегистрБухгалтерииЗапись";
	ИначеЕсли СтрНайти(Часть1, "AccumulationRegister") > 0 Тогда
		ИмяРодителя = "РегистрыНакопления";
		ПрефиксИмени = "РегистрНакопленияЗапись";
	ИначеЕсли СтрНайти(Часть1, "CalculationRegister") > 0 Тогда
		ИмяРодителя = "РегистрыРасчета";
		ПрефиксИмени = "РегистрРасчетаЗапись";
	ИначеЕсли СтрНайти(Часть1, "InformationRegister") > 0 Тогда
		ИмяРодителя = "РегистрыСведений";
		ПрефиксИмени = "РегистрСведенийЗапись";
	КонецЕсли;
	Если ПрефиксИмени = "" Тогда
		Возврат "";
	КонецЕсли;
	
	Если ИмяРодителя <> "" Тогда
		РодительОбъекта = Справочники.Объекты.НайтиПоНаименованию(ИмяРодителя, Истина,, ОбщиеПеременные.Конфигурация);
	КонецЕсли;
	Возврат "" + ПрефиксИмени + "." + Часть2;
КонецФункции

Процедура УдалитьОбъектИзСписка(ОбъектСсылка, СуществующиеОбъекты)
	Если СуществующиеОбъекты.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	 РезультатПоиска = СуществующиеОбъекты.Найти(ОбъектСсылка);
	 Пока РезультатПоиска <> Неопределено Цикл
		 СуществующиеОбъекты.Удалить(РезультатПоиска);
		 РезультатПоиска = СуществующиеОбъекты.Найти(ОбъектСсылка);
	 КонецЦикла;	 
КонецПроцедуры

Процедура НайтиСоздатьСвойстваНаборДвижений(ИмяДокумента, ДанныеРегистра, ИзмеренияРегистра, ОбщиеПеременные)
	ОбъектСсылка = Справочники.Объекты.НайтиПоНаименованию(ИмяДокумента, Истина, ,ОбщиеПеременные.Конфигурация);
	Если НЕ ЗначениеЗаполнено(ОбъектСсылка) Тогда
		Возврат;
	КонецЕсли;
	ТекстЗапросаДляПоиска = "ВЫБРАТЬ ПЕРВЫЕ 1
	|	Ссылка,
	|	ПометкаУдаления
	|ИЗ Справочник.Свойства
	|ГДЕ Владелец = &Владелец И Наименование = &Наименование 
	|	И Вид = &Вид И ЭтоГруппа = &ЭтоГруппа И Родитель = &Родитель";
	// Поиск / создание группы свойств.
	ИмяГруппыСвойств = "{" + ДанныеРегистра.Имя + "}";
	Если ДанныеРегистра.Тип = Перечисления.ТипыОбъектов.РегистрБухгалтерии Тогда
		ВидГруппыСвойств = Перечисления.ВидыСвойств.НаборДвиженийРегистраБухгалтерии;
	ИначеЕсли ДанныеРегистра.Тип = Перечисления.ТипыОбъектов.РегистрНакопления Тогда
		ВидГруппыСвойств = Перечисления.ВидыСвойств.НаборДвиженийРегистраНакопления;
	ИначеЕсли ДанныеРегистра.Тип = Перечисления.ТипыОбъектов.РегистрРасчета Тогда
		ВидГруппыСвойств = Перечисления.ВидыСвойств.НаборДвиженийРегистраРасчета;
	ИначеЕсли ДанныеРегистра.Тип = Перечисления.ТипыОбъектов.РегистрСведений Тогда
		ВидГруппыСвойств = Перечисления.ВидыСвойств.НаборДвиженийРегистраСведений;
	Иначе
		Возврат;
	КонецЕсли;
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапросаДляПоиска;
	Запрос.УстановитьПараметр("Владелец", ОбъектСсылка);
	Запрос.УстановитьПараметр("Наименование", ИмяГруппыСвойств);
	Запрос.УстановитьПараметр("Вид", ВидГруппыСвойств);
	Запрос.УстановитьПараметр("ЭтоГруппа", Истина);
	Запрос.УстановитьПараметр("Родитель", Справочники.Свойства.ПустаяСсылка());

	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Если Выборка.ПометкаУдаления Тогда
			ТекГруппа = Выборка.ссылка.ПолучитьОбъект();
			ТекГруппа.ПометкаУдаления = Ложь;
			ТекГруппа.ОбменДанными.Загрузка = Истина;
			ТекГруппа.Записать();
		КонецЕсли;
		РодительСвойств = Выборка.Ссылка;
		УдалитьОбъектИзСписка(Выборка.Ссылка, ОбщиеПеременные.СуществующиеСвойства);
	Иначе
		ТекГруппа = Справочники.Свойства.СоздатьГруппу();
		ТекГруппа.Владелец = ОбъектСсылка;
		ТекГруппа.Вид = ВидГруппыСвойств;
		ТекГруппа.Наименование = ИмяГруппыСвойств;
		ТекГруппа.ОбменДанными.Загрузка = Истина;
		ТекГруппа.Записать();
		РодительСвойств = ТекГруппа.Ссылка;
	КонецЕсли;
	Для Каждого Строка Из ИзмеренияРегистра Цикл
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапросаДляПоиска;
		// Для элемента вид не важен, может быть переопределен.
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "И Вид = &Вид", "");
		Запрос.УстановитьПараметр("Владелец", ОбъектСсылка);
		Запрос.УстановитьПараметр("Наименование", Строка.Наименование);
		Запрос.УстановитьПараметр("ЭтоГруппа", Ложь);
		Запрос.УстановитьПараметр("Родитель", РодительСвойств);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ТекСвойство = Выборка.ссылка.ПолучитьОбъект();
			Если Выборка.ПометкаУдаления Тогда
				ТекСвойство.ПометкаУдаления = Ложь;
				Если ОбщиеПеременные.ТолькоДобавлятьНовые Тогда
					ТекСвойство.Записать();
				КонецЕсли;
			КонецЕсли;
			УдалитьОбъектИзСписка(Выборка.Ссылка, ОбщиеПеременные.СуществующиеСвойства);
			Если ОбщиеПеременные.ТолькоДобавлятьНовые Тогда
				Продолжить;
			КонецЕсли;
		Иначе
			ТекСвойство = Справочники.Свойства.СоздатьЭлемент();
			ТекСвойство.Владелец = ОбъектСсылка;
			ТекСвойство.Родитель = РодительСвойств;
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ТекСвойство, Строка,,"Типы");
		ТекСвойство.Типы.Очистить(); 
		Если Строка.Наименование = "Регистратор" Тогда  
			НовТип = ТекСвойство.Типы.Добавить();
			НовТип.Тип = ОбъектСсылка;
			ТекСвойство.ТипыСтрокой = ИмяДокумента;
		Иначе
			Для Каждого СтрТип Из Строка.Типы Цикл
				НовТип = ТекСвойство.Типы.Добавить();
				ЗаполнитьЗначенияСвойств(НовТип, СтрТип);
			КонецЦикла;
		КонецЕсли;
		ТекСвойство.ОбменДанными.Загрузка = Истина;
		ТекСвойство.Записать();
	КонецЦикла;
КонецПроцедуры

Функция ПрочитатьСиноним(ЧтениеXML)
	ЧтениеXML.Прочитать(); // v8:item/key либо конец узла Синоним.
	Если ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
		// Синоним пуст.
		Возврат "";
	КонецЕсли;
	ЧтениеXML.Прочитать(); // v8:lang/key
	ЧтениеXML.Пропустить();
	ЧтениеXML.Прочитать(); // v8:content/value
	Возврат ПрочитатьXML(ЧтениеXML, Тип("Строка"));
КонецФункции

// Читает текст элемента и приводит значение к указанному типу
//
// Параметры:
//  Объект           - объект типа XMLЧтение, из которого происходит чтение
//  Тип              - тип получаемого значения
//  ИскатьПоСвойству - для ссылочных типов может быть указано свойство, по которому
//                     следует искать объект: "Код", "Наименование", <ИмяРеквизита>, "Имя" (предопределенного значения)
//
// Возвращаемое значение:
//  Значение xml-элемента, приведенное к соответствующему типу
//
Функция одЗначениеЭлемента(Объект, Знач Тип = Неопределено)
	Если Тип = Неопределено Тогда
		Тип = Тип("Строка");
	КонецЕсли;

	Значение = "";
	Имя      = Объект.ЛокальноеИмя;

	Пока Объект.Прочитать() Цикл
		ИмяУзла = Объект.ЛокальноеИмя;
		ТипУзла = Объект.ТипУзла;
		Если ТипУзла = ТипУзлаXML.Текст Тогда
			Значение = СокрП(Объект.Значение);
		ИначеЕсли (ИмяУзла = Имя) И (ТипУзла = ТипУзлаXML.КонецЭлемента) Тогда
			Прервать;
		Иначе
			Возврат Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Возврат XMLЗначение(Тип, Значение);	
	
КонецФункции // одЗначениеЭлемента() 

// Читает значение атрибута по имени из указанного объекта, приводит значение
// к указанному примитивному типу
//
// Параметры:
//  Объект      - объект типа XMLЧтение, спозиционированный на начале элемента,
//                атрибут которого требуется получить
//  Тип         - Значение типа Тип. Тип атрибута. Если не передан, значит Строка
//  Имя         - Строка. Имя атрибута
//
// Возвращаемое значение:
//  Значение атрибута полученное по имени и приведенное к указанному типу
// 
Функция одАтрибут(Объект, Имя, Знач Тип = Неопределено)
	Если Тип = Неопределено Тогда
		Тип = Тип("Строка");
	КонецЕсли;
	СтрЗначение = СокрП(Объект.ПолучитьАтрибут(Имя));
	Если Не ПустаяСтрока(СтрЗначение) Тогда
		Возврат XMLЗначение(Тип, СтрЗначение);
	Иначе
		Если Тип = Тип("Строка") Тогда  // Строка
			Возврат ""; 
		ИначеЕсли Тип = Тип("Булево") Тогда
			Возврат Ложь;
		ИначеЕсли Тип = Тип("Число") Тогда
			Возврат 0;
		ИначеЕсли Тип = Тип("Дата") Тогда
			Возврат Дата("01.01.0001 00:00:00");
		КонецЕсли; 
	КонецЕсли; 
	
КонецФункции // одАтрибут() 

Процедура ЗаписатьСообщениеОбОшибке(ОбщиеПеременные, ТекстСообщения)
	СообщениеОшибка = Новый СообщениеПользователю;
	СообщениеОшибка.Текст = ТекстСообщения;
	СообщениеОшибка.Сообщить();

	Если ОбщиеПеременные.АдресВременногоХранилища <> Неопределено Тогда
		ВозвращаемоеЗначение = Новый Структура("Успешно, Ошибки", Ложь, ТекстСообщения);
		ПоместитьВоВременноеХранилище(ВозвращаемоеЗначение, ОбщиеПеременные.АдресВременногоХранилища);
	КонецЕсли;
	ОбщиеПеременные.Отказ = Истина;	
КонецПроцедуры

Функция НайтиПомещенныеФайлыПоИмени(ОбщиеПеременные, ИмяФайла)
	Результат = "";
	Для Каждого ТекФайл Из ОбщиеПеременные.ПомещенныеФайлы Цикл
		Если ТекФайл.ПолноеИмя = ИмяФайла 
			Или СтрНайти(ТекФайл.ПолноеИмя, ОбщиеПеременные.РазделительПути + ИмяФайла, НаправлениеПоиска.СКонца) > 0 Тогда
			Результат = ТекФайл.Хранение;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция НайтиПомещенныеФайлыПоМаске(ПомещенныеФайлы, ВложенныйКаталог, РасширениеФайла, ОбщиеПеременные)
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ИсходноеИмяФайла");
	Результат.Колонки.Добавить("ТекущееИмяФайла");
	
	ИсключитьВложенныеКаталоги = (СтрНайти(РасширениеФайла, "mdo") = 0);

	Для Каждого ТекФайл Из ПомещенныеФайлы Цикл
		Если ТекФайл.ЭтоРасширение <> ОбщиеПеременные.ЭтоРасширение Тогда
			Продолжить;
		ИначеЕсли ОбщиеПеременные.ЭтоРасширение Тогда
			ЭтоНаше = СтрНайти(ТекФайл.ПолноеИмя, ОбщиеПеременные.ПутьКФайламРасширения, НаправлениеПоиска.СКонца) > 0;
			Если НЕ ЭтоНаше Тогда
				Продолжить;
			КонецЕсли;
		КонецЕсли;
		ПозицияКаталога = СтрНайти(ТекФайл.ПолноеИмя, ВложенныйКаталог,НаправлениеПоиска.СКонца);
		ПозицияРасширения = СтрНайти(ТекФайл.ПолноеИмя, РасширениеФайла,НаправлениеПоиска.СКонца);
		Если ПозицияКаталога > 0
			И ПозицияРасширения > 0 Тогда
			Если ИсключитьВложенныеКаталоги Тогда
				// Исключаем вариант с вложенными каталогами.
				ПозицияКонцаКаталога = ПозицияКаталога + СтрДлина(ВложенныйКаталог) + 1;
				КраткоеИмяФайла = Сред(ТекФайл.ПолноеИмя, ПозицияКонцаКаталога, ПозицияРасширения);
				ЭтоПодкаталог = СтрНайти(КраткоеИмяФайла, "/") > 0 Или СтрНайти(КраткоеИмяФайла, "\") > 0;
				Если ЭтоПодкаталог Тогда
					Продолжить;
				КонецЕсли;
			КонецЕсли;
			НовСтрока = Результат.Добавить();
			НовСтрока.ИсходноеИмяФайла = ТекФайл.ПолноеИмя;
			НовСтрока.ТекущееИмяФайла = ТекФайл.Хранение;
		КонецЕсли;
	КонецЦикла;
	Возврат Результат;
КонецФункции

Функция ПолучитьРазделительПутиФайлов(ОбщиеПеременные)
	ПервыйФайл = ОбщиеПеременные.ПомещенныеФайлы[0].ПолноеИмя;
	Если СтрНайти(ПервыйФайл, "/") > 0 Тогда
		ОбщиеПеременные.Вставить("РазделительПути", "/");
	ИначеЕсли СтрНайти(ПервыйФайл, "\") > 0 Тогда
		ОбщиеПеременные.Вставить("РазделительПути", "\");
	КонецЕсли;
КонецФункции

Процедура ЗаполнитьПростойТипСвойства(ОбщиеПеременные, ТекСвойство, ПростойТип, ТекТипыСвойства)
	ПроверятьНаличие = Ложь;
	Если ОбщиеПеременные.ЭтоРасширение Тогда
		Если  ТекСвойство.Типы.Количество() > 0 Тогда
			ПроверятьНаличие = Истина;
		КонецЕсли;
	Иначе
		ТекСвойство.Типы.Очистить();
		ТекСвойство.ТипыСтрокой = ПростойТип;
	КонецЕсли;
	Для Каждого ТекТип Из ТекТипыСвойства Цикл
		ТипСсылка = Неопределено;
		ОбщиеПеременные.ПростыеТипы.Свойство(ТекТип, ТипСсылка);
		Если ТипСсылка = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если ПроверятьНаличие Тогда
			Если ТекСвойство.Типы.Найти(ТипСсылка, "Тип") <> Неопределено Тогда
				Продолжить;
			Иначе
				ТекСвойство.ТипыСтрокой = ТекСвойство.ТипыСтрокой + ?(ЗначениеЗаполнено(ТекСвойство.ТипыСтрокой), ",", "") + ПростойТип;
			КонецЕсли;
		КонецЕсли;
		СтрокаТип = ТекСвойство.Типы.Добавить();
		СтрокаТип.Тип = ТипСсылка;
	КонецЦикла;
	ТекТипыСвойства.Очистить(); 
КонецПроцедуры

Процедура ДобавитьТипВОтложенноеЗаполнение(ОбщиеПеременные, ТекСвойствоСсылка, ТекТипыСвойства)
	ТипыСвойстваЗагруженные = Неопределено;
	Если ТекТипыСвойства.Количество() = 0 Тогда
		Возврат;
	ИначеЕсли ОбщиеПеременные.ЭтоРасширение Тогда
		ТипыСвойстваЗагруженные = ОбщиеПеременные.ОтложенноеЗаполнениеТиповСвойств.Получить(ТекСвойствоСсылка);
	КонецЕсли;
	Если ТипыСвойстваЗагруженные = Неопределено Тогда
		ОбщиеПеременные.ОтложенноеЗаполнениеТиповСвойств.Вставить(ТекСвойствоСсылка, ТекТипыСвойства);
		Возврат;
	КонецЕсли;
	ЕстьДополнения = Ложь;
	Для Каждого ТекТип Из ТекТипыСвойства Цикл
		Если ТипыСвойстваЗагруженные.Найти(ТекТип) = Неопределено Тогда
			ТипыСвойстваЗагруженные.Добавить(ТекТип);
			ЕстьДополнения = Истина;
		КонецЕсли;
	КонецЦикла;
	Если  ЕстьДополнения Тогда
		ОбщиеПеременные.ОтложенноеЗаполнениеТиповСвойств.Вставить(ТекСвойствоСсылка, ТипыСвойстваЗагруженные);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти
#КонецЕсли

Процедура КД3_ИнициализацияОбъектовОтложенногоЗаполнения(ОбщиеПеременные)
	
	ТабРегистраторыИРегистры = Новый ТаблицаЗначений;
	ТабРегистраторыИРегистры.Колонки.Добавить("Регистр");
	ТабРегистраторыИРегистры.Колонки.Добавить("Регистратор");
	ТабРегистраторыИРегистры.Колонки.Добавить("ЗапрещеноПроведениеДокумента");
	
	ОбщиеПеременные.Вставить("КД3_ОтложеннаяПривязкаБизнесПроцессаКЗадаче", Новый Соответствие);
	ОбщиеПеременные.Вставить("ОтложенноеЗаполнениеТиповСвойств", Новый Соответствие);
	ОбщиеПеременные.Вставить("РегистраторыИРегистры", ТабРегистраторыИРегистры);
	
КонецПроцедуры

Процедура КД3_ОбработатьКаталогиСДанными(ОбщиеПеременные, ГруппыОбъектов, ЭтоРасширение, ПутьКФайламЗагрузки = "")
	
	// Добавление этапов обработки групп объектов для конфигурации и всех расширений. Выполняются в КД3_ОбработатьГруппыОбъектовМногопоточно
	
	Если ОбщиеПеременные.ТолькоОбновитьПланыОбмена Тогда
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 1, "ПланыОбмена", "ExchangePlans", "ПланОбменаСсылка", Перечисления.ТипыОбъектов.ПланОбмена, ЭтоРасширение, ПутьКФайламЗагрузки);
	Иначе
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 1, "Справочники", "Catalogs", "СправочникСсылка", Перечисления.ТипыОбъектов.Справочник, ЭтоРасширение, ПутьКФайламЗагрузки);
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 1, "Документы", "Documents", "ДокументСсылка", Перечисления.ТипыОбъектов.Документ, ЭтоРасширение, ПутьКФайламЗагрузки);
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 1, "Перечисления", "Enums", "ПеречислениеСсылка", Перечисления.ТипыОбъектов.Перечисление, ЭтоРасширение, ПутьКФайламЗагрузки);
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 1, "Задачи", "Tasks", "ЗадачаСсылка", Перечисления.ТипыОбъектов.Задача, ЭтоРасширение, ПутьКФайламЗагрузки);
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 1, "БизнесПроцессы", "BusinessProcesses", "БизнесПроцессСсылка", Перечисления.ТипыОбъектов.БизнесПроцесс, ЭтоРасширение, ПутьКФайламЗагрузки);
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 1, "ПланыСчетов", "ChartsOfAccounts", "ПланСчетовСсылка", Перечисления.ТипыОбъектов.ПланСчетов, ЭтоРасширение, ПутьКФайламЗагрузки);
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 1, "ПланыВидовРасчета", "ChartsOfCalculationTypes", "ПланВидовРасчетаСсылка", Перечисления.ТипыОбъектов.ПланВидовРасчета, ЭтоРасширение, ПутьКФайламЗагрузки);
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 1, "ПланыВидовХарактеристик", "ChartsOfCharacteristicTypes", "ПланВидовХарактеристикСсылка", Перечисления.ТипыОбъектов.ПланВидовХарактеристик, ЭтоРасширение, ПутьКФайламЗагрузки);
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 1, "РегистрыБухгалтерии", "AccountingRegisters", "РегистрБухгалтерииЗапись", Перечисления.ТипыОбъектов.РегистрБухгалтерии, ЭтоРасширение, ПутьКФайламЗагрузки);
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 1, "РегистрыНакопления", "AccumulationRegisters", "РегистрНакопленияЗапись", Перечисления.ТипыОбъектов.РегистрНакопления, ЭтоРасширение, ПутьКФайламЗагрузки);
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 1, "РегистрыРасчета", "CalculationRegisters", "РегистрРасчетаЗапись", Перечисления.ТипыОбъектов.РегистрРасчета, ЭтоРасширение, ПутьКФайламЗагрузки);
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 1, "РегистрыСведений", "InformationRegisters", "РегистрСведенийЗапись", Перечисления.ТипыОбъектов.РегистрСведений, ЭтоРасширение, ПутьКФайламЗагрузки);
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 1, "ПланыОбмена", "ExchangePlans", "ПланОбменаСсылка", Перечисления.ТипыОбъектов.ПланОбмена, ЭтоРасширение, ПутьКФайламЗагрузки);
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 1, "Константы", "Constants", "Константа", Перечисления.ТипыОбъектов.НаборКонстант, ЭтоРасширение, ПутьКФайламЗагрузки);
		//+КД3
		Если ОбщиеПеременные.КД3_ИспользоватьКонтекстнуюПодсказку И ОбщиеПеременные.КД3_МестоХраненияИндексов <> 0 Тогда
			КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 1, "КД3_ОбщиеМодули",,,, ЭтоРасширение, ПутьКФайламЗагрузки);
		КонецЕсли;
		//-КД3
		
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 2, "ОпределяемыеТипы", "", "", Неопределено, ЭтоРасширение, ПутьКФайламЗагрузки);
		
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 3, "ПодпискиНаСобытия", "EventSubscriptions", "ПодпискаНаСобытие", Перечисления.ТипыОбъектов.ПодпискаНаСобытие, ЭтоРасширение, ПутьКФайламЗагрузки);
		
		КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, 4, "ОбщиеРеквизиты", "CommonAttributes", "", Неопределено, ЭтоРасширение, ПутьКФайламЗагрузки);
	КонецЕсли;
	
КонецПроцедуры

Функция КД3_НовыйГруппыОбъектов()
	
	КД3_ГруппыОбъектов = Новый ТаблицаЗначений;
	
	КД3_ГруппыОбъектов.Колонки.Добавить("НомерЭтапа");
	КД3_ГруппыОбъектов.Колонки.Добавить("ПараметрыЗадания");
	КД3_ГруппыОбъектов.Колонки.Добавить("АдресРезультата");
	КД3_ГруппыОбъектов.Колонки.Добавить("ИД");
	КД3_ГруппыОбъектов.Колонки.Добавить("ЭтоРасширение", Новый ОписаниеТипов("Булево"));
	КД3_ГруппыОбъектов.Колонки.Добавить("ИмяГруппы");
	КД3_ГруппыОбъектов.Колонки.Добавить("Завершено", Новый ОписаниеТипов("Булево"));
	
	Возврат КД3_ГруппыОбъектов;
	
КонецФункции

Процедура КД3_ДобавитьГруппуКОбработке(ГруппыОбъектов, НомерЭтапа, ИмяГруппы, ИмяКаталога, ПрефиксИменЭлементов, ТипОбъекта, ЭтоРасширение, ПутьКФайламРасширения)
	
	ПараметрыЗадания = Новый Структура;
	ПараметрыЗадания.Вставить("ИмяГруппы", ИмяГруппы);
	ПараметрыЗадания.Вставить("ИмяКаталога", ИмяКаталога);
	ПараметрыЗадания.Вставить("ПрефиксИменЭлементов", ПрефиксИменЭлементов);
	ПараметрыЗадания.Вставить("ТипОбъекта", ТипОбъекта);
	ПараметрыЗадания.Вставить("ЭтоРасширение", ЭтоРасширение);
	Если ЭтоРасширение Тогда
		ПараметрыЗадания.Вставить("ОтложенноеЗаполнение", Новый Структура);
		ПараметрыЗадания.Вставить("ПутьКФайламРасширения", ПутьКФайламРасширения);
	КонецЕсли;
	
	СтрокаГруппы = ГруппыОбъектов.Добавить();
	СтрокаГруппы.НомерЭтапа = НомерЭтапа;
	СтрокаГруппы.ИмяГруппы = ИмяГруппы;
	СтрокаГруппы.ЭтоРасширение = ЭтоРасширение;
	СтрокаГруппы.ПараметрыЗадания = ПараметрыЗадания;
	
КонецПроцедуры

Процедура КД3_ПолучитьСуществующиеСсылки(ОбщиеПеременные, ТипОбъекта)
	
	Если ОбщиеПеременные.ЗагрузитьНовую Тогда
		ОбщиеПеременные.Вставить("СуществующиеОбъекты", Новый Массив);
		ОбщиеПеременные.Вставить("СуществующиеСвойства", Новый Массив);
		ОбщиеПеременные.Вставить("СуществующиеЗначения", Новый Массив);
	Иначе
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Конфигурация", ОбщиеПеременные.Конфигурация);
		Если ТипОбъекта = Неопределено Тогда
			Запрос.Текст =
			"ВЫБРАТЬ
			|	Объекты.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Объекты КАК Объекты
			|ГДЕ
			|	Объекты.Владелец = &Конфигурация
			|	И НЕ Объекты.ПометкаУдаления
			|	И НЕ Объекты.ЭтоГруппа
			|	И Объекты.Родитель <> ЗНАЧЕНИЕ(Справочник.Объекты.ПустаяСсылка)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Свойства.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Свойства КАК Свойства
			|ГДЕ
			|	Свойства.Владелец.Владелец = &Конфигурация
			|	И НЕ Свойства.ПометкаУдаления
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Значения.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Значения КАК Значения
			|ГДЕ
			|	Значения.Владелец.Владелец = &Конфигурация
			|	И НЕ Значения.ПометкаУдаления";
		Иначе
			Запрос.УстановитьПараметр("ТипОбъекта", ТипОбъекта);
			Запрос.Текст =
			"ВЫБРАТЬ
			|	Объекты.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Объекты КАК Объекты
			|ГДЕ
			|	Объекты.Владелец = &Конфигурация
			|	И НЕ Объекты.ПометкаУдаления
			|	И НЕ Объекты.ЭтоГруппа
			|	И Объекты.Тип = &ТипОбъекта
			|	И Объекты.Родитель <> ЗНАЧЕНИЕ(Справочник.Объекты.ПустаяСсылка)
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Свойства.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Свойства КАК Свойства
			|ГДЕ
			|	Свойства.Владелец.Владелец = &Конфигурация
			|	И Свойства.Владелец.Тип = &ТипОбъекта
			|	И НЕ Свойства.ПометкаУдаления
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	Значения.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.Значения КАК Значения
			|ГДЕ
			|	Значения.Владелец.Владелец = &Конфигурация
			|	И Значения.Владелец.Тип = &ТипОбъекта
			|	И НЕ Значения.ПометкаУдаления";
		КонецЕсли;
		
		РезультатЗапроса = Запрос.ВыполнитьПакет();
		
		ОбщиеПеременные.Вставить("СуществующиеОбъекты", РезультатЗапроса[0].Выгрузить().ВыгрузитьКолонку("Ссылка"));
		ОбщиеПеременные.Вставить("СуществующиеСвойства", РезультатЗапроса[1].Выгрузить().ВыгрузитьКолонку("Ссылка"));
		ОбщиеПеременные.Вставить("СуществующиеЗначения", РезультатЗапроса[2].Выгрузить().ВыгрузитьКолонку("Ссылка"));
	КонецЕсли;
	
КонецПроцедуры

Процедура КД3_УдалитьОтсутствующиеСсылки(ОбщиеПеременные, СуществующиеОбъекты)
	
	Для Каждого Ссылка Из СуществующиеОбъекты Цикл
		КУдалению = Ссылка.ПолучитьОбъект();
		КУдалению.ПометкаУдаления = Истина;
		КУдалению.Записать();
	КонецЦикла;
	
КонецПроцедуры

Процедура КД3_ОбработатьГруппыОбъектовМногопоточно(ОбщиеПеременные)
	
	ПрогрессЗаголовок = НСтр("ru='Обработка загруженных данных.'");
	КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессОбщий(ОбщиеПеременные, ПрогрессЗаголовок);
	
	// Инициализация обрабатываемых групп объектов для конфигурации и расширений
	
	ГруппыОбъектов = КД3_НовыйГруппыОбъектов();
	
	КД3_ОбработатьКаталогиСДанными(ОбщиеПеременные, ГруппыОбъектов, Ложь);
	Если ОбщиеПеременные.ЕстьРасширения Тогда
		Для Каждого ПутьКФайламРасширения Из ОбщиеПеременные.КаталогиРасширений Цикл
			КД3_ОбработатьКаталогиСДанными(ОбщиеПеременные, ГруппыОбъектов, Истина, ПутьКФайламРасширения);
		КонецЦикла;
	КонецЕсли;
	
	ГруппыОбъектов.Сортировать("НомерЭтапа,ЭтоРасширение");
	
	Если ОбщиеПеременные.КД3_КоличествоПотоков = 1 Тогда
		КД3_ИнициализацияОбъектовОтложенногоЗаполнения(ОбщиеПеременные);
		
	Иначе
		КД3_ПервыйЭтапОсновногоПотока = 2;
		
		ТаймаутЗадания = ?(КД3_ЗагрузкаМетаданных.НужноСообщатьПрогресс(ОбщиеПеременные), 5, 60);
		ПараметрыФоновыхЗаданий = КД_ПараметрыСлужебныхФоновыхЗаданий(ОбщиеПеременные.КД3_УникальныйИдентификаторФормы);
		
		Задания = Новый Массив;
		ИменаГруппВОбработке = Новый Соответствие;
		
		ОтложенноеЗаполнение = Новый Структура;
		КД3_ИнициализацияОбъектовОтложенногоЗаполнения(ОтложенноеЗаполнение);
		
		Если НЕ ОбщиеПеременные.ЗагрузитьНовую Тогда
			ОтложенноеЗаполнение.Вставить("СуществующиеОбъекты", Новый Соответствие);
			ОтложенноеЗаполнение.Вставить("СуществующиеСвойства", Новый Соответствие);
			ОтложенноеЗаполнение.Вставить("СуществующиеЗначения", Новый Соответствие);
		КонецЕсли;
	
		ТекущийЭтап = 1;
		
		// Обработка групп объектов многопоточно в фоновых заданиях
		
		Пока Истина Цикл
			
			Если ТекущийЭтап = КД3_ПервыйЭтапОсновногоПотока Тогда
				Прервать;
			КонецЕсли;
			
			Для Каждого ГруппаОбъектов Из ГруппыОбъектов Цикл
				Если Задания.Количество() = ОбщиеПеременные.КД3_КоличествоПотоков Тогда
					Прервать; // После заполнения пула заданий переход к их ожиданию
				КонецЕсли;
				Если ГруппаОбъектов.НомерЭтапа > ТекущийЭтап Тогда
					Прервать; // Обработаны все группы текущего этапа
				КонецЕсли;
				Если ГруппаОбъектов.ИД <> Неопределено Тогда
					Продолжить; // Группа выполняется
				КонецЕсли;
				Если ГруппаОбъектов.ЭтоРасширение И ИменаГруппВОбработке[ГруппаОбъектов.ИмяГруппы] = Истина Тогда
					Продолжить; // Одновременно может обрабатываться только одна аналогичная группа
				КонецЕсли;
				
				ГруппаОбъектов.АдресРезультата = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор);
				ИменаГруппВОбработке[ГруппаОбъектов.ИмяГруппы] = Истина;
				
				КД3_ЗагрузкаМетаданных.НачатьПолучениеРасшифровки(ОбщиеПеременные, ГруппаОбъектов.ИмяГруппы);
				
				// При обработке объектов расширения используются оставшиеся существующие объекты/свойства/значения
				Если НЕ ОбщиеПеременные.ЗагрузитьНовую И ГруппаОбъектов.ЭтоРасширение И ЗначениеЗаполнено(ГруппаОбъектов.ПараметрыЗадания.ТипОбъекта) Тогда
					ОтложенноеЗаполнениеЗадания = ГруппаОбъектов.ПараметрыЗадания.ОтложенноеЗаполнение;
					ОтложенноеЗаполнениеЗадания.Вставить("СуществующиеОбъекты", ОтложенноеЗаполнение.СуществующиеОбъекты[ГруппаОбъектов.ИмяГруппы]);
					ОтложенноеЗаполнениеЗадания.Вставить("СуществующиеСвойства", ОтложенноеЗаполнение.СуществующиеСвойства[ГруппаОбъектов.ИмяГруппы]);
					ОтложенноеЗаполнениеЗадания.Вставить("СуществующиеЗначения", ОтложенноеЗаполнение.СуществующиеЗначения[ГруппаОбъектов.ИмяГруппы]);
				КонецЕсли;
				
				ПараметрыМетода = Новый Массив;
				ПараметрыМетода.Добавить(ОбщиеПеременные);
				ПараметрыМетода.Добавить(ГруппаОбъектов.ПараметрыЗадания);
				ПараметрыМетода.Добавить(ГруппаОбъектов.АдресРезультата);
				
				НовоеЗадание = ФоновыеЗадания.Выполнить(ПараметрыФоновыхЗаданий.ИмяМетода, ПараметрыМетода, , ПараметрыФоновыхЗаданий.Наименование);
				Задания.Добавить(НовоеЗадание);
				ГруппаОбъектов.ИД = НовоеЗадание.УникальныйИдентификатор;
			КонецЦикла;
			
			Если Задания.Количество() = 0 Тогда
				Если ГруппыОбъектов.Количество() = 0 Тогда
					Прервать;
				КонецЕсли;
				ТекущийЭтап = ТекущийЭтап + 1;
				Продолжить;
			КонецЕсли;
			
			Задания = ФоновыеЗадания.ОжидатьЗавершенияВыполнения(Задания, ТаймаутЗадания);
			
			// Проверка завершенных заданий и заданий с ошибками
			ИзмененПрогрессРасшифровки = Ложь;
			Индекс = Задания.Количество() - 1;
			Пока Индекс >= 0 Цикл
				Задание = Задания[Индекс];
				
				// Получение последнего сообщения прогресса и вывод остальных сообщений
				СообщениеПрогресса = Неопределено;
				Сообщения = Задание.ПолучитьСообщенияПользователю(Истина);
				Для Каждого Сообщение Из Сообщения Цикл
					Если КД3_ЗагрузкаМетаданных.ЭтоСообщениеПрогрессаРасшифровки(Сообщение) Тогда
						СообщениеПрогресса = Сообщение;
					Иначе
						ОбщегоНазначения.СообщитьПользователю(Сообщение.Текст);
					КонецЕсли;
				КонецЦикла;
				
				Если Задание.Состояние = СостояниеФоновогоЗадания.Завершено Тогда
					
					ГруппаОбъектов = ГруппыОбъектов.Найти(Задание.УникальныйИдентификатор, "ИД");
					
					СообщениеПрогресса = Неопределено;
					КД3_ЗагрузкаМетаданных.ЗавершитьПолучениеРасшифровки(ОбщиеПеременные, ГруппаОбъектов.ИмяГруппы);
					КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессОбщий(ОбщиеПеременные);
					
					ИменаГруппВОбработке[ГруппаОбъектов.ИмяГруппы] = Неопределено;
					КД3_ОбработатьРезультатГруппыОбъектов(ГруппаОбъектов, ОтложенноеЗаполнение);
					ГруппыОбъектов.Удалить(ГруппаОбъектов);
					
					Задания.Удалить(Индекс);
					
				ИначеЕсли Задание.Состояние <> СостояниеФоновогоЗадания.Активно Тогда
					// Задание завершено аварийно или отменено. Завершение всех активных заданий
					Для Каждого Задание Из Задания Цикл
						Если Задание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
							Задание.Отменить();
						КонецЕсли;
					КонецЦикла;
					
					ОписаниеОшибки = ?(Задание.ИнформацияОбОшибке = Неопределено, "", ПодробноеПредставлениеОшибки(Задание.ИнформацияОбОшибке));
					ВызватьИсключение  СтрШаблон(НСтр("ru='Ошибка при загрузке данных: %1. Подробнее в журнале регистрации.'"), ОписаниеОшибки);
					
				ИначеЕсли СообщениеПрогресса <> Неопределено Тогда
					ИзмененПрогрессРасшифровки = Истина;
					КД3_ЗагрузкаМетаданных.ПрочитатьПрогрессРасшифровки(ОбщиеПеременные, СообщениеПрогресса);
				КонецЕсли;
				Индекс = Индекс - 1;
			КонецЦикла;
			
			Если ИзмененПрогрессРасшифровки Тогда
				КД3_ЗагрузкаМетаданных.СообщитьПрогрессОбщий(ОбщиеПеременные);
			КонецЕсли;
		КонецЦикла;
		
		// Перенос полученных данных отложенного заполнения в переменные основного потока
		Для Каждого ОписаниеСвойства Из ОтложенноеЗаполнение Цикл
			Если СтрНачинаетсяС(ОписаниеСвойства.Ключ, "Существующие") Тогда
				// Существующие Объекты, Свойства, Значения храняться в разрезе групп
				Для Каждого ОписаниеГруппы Из ОписаниеСвойства.Значение  Цикл
					ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ОбщиеПеременные[ОписаниеСвойства.Ключ], ОписаниеГруппы.Значение);
				КонецЦикла;
			Иначе
				ОбщиеПеременные.Вставить(ОписаниеСвойства.Ключ, ОписаниеСвойства.Значение);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Обработка остальных этапов в основном потоке
	
	Для Каждого ГруппаОбъектов Из ГруппыОбъектов Цикл
		КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессОбщий(ОбщиеПеременные, ПрогрессЗаголовок);
		КД3_ОбработатьГруппуОбъектовМногопоточно(ОбщиеПеременные, ГруппаОбъектов.ПараметрыЗадания, Неопределено, Истина);
	КонецЦикла;
	
КонецПроцедуры

Процедура КД3_ОбработатьРезультатГруппыОбъектов(ГруппаОбъектов, ОтложенноеЗаполнение)
	
	// Перенос результата обработки групп объектов в общие коллекции для постобработки
	// (РегистраторыИРегистры, ОтложенноеЗаполнениеТиповСвойств, СуществующиеОбъекты, СуществующиеСвойства, СуществующиеЗначения)
	
	Результат = ПолучитьИзВременногоХранилища(ГруппаОбъектов.АдресРезультата);
	Если Результат = Неопределено Тогда
		ВызватьИсключение "Отсутствует результат для группы: " + ГруппаОбъектов.ИмяГруппы;
	КонецЕсли;
	Для Каждого КлючИЗначение Из Результат Цикл
		ОбщаяКоллекция = ОтложенноеЗаполнение[КлючИЗначение.Ключ];
		
		Если СтрНачинаетсяС(КлючИЗначение.Ключ, "Существующие") Тогда
			// Существующие Объекты, Свойства, Значения хранятся в разрезе групп объектов для обработке расширений
			ОбщаяКоллекция[ГруппаОбъектов.ИмяГруппы] = КлючИЗначение.Значение;
			
		ИначеЕсли ОбщаяКоллекция.Количество() = 0 Тогда
			// Полное замещение
			ОтложенноеЗаполнение.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
			
		ИначеЕсли КлючИЗначение.Ключ = "ОтложенноеЗаполнениеТиповСвойств" Тогда
			// Перенос свойств с объединением типов
			ОтложенноеЗаполнение.Вставить("ЭтоРасширение", ГруппаОбъектов.ЭтоРасширение);
			Для Каждого ЭлементКоллекции Из КлючИЗначение.Значение Цикл
				ДобавитьТипВОтложенноеЗаполнение(ОтложенноеЗаполнение, ЭлементКоллекции.Ключ, ЭлементКоллекции.Значение);
			КонецЦикла;
			
		ИначеЕсли ТипЗнч(КлючИЗначение.Значение) = Тип("ТаблицаЗначений") Тогда
			Для Каждого СтрокаТЗ Из КлючИЗначение.Значение Цикл
				ЗаполнитьЗначенияСвойств(ОбщаяКоллекция .Добавить(), СтрокаТЗ);
			КонецЦикла;
			
		ИначеЕсли ТипЗнч(КлючИЗначение.Значение) = Тип("Массив") Тогда
			Для Каждого ЭлементМассива Из КлючИЗначение.Значение Цикл
				ОбщаяКоллекция.Добавить( ЭлементМассива);
			КонецЦикла;
			
		Иначе
			Для Каждого ЭлементКоллекции Из КлючИЗначение.Значение Цикл
				ОбщаяКоллекция.Вставить(ЭлементКоллекции.Ключ, ЭлементКоллекции.Значение);
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	УдалитьИзВременногоХранилища(ГруппаОбъектов.АдресРезультата);
	
КонецПроцедуры

Процедура КД3_ОбработатьГруппуОбъектовМногопоточно(ОбщиеПеременные, ПараметрыЗадания, АдресРезультата, ЭтоОсновнойПоток = Ложь) Экспорт
	
	ОбщиеПеременные.Вставить("ЭтоРасширение", ПараметрыЗадания.ЭтоРасширение);
	Если ПараметрыЗадания.ЭтоРасширение Тогда
		ОбщиеПеременные.Вставить("ПутьКФайламРасширения", ПараметрыЗадания.ПутьКФайламРасширения);
	КонецЕсли;
	
	Если НЕ ЭтоОсновнойПоток Тогда
		// Заполнение в копии переменных признака основного потока для вывода прогресса
		ОбщиеПеременные.КД3_ОсновнойПоток = Ложь;
		КД3_ИнициализацияОбъектовОтложенногоЗаполнения(ОбщиеПеременные);
	КонецЕсли;
	
	Попытка
		Если ПараметрыЗадания.ИмяГруппы = "КД3_ОбщиеМодули" Тогда
			КД3_ЗагрузкаМетаданных.ОбработатьГруппуОбщихМодулей(ОбщиеПеременные, НЕ ОбщиеПеременные.КД3_НастройкиКонфигурации.ЗагружатьИзФайлов);
			Если НЕ ЭтоОсновнойПоток Тогда
				ПоместитьВоВременноеХранилище(Новый Структура, АдресРезультата);
			КонецЕсли;
		Иначе
			Если НЕ ЭтоОсновнойПоток Тогда
				Если ПараметрыЗадания.ЭтоРасширение Тогда
					// При обработке объектов расширения используются оставшиеся существующие объекты/свойства/значения
					ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ОбщиеПеременные, ПараметрыЗадания.ОтложенноеЗаполнение, Истина);
				Иначе
					КД3_ПолучитьСуществующиеСсылки(ОбщиеПеременные, ПараметрыЗадания.ТипОбъекта);
				КонецЕсли;
			КонецЕсли;
			
			Если ПараметрыЗадания.ИмяГруппы = "Константы" Тогда
				ОбработатьКонстанты(ОбщиеПеременные);
			ИначеЕсли ПараметрыЗадания.ИмяГруппы = "ОпределяемыеТипы" Тогда
				ОбработатьОпределяемыеТипы(ОбщиеПеременные);
			Иначе
				ОбработатьГруппуОбъектов(ОбщиеПеременные, ПараметрыЗадания.ИмяГруппы, ПараметрыЗадания.ИмяКаталога, ПараметрыЗадания.ПрефиксИменЭлементов, ПараметрыЗадания.ТипОбъекта);
			КонецЕсли;
			
			Если НЕ ОбщиеПеременные.КД3_ОсновнойПоток Тогда
				// Передача в основное задание коллекций, которые обрабатываются после создания/обновления всех объектов
				Результат = Новый Структура;
				КД3_ИнициализацияОбъектовОтложенногоЗаполнения(Результат);
				
				Для Каждого КлючИЗначение Из Результат Цикл
					Результат[КлючИЗначение.Ключ] = ОбщиеПеременные[КлючИЗначение.Ключ];
				КонецЦикла;
				Если НЕ ОбщиеПеременные.ЗагрузитьНовую Тогда
					Результат.Вставить("СуществующиеОбъекты", ОбщиеПеременные.СуществующиеОбъекты);
					Результат.Вставить("СуществующиеСвойства", ОбщиеПеременные.СуществующиеСвойства);
					Результат.Вставить("СуществующиеЗначения", ОбщиеПеременные.СуществующиеЗначения);
				КонецЕсли;
				ПоместитьВоВременноеХранилище(Результат, АдресРезультата);
			КонецЕсли;
		КонецЕсли;
	Исключение
		ВызватьИсключение СтрШаблон(НСтр("ru='КД3: Ошибка обработки %1: %2'"), ПараметрыЗадания.ТипОбъекта, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
	
	Если ОбщиеПеременные.КД3_ИспользоватьКонтекстнуюПодсказку И ОбщиеПеременные.КД3_МестоХраненияИндексов <> 0 Тогда
		// Сохранение индексов метаданных (отчетов, обработок, общих модулей), обработанных в этом потоке
		Если ОбщиеПеременные.Свойство("КД3_Конфигурация") Тогда
			Для Каждого КлючИЗначение Из ОбщиеПеременные.КД3_Конфигурация Цикл
				Если КлючИЗначение.Значение.count > 0 Тогда
					КД3_Метаданные.ИзменитьОписаниеМетаданных(ОбщиеПеременные.Конфигурация, НРег(КлючИЗначение.Ключ), КлючИЗначение.Значение, ОбщиеПеременные.КД3_НастройкиКонфигурации);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура КД3_ОбработатьРегистраторы(ОбщиеПеременные)
	
	// Заполнение типов у регистраторов - свойств регистра.
	// Заполнение движений по регистрам у документов-регистраторов.
	Если НЕ ОбщиеПеременные.ТолькоОбновитьПланыОбмена И ОбщиеПеременные.ВариантЗагрузкиДвижений > 0 Тогда
		Если ОбщиеПеременные.ЕстьРасширения Тогда
			ОбщиеПеременные.РегистраторыИРегистры.Свернуть("Регистр, Регистратор, ЗапрещеноПроведениеДокумента");
		КонецЕсли;
		ОбщиеПеременные.РегистраторыИРегистры.Сортировать("Регистр, Регистратор");
		СвойстваРегистра = Неопределено;
		ТекстЗапросаСвойстваРегистра = "ВЫБРАТЬ
		|	Наименование,
		|	Синоним,
		|	Комментарий,
		|	Идентификатор,
		|	КвалификаторыЧисла_Длина,
		|	КвалификаторыЧисла_Точность,
		|	КвалификаторыЧисла_Неотрицательное,
		|	КвалификаторыСтроки_Фиксированная,
		|	КвалификаторыДаты_Состав,
		|	КвалификаторыЧисла_Длина,
		|	Вид,
		|	ТипыСтрокой,
		|	Типы
		|ИЗ Справочник.Свойства
		|ГДЕ Владелец.Владелец = &Конфигурация И Владелец = &Владелец И ЭтоГруппа = ЛОЖЬ
		|	И НЕ ПометкаУдаления";
		Запрос = Новый Запрос;
		Запрос.Текст = ТекстЗапросаСвойстваРегистра;
		Запрос.УстановитьПараметр("Конфигурация", ОбщиеПеременные.Конфигурация);
		ВидыСвойств = Новый Массив;
		ВидыСвойств.Добавить(Перечисления.ВидыСвойств.Измерение);
		ВидыСвойств.Добавить(Перечисления.ВидыСвойств.Реквизит);
		ВидыСвойств.Добавить(Перечисления.ВидыСвойств.Ресурс);
		ВидыСвойств.Добавить(Перечисления.ВидыСвойств.Свойство);
		
		ЗапросОбъекта = Новый Запрос;
		ЗапросОбъекта.Текст =
		"ВЫБРАТЬ
		|	Объекты.Ссылка КАК Ссылка,
		|	Объекты.Имя КАК Имя,
		|	Объекты.Тип КАК Тип
		|ИЗ
		|	Справочник.Объекты КАК Объекты
		|ГДЕ
		|	Объекты.Наименование = &Наименование
		|	И Объекты.Владелец = &Владелец
		|	И Объекты.Родитель = &Родитель";
		ЗапросОбъекта.УстановитьПараметр("Владелец", ОбщиеПеременные.Конфигурация);
		
		ТипыСтрокой = "";
		ТекРегистр = "";
		ОбъектРегистратор = Неопределено;
		//+КД3 Отображение прогресса
		Если ОбщиеПеременные.РегистраторыИРегистры.Количество() > 0 Тогда
			КД3_ЗагрузкаМетаданных.НачатьПрогрессРасшифровки(ОбщиеПеременные, ОбщиеПеременные.РегистраторыИРегистры.Количество());
		КонецЕсли;
		//+КД3 Отображение прогресса
		Для Каждого Строка Из ОбщиеПеременные.РегистраторыИРегистры Цикл
			//+КД3 Отображение прогресса
			КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессРасшифровки(ОбщиеПеременные);
			//-КД3 Отображение прогресса
			Если ОбщиеПеременные.ВариантЗагрузкиДвижений = 1 И Строка.ЗапрещеноПроведениеДокумента = Ложь Тогда
				Продолжить;
			КонецЕсли;
			Если ТекРегистр = "" Или ТекРегистр <> Строка.Регистр Тогда
				
				Если ОбъектРегистратор <> Неопределено Тогда
					ОбъектРегистратор.ТипыСтрокой = Лев(ТипыСтрокой, СтрДлина(ТипыСтрокой) - 1);
					ОбъектРегистратор.Записать();
				КонецЕсли;
				РодительОбъекта = Неопределено;
				ПолноеИмяОбъекта = ПреобразоватьИмяТипаВИмяОбъекта(Строка.Регистр, РодительОбъекта, ОбщиеПеременные);
				Если НЕ ЗначениеЗаполнено(ПолноеИмяОбъекта) Тогда
					ОбъектРегистратор = Неопределено;
					Продолжить;
				КонецЕсли;
				
				ЗапросОбъекта.УстановитьПараметр("Наименование", ПолноеИмяОбъекта);
				ЗапросОбъекта.УстановитьПараметр("Родитель", РодительОбъекта);
				ДанныеРегистра = ЗапросОбъекта.Выполнить().Выбрать();
				Если ДанныеРегистра.Следующий() Тогда
					ОбъектСсылка = ДанныеРегистра.Ссылка;
				Иначе
					ОбъектСсылка = Неопределено;
				КонецЕсли;
				//ОбъектСсылка = Справочники.Объекты.НайтиПоНаименованию(ПолноеИмяОбъекта, Истина, РодительОбъекта,ОбщиеПеременные.Конфигурация);
				Если НЕ ЗначениеЗаполнено(ОбъектСсылка) Тогда
					ОбъектРегистратор = Неопределено;
					Продолжить;
				КонецЕсли;
				// Зачитываем структуру регистра для последующего использования.
				Запрос.УстановитьПараметр("Владелец", ОбъектСсылка);
				Запрос.УстановитьПараметр("ВидыСвойств", ВидыСвойств);
				ИзмеренияРегистра = Запрос.Выполнить().Выгрузить();		
				
				//ДанныеРегистра = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОбъектСсылка, "Имя, Тип");
				СвойствоРегистратор = Справочники.Свойства.НайтиПоНаименованию("Регистратор", Истина,, ОбъектСсылка);
				Если ЗначениеЗаполнено(СвойствоРегистратор) Тогда
					ОбъектРегистратор = СвойствоРегистратор.ПолучитьОбъект();
					ОбъектРегистратор.Типы.Очистить();
					ТипыСтрокой = "";
				КонецЕсли;
			КонецЕсли;
			// Добавление свойств - движений в регистратор.
			Если ИзмеренияРегистра.Количество() > 0 Тогда
				НайтиСоздатьСвойстваНаборДвижений(Строка.Регистратор, ДанныеРегистра, ИзмеренияРегистра, ОбщиеПеременные);
			КонецЕсли;
			Если ОбъектРегистратор = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			СтрокаТип = ОбъектРегистратор.Типы.Добавить();
			СтрокаТип.Тип = Строка.Регистратор;
			ТипыСтрокой = ТипыСтрокой + СокрЛП(Строка.Регистратор) + ",";
		КонецЦикла;
		Если СвойствоРегистратор <> Неопределено И ОбъектРегистратор <> Неопределено Тогда
			Если ЗначениеЗаполнено(ТипыСтрокой) Тогда
				ОбъектРегистратор.ТипыСтрокой = Лев(ТипыСтрокой, СтрДлина(ТипыСтрокой) - 1);
			КонецЕсли;
			ОбъектРегистратор.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура КД3_ЗаполнениеТиповПлановВидовРасчета(ОбщиеПеременные)
	
	// Предопределенные ТЧ планов видов расчета.
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	Объекты.Ссылка,
		|	ВЫБОР КОГДА Свойства.Ссылка is null ТОГДА ЛОЖЬ
		|		ИНАЧЕ
		|	ИСТИНА Конец КАК ЕстьПериодДействияБазовый
		|ИЗ Справочник.Объекты КАК Объекты
		|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Свойства КАК Свойства
		|ПО Свойства.Владелец = Объекты.Ссылка
		|	И Свойства.Наименование = ""ПериодДействияБазовый""
		|ГДЕ Объекты.Владелец = &Конфигурация
		| И НЕ Объекты.ЭтоГруппа И Объекты.Родитель <> ЗНАЧЕНИЕ(Справочник.Объекты.ПустаяСсылка) И Объекты.ПометкаУдаления = ЛОЖЬ
		|	И Объекты.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектов.ПланВидовРасчета)";
	Запрос.УстановитьПараметр("Конфигурация", ОбщиеПеременные.Конфигурация);
	Результат = Запрос.Выполнить();
	ВсеПВР = Результат.Выгрузить().ВыгрузитьКолонку("Ссылка");
	Выборка = Результат.Выбрать();
	
	//+КД3 Отображение прогресса
	КД3_ЗагрузкаМетаданных.НачатьПрогрессРасшифровки(ОбщиеПеременные, Выборка.Количество());
	//-КД3 Отображение прогресса
	
	Пока Выборка.Следующий() Цикл
		//+КД3 Отображение прогресса
		КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессРасшифровки(ОбщиеПеременные);
		//-КД3 Отображение прогресса
		ДобавитьТабличныеЧастиВПланВидовРасчета(Выборка.Ссылка, ОбщиеПеременные, ВсеПВР, Выборка.ЕстьПериодДействияБазовый);
	КонецЦикла;
	
КонецПроцедуры

Процедура КД3_ЗаполнениеТиповЗагруженныхСвойств(ОбщиеПеременные, СвойстваТипаХарактеристика)
	
	// Заполнение типов загруженных свойств. 
	
	//+КД3 Отображение прогресса
	КД3_ЗагрузкаМетаданных.НачатьПрогрессРасшифровки(ОбщиеПеременные, ОбщиеПеременные.ОтложенноеЗаполнениеТиповСвойств.Количество());
	//-КД3 Отображение прогресса
	
	Для Каждого СвойстваИТипы Из ОбщиеПеременные.ОтложенноеЗаполнениеТиповСвойств Цикл
		//+КД3 Отображение прогресса
		КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессРасшифровки(ОбщиеПеременные);
		//-КД3 Отображение прогресса
		ТипыСвойства = СвойстваИТипы.Значение;
		Попытка
			СвойствоОбъект = СвойстваИТипы.Ключ.ПолучитьОбъект();
		Исключение
			Продолжить;
		КонецПопытки;
		СвойствоОбъект.Типы.Очистить();
		ТипыЗаполнены = Ложь;
		ТипыСтрокой = "";
		Для Каждого ТипСвойства Из ТипыСвойства Цикл
			Если СтрНайти(НРег(ТипСвойства), "definedtype.") > 0 И ОбщиеПеременные.Свойство("ОпределяемыеТипы") Тогда
				ПозицияТочки = СтрНайти(ТипСвойства, ".");
				ИмяТипа = Сред(ТипСвойства, ПозицияТочки + 1);
				ТипыВсе = Неопределено;
				Если ОбщиеПеременные.ОпределяемыеТипы.Свойство(ИмяТипа, ТипыВсе) Тогда
					Для Каждого ТекТип Из ТипыВсе Цикл
						СтрокаТип = СвойствоОбъект.Типы.Добавить();
						Если ТипЗнч(ТекТип) = Тип("Структура") Тогда
							СтрокаТип.Тип = ТекТип.Тип;
							ЗаполнитьЗначенияСвойств(СвойствоОбъект, ТекТип.Квалификаторы);
							ТипыСтрокой = ТипыСтрокой + СокрЛП(ТекТип.Тип) + ",";
						Иначе
							СтрокаТип.Тип = ТекТип;
							ТипыСтрокой = ТипыСтрокой + СокрЛП(ТекТип) + ",";
						КонецЕсли;
					КонецЦикла;
					ТипыЗаполнены = Истина;
				КонецЕсли;
				Продолжить; 
			ИначеЕсли СтрНайти(НРег(ТипСвойства), ":characteristic.") > 0
			//+КД3 FIX Обработка типа Характеристика в режиме загрузки из EDT
			ИЛИ (ОбщиеПеременные.ИсточникДанных = 1 И СтрНачинаетсяС(НРег(ТипСвойства), "characteristic.")) Тогда
			//-КД3 FIX
				// Тип - составной по типам входящим в ПВХ
				ПозицияТочки = СтрНайти(ТипСвойства, ".");
				ИмяПВХ = Сред(ТипСвойства, ПозицияТочки + 1);
				СвойстваТипаХарактеристика.Вставить(СвойстваИТипы.Ключ, ИмяПВХ);
				// Заполнение будет потом после обработки всех отложенных типов.
				Продолжить;
			КонецЕсли;
			РодительОбъекта = Неопределено;
			ПолноеИмяОбъекта = ПреобразоватьИмяТипаВИмяОбъекта(ТипСвойства, РодительОбъекта, ОбщиеПеременные);
			Если ЗначениеЗаполнено(ПолноеИмяОбъекта) Тогда
				ОбъектСсылка = Справочники.Объекты.НайтиПоНаименованию(ПолноеИмяОбъекта, Истина, РодительОбъекта,ОбщиеПеременные.Конфигурация);
				Если ЗначениеЗаполнено(ОбъектСсылка) Тогда
					СтрокаТип = СвойствоОбъект.Типы.Добавить();
					СтрокаТип.Тип = ОбъектСсылка;
					ТипыЗаполнены = Истина;
					ТипыСтрокой = ТипыСтрокой + СокрЛП(ОбъектСсылка) + ",";
				КонецЕсли;
			Иначе
				// Возможен простой тип вперемешку со ссылочными.
				Если СтроковыеФункцииКлиентСервер.ТолькоКириллицаВСтроке(ТипСвойства) Тогда
					ТипСсылка = Неопределено;
					Если ОбщиеПеременные.ПростыеТипы.Свойство(ТипСвойства, ТипСсылка) Тогда
						СтрокаТип = СвойствоОбъект.Типы.Добавить();
						СтрокаТип.Тип = ТипСсылка;
						ТипыЗаполнены = Истина;
						ТипыСтрокой = ТипыСтрокой + СокрЛП(ТипСвойства) + ",";
					КонецЕсли;   
				Иначе
					// Проверка на простой тип в перечне составного типа.
					ПозСкобки = СтрНайти(ТипСвойства, "(");
					Если ПозСкобки > 0 Тогда
						Часть1 = Лев(ТипСвойства, ПозСкобки - 1);
					Иначе
						Часть1 = СокрЛП(ТипСвойства);
					КонецЕсли;
					ПростойТипОбъект = Неопределено;
					Попытка
						ОбщиеПеременные.ПростыеТипы.Свойство(Часть1, ПростойТипОбъект);
					Исключение
						ПростойТипОбъект = Неопределено;
					КонецПопытки;
					Если ПростойТипОбъект <> Неопределено Тогда
						СтрокаТип = СвойствоОбъект.Типы.Добавить();
						СтрокаТип.Тип = ПростойТипОбъект;
						ТипыЗаполнены = Истина;
						ТипыСтрокой = ТипыСтрокой + СокрЛП(ТипСвойства) + ",";
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если ТипыЗаполнены Тогда
			СвойствоОбъект.ТипыСтрокой = Лев(ТипыСтрокой, СтрДлина(ТипыСтрокой) - 1);
			СвойствоОбъект.Записать();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура КД3_ЗаполнениеТипаСвойствХарактеристика(ОбщиеПеременные, СвойстваТипаХарактеристика)
	
	// Заполнение типа свойств Характеристика...
	ТекстЗапроса = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Тип
	|ИЗ Справочник.Свойства.Типы КАК ТипыПВХ 
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Свойства КАК СвойстваШ
	|ПО ТипыПВХ.Ссылка = СвойстваШ.Ссылка
	|ГДЕ СвойстваШ.Владелец.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыОбъектов.ПланВидовХарактеристик)
	|	И СвойстваШ.Владелец.Имя = &ИмяПВХ И СвойстваШ.Наименование = ""ТипЗначения""
	|	И СвойстваШ.Владелец.Владелец = &Владелец";
	
	//+КД3 Отображение прогресса
	КД3_ЗагрузкаМетаданных.НачатьПрогрессРасшифровки(ОбщиеПеременные, СвойстваТипаХарактеристика.Количество());
	//-КД3 Отображение прогресса
	
	Для Каждого СвойствоИИмяПВХ Из СвойстваТипаХарактеристика Цикл
		//+КД3 Отображение прогресса
		КД3_ЗагрузкаМетаданных.УвеличитьСообщитьПрогрессРасшифровки(ОбщиеПеременные);
		//-КД3 Отображение прогресса
		Попытка
			СвойствоОбъект = СвойствоИИмяПВХ.Ключ.ПолучитьОбъект();
		Исключение
			Продолжить;
		КонецПопытки;
		СвойствоОбъект.Типы.Очистить(); 
		ЗапросТипы = Новый Запрос;
		ЗапросТипы.Текст = ТекстЗапроса;
		ЗапросТипы.УстановитьПараметр("ИмяПВХ", СвойствоИИмяПВХ.Значение); 
		ЗапросТипы.УстановитьПараметр("Владелец", ОбщиеПеременные.Конфигурация);
		ТипыСтрокой = "";
		ВыборкаТипы = ЗапросТипы.Выполнить().Выбрать();
		ТипыЗаполнены = Ложь;
		Пока ВыборкаТипы.Следующий() Цикл 
			СтрокаТип = СвойствоОбъект.Типы.Добавить();
			СтрокаТип.Тип = ВыборкаТипы.Тип;
			ТипыЗаполнены = Истина;
			ТипыСтрокой = ТипыСтрокой + СокрЛП(ВыборкаТипы.Тип) + ",";
		КонецЦикла;
		Если ТипыЗаполнены Тогда
			СвойствоОбъект.ТипыСтрокой = Лев(ТипыСтрокой, СтрДлина(ТипыСтрокой) - 1);
			СвойствоОбъект.Записать();
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

Процедура КД3_ПривязкаЗадачКБизнесПроцессам(ОбщиеПеременные)
	
	//+КД3
	Для Каждого КлючИЗначение Из ОбщиеПеременные.КД3_ОтложеннаяПривязкаБизнесПроцессаКЗадаче Цикл
		ПривязатьБизнесПроцессКЗадаче(КлючИЗначение.Ключ, КлючИЗначение.Значение, ОбщиеПеременные);
	КонецЦикла;
	//-КД3
	
КонецПроцедуры

Процедура КД3_СохранитьКаталогКонфигурации(ОбщиеПеременные, НастройкиОнфигурации)
	
	КонфигурацияОбъект = ОбщиеПеременные.Конфигурация.ПолучитьОбъект();
	КонфигурацияОбъект.ОбновлятьМетаданныеПоРасписанию = Истина;
	КонфигурацияОбъект.КаталогЗагрузки = ОбщиеПеременные.КаталогЗагрузки;
	Если ОбщиеПеременные.ЕстьРасширения Тогда
		КонфигурацияОбъект.ЕстьРасширения = Истина;
		КонфигурацияОбъект.Расширения.Очистить();
		Для Каждого ПутьКФайламРасширения Из ОбщиеПеременные.КаталогиРасширений Цикл
			КонфигурацияОбъект.Расширения.Добавить().ИмяКаталогаЗагрузки = ПутьКФайламРасширения;
		КонецЦикла;
	КонецЕсли;
	КонфигурацияОбъект.Записать();
	
КонецПроцедуры
