#Область Индексы_метаданных

Функция НастройкиКонфигурации(Конфигурация) Экспорт
	
	КэшНастроек = ПараметрыПриложения["КД3_НастройкиКонфигурации"];
	Если КэшНастроек = Неопределено Тогда
		КэшНастроек = Новый Соответствие;
		ПараметрыПриложения["КД3_НастройкиКонфигурации"] = КэшНастроек;
	КонецЕсли;
	
	НастройкиКонфигурации = КэшНастроек[Конфигурация];
	Если НастройкиКонфигурации = Неопределено Тогда
		НастройкиКонфигурации = КД3_Метаданные.НастройкиКонфигурации(Конфигурация);
		КэшНастроек.Вставить(Конфигурация, НастройкиКонфигурации);
	КонецЕсли;
	Возврат НастройкиКонфигурации;
	
КонецФункции

Процедура СброситьНастройкиКонфигурацииВКэше(Конфигурация) Экспорт
	
	КэшНастроек = ПараметрыПриложения["КД3_НастройкиКонфигурации"];
	Если КэшНастроек <> Неопределено Тогда
		КэшНастроек.Вставить(Конфигурация, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

Функция ОписаниеМетаданных(Конфигурация, КлючКоллекции) Экспорт
	
	КэшКонфигурации = ПараметрыПриложения[Конфигурация];
	Если КэшКонфигурации = Неопределено Тогда
		КэшКонфигурации = Новый Соответствие;
		ПараметрыПриложения[Конфигурация] = КэшКонфигурации;
	КонецЕсли;
	
	ОписаниеМетаданных = КэшКонфигурации[КлючКоллекции];
	Если ОписаниеМетаданных = Неопределено Тогда
		ОписаниеМетаданных = КД3_Метаданные.ОписаниеМетаданных(Конфигурация, КлючКоллекции);
		Если ОписаниеМетаданных <> Неопределено Тогда
			КэшКонфигурации.Вставить(КлючКоллекции, ОписаниеМетаданных);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ОписаниеМетаданных;
	
КонецФункции

Функция ОписаниеСпискаМетаданных(Конфигурация, СписокМетаданных) Экспорт
	
	КэшКонфигурации = ПараметрыПриложения[Конфигурация];
	Если КэшКонфигурации = Неопределено Тогда
		КэшКонфигурации = Новый Соответствие;
		ПараметрыПриложения[Конфигурация] = КэшКонфигурации;
	КонецЕсли;
	
	ОписанияМетаданных = Новый Соответствие;
	ОписанияМетаданныхСервер = Новый Соответствие;
	Для Каждого ПолноеИмяМетаданного Из СтрРазделить(СписокМетаданных, ",") Цикл
		ОписаниеМетаданных = КэшКонфигурации[ПолноеИмяМетаданного];
		Если ОписаниеМетаданных = Неопределено Тогда
			ОписанияМетаданныхСервер.Вставить(ПолноеИмяМетаданного);
		Иначе
			ОписанияМетаданных.Вставить(ПолноеИмяМетаданного, ОписаниеМетаданных);
		КонецЕсли;
	КонецЦикла;
	
	КД3_Метаданные.ЗаполнитьОписаниеСпискаМетаданных(Конфигурация, ОписанияМетаданныхСервер);
	Для Каждого КлючИЗначение Из ОписанияМетаданныхСервер Цикл
		ОписанияМетаданных.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		Если КлючИЗначение.Значение <> Неопределено Тогда
			КэшКонфигурации.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ОписанияМетаданных;
	
КонецФункции

Процедура СброситьОписаниеМетаданныхВКэше(Конфигурация) Экспорт
	ПараметрыПриложения.Вставить(Конфигурация, Неопределено);
КонецПроцедуры

Функция ОписаниеЛокальногоКонтекста(Форма, ЭтоКонвертацияXDTO, ИзмененныйКонтекст) Экспорт
	
	Если Форма.КД3_ИмяКонтекста = "Запрос" Тогда
		// Локальный контекст отсутствует
		Возврат Новый Структура("Переменные,Методы");
	КонецЕсли;
	
	КэшКонтекста = ПараметрыПриложения["КД3_ЛокальныйКонтекст"];
	Если КэшКонтекста = Неопределено Тогда
		КэшКонтекста = Новый Соответствие;
		ПараметрыПриложения["КД3_ЛокальныйКонтекст"] = КэшКонтекста;
	КонецЕсли;
	
	ОписаниеКонтекста = КэшКонтекста[Форма.УникальныйИдентификатор];
	Если ОписаниеКонтекста = Неопределено Тогда
		// Получение локального контекста для всех обработчиков
		ПараметрыКонтекста = Новый Структура;
		ПараметрыКонтекста.Вставить("ИмяКонтекста", Форма.КД3_ИмяКонтекста);
		ПараметрыКонтекста.Вставить("ЭтоКонвертацияXDTO", ЭтоКонвертацияXDTO);
		ПараметрыКонтекста.Вставить("ЭтоИнициализация", Истина);
		Если ПараметрыКонтекста.ИмяКонтекста = "Конвертация" Тогда
			ПараметрыКонтекста.Вставить("Конвертации", ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Форма.Объект.Ссылка));
		ИначеЕсли ПараметрыКонтекста.ИмяКонтекста = "ПКС" ИЛИ ПараметрыКонтекста.ИмяКонтекста = "ПКГС" Тогда
			ПараметрыКонтекста.Вставить("Конвертации", Форма.КД3_Конвертации);
		ИначеЕсли ПараметрыКонтекста.ИмяКонтекста <> "ПРО" Тогда
			ПараметрыКонтекста.Вставить("Конвертации", Форма.СписокКонвертаций.ВыгрузитьЗначения());
		КонецЕсли;
		ОписаниеКонтекста = КД3_Метаданные.ОписаниеЛокальногоКонтекста(ПараметрыКонтекста, ИзмененныйКонтекст);
		
		КэшКонтекста[Форма.УникальныйИдентификатор] = ОписаниеКонтекста;
	КонецЕсли;
	
	Возврат ОписаниеКонтекста;
	
КонецФункции

Функция ОбновитьЛокальныйКонтекст(Форма, ЭтоКонвертацияXDTO, ИзмененныйКонтекст) Экспорт
	
	Если Форма.КД3_ИмяКонтекста = "Запрос" Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	КэшКонтекста = ПараметрыПриложения["КД3_ЛокальныйКонтекст"];
	Если КэшКонтекста = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	ОписаниеКонтекста = КэшКонтекста[Форма.УникальныйИдентификатор];
	Если ОписаниеКонтекста = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	// Получение локального контекста для всех обработчиков
	ПараметрыКонтекста = Новый Структура;
	ПараметрыКонтекста.Вставить("Объект", Форма.Объект);
	ПараметрыКонтекста.Вставить("ИмяКонтекста", Форма.КД3_ИмяКонтекста);
	ПараметрыКонтекста.Вставить("ЭтоКонвертацияXDTO", ЭтоКонвертацияXDTO);
	ПараметрыКонтекста.Вставить("ЭтоИнициализация", Ложь);
	
	КД3_Метаданные.ОбновитьЛокальныйКонтекст(ПараметрыКонтекста, ОписаниеКонтекста, ИзмененныйКонтекст);
	
	Возврат ОписаниеКонтекста;
	
КонецФункции

Функция СброситьЛокальныйКонтекстВКэше(Форма) Экспорт
	
	КэшКонтекста = ПараметрыПриложения["КД3_ЛокальныйКонтекст"];
	Если КэшКонтекста <> Неопределено Тогда
		КэшКонтекста[Форма.УникальныйИдентификатор]= Неопределено;
	КонецЕсли;
	
КонецФункции

#КонецОбласти
