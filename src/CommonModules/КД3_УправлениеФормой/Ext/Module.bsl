
// Создает дополнительные реквизиты для контекстной подсказки, создает на закладках обработчиков 
// элементы выбора конфигураций (источника или приемника) и поля HTML-документов
//
// Параметры:
//  Форма
//  Отказ
//  ЭтоКонвертацияXDTO - Булево - Признак конвертации XDTO, а не XML
//  ИмяКонтекста - Строка - Название контекста формы (Конвертации, ПКО, ПКС и т.д.)
//  Обработчики - Массив - массив имен реквизитов с текстом обработчиков
//  СтраницыОбработчиков
//
Процедура ПриСозданииНаСервере(Форма, Отказ, ЭтоКонвертацияXDTO, ИмяКонтекста, Обработчики, СтраницыОбработчиков) Экспорт
	
	ИспользоватьКонтекстнуюПодсказку = КД3_Настройки.ИспользоватьКонтекстнуюПодсказку();
	
	ДобавитьРеквизитыФормы(Форма, ЭтоКонвертацияXDTO, ИмяКонтекста, Обработчики, ИспользоватьКонтекстнуюПодсказку);
	
	Форма.КД3_ИмяКонтекста = ИмяКонтекста;
	
	ДобавитьЭлементыОбработчиков(Форма, ЭтоКонвертацияXDTO, Обработчики, СтраницыОбработчиков);
	
	Если ИспользоватьКонтекстнуюПодсказку Тогда
		ДоступныеКонфигурации = КД3_УправлениеФормойКлиентСервер.ДоступныеКонфигурации(Форма, ЭтоКонвертацияXDTO);
		
		Если ИмяКонтекста = "Конвертация" Тогда
			// Это открытие формы справочника "Конвертации"
			Форма.КД3_Конфигурация = ДоступныеКонфигурации.Конфигурация;
			Если НЕ ЭтоКонвертацияXDTO Тогда
				Форма.КД3_КонфигурацияКорреспондент = ДоступныеКонфигурации.КонфигурацияКорреспондент;
			КонецЕсли;
		Иначе
			ДобавитьЭлементыКонфигураций(Форма, ЭтоКонвертацияXDTO, СтраницыОбработчиков);
			
			КД3_УправлениеФормойКлиентСервер.УстановитьВидимостьКонфигураций(ДоступныеКонфигурации, ЭтоКонвертацияXDTO, Форма);
			
			Если ДоступныеКонфигурации.Конфигурация.Количество() > 0 Тогда
				Форма.КД3_Конфигурация = ДоступныеКонфигурации.Конфигурация[0];
			КонецЕсли;
			Если НЕ ЭтоКонвертацияXDTO Тогда
				Если ДоступныеКонфигурации.КонфигурацияКорреспондент.Количество() > 0 Тогда
					Форма.КД3_КонфигурацияКорреспондент = ДоступныеКонфигурации.КонфигурацияКорреспондент[0];
				КонецЕсли;
				// Для ПКС и ПКГС список конвертаций сохраняется в реквизите "КД3_Конвертации" для заполнения локального контекста.
				// Для остальных форм список конвертаций доступен через Форма.СписокКонвертаций
				Если ИмяКонтекста = "ПКС" ИЛИ ИмяКонтекста = "ПКГС" Тогда
					Форма.КД3_Конвертации.ЗагрузитьЗначения(ДоступныеКонфигурации.Конвертации.ВыгрузитьЗначения());
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьРеквизитыФормы(Форма, ЭтоКонвертацияXDTO, ИмяКонтекста, Обработчики, ИспользоватьКонтекстнуюПодсказку)
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("КД3_ИмяКонтекста", Новый ОписаниеТипов("Строка")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("КД3_Инициализация", Новый ОписаниеТипов("СписокЗначений")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("КД3_Обработчики", Новый ОписаниеТипов("СписокЗначений")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("КД3_СтраницыОбработчиков", Новый ОписаниеТипов("СписокЗначений")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("КД3_ИзмененныйРеквизит", Новый ОписаниеТипов("Строка")));
	
	ТипСтрока = Новый ОписаниеТипов("Строка");
	Для Каждого ЭлементСписка Из Обработчики Цикл
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("КД3_" + ЭлементСписка.Значение, ТипСтрока));
	КонецЦикла;
	
	Если ИспользоватьКонтекстнуюПодсказку Тогда
		ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("КД3_Конфигурация", Новый ОписаниеТипов("СправочникСсылка.Релизы")));
		Если НЕ ЭтоКонвертацияXDTO Тогда
			ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("КД3_КонфигурацияКорреспондент", Новый ОписаниеТипов("СправочникСсылка.Релизы")));
			Если ИмяКонтекста = "ПКС" ИЛИ ИмяКонтекста = "ПКГС" Тогда
				ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("КД3_Конвертации", Новый ОписаниеТипов("СписокЗначений")));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты, Новый Массив);
	
КонецПроцедуры

Процедура ДобавитьЭлементыОбработчиков(Форма, ЭтоКонвертацияXDTO, Обработчики, СтраницыОбработчиков)
	
	Для Каждого ЭлементСписка Из Обработчики Цикл
		
		ИмяОбработчика = ЭлементСписка.Значение;
		ИмяРеквизита = "КД3_" + ИмяОбработчика;
		ДляКорреспондента = ЭлементСписка.Пометка;
		
		ЭлементЭталон = Форма.Элементы[ИмяОбработчика];
		ЭлементЭталон.Видимость = Ложь;
		
		Элемент = Форма.Элементы.Добавить("КД3_" + ИмяОбработчика, Тип("ПолеФормы"), ЭлементЭталон.Родитель);
		Элемент.Вид = ВидПоляФормы.ПолеHTMLДокумента;
		Элемент.ПутьКДанным = ИмяРеквизита;
		Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элемент = Форма.Элементы[ИмяРеквизита];
		Элемент.Видимость = Истина;
		Элемент.УстановитьДействие("ДокументСформирован", "КД3_Подключаемый_ДокументСформирован");
		Элемент.УстановитьДействие("ПриНажатии", "КД3_Подключаемый_HTMLПриНажатии");
		Элемент.ТолькоПросмотр = ЭлементЭталон.ТолькоПросмотр;
		
		Форма.КД3_Обработчики.Добавить(ИмяОбработчика, ЭлементСписка.Представление, ДляКорреспондента);
		
		Владелец = ЭлементЭталон.Родитель;
		Если ТипЗнч(Владелец) = Тип("ГруппаФормы") Тогда
			// Добавление страницы обработчика к отслеживаемым
			Форма.КД3_СтраницыОбработчиков.Добавить(Владелец.Имя, ИмяОбработчика);
		КонецЕсли;
		
		Если ЭтоКонвертацияXDTO И (Форма.КД3_ИмяКонтекста = "ПКО" ИЛИ Форма.КД3_ИмяКонтекста = "ПОД") Тогда
			ДобавитьЭлементыКонтекстногоМеню_ЗадатьКомментарийОбработчика(Форма, ИмяРеквизита, Элемент);
		КонецЕсли;
		ДобавитьЭлементыКонтекстногоМеню(Форма, ИмяРеквизита, Элемент);
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьЭлементыКонтекстногоМеню(Форма, ИмяРеквизита, ЭлементОбработчика)
		
		// Группа комментирование
		
		ЭлементГруппа = Форма.Элементы.Добавить(ИмяРеквизита + "_ГруппаКомментарии", Тип("ГруппаФормы"), ЭлементОбработчика.КонтекстноеМеню);
		ЭлементГруппа.Вид = ВидГруппыФормы.ГруппаКнопок;
		
		// Закомментировать
		
		ИмяКоманды = ИмяРеквизита + "_Закомментировать";
		
		Команда = Форма.Команды.Добавить(ИмяКоманды);
		Команда.Картинка = БиблиотекаКартинок.КД3_Закомментировать;
		Команда.СочетаниеКлавиш = Новый СочетаниеКлавиш(Клавиша.NumDivide, , Истина);
		Команда.Заголовок = "Закомментировать текст";
		Команда.Действие = "КД3_Подключаемый_КомандаРедактора";
		
		ЭлементКоманды = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭлементГруппа);
		ЭлементКоманды.ИмяКоманды = ИмяКоманды;
		
		// Убрать комментарии
		
		ИмяКоманды = ИмяРеквизита + "_УбратьКомментарии";
		
		Команда = Форма.Команды.Добавить(ИмяКоманды);
		Команда.Картинка = БиблиотекаКартинок.КД3_УбратьКомментарии;
		Команда.СочетаниеКлавиш = Новый СочетаниеКлавиш(Клавиша.NumDivide, , Истина, Истина);
		Команда.Заголовок = "Убрать комментарии";
		Команда.Действие = "КД3_Подключаемый_КомандаРедактора";
		
		ЭлементКоманды = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭлементГруппа);
		ЭлементКоманды.ИмяКоманды = ИмяКоманды;
		
		// Конструктор форматной строки
		
		ИмяКоманды = ИмяРеквизита + "_КонструкторФорматнойСтроки";
		
		Команда = Форма.Команды.Добавить(ИмяКоманды);
		Команда.Заголовок = "Конструктор форматной строки ...";
		Команда.Действие = "КД3_Подключаемый_КомандаРедактора";
		
		ЭлементКоманды = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭлементОбработчика.КонтекстноеМеню);
		ЭлементКоманды.ИмяКоманды = ИмяКоманды;
		
		// Форматировать
		
		ИмяКоманды = ИмяРеквизита + "_Форматировать";
		
		Команда = Форма.Команды.Добавить(ИмяКоманды);
		Команда.Картинка = БиблиотекаКартинок.КД3_Форматировать;
		Команда.СочетаниеКлавиш = Новый СочетаниеКлавиш(Клавиша.F, Истина, , Истина);
		Команда.Заголовок = "Форматировать ...";
		Команда.Действие = "КД3_Подключаемый_КомандаРедактора";
		
		ЭлементКоманды = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭлементОбработчика.КонтекстноеМеню);
		ЭлементКоманды.ИмяКоманды = ИмяКоманды;
		
		// Группа переносов строк
		
		ЭлементГруппа = Форма.Элементы.Добавить(ИмяРеквизита + "_ГруппаПереносыСтрок", Тип("ГруппаФормы"), ЭлементОбработчика.КонтекстноеМеню);
		ЭлементГруппа.Вид = ВидГруппыФормы.ГруппаКнопок;
		
		// Добавить перенос строки
		
		ИмяКоманды = ИмяРеквизита + "_ДобавитьПереносСтроки";
		
		Команда = Форма.Команды.Добавить(ИмяКоманды);
		Команда.Картинка = БиблиотекаКартинок.КД3_ДобавитьПереносСтроки;
		Команда.Заголовок = "Добавить перенос строки";
		Команда.Действие = "КД3_Подключаемый_КомандаРедактора";
		
		ЭлементКоманды = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭлементГруппа);
		ЭлементКоманды.ИмяКоманды = ИмяКоманды;
		
		// Удалить пренос строки
		
		ИмяКоманды = ИмяРеквизита + "_УдалитьПереносСтроки";
		
		Команда = Форма.Команды.Добавить(ИмяКоманды);
		Команда.Картинка = БиблиотекаКартинок.КД3_УдалитьПереносСтроки;
		Команда.Заголовок = "Удалить перенос строки";
		Команда.Действие = "КД3_Подключаемый_КомандаРедактора";
		
		ЭлементКоманды = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭлементГруппа);
		ЭлементКоманды.ИмяКоманды = ИмяКоманды;
		
КонецПроцедуры

Процедура ДобавитьЭлементыКонтекстногоМеню_ЗадатьКомментарийОбработчика(Форма, ИмяРеквизита, ЭлементОбработчика)
	
	// Группа Задать комментарий обработчика
	
	ЭлементГруппа = Форма.Элементы.Добавить(ИмяРеквизита + "_ГруппаЗадатьКомментарийОбработчика", Тип("ГруппаФормы"), ЭлементОбработчика.КонтекстноеМеню);
	ЭлементГруппа.Вид = ВидГруппыФормы.ГруппаКнопок;
	
	// Задать комментарий обработчика
	
	ИмяКоманды = ИмяРеквизита + "_ЗадатьКомментарийОбработчика";
	
	Команда = Форма.Команды.Добавить(ИмяКоманды);
	Команда.Заголовок = "Задать комментарий обработчика";
	Команда.Действие = "КД3_Подключаемый_КомандаРедактора";
	
	ЭлементКоманды = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭлементГруппа);
	ЭлементКоманды.ИмяКоманды = ИмяКоманды;
	
КонецПроцедуры

Процедура ДобавитьЭлементыКонфигураций(Форма, ЭтоКонвертацияXDTO, СтраницыОбработчиков)
	
	ЭлементГруппа = Форма.Элементы.Вставить("КД3_ГруппаКонфигурация", Тип("ГруппаФормы"), СтраницыОбработчиков.Родитель, СтраницыОбработчиков);
	ЭлементГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ЭлементГруппа.Поведение = ПоведениеОбычнойГруппы.Свертываемая;
	ЭлементГруппа.Заголовок = "Контекстная подсказка";
	
	Элемент = Форма.Элементы.Добавить("КД3_Конфигурация", Тип("ПолеФормы"), ЭлементГруппа);
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.ПутьКДанным = "КД3_Конфигурация";
	Элемент.УстановитьДействие("ПриИзменении", "КД3_Подключаемый_ПриИзмененииКонфигурации");
	Элемент.РежимВыбораИзСписка = Истина;
	
	Если ЭтоКонвертацияXDTO Тогда
		Элемент.Заголовок = "Конфигурация";
	Иначе
		Элемент.Заголовок = "Конфигурация источник";
		
		ЭлементКорреспондент = Форма.Элементы.Добавить("КД3_КонфигурацияКорреспондент", Тип("ПолеФормы"), ЭлементГруппа);
		ЭлементКорреспондент.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементКорреспондент.ПутьКДанным = "КД3_КонфигурацияКорреспондент";
		ЭлементКорреспондент.Заголовок = "Конфигурация приемник";
		ЭлементКорреспондент.УстановитьДействие("ПриИзменении", "КД3_Подключаемый_ПриИзмененииКонфигурацииКорреспондента");
		ЭлементКорреспондент.РежимВыбораИзСписка = Истина;
	КонецЕсли;
	
КонецПроцедуры
