// Создает дополнительные реквизиты для контекстной подсказки, создает на закладках обработчиков 
// элементы выбора конфигураций (источника или приемника) и поля HTML-документов
//
// Параметры:
//  Форма
//  Отказ
//  ЭтоКонвертацияXDTO
//  Обработчики - Массив - массив имен реквизитов с текстом обработчиков
//  СтраницыОбработчиков
//
Процедура ПриСозданииНаСервере(Форма, Отказ, ЭтоКонвертацияXDTO, Обработчики, СтраницыОбработчиков) Экспорт
	
	ДобавитьРеквизитыФормы(Форма, ЭтоКонвертацияXDTO, Обработчики, СтраницыОбработчиков);
	ДобавитьЭлементыОбработчиков(Форма, Обработчики, СтраницыОбработчиков);
	
КонецПроцедуры

// Вызывается при изменения конвертаций в которые входит объект
// Изменяет доступные для выбора конфигурации, связанные с подсказкой
//
// Параметры:
//  Форма
//  ЭтоКонвертацияXDTO
//
Процедура ПриИзмененииКонвертаций(Форма, ЭтоКонвертацияXDTO) Экспорт
	
	Если НЕ КД3_Настройки.ИспользоватьКонтекстнуюПодсказку() Тогда
		Возврат;
	КонецЕсли;
	
	ДоступныеКонфигурации = ДоступныеКонфигурации(Форма, ЭтоКонвертацияXDTO);
	УстановитьВидимостьКонфигураций(ДоступныеКонфигурации, ЭтоКонвертацияXDTO, Форма);
	
	Если ДоступныеКонфигурации.Конфигурация.Найти(Форма.КД3_Конфигурация) = Неопределено Тогда
		Форма.КД3_Конфигурация = Справочники.Релизы.ПустаяСсылка();
	КонецЕсли;
	Если НЕ ЭтоКонвертацияXDTO Тогда
		Если ДоступныеКонфигурации.КонфигурацияКорреспондент.Найти(Форма.КД3_КонфигурацияКорреспондент) = Неопределено Тогда
			Форма.КД3_КонфигурацияКорреспондент = Справочники.Релизы.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьРеквизитыФормы(Форма, ЭтоКонвертацияXDTO, Обработчики, СтраницыОбработчиков)
	
	ИспользоватьКонтекстнуюПодсказку = КД3_Настройки.ИспользоватьКонтекстнуюПодсказку();
	
	ДобавляемыРеквизиты = Новый Массив;
	ДобавляемыРеквизиты.Добавить(Новый РеквизитФормы("КД3_Обработчики", Новый ОписаниеТипов("СписокЗначений")));
	ДобавляемыРеквизиты.Добавить(Новый РеквизитФормы("КД3_СтраницыОбработчиков", Новый ОписаниеТипов("СписокЗначений")));
	ДобавляемыРеквизиты.Добавить(Новый РеквизитФормы("КД3_ИзмененныйРеквизит", Новый ОписаниеТипов("Строка")));
	
	Если ИспользоватьКонтекстнуюПодсказку Тогда
		ДобавляемыРеквизиты.Добавить(Новый РеквизитФормы("КД3_Конфигурация", Новый ОписаниеТипов("СправочникСсылка.Релизы")));
		Если НЕ ЭтоКонвертацияXDTO Тогда
			ДобавляемыРеквизиты.Добавить(Новый РеквизитФормы("КД3_КонфигурацияКорреспондент", Новый ОписаниеТипов("СправочникСсылка.Релизы")));
		КонецЕсли;
	КонецЕсли;
	ТипСтрока = Новый ОписаниеТипов("Строка");
	Для Каждого ЭлементСписка Из Обработчики Цикл
		ДобавляемыРеквизиты.Добавить(Новый РеквизитФормы("КД3_" + ЭлементСписка.Значение, ТипСтрока));
	КонецЦикла;
	Форма.ИзменитьРеквизиты(ДобавляемыРеквизиты, Новый Массив);
	
	Если ИспользоватьКонтекстнуюПодсказку Тогда
		ЗаполнитьКонфигурацииКонвертаций(Форма, ЭтоКонвертацияXDTO, СтраницыОбработчиков);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьКонфигурацииКонвертаций(Форма, ЭтоКонвертацияXDTO, СтраницыОбработчиков)
	
	ДоступныеКонфигурации = ДоступныеКонфигурации(Форма, ЭтоКонвертацияXDTO);
	
	Если ДоступныеКонфигурации.КонфигурацияЕстьНаФорме Тогда
		// Это открытие формы справочника "Конвертации"
		Форма.КД3_Конфигурация = ДоступныеКонфигурации.Конфигурация;
		Если НЕ ЭтоКонвертацияXDTO Тогда
			Форма.КД3_КонфигурацияКорреспондент = ДоступныеКонфигурации.КонфигурацияКорреспондент;
		КонецЕсли;
	Иначе
		ДобавитьЭлементыКонфигураций(Форма, ЭтоКонвертацияXDTO, СтраницыОбработчиков);
		УстановитьВидимостьКонфигураций(ДоступныеКонфигурации, ЭтоКонвертацияXDTO, Форма);
		
		Если ДоступныеКонфигурации.Конфигурация.Количество() > 0 Тогда
			Форма.КД3_Конфигурация = ДоступныеКонфигурации.Конфигурация[0];
		КонецЕсли;
		Если ДоступныеКонфигурации.КонфигурацияКорреспондент.Количество() > 0 Тогда
			Форма.КД3_КонфигурацияКорреспондент = ДоступныеКонфигурации.КонфигурацияКорреспондент[0];
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьЭлементыОбработчиков(Форма, Обработчики, СтраницыОбработчиков)
	
	Для Каждого ЭлементСписка Из Обработчики Цикл
		
		ИмяОбработчика = ЭлементСписка.Значение;
		ИмяРеквизита = "КД3_" + ИмяОбработчика;
		ДляКорреспондента = ЭлементСписка.Пометка;
		
		ЭлементЭталон = Форма.Элементы[ИмяОбработчика];
		ЭлементЭталон.Видимость = Ложь;
		
		Элемент = Форма.Элементы.Добавить("КД3_" + ИмяОбработчика, Тип("ПолеФормы"), ЭлементЭталон.Родитель);
		Элемент.Вид = ВидПоляФормы.ПолеHTMLДокумента;
		Элемент.ПутьКДанным = ИмяРеквизита;
		Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		Элемент = Форма.Элементы[ИмяРеквизита];
		Элемент.Видимость = Истина;
		Элемент.УстановитьДействие("ДокументСформирован", "КД3_Подключаемый_ДокументСформирован");
		Элемент.УстановитьДействие("ПриНажатии", "КД3_Подключаемый_HTMLПриНажатии");
		Элемент.ТолькоПросмотр = ЭлементЭталон.ТолькоПросмотр;
		
		Форма.КД3_Обработчики.Добавить(ИмяОбработчика, ЭлементСписка.Представление, ДляКорреспондента);
		
		Владелец = ЭлементЭталон.Родитель;
		Если ТипЗнч(Владелец) = Тип("ГруппаФормы") Тогда
			// Добавление страницы обработчика к отслеживаемым
			Форма.КД3_СтраницыОбработчиков.Добавить(Владелец.Имя, ИмяОбработчика);
		КонецЕсли;
		
		ДобавитьЭлементыКонтекстногоМеню(Форма, ИмяРеквизита, Элемент);
	КонецЦикла;
	
	Если ТипЗнч(СтраницыОбработчиков) = Тип("ГруппаФормы") Тогда
		СтраницыОбработчиков.УстановитьДействие("ПриСменеСтраницы", "КД3_Подключаемый_ОбработчикиПриСменеСтраницы");
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьЭлементыКонтекстногоМеню(Форма, ИмяРеквизита, ЭлементОбработчика)
	
		// Группа комментирование
		
		ЭлементГруппа = Форма.Элементы.Добавить(ИмяРеквизита + "_ГруппаКомментарии", Тип("ГруппаФормы"), ЭлементОбработчика.КонтекстноеМеню);
		ЭлементГруппа.Вид = ВидГруппыФормы.ГруппаКнопок;
		
		// Закомментировать
		
		ИмяКоманды = ИмяРеквизита + "_Закомментировать";
		
		Команда = Форма.Команды.Добавить(ИмяКоманды);
		Команда.Картинка = БиблиотекаКартинок.КД3_Закомментировать;
		Команда.СочетаниеКлавиш = Новый СочетаниеКлавиш(Клавиша.NumDivide, , Истина);
		Команда.Заголовок = "Закомментировать текст";
		Команда.Действие = "КД3_Подключаемый_КомандаРедактора";
		
		ЭлементКоманды = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭлементГруппа);
		ЭлементКоманды.ИмяКоманды = ИмяКоманды;
		
		// Убрать комментарии
		
		ИмяКоманды = ИмяРеквизита + "_УбратьКомментарии";
		
		Команда = Форма.Команды.Добавить(ИмяКоманды);
		Команда.Картинка = БиблиотекаКартинок.КД3_УбратьКомментарии;
		Команда.СочетаниеКлавиш = Новый СочетаниеКлавиш(Клавиша.NumDivide, , Истина, Истина);
		Команда.Заголовок = "Убрать комментарии";
		Команда.Действие = "КД3_Подключаемый_КомандаРедактора";
		
		ЭлементКоманды = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭлементГруппа);
		ЭлементКоманды.ИмяКоманды = ИмяКоманды;
		
		// Конструктор форматной строки
		
		ИмяКоманды = ИмяРеквизита + "_КонструкторФорматнойСтроки";
		
		Команда = Форма.Команды.Добавить(ИмяКоманды);
		Команда.Заголовок = "Конструктор форматной строки ...";
		Команда.Действие = "КД3_Подключаемый_КомандаРедактора";
		
		ЭлементКоманды = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭлементОбработчика.КонтекстноеМеню);
		ЭлементКоманды.ИмяКоманды = ИмяКоманды;
		
		// Форматировать
		
		ИмяКоманды = ИмяРеквизита + "_Форматировать";
		
		Команда = Форма.Команды.Добавить(ИмяКоманды);
		Команда.Картинка = БиблиотекаКартинок.КД3_Форматировать;
		Команда.СочетаниеКлавиш = Новый СочетаниеКлавиш(Клавиша.F, Истина, , Истина);
		Команда.Заголовок = "Форматировать ...";
		Команда.Действие = "КД3_Подключаемый_КомандаРедактора";
		
		ЭлементКоманды = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭлементОбработчика.КонтекстноеМеню);
		ЭлементКоманды.ИмяКоманды = ИмяКоманды;
		
		// Группа переносов строк
		
		ЭлементГруппа = Форма.Элементы.Добавить(ИмяРеквизита + "_ГруппаПереносыСтрок", Тип("ГруппаФормы"), ЭлементОбработчика.КонтекстноеМеню);
		ЭлементГруппа.Вид = ВидГруппыФормы.ГруппаКнопок;
		
		// Добавить перенос строки
		
		ИмяКоманды = ИмяРеквизита + "_ДобавитьПереносСтроки";
		
		Команда = Форма.Команды.Добавить(ИмяКоманды);
		Команда.Картинка = БиблиотекаКартинок.КД3_ДобавитьПереносСтроки;
		Команда.Заголовок = "Добавить перенос строки";
		Команда.Действие = "КД3_Подключаемый_КомандаРедактора";
		
		ЭлементКоманды = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭлементГруппа);
		ЭлементКоманды.ИмяКоманды = ИмяКоманды;
		
		// Удалить пренос строки
		
		ИмяКоманды = ИмяРеквизита + "_УдалитьПереносСтроки";
		
		Команда = Форма.Команды.Добавить(ИмяКоманды);
		Команда.Картинка = БиблиотекаКартинок.КД3_УдалитьПереносСтроки;
		Команда.Заголовок = "Удалить перенос строки";
		Команда.Действие = "КД3_Подключаемый_КомандаРедактора";
		
		ЭлементКоманды = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ЭлементГруппа);
		ЭлементКоманды.ИмяКоманды = ИмяКоманды;
		
КонецПроцедуры

Функция ДоступныеКонфигурации(Форма, ЭтоКонвертацияXDTO)
	
	ПроверкаРеквизита = Новый Структура("СписокКонвертаций,ОбъектКонфигурации,ОбъектКонфигурацииКорреспондент,Конфигурация");
	ЗаполнитьЗначенияСвойств(ПроверкаРеквизита, Форма);
	Если ПроверкаРеквизита.СписокКонвертаций = Неопределено И ПроверкаРеквизита.ОбъектКонфигурации = Неопределено Тогда
		ДоступныеКонфигурации = Новый Структура;
		ДоступныеКонфигурации.Вставить("КонфигурацияЕстьНаФорме", Истина);
		Если ПроверкаРеквизита.Конфигурация = Неопределено Тогда
			// Это открытие формы справочника "Конвертации"
			ДоступныеКонфигурации.Вставить("Конфигурация", Форма.Объект.Конфигурация);
			Если НЕ ЭтоКонвертацияXDTO Тогда
				ДоступныеКонфигурации.Вставить("КонфигурацияКорреспондент", Форма.Объект.КонфигурацияКорреспондент);
			КонецЕсли;
		Иначе
			// Это открытие формы справочника "ПравилаРегистрацииОбъектов"
			ДоступныеКонфигурации.Вставить("Конфигурация", Форма.Конфигурация);
			ДоступныеКонфигурации.Вставить("КонфигурацияКорреспондент");
		КонецЕсли;
	Иначе
		Если ПроверкаРеквизита.СписокКонвертаций = Неопределено Тогда
			// Это открытие формы ПравилаКонвертацииСвойств
			ПроверкаРеквизита.СписокКонвертаций = Новый СписокЗначений;
			КонвертацияДанныхXDTOВызовСервера.ПолучитьВхожденияВКонвертацииДляЭлементаКонвертации(Форма.Объект.Владелец, ПроверкаРеквизита.СписокКонвертаций);
		КонецЕсли;
		ДоступныеКонфигурации = КД3_Метаданные.ПолучитьКонфигурации(ПроверкаРеквизита.СписокКонвертаций, ЭтоКонвертацияXDTO);
		ДоступныеКонфигурации.Вставить("КонфигурацияЕстьНаФорме", Ложь);
	КонецЕсли;
	
	Возврат ДоступныеКонфигурации;
	
КонецФункции

Процедура ДобавитьЭлементыКонфигураций(Форма, ЭтоКонвертацияXDTO, СтраницыОбработчиков)
	
	ЭлементГруппа = Форма.Элементы.Вставить("КД3_ГруппаКонфигурация", Тип("ГруппаФормы"), СтраницыОбработчиков.Родитель, СтраницыОбработчиков);
	ЭлементГруппа.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ЭлементГруппа.Поведение = ПоведениеОбычнойГруппы.Свертываемая;
	ЭлементГруппа.Заголовок = "Контекстная подсказка";
	
	Элемент = Форма.Элементы.Добавить("КД3_Конфигурация", Тип("ПолеФормы"), ЭлементГруппа);
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.ПутьКДанным = "КД3_Конфигурация";
	Элемент.УстановитьДействие("ПриИзменении", "КД3_Подключаемый_ПриИзмененииКонфигурации");
	Элемент.РежимВыбораИзСписка = Истина;
	
	Если ЭтоКонвертацияXDTO Тогда
		Элемент.Заголовок = "Конфигурация";
	Иначе
		Элемент.Заголовок = "Конфигурация источник";
		
		ЭлементКорреспондент = Форма.Элементы.Добавить("КД3_КонфигурацияКорреспондент", Тип("ПолеФормы"), ЭлементГруппа);
		ЭлементКорреспондент.Вид = ВидПоляФормы.ПолеВвода;
		ЭлементКорреспондент.ПутьКДанным = "КД3_КонфигурацияКорреспондент";
		ЭлементКорреспондент.Заголовок = "Конфигурация приемник";
		ЭлементКорреспондент.УстановитьДействие("ПриИзменении", "КД3_Подключаемый_ПриИзмененииКонфигурацииКорреспондента");
		ЭлементКорреспондент.РежимВыбораИзСписка = Истина;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьВидимостьКонфигураций(ДоступныеКонфигурации, ЭтоКонвертацияXDTO, Форма)
	
	Элемент = Форма.Элементы.КД3_Конфигурация;
	Элемент.СписокВыбора.ЗагрузитьЗначения(ДоступныеКонфигурации.Конфигурация);
	Элемент.ТолькоПросмотр = ДоступныеКонфигурации.Конфигурация.Количество() = 1;
	
	ВидимостьГруппы = НЕ Элемент.ТолькоПросмотр;
	
	Если НЕ ЭтоКонвертацияXDTO Тогда
		ЭлементКорреспондент = Форма.Элементы.КД3_КонфигурацияКорреспондент;
		ЭлементКорреспондент.СписокВыбора.ЗагрузитьЗначения(ДоступныеКонфигурации.КонфигурацияКорреспондент);
		ЭлементКорреспондент.ТолькоПросмотр = ДоступныеКонфигурации.КонфигурацияКорреспондент.Количество() = 1;
		ВидимостьГруппы = ВидимостьГруппы ИЛИ НЕ ЭлементКорреспондент.ТолькоПросмотр;
	КонецЕсли;
	
	Форма.Элементы.КД3_ГруппаКонфигурация.Видимость = ВидимостьГруппы;
	
КонецПроцедуры

