
&Вместо("ЗагрузитьСтруктуруМетаданныхПоРегламентномуЗаданию")
Процедура КД3_ЗагрузитьСтруктуруМетаданныхПоРегламентномуЗаданию(УИДРелизаСтрокой)
	//+КД3
	Если НЕ КД3_Настройки.ИспользоватьКонтекстнуюПодсказку() Тогда
		ПродолжитьВызов(УИДРелизаСтрокой); // Типовой обработчик
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначения.ПриНачалеВыполненияРегламентногоЗадания(Метаданные.РегламентныеЗадания.ЗагрузкаМетаданных);
	
	Если Не ЗначениеЗаполнено(УИДРелизаСтрокой) Тогда
		ВызватьИсключение НСтр("ru = 'Не задан релиз.'");
	КонецЕсли;
	
	// Получение ссылки на релиз.
	Идентификатор = Новый УникальныйИдентификатор(УИДРелизаСтрокой);
	Релиз = Справочники.Релизы.ПолучитьСсылку(Идентификатор);
	Если НЕ ОбщегоНазначения.СсылкаСуществует(Релиз) Тогда
		ВызватьИсключение НСтр("ru = 'Не найден релиз.'");
	КонецЕсли;
	ПараметрыЗагрузки = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Релиз, "ИсточникДанных, КаталогЗагрузки, РегламентноеЗаданиеGUID, ВариантЗагрузкиДвижений, ЕстьРасширения");
	Если ПараметрыЗагрузки.ИсточникДанных = 1 Тогда
		Результат = Неопределено;
		Попытка
			Результат = ПолучитьФайлыИзGIT(Релиз);
		Исключение
			Результат = Неопределено;
		КонецПопытки;
		Если ЗначениеЗаполнено(Результат) И ТипЗнч(Результат) = Тип("Число") Тогда
			ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не удалось выполнить клонирование ветки из GIT репозитория. Код ошибки:'") + Результат);
		КонецЕсли; 
	ИначеЕсли ПараметрыЗагрузки.ЕстьРасширения Тогда
		КаталогиРасширений = КД3_КаталогиРасширенийРелиза(Релиз);
		Если КаталогиРасширений.Количество() > 0 Тогда
			ПараметрыЗагрузки.Вставить("КаталогиРасширений", КаталогиРасширений);
		Иначе
			ПараметрыЗагрузки.ЕстьРасширения = Ложь;
		КонецЕсли;
	КонецЕсли;
	ПараметрыЗагрузки.Вставить("СпособЗагрузки", 1);
	ПараметрыЗагрузки.Вставить("Конфигурация", Релиз);
	ПараметрыЗагрузки.Вставить("ТолькоОбновитьПланыОбмена", Ложь);
	ПараметрыЗагрузки.Вставить("ТолькоДобавлятьНовые", Ложь);
	
	НастройкиКонфигурации = КД3_Метаданные.НастройкиКонфигурации(Релиз);
	
	ПараметрыЗагрузки.Вставить("КД3_ЭтоСервер", Истина);
	ПараметрыЗагрузки.Вставить("КД3_ИспользоватьКонтекстнуюПодсказку",
		КД3_Настройки.ИспользоватьКонтекстнуюПодсказку() И НастройкиКонфигурации.МестоХраненияИндексов <> 0);
	ПараметрыЗагрузки.Вставить("КД3_МестоХраненияИндексов", НастройкиКонфигурации.МестоХраненияИндексов);
	ПараметрыЗагрузки.Вставить("КД3_КаталогИндексов", НастройкиКонфигурации.КаталогИндексов);
	ПараметрыЗагрузки.Вставить("КД3_ЗагружатьМетодыМодулей", НастройкиКонфигурации.ЗагружатьМетодыМодулей);
	ПараметрыЗагрузки.Вставить("КД3_СохранитьКаталогДляКонфигурации", Ложь);
	
	КД3_ЗагрузкаМетаданныхКлиентСервер.ПодготовитьФайлыДляЗагрузки(ПараметрыЗагрузки);
	
	ПараметрыЗагрузки.Вставить("КД3_КоличествоПотоков", 1); // Изменить на нужное количество !!!
	
	Если ПараметрыЗагрузки.КД3_КоличествоПотоков = 1 Тогда 
		Обработки.КД3_ЗагрузкаСтруктурыКонфигурацииИзФайловXML.ВыполнитьЗагрузкуМетаданных(ПараметрыЗагрузки, Неопределено);
	Иначе
		Обработки.КД3_ЗагрузкаСтруктурыКонфигурацииИзФайловXMLМногопоточно.ВыполнитьЗагрузкуМетаданных(ПараметрыЗагрузки, Неопределено);
	КонецЕсли;
	//-КД3
КонецПроцедуры

Функция КД3_КаталогиРасширенийРелиза(Релиз)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ИмяКаталогаЗагрузки ИЗ Справочник.Релизы.Расширения ГДЕ Ссылка = &Релиз";
	Запрос.УстановитьПараметр("Релиз", Релиз);
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИмяКаталогаЗагрузки");
КонецФункции