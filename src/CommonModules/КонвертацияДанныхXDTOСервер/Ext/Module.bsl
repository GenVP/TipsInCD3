// Заполняет файлы конфигурации и расширений в ПараметрыЗагрузки.ПомещенныеФайлы
// Используется в Обработки.КД3_ЗагрузкаСтруктурыКонфигурацииИзФайловXMLМногопоточно.ВыполнитьЗагрузкуМетаданных() для режима КД3_ЭтоСервер
// Добавлен для вызова типовой не экспортной КонвертацияДанныхXDTOСервер.ПодготовитьФайлыДляЗагрузки()
//
Процедура КД3_ПодготовитьФайлыДляЗагрузкиНаСервере(ПараметрыЗагрузки) Экспорт
	
	ПомещенныеФайлыДляПередачи = Новый Массив;
	
	ЗагрузкаИзEDT = ПараметрыЗагрузки.ИсточникДанных = 1;
	
	// Типовая подготовка файлов конфигурации и расширений
	
	ПодготовитьФайлыДляЗагрузки(ПомещенныеФайлыДляПередачи, ПараметрыЗагрузки.КаталогЗагрузки, ЗагрузкаИзEDT, Ложь, ПараметрыЗагрузки.КД3_КаталогВременныхФайлов);
	//+КД3 FIX Добавление загрузки общих реквизитов
	КД3_FixCommonAttributes(ПомещенныеФайлыДляПередачи, ПараметрыЗагрузки.КаталогЗагрузки, ЗагрузкаИзEDT, Ложь, ПараметрыЗагрузки.КД3_КаталогВременныхФайлов);
	//-КД3 FIX
	
	Если ПараметрыЗагрузки.ЕстьРасширения Тогда
		Для Каждого ПутьКФайламРасширения Из ПараметрыЗагрузки.КаталогиРасширений Цикл
			ПодготовитьФайлыДляЗагрузки(ПомещенныеФайлыДляПередачи, ПутьКФайламРасширения, ЗагрузкаИзEDT, Истина, ПараметрыЗагрузки.КД3_КаталогВременныхФайлов);
			//+КД3 FIX Добавление загрузки общих реквизитов
			КД3_FixCommonAttributes(ПомещенныеФайлыДляПередачи, ПараметрыЗагрузки.КаталогЗагрузки, ЗагрузкаИзEDT, Ложь, ПараметрыЗагрузки.КД3_КаталогВременныхФайлов);
			//-КД3 FIX
		КонецЦикла;
	КонецЕсли;
	
	/// Обработка методов модулей для контекстной подсказки
	
	Если НЕ ПараметрыЗагрузки.КД3_ТиповаяЗагрузка И ПараметрыЗагрузки.КД3_ЗагружатьМетодыМодулей Тогда
		
		КД3_ПодготовитьФайлыДляЗагрузкиКонтекстнаяПодсказка(ПомещенныеФайлыДляПередачи, ПараметрыЗагрузки.КаталогЗагрузки, ЗагрузкаИзEDT, Ложь, ПараметрыЗагрузки.КД3_КаталогВременныхФайлов);
		
		Если ПараметрыЗагрузки.ЕстьРасширения Тогда
			Для Каждого ПутьКФайламРасширения Из ПараметрыЗагрузки.КаталогиРасширений Цикл
				КД3_ПодготовитьФайлыДляЗагрузкиКонтекстнаяПодсказка(ПомещенныеФайлыДляПередачи, ПутьКФайламРасширения, ЗагрузкаИзEDT, Истина, ПараметрыЗагрузки.КД3_КаталогВременныхФайлов);
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыЗагрузки.Вставить("ПомещенныеФайлы", ПомещенныеФайлыДляПередачи);
	
КонецПроцедуры

Процедура КД3_FixCommonAttributes(ФайлыДляЗагрузки, КаталогЗагрузки, ЗагрузкаИзEDT, ЭтоРасширение, ИмяВременногоКаталогаФайлов)
	
	// Загрузка CommonAttributes есть через форму, но нет через регламентное задачние в КонвертацияДанныхXDTOСервер.ПодготовитьФайлыДляЗагрузки()
	
	КаталогиДляОбработки = Новый Массив;
	КаталогиДляОбработки.Добавить("CommonAttributes");
	
	РазделительПути = ПолучитьРазделительПутиСервера();
	
	Для Каждого ИмяКаталога Из КаталогиДляОбработки Цикл
		Если НЕ ЗагрузкаИзEDT Тогда
			НайденныеФайлы = НайтиФайлы(КаталогЗагрузки + РазделительПути + ИмяКаталога, "*.xml", Ложь);
		Иначе
			НайденныеФайлы = НайтиФайлы(КаталогЗагрузки + РазделительПути + "src" + РазделительПути + ИмяКаталога, "*.mdo", Истина);
		КонецЕсли;
		ОбработатьНайденныеФайлы(НайденныеФайлы, ФайлыДляЗагрузки, ЭтоРасширение, ИмяВременногоКаталогаФайлов);
	КонецЦикла;
	
КонецПроцедуры

// Аналогична типовой КонвертацияДанныхXDTOСервер.ПодготовитьФайлыДляЗагрузки, но для КаталогиДляОбработки = "CommonModules"
// Для вызова типовой не экспортной КонвертацияДанныхXDTOСервер.ОбработатьНайденныеФайлы
//
Процедура КД3_ПодготовитьФайлыДляЗагрузкиКонтекстнаяПодсказка(ФайлыДляЗагрузки, КаталогЗагрузки, ЗагрузкаИзEDT, ЭтоРасширение, ИмяВременногоКаталогаФайлов)
	
	КаталогиДляОбработки = Новый Массив;
	КаталогиДляОбработки.Добавить("CommonModules");
	
	РазделительПути = ПолучитьРазделительПутиСервера();
	
	Для Каждого ИмяКаталога Из КаталогиДляОбработки Цикл
		Если НЕ ЗагрузкаИзEDT Тогда
			НайденныеФайлы = НайтиФайлы(КаталогЗагрузки + РазделительПути + ИмяКаталога, "*.xml", Ложь);
		Иначе
			НайденныеФайлы = НайтиФайлы(КаталогЗагрузки + РазделительПути + "src" + РазделительПути + ИмяКаталога, "*.mdo", Истина);
		КонецЕсли;
		ОбработатьНайденныеФайлы(НайденныеФайлы, ФайлыДляЗагрузки, ЭтоРасширение, ИмяВременногоКаталогаФайлов);
	КонецЦикла;
	
КонецПроцедуры
