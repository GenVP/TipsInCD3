
Процедура ИнициализацияРедактора(Форма, ИмяРеквизита, Объект, ТолькоПросмотр = Ложь) Экспорт
	
	ИмяОбработчика = Сред(ИмяРеквизита, 5);
	ТекстОбработчика = ?(ТолькоПросмотр, Форма[ИмяОбработчика], Объект[ИмяОбработчика]);
	
	ТолькоПросмотрСУчетомЗахвата = ТолькоПросмотр ИЛИ Форма.ТолькоПросмотр;
	Если КД3_РедакторКодаКлиент.ИнициализацияРедактора(Форма, ИмяРеквизита, ТекстОбработчика, ТолькоПросмотрСУчетомЗахвата) Тогда
		Форма.КД3_Инициализация.Добавить(ИмяРеквизита);
		
		Если КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() И НЕ ТолькоПросмотрСУчетомЗахвата Тогда
			Форма.ПодключитьОбработчикОжидания("КД3_Подключаемый_ИнициализацияМетаданных", 0.5, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Форма, Отказ) Экспорт
	
	Если НЕ КД3_НастройкиКлиент.ИспользоватьРедакторКода() Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьИзмененныйОбработчик(Форма);
	
КонецПроцедуры

Процедура ПриЗакрытии(Форма) Экспорт
	
	Если НЕ КД3_НастройкиКлиент.ИспользоватьРедакторКода() Тогда
		Возврат;
	КонецЕсли;
	
	КД3_МетаданныеКлиент.СброситьЛокальныйКонтекстВКэше(Форма);
	
КонецПроцедуры

Процедура ОбработкаОповещения(Форма, ИмяСобытия, Параметр, Источник) Экспорт
	
	Если НЕ КД3_НастройкиКлиент.ИспользоватьРедакторКода() Тогда
		Возврат;
	КонецЕсли;
	
	Если Форма.СовместнаяРабота И ИмяСобытия = "ИзмененСтатусЗахвата" И Источник = Форма.Объект.Ссылка Тогда
		
		Для Каждого ЭлементСписка Из Форма.КД3_Обработчики Цикл
			ИмяРеквизита = "КД3_" + ЭлементСписка.Значение;
			Если НЕ Форма.Элементы[ИмяРеквизита].ТолькоПросмотр Тогда
				КД3_РедакторКодаКлиент.ИзменитьТолькоПросмотр(Форма, ИмяРеквизита, Форма.ТолькоПросмотр);
			КонецЕсли;
		КонецЦикла;
		
		Если КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() И НЕ Форма.ТолькоПросмотр Тогда
			Форма.ПодключитьОбработчикОжидания("КД3_Подключаемый_ИнициализацияМетаданных", 0.5, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриСменеСтраницы(Форма, СтраницаИлиОбработчик) Экспорт
	
	Если НЕ КД3_НастройкиКлиент.ИспользоватьРедакторКода() Тогда
		Возврат;
	КонецЕсли;
	
	СохранитьИзмененныйОбработчик(Форма);
	
	Если ТипЗнч(СтраницаИлиОбработчик) = Тип("Строка") Тогда
		ИмяОбработчика = СтраницаИлиОбработчик;
	Иначе
		ЭлементСписка = Форма.КД3_СтраницыОбработчиков.НайтиПоЗначению(СтраницаИлиОбработчик.Имя);
		Если ЭлементСписка = Неопределено Тогда
			Возврат; // Это алгоритм или запрос
		КонецЕсли;
		ИмяОбработчика = ЭлементСписка.Представление;
	КонецЕсли;
	
	КД3_РедакторКодаКлиент.ИзвлечьИсходники();
	
	// Инициализация загрузки обработчика будет выполнена в процедуре формы
	// КД3_Подключаемый_ДокументСформирован после того, как он станет видимым
	ИмяРеквизита = "КД3_" + ИмяОбработчика;
	Если Форма[ИмяРеквизита] = "" Тогда
		Форма[ИмяРеквизита] = КД3_РедакторКодаКлиент.ПутьРедактораКода();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриИзмененииОбъектаКонфигурации(Форма, ОбъектКонфигурации, ЭтоКонвертацияXDTO, ДляКорреспондента) Экспорт
	
	Если НЕ КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьСписокКонфигураций = Ложь;
	ОчиститьМетаданные = Ложь;
	
	Если НЕ ЗначениеЗаполнено(ОбъектКонфигурации) ИЛИ ТипЗнч(ОбъектКонфигурации) = Тип("Строка") Тогда
		// Если объект не заполнен, то получение конфигураций по конвертациям и проверка текущих конфигураций (как сейчас)
		ОбновитьСписокКонфигураций = Истина;
	Иначе
		// Если объект заполнен, то получение его конфигурации
		Конфигурация = КД3_УправлениеФормойВызовСервера.КонфигурацияОбъекта(ОбъектКонфигурации);
		ТекущаяКонфигурация = ?(ДляКорреспондента, Форма.КД3_КонфигурацияКорреспондент, Форма.КД3_Конфигурация);
		
		СписокКонфигураций = ?(ДляКорреспондента, Форма.Элементы.КД3_КонфигурацияКорреспондент.СписокВыбора, Форма.Элементы.КД3_Конфигурация.СписокВыбора);
		Если СписокКонфигураций.Количество() > 1 Тогда
			// Если конфигураций несколько (значит раньше объект был не заполнен):
			// Если конфигурации нет в списке, то вывод ошибке и ничего не меняем
			Если СписокКонфигураций.НайтиПоЗначению(Конфигурация) = Неопределено Тогда
				ОбщегоНазначенияКлиент.СообщитьПользователю(СтрШаблон("Конфигурация объекта %1 не входит в состав конвертаций", Конфигурация));
				Возврат;
			КонецЕсли;
			Если Конфигурация = ТекущаяКонфигурация Тогда
				// Только ограничения списка доступных конфигурация до одного значения
				СписокКонфигураций.ЗагрузитьЗначения(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Конфигурация));
				УстановитьВидимостьГруппыКонфигураций(Форма, ЭтоКонвертацияXDTO);
			Иначе
				// Обновление текущей конфигурации и метаданных ее обработчиков
				Если ДляКорреспондента Тогда
					Форма.КД3_КонфигурацияКорреспондент = Конфигурация;
				Иначе
					Форма.КД3_Конфигурация = Конфигурация;
				КонецЕсли;
				ОчиститьМетаданные = Истина;
				КД3_УправлениеФормойКлиент.ОчиститьМетаданныеКонфигурации(Форма, ДляКорреспондента);
			КонецЕсли;
		Иначе
			// Если конфигурация одна (значит объект был заполнен или конфигурация всего одна)
			Если Конфигурация = ТекущаяКонфигурация Тогда
				Возврат; // Если конфигурация одна и она не поменялась, то ничего не меняем
			Иначе
				// Получение доступных конфигураци по конвертациям и объекту и обновление метаданных
				ОбновитьСписокКонфигураций = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ОбновитьСписокКонфигураций Тогда
		ОчиститьМетаданные = Ложь;
		ДоступныеКонфигурации = КД3_УправлениеФормойКлиентСервер.ДоступныеКонфигурации(Форма, ЭтоКонвертацияXDTO);
		КД3_УправлениеФормойКлиентСервер.УстановитьВидимостьКонфигураций(ДоступныеКонфигурации, ЭтоКонвертацияXDTO, Форма);
	КонецЕсли;
	Если ОчиститьМетаданные Тогда
		ОчиститьМетаданныеКонфигурации(Форма, ДляКорреспондента);
	КонецЕсли;
	
КонецПроцедуры

Процедура ИнициализацияМетаданных(Форма, ЭтоКонвертацияXDTO, ИзмененныйКонтекст = Неопределено) Экспорт
	КД3_РедакторКодаКлиент.ИнициализацияМетаданных(Форма, ЭтоКонвертацияXDTO, ИзмененныйКонтекст);
КонецПроцедуры

Процедура ОбновитьЛокальныйКонтекст(Форма, ЭтоКонвертацияXDTO, ИзмененныйКонтекст) Экспорт
	КД3_РедакторКодаКлиент.ОбновитьЛокальныйКонтекст(Форма, ЭтоКонвертацияXDTO, ИзмененныйКонтекст);
КонецПроцедуры

Процедура ОчиститьМетаданныеКонфигурации(Форма, ДляКорреспондента) Экспорт
	// Добавление обработчиков в список требуемых для инициализации
	Для Каждого ЭлементСписка Из Форма.КД3_Обработчики Цикл
		Если ЭлементСписка.Пометка = ДляКорреспондента Тогда
			ИмяРеквизита = "КД3_" + ЭлементСписка.Значение;
			ОбработчикДляИнициализации = Форма.КД3_Инициализация.НайтиПоЗначению(ИмяРеквизита);
			Если ОбработчикДляИнициализации = Неопределено Тогда
				Форма.КД3_Инициализация.Добавить(ИмяРеквизита);
			Иначе
				ОбработчикДляИнициализации.Пометка = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	Форма.ПодключитьОбработчикОжидания("КД3_Подключаемый_ИнициализацияМетаданных", 0.5, Истина);
КонецПроцедуры

Процедура ОбработатьСобытиеHTML(Форма, Элемент, ДанныеСобытия) Экспорт
	Событие = ДанныеСобытия.Event.eventData1C;
	Если Событие = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Событие.event = "EVENT_GET_METADATA" Тогда
		Если КД3_НастройкиКлиент.ИспользоватьКонтекстнуюПодсказку() Тогда
			СписокМетаданных = Событие.params;
			Если ТипЗнч(СписокМетаданных) <> Тип("Строка") Тогда
				СписокМетаданных = Событие.params.metadata;
			КонецЕсли;
			ВызватьОбновлениеМетаданных(Форма, Элемент.Имя, СписокМетаданных)
		КонецЕсли;
	ИначеЕсли Событие.event = "EVENT_CONTENT_CHANGED" Тогда
		Форма.Модифицированность = Истина;
		Если Форма.КД3_ИзмененныйРеквизит <> Элемент.Имя Тогда
			СохранитьИзмененныйОбработчик(Форма);
			Форма.КД3_ИзмененныйРеквизит = Элемент.Имя;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура КомандаРедактора(ЭтотОбъект, Команда) Экспорт
	ПозРазделителя = СтрНайти(Команда.Имя, "_", НаправлениеПоиска.СКонца);
	ИмяРеквизита = Лев(Команда.Имя, ПозРазделителя -1);
	ИмяКоманды = Сред(Команда.Имя, ПозРазделителя + 1);
	Если ИмяКоманды = "Закомментировать" Тогда
		КД3_РедакторКодаКлиент.Закомментировать(ЭтотОбъект, ИмяРеквизита);
	ИначеЕсли ИмяКоманды = "УбратьКомментарии" Тогда
		КД3_РедакторКодаКлиент.УбратьКомментарии(ЭтотОбъект, ИмяРеквизита);
	ИначеЕсли ИмяКоманды = "КонструкторФорматнойСтроки" Тогда
		ВызватьКонструкторФорматнойСтроки(ЭтотОбъект, ИмяРеквизита);
	ИначеЕсли ИмяКоманды = "Форматировать" Тогда
		КД3_РедакторКодаКлиент.Форматировать(ЭтотОбъект, ИмяРеквизита);
	ИначеЕсли ИмяКоманды = "ДобавитьПереносСтроки" Тогда
		КД3_РедакторКодаКлиент.ДобавитьПереносСтроки(ЭтотОбъект, ИмяРеквизита);
	ИначеЕсли ИмяКоманды = "УдалитьПереносСтроки" Тогда
		КД3_РедакторКодаКлиент.УдалитьПереносСтроки(ЭтотОбъект, ИмяРеквизита);
	ИначеЕсли ИмяКоманды = "ЗадатьКомментарийОбработчика" Тогда
		ЗадатьКомментарийОбработчика(ЭтотОбъект, ИмяРеквизита);
	КонецЕсли;
КонецПроцедуры

Процедура СохранитьИзмененныйОбработчик(Форма)
	
	Если ПустаяСтрока(Форма.КД3_ИзмененныйРеквизит) Тогда
		Возврат;
	КонецЕсли;
	
	ИмяОбработчика = Сред(Форма.КД3_ИзмененныйРеквизит, 5);
	НовоеЗначение = КД3_РедакторКодаКлиент.ПолучитьТекст(Форма, Форма.КД3_ИзмененныйРеквизит);
	НовоеЗначение = СтрЗаменить(НовоеЗначение, Символ(13) + Символ(10), Символ(10));
	
	Форма.КД3_ИзмененныйРеквизит = "";
	
	Если НовоеЗначение = Форма.Объект[ИмяОбработчика] Тогда
		Возврат; // Произошла только замена LF на CR+LF
	КонецЕсли;
	Форма.Объект[ИмяОбработчика] = НовоеЗначение;
	
	СтраницаОбработчика = Форма.Элементы[ИмяОбработчика].Родитель;
	Если ТипЗнч(СтраницаОбработчика) = Тип("ФормаКлиентскогоПриложения") Тогда
		Возврат; // Обработчик алгоритма или запроса
	КонецЕсли;
	СтраницаОбработчика.Картинка = ?(ПустаяСтрока(НовоеЗначение), Новый Картинка, БиблиотекаКартинок.Успешно);
	
КонецПроцедуры

Процедура ВызватьКонструкторФорматнойСтроки(Форма, ИмяРеквизита)
	
	ПараметрыСтроки = КД3_РедакторКодаКлиент.ПолучитьФорматнуюСтроку(Форма, ИмяРеквизита);
	
	ДопПараметры = Новый Структура("Форма,ИмяРеквизита", Форма, ИмяРеквизита);
	Если ПараметрыСтроки = Неопределено Тогда
		ДопПараметры.Вставить("ФорматНаяСтрока", "");
		ДопПараметры.Вставить("Позиция", Неопределено);
		
		//@skip-warning
		Оповещение = Новый ОписаниеОповещения ("ОткрытьКонструкторФорматнойСтроки",
			ОбщегоНазначенияКлиент.ОбщийМодуль("КД3_УправлениеФормойКлиент"), ДопПараметры);
		ТекстВопроса =
			"Форматная строка не найдена.
			|Создать новую форматную строку?";
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ФорматнаяСтрока = СтрЗаменить(СтрЗаменить(ПараметрыСтроки.text, "|", ""), """", "");
		ДопПараметры.Вставить("ФорматНаяСтрока", ФорматнаяСтрока);
		ДопПараметры.Вставить("Позиция", ПараметрыСтроки.range);
		
		//@skip-warning
		Оповещение = Новый ОписаниеОповещения ("ОткрытьКонструкторФорматнойСтроки",
			ОбщегоНазначенияКлиент.ОбщийМодуль("КД3_УправлениеФормойКлиент"), ДопПараметры);
		ВыполнитьОбработкуОповещения(Оповещение, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОткрытьКонструкторФорматнойСтроки(Ответ, ДопПараметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	//@skip-warning
	Оповещение = Новый ОписаниеОповещения("ПриЗакрытииКонструктораФорматнойСтроки",
		ОбщегоНазначенияКлиент.ОбщийМодуль("КД3_УправлениеФормойКлиент"), ДопПараметры);
	Конструктор = Новый КонструкторФорматнойСтроки();
	Попытка
		Конструктор.Текст = ДопПараметры.ФорматнаяСтрока;
		Конструктор.Показать(Оповещение);
	Исключение
		ТекстСообщения = ИнформацияОбОшибке();
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецПопытки;
	
КонецПроцедуры

Процедура ПриЗакрытииКонструктораФорматнойСтроки(Текст, ДопПараметры) Экспорт
	
	Если Текст = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Текст = СтрЗаменить(Текст, Символы.ПС, Символы.ПС + "|");
	Текст = СтрЗаменить(Текст, """", """""");
	Текст = """" + Текст + """";
	КД3_РедакторКодаКлиент.УстановитьТекст(ДопПараметры.Форма, ДопПараметры.ИмяРеквизита, Текст, ДопПараметры.Позиция);
	
КонецПроцедуры

Процедура ВызватьОбновлениеМетаданных(Форма, ИмяРеквизита, СписокМетаданных)
	
	ИмяОбработчика = Сред(ИмяРеквизита, 5);
	ЭлементСписка = Форма.КД3_Обработчики.НайтиПоЗначению(ИмяОбработчика);
	Конфигурация = ?(ЭлементСписка.Пометка, Форма.КД3_КонфигурацияКорреспондент, Форма.КД3_Конфигурация);
	
	ОписанияМетаданных = КД3_МетаданныеКлиент.ОписаниеСпискаМетаданных(Конфигурация, СписокМетаданных);
	
	ТекстОшибки = КД3_РедакторКодаКлиент.ОбновитьСписокМетаданных(Форма, ИмяРеквизита, ОписанияМетаданных);
	Если НЕ ПустаяСтрока(ТекстОшибки) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Ошибка обновления метаданных: " + ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьТекстПриТолькоПросмотр(Форма, ИмяОбработчика) Экспорт
	КД3_РедакторКодаКлиент.УстановитьТекстПриТолькоПросмотр(Форма, ИмяОбработчика);
КонецПроцедуры

Процедура УстановитьВидимостьГруппыКонфигураций(Форма, ЭтоКонвертацияXDTO)
	
	Элемент = Форма.Элементы.КД3_Конфигурация;
	Элемент.ТолькоПросмотр = Элемент.СписокВыбора.Количество() = 1;
	
	ВидимостьГруппы = НЕ Элемент.ТолькоПросмотр;
	Если НЕ ЭтоКонвертацияXDTO Тогда
		Элемент = Форма.Элементы.КД3_КонфигурацияКорреспондент;
		Элемент.ТолькоПросмотр = Элемент.СписокВыбора.Количество() = 1;
		ВидимостьГруппы = ВидимостьГруппы ИЛИ НЕ Элемент.ТолькоПросмотр;
	КонецЕсли;
	
	Форма.Элементы.КД3_ГруппаКонфигурация.Видимость = ВидимостьГруппы;
	
КонецПроцедуры

// Вызывается при изменения конвертаций в которые входит объект
// Изменяет доступные для выбора конфигурации, связанные с подсказкой
//
// Параметры:
//  Форма
//  ЭтоКонвертацияXDTO
//
Процедура ПриИзмененииКонвертаций(Форма, ЭтоКонвертацияXDTO) Экспорт
	
	ДоступныеКонфигурации = КД3_УправлениеФормойКлиентСервер.ДоступныеКонфигурации(Форма, ЭтоКонвертацияXDTO);
	КД3_УправлениеФормойКлиентСервер.УстановитьВидимостьКонфигураций(ДоступныеКонфигурации, ЭтоКонвертацияXDTO, Форма);
	
	Если ДоступныеКонфигурации.Конфигурация.Найти(Форма.КД3_Конфигурация) = Неопределено Тогда
		Если ДоступныеКонфигурации.Количество() > 0 Тогда
			Форма.КД3_Конфигурация = ДоступныеКонфигурации.Конфигурация[0];
		Иначе
			Форма.КД3_Конфигурация = ПредопределенноеЗначение("Справочник.Релизы.ПустаяСсылка");
		КонецЕсли;
		ОчиститьМетаданныеКонфигурации(Форма, Ложь);
	КонецЕсли;
	Если НЕ ЭтоКонвертацияXDTO Тогда
		Если ДоступныеКонфигурации.КонфигурацияКорреспондент.Найти(Форма.КД3_КонфигурацияКорреспондент) = Неопределено Тогда
			Если ДоступныеКонфигурации.Количество() > 0 Тогда
				Форма.КД3_КонфигурацияКорреспондент = ДоступныеКонфигурации[0];
			Иначе
				Форма.КД3_КонфигурацияКорреспондент = ПредопределенноеЗначение("Справочник.Релизы.ПустаяСсылка");
			КонецЕсли;
			ОчиститьМетаданныеКонфигурации(Форма, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗадатьКомментарийОбработчика(Форма, ИмяРеквизита)
	
	ВидОбработчика = Неопределено;
	
	// ПКО
	Если ИмяРеквизита = "КД3_АлгоритмПриОтправкеДанных" Тогда
		ВидОбработчика = ПредопределенноеЗначение("Перечисление.ВидыОбработчиков.ПриОтправке");
	ИначеЕсли ИмяРеквизита = "КД3_АлгоритмПередПолучениемДанных" Тогда
		ВидОбработчика = ПредопределенноеЗначение("Перечисление.ВидыОбработчиков.ПриКонвертацииДанныхXDTO");
	ИначеЕсли ИмяРеквизита = "КД3_АлгоритмПриПолученииДанных" Тогда
		ВидОбработчика = ПредопределенноеЗначение("Перечисление.ВидыОбработчиков.ПередЗаписьюПолученныхДанных");
	КонецЕсли;
	// ПОД
	Если ИмяРеквизита = "КД3_АлгоритмПриОбработке" Тогда
		ВидОбработчика = ПредопределенноеЗначение("Перечисление.ВидыОбработчиков.ПриОбработке");
	ИначеЕсли ИмяРеквизита = "КД3_АлгоритмВыборкаДанных" Тогда
		ВидОбработчика = ПредопределенноеЗначение("Перечисление.ВидыОбработчиков.ВыборкаДанных");
	КонецЕсли;
	
	Если ВидОбработчика = Неопределено Тогда
		Возврат; // Комментарий к этому обработчику не поддерживается
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("СсылкаНаОбъект,КД3_ВидОбработчика", Форма.Объект.Ссылка, ВидОбработчика);
	ОткрытьФорму("РегистрСведений.КомментарииКОбработчикам.Форма.Форма", ПараметрыФормы, Форма, Форма.УникальныйИдентификатор,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры