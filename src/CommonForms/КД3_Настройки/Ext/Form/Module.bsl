
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Настройки = КД3_Настройки.ЗагрузитьНастройки();
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Настройки);
	Элементы.ГруппаНастройки.Доступность = ИспользоватьРедакторКода;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьИнформациюПоКонсолиКода();
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Настройки = ЗаписатьНастройкиНаСервере();
	КД3_НастройкиКлиент.СохранитьНастройкиВКэше(Настройки);
	Закрыть();
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьНастройкиНаСервере()
	
	Настройки = КД3_Настройки.НастройкиПоУмолчанию();
	ЗаполнитьЗначенияСвойств(Настройки, ЭтотОбъект);
	КД3_Настройки.СохранитьНастройки(Настройки);
	Возврат Настройки;
	
КонецФункции

&НаКлиенте
Процедура ИспользоватьРедакторКодаПриИзменении(Элемент)
	Если НЕ ИспользоватьРедакторКода Тогда
		ИспользоватьКонтекстнуюПодсказку = Ложь;
	КонецЕсли;
	Элементы.ГруппаНастройки.Доступность = ИспользоватьРедакторКода;
КонецПроцедуры

&НаКлиенте
Процедура КаталогИсходниковНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОбщегоНазначенияКлиент.ОткрытьПроводник(КаталогИсходников);
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьКонсольКодаИзМакета(Команда)
	КД3_Настройки.УстановитьФайлМакетаИсходников(Неопределено);
	КД3_РедакторКодаКлиент.УдалитьИсходники();
	ОбновитьИнформациюПоКонсолиКода();
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьКонсольКодаИзФайла(Команда)
	
	НастройкиДиалога = Новый Структура;
	НастройкиДиалога.Вставить("Фильтр", НСтр("ru = 'Файл архива исходников (*.zip)|*.zip|Файл console.epf|console.epf'"));
	НастройкиДиалога.Вставить("Заголовок", НСтр("ru='Выберите файл с архивом исходников'"));
	
	Оповещение = Новый ОписаниеОповещения("ЗагрузитьКонсольКодаИзФайлаЗавершение", ЭтотОбъект);
	ОбменДаннымиКлиент.ВыбратьИПередатьФайлНаСервер(Оповещение, НастройкиДиалога, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьКонсольКодаИзФайлаЗавершение(РезультатПомещенияФайлов, ДопПараметры) Экспорт
	
	АдресФайлаЗагрузки = РезультатПомещенияФайлов.Хранение;
	ТекстОшибки = РезультатПомещенияФайлов.ОписаниеОшибки;
	
	Если ПустаяСтрока(ТекстОшибки) И ПустаяСтрока(АдресФайлаЗагрузки) Тогда
		ТекстОшибки = НСтр("ru = 'Ошибка передачи файла структуры конфигурации на сервер'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
	КД3_Настройки.УстановитьФайлМакетаИсходников(АдресФайлаЗагрузки, РезультатПомещенияФайлов.Имя);
	КД3_РедакторКодаКлиент.УдалитьИсходники();
	ОбновитьИнформациюПоКонсолиКода();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформациюПоКонсолиКода()
	КД3_РедакторКодаКлиент.ИзвлечьИсходники();
	КаталогИсходников = КД3_НастройкиКлиент.ЗначениеИзКэша("КаталогИсходников");
	Элементы.КонсольКодаИнформация.Заголовок = КД3_НастройкиКлиент.ЗначениеИзКэша("КонсольКодаИнформация");
КонецПроцедуры
